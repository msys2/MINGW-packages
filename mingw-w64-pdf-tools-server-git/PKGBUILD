# Maintainer: Andr√°s Svraka <svraka.andras@gmail.com>

_basename=pdf-tools-server
_realname=${_basename}-git
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=r927.c510442
pkgrel=2
pkgdesc="Server for Emacs's pdf-tools"
source=("${_realname}::git+https://github.com/politza/pdf-tools.git#branch=master")
sha256sums=("SKIP")
arch=("any")
url="https://github.com/politza/pdf-tools"
license=("GPL3")
depends=("${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-poppler")
optdepends=("${MINGW_PACKAGE_PREFIX}-imagemagick")
makedepends=("${MINGW_PACKAGE_PREFIX}-emacs"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "automake"
             "autoconf"
             "${optdepends[@]}")

pkgver() {
  cd "${srcdir}"/${_realname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}"/${_realname}
  make server/epdfinfo
}

package() {
  cd "${srcdir}"/${_realname}
  install -D server/epdfinfo.exe "${pkgdir}"${MINGW_PREFIX}/bin/epdfinfo.exe
}
