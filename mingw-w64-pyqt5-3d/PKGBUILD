# Contributor: Melven Roehrig-Zoellner <Melven.Roehrig-Zoellner@DLR.de>
# mostly copied from ../mingw-w64-python-pyqt5/PKGBUILD

_realname=pyqt5-3d
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=5.15.6
pkgrel=2
pkgdesc="Qt5 Qt3D bindings for Python (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64' 'clang32')
msys2_references=(
  'pypi: PyQt3D'
)
license=('GPL')
url="https://riverbankcomputing.com/software/pyqt3d"
depends=("${MINGW_PACKAGE_PREFIX}-python-pyqt5"
         "${MINGW_PACKAGE_PREFIX}-qt5-3d"
         "${MINGW_PACKAGE_PREFIX}-qt5-gamepad")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-sip"
             "${MINGW_PACKAGE_PREFIX}-pyqt-builder"
             "${MINGW_PACKAGE_PREFIX}-python-packaging")
source=(https://pypi.python.org/packages/source/P/PyQt3D/PyQt3D-${pkgver}.tar.gz)
sha256sums=('7d6c6d55cd8fc221b313c995c0f8729a377114926f0377f8e9011d45ebf3881c')

build() {
  [[ -d python-${MSYSTEM} ]] && rm -rf python-${MSYSTEM}
  cp -r PyQt3D-${pkgver} python-${MSYSTEM} && cd python-${MSYSTEM}

  MSYS2_ARG_CONV_EXCL="--api-dir=;" \
  ${MINGW_PREFIX}/bin/sip-build \
    --no-make \
    --api-dir=${MINGW_PREFIX}/share/qt5/qsci/api/python \
    --qmake=${MINGW_PREFIX}/bin/qmake.exe

    cd build
    make
}

package(){
  cd python-${MSYSTEM}/build

  make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install
}
