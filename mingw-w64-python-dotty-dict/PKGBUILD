# Maintainer: fauxpark <fauxpark@gmail.com>

_realname=dotty_dict
pkgbase=mingw-w64-python-${_realname/_/-}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname/_/-}")
pkgver=1.3.0
pkgrel=2
pkgdesc="Dictionary wrapper for quick access to deeply nested keys (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='https://github.com/pawelzny/dotty_dict'
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools-scm")
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz"
        "0001-readme-utf8.patch"
        "0002-setuptools-scm-dependency.patch")
sha256sums=('eb0035a3629ecd84397a68f1f42f1e94abd1c34577a19cd3eacad331ee7cbaf0'
            '072e21c8e963ac7d446d36e8946a94b0be333e0a3d597b19e6f2ab068bc56ba6'
            '7deb7ff6d1ab9519667af7423daa7f83a8fc3efb4f7591b0d0369b2f5e964fe1')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -Np1 -i "${srcdir}/0001-readme-utf8.patch"
  patch -Np1 -i "${srcdir}/0002-setuptools-scm-dependency.patch"
}

build() {
  cd "${srcdir}"
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  "${MINGW_PREFIX}"/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    "${MINGW_PREFIX}"/bin/python setup.py install --prefix="${MINGW_PREFIX}" \
      --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname/_/-}/LICENSE"
}
