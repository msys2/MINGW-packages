# Maintainer: fauxpark <fauxpark@gmail.com>

_realname=dotty_dict
pkgbase=mingw-w64-python-${_realname/_/-}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname/_/-}")
pkgver=1.3.0
pkgrel=1
pkgdesc="Dictionary wrapper for quick access to deeply nested keys (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='https://github.com/pawelzny/dotty_dict'
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools")
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz"
        "0001-Remove-Polish-letters.patch"
        "0002-Remove-Polish-letters.patch")
sha256sums=('eb0035a3629ecd84397a68f1f42f1e94abd1c34577a19cd3eacad331ee7cbaf0'
            '75118656c08fae0cb1a70bee32b00b4599e8ae3b175b9ad208783270fa302adf'
            '1525035f8809e3a5729770b024f409922f8c93e1eb1e75b4135d2848f09cc970')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -Np1 -i "${srcdir}/0001-Remove-Polish-letters.patch"
  patch -Np1 -i "${srcdir}/0002-Remove-Polish-letters.patch"
}

build() {
  cd "${srcdir}"
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  "${MINGW_PREFIX}"/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    "${MINGW_PREFIX}"/bin/python setup.py install --prefix="${MINGW_PREFIX}" \
      --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname/_/-}/LICENSE"
}
