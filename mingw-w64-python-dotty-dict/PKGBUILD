# Maintainer: fauxpark <fauxpark@gmail.com>

_realname=dotty_dict
pkgbase=mingw-w64-python-${_realname/_/-}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname/_/-}")
pkgver=1.3.1
pkgrel=4
pkgdesc="Dictionary wrapper for quick access to deeply nested keys (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/pawelzny/dotty_dict'
msys2_references=(
  'purl: pkg:pypi/dotty-dict'
)
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-poetry-core")
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('4b016e03b8ae265539757a53eba24b9bfda506fb94fbce0bee843c6f05541a15')

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}"/python-build-${MSYSTEM}

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname/_/-}/LICENSE"
}
