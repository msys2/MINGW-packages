# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=coin-or-clp
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.17.9
pkgrel=1
pkgdesc="COIN-OR Linear Programming Solver. (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64')
url='https://github.com/coin-or/Clp'
msys2_references=(
  'archlinux: coin-or-clp'
)
license=('spdx:EPL-2.0')
groups=("${MINGW_PACKAGE_PREFIX}-coin-or")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-coin-or-asl"
         "${MINGW_PACKAGE_PREFIX}-coin-or-coinutils"
         "${MINGW_PACKAGE_PREFIX}-coin-or-osi"
         "${MINGW_PACKAGE_PREFIX}-suitesparse")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools")
source=("${url}/archive/releases/${pkgver}/${_realname}-${pkgver}.tar.gz"
        001-fix-build-on-mingw.patch
        002-fix-build-with-clang.patch)
sha256sums=('b02109be54e2c9c6babc9480c242b2c3c7499368cfca8c0430f74782a694a49f'
            'e6903e9c8d497ae081abb2bbb02766fd6ab5893c0f7575931c992f0aa19f7198'
            '84e313d04e4d35775070edb595b40700cdf41f7d29dd4fc66e051d717b2ac06a')

prepare() {
  cd Clp-releases-${pkgver}
  patch -p1 -i "${srcdir}"/001-fix-build-on-mingw.patch

  if [[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]]; then
    patch -p1 -i "${srcdir}"/002-fix-build-with-clang.patch
  fi
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  export lt_cv_deplibs_check_method='pass_all'

  CFLAGS+=" -Wno-implicit-function-declaration" \
  ../"Clp-releases-${pkgver}"/configure \
    --prefix="${MINGW_PREFIX}" \
    --enable-shared \
    --disable-static \
    --enable-dependency-linking \
    --with-cholmod-lib='-lcholmod' \
    --with-cholmod-incdir=${MINGW_PREFIX}/include/suitesparse \
    --with-amd-lib='-lamd' \
    --with-amd-incdir=${MINGW_PREFIX}/include/suitesparse

  make
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  make check
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  make install DESTDIR="${pkgdir}"

  install -Dm644 "${srcdir}/Clp-releases-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
