# Maintainer: @naveen521kk on Github Naveen M K <naveen521kk@gmail.com>

_realname=build
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.10.0
pkgrel=3
pkgdesc="A simple, correct Python build frontend (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://github.com/pypa/build'
license=('spdx:MIT')
depends=(
  "${MINGW_PACKAGE_PREFIX}-python-colorama"
  "${MINGW_PACKAGE_PREFIX}-python-packaging"
  "${MINGW_PACKAGE_PREFIX}-python-pyproject-hooks"
  "${MINGW_PACKAGE_PREFIX}-python-tomli"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python-flit-core"
  "${MINGW_PACKAGE_PREFIX}-python-installer"
)
options=('!strip')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz"
        "0001-pass-ignore_errors-to-shutil.patch")
sha256sums=('d5b71264afdb5951d6704482aac78de887c80691c52b88a9ad195983ca2c9269'
            '93e3313870456b8898f1a012435062e78032d91cef9b5770b611b26069f223f1')

prepare() {
  cp -r "${_realname}-${pkgver}" "python-build-${CARCH}"
  cd "python-build-${CARCH}"

  patch -p1 -i ${srcdir}/0001-pass-ignore_errors-to-shutil.patch
}

build() {
  cd "${srcdir}/python-build-${CARCH}"

  python -m flit_core.wheel
}

package() {
  cd "${srcdir}/python-build-${CARCH}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -D -m644 LICENSE "${pkgdir}${MINGW_PREFIX}"/share/licenses/python-${_realname}/LICENSE
}
