# Maintainer: @naveen521kk on Github Naveen M K <naveen@syrusdark.website>

_realname=build
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.2.1
pkgrel=1
pkgdesc="A simple, correct PEP517 package builder (mingw-w64)"
arch=('any')
url='https://github.com/pypa/build'
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python-toml"
         "${MINGW_PACKAGE_PREFIX}-python-pep517"
         "${MINGW_PACKAGE_PREFIX}-python-packaging"
#standard python venv doesn't work good with this. So virtualenv is required.
         "${MINGW_PACKAGE_PREFIX}-python-virtualenv")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-mock"
            "${MINGW_PACKAGE_PREFIX}-python-filelock"
            "${MINGW_PACKAGE_PREFIX}-python-pytest")
source=("${_realname}-$pkgver.tar.gz"::"https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz"
        "0001-pass-ignore_errors-to-shutil.patch")
sha512sums=('986e1a62c7a2b40f9d1e2338955f8a8754bf85e8c0650ce340c6beddbd91bcb13835daed24ca17e94ab62bb5675527cce75ad22eba5909a325b310e2eeee1c15'
            '80fb412e0e33e25de1e4b470321316b193a457ce6f89244999ab4ae70ea5cc49f64f34c8666456548af720017f13c61123009a8c8628b5e0627fa7023eea73fb')


prepare() {
  rm -rf python-build-${CARCH}| true
  cp -r "${_realname}-${pkgver}" "python-build-${CARCH}"
  cd "python-build-${CARCH}"
  patch -p1 -i ${srcdir}/0001-pass-ignore_errors-to-shutil.patch
  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  msg "Python build for ${CARCH}"
  cd "${srcdir}/python-build-${CARCH}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

check() {
  msg "Python test for ${CARCH}"
  cd "${srcdir}/python-build-${CARCH}"

  PYTHONPATH=src ${MINGW_PREFIX}/bin/python -m pytest
}

package() {
  cd "${srcdir}/python-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -D -m644 LICENSE "${pkgdir}${MINGW_PREFIX}"/share/licenses/python-${_realname}/LICENSE

  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*.py; do
    sed -i 's/\#\!.*//' $_f
  done
}
