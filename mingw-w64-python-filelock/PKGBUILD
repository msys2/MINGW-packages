# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=filelock
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}"  "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=3.0.9
_commit=b71c3a494b1a9a84ddaa4a4d43bfd985a01e81e7
pkgrel=1
pkgdesc="A platform independent file lock (mingw-w64)"
arch=('any')
url='https://github.com/benediktschmitt/py-filelock'
license=('custom:Unlicense')
makedepends=("${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools")
options=('staticlibs' 'strip' '!debug')
checkdepends=("${MINGW_PACKAGE_PREFIX}-python2-pytest"
              "${MINGW_PACKAGE_PREFIX}-python3-pytest")
source=("${_realname}-$_commit.tar.gz::https://github.com/benediktschmitt/py-filelock/archive/$_commit.tar.gz")
sha512sums=('bf2c3a27488e4e0b0469599c5037658482b27eee08ef1844de5419865a67f01966b72f1f463ce7418660901c991ad06740a90f332db0b2d3d62967f0f270aba9')

prepare() {
  cd "${srcdir}"
  rm -rf py-filelock-$pkgver
  mv py-${_realname}-{$_commit,$pkgver}
  for builddir in python{2,3}-build-${CARCH}; do
    rm -rf ${builddir} | true
    cp -r "py-${_realname}-${pkgver}" "${builddir}"
  done
  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  for pver in {2,3}; do  
    msg "Python ${pver} build for ${CARCH}"  
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py build
  done  
}

check() {
    msg "Python 2 test for ${CARCH}"
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/pytest2 test.py

    msg "Python 2 test for ${CARCH}"
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/pytest test.py
}

package_python3-filelock() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3")

  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE.rst "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE.rst"
}

package_python2-filelock() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2")

  cd "${srcdir}/python2-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE.rst "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE.rst"
}

package_mingw-w64-i686-python2-filelock() {
  package_python2-filelock
}

package_mingw-w64-i686-python3-filelock() {
  package_python3-filelock
}

package_mingw-w64-x86_64-python2-filelock() {
  package_python2-filelock
}

package_mingw-w64-x86_64-python3-filelock() {
  package_python3-filelock
}
