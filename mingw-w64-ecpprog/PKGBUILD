# Maintainer: umarcor <unai.martinezcorral@ehu.eus>

_realname=ecpprog
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.0.r36.a19ec56
pkgrel=1
pkgdesc="ecpprog: basic driver for FTDI based JTAG probes, to program ECP5 FPGAs (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://github.com/gregdavill/ecpprog"
license=('ISC')
groups=("${MINGW_PACKAGE_PREFIX}-eda")
depends=("${MINGW_PACKAGE_PREFIX}-libftdi")
makedepends=("git" "${MINGW_PACKAGE_PREFIX}-cc")
_commit="a19ec56"
source=("ecpprog::git+https://github.com/gregdavill/ecpprog#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd "${_realname}/${_realname}"
  printf "0.0.r%s.%s" "$(git rev-list --count "${_commit}")" "$(git rev-parse --short "${_commit}")"
}

build() {
  cd "${srcdir}/${_realname}/${_realname}"
  make
}

check() {
  "${srcdir}/${_realname}/${_realname}/${_realname}".exe --help
}

package() {
  mkdir -p "${pkgdir}${MINGW_PREFIX}"/bin/
  cp "${srcdir}/${_realname}/${_realname}/${_realname}".exe "${pkgdir}${MINGW_PREFIX}"/bin/

  _licenses="${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
  mkdir -p "${_licenses}"
  install -m 644 "${srcdir}/${_realname}"/COPYING "${_licenses}"
}
