# Maintainer: umarcor <unai.martinezcorral@ehu.eus>

_realname=ecpprog
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.0.r54.8af8863
pkgrel=1
pkgdesc="A basic driver for FTDI based JTAG probes (FT232H, FT2232H, FT4232H), to program Lattice ECP5/Nexus FPGAs. (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/gregdavill/ecpprog"
msys2_repository_url='https://github.com/gregdavill/ecpprog'
license=('spdx:ISC')
groups=("${MINGW_PACKAGE_PREFIX}-eda")
depends=("${MINGW_PACKAGE_PREFIX}-libftdi")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "git")
_commit="8af8863855599f4b8ef8f46a336408b1aba60e9d"
source=("ecpprog::git+https://github.com/gregdavill/ecpprog#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd "${_realname}/${_realname}"
  printf "0.0.r%s.%s" "$(git rev-list --count "${_commit}")" "$(git rev-parse --short "${_commit}")"
}

build() {
  cd "${srcdir}/${_realname}/${_realname}"
  make
}

check() {
  "${srcdir}/${_realname}/${_realname}/${_realname}".exe --help
}

package() {
  mkdir -p "${pkgdir}${MINGW_PREFIX}"/bin/
  cp "${srcdir}/${_realname}/${_realname}/${_realname}".exe "${pkgdir}${MINGW_PREFIX}"/bin/

  install -Dm644 "${srcdir}/${_realname}"/COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
