# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=transmission
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-lib${_realname}"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-cli"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-gtk"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-qt")
pkgver=3.00
pkgrel=2
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='https://www.transmissionbt.com/'
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-curl"
             "${MINGW_PACKAGE_PREFIX}-dht"
             "${MINGW_PACKAGE_PREFIX}-gtk3"
             "${MINGW_PACKAGE_PREFIX}-libb64"
             "${MINGW_PACKAGE_PREFIX}-libevent"
             "${MINGW_PACKAGE_PREFIX}-libidn"
             "${MINGW_PACKAGE_PREFIX}-libnatpmp"
             "${MINGW_PACKAGE_PREFIX}-libutp"
             "${MINGW_PACKAGE_PREFIX}-miniupnpc"
             "${MINGW_PACKAGE_PREFIX}-openssl"
             "${MINGW_PACKAGE_PREFIX}-qt5-base"
             "${MINGW_PACKAGE_PREFIX}-qt5-tools"
             "${MINGW_PACKAGE_PREFIX}-qt5-winextras"
             "${MINGW_PACKAGE_PREFIX}-zlib"
             "intltool")
options=('strip' 'staticlibs')
source=("https://github.com/transmission/transmission-releases/raw/master/transmission-${pkgver}.tar.xz"
        "https://raw.githubusercontent.com/transmission/transmission/${pkgver}/po/pt_PT.po" # missing in tarball
        "https://raw.githubusercontent.com/transmission/transmission/${pkgver}/gtk/transmission.rc"
        "https://raw.githubusercontent.com/transmission/transmission/${pkgver}/gtk/transmission.ico"
        "0001-missing-libs.patch")
sha256sums=('9144652fe742f7f7dd6657716e378da60b751aaeda8bef8344b3eefc4db255f2'
            'f96b4b03d977fadca51716bcecc51582c980a7d5f1e1519b97837955f5fae59e'
            'aa94a7d5836c1f48df397bfe455a4a5d0f95065abc41fa36c524fefe01c18aa9'
            '989ea8136d164bd16951d19be8e75f898f860a17acbff7060f04e7f2c81b0df2'
            '56316880a3c9b837aac692b5052c73787212dc859d0a18d692a9b84b69558650')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  patch -p1 -i ../0001-missing-libs.patch

  cp "${srcdir}/pt_PT.po" po/
  cp "${srcdir}/transmission.rc" gtk/
  cp "${srcdir}/transmission.ico" gtk/

  # Fix build with autoconf 2.70
  sed -i 's/\[IT_PROG_INTLTOOL(\[/[\nIT_PROG_INTLTOOL(\[/' configure.ac
  rm -f m4/glib-gettext.m4
  autoreconf -fi

  sed -i '/^Icon=/ s/$/-qt/' qt/transmission-qt.desktop
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ../${_realname}-${pkgver}/configure \
    --enable-external-dht \
    --enable-external-b64 \
    --enable-external-natpmp
  make

  cd qt
  qmake qtr.pro \
    DEFINES+=TRANSLATIONS_DIR=\\\\\\\"${MINGW_PREFIX}/share/transmission-qt/translations\\\\\\\"
  make
  lrelease translations/*.ts
}

package_libtransmission() {
  pkgdesc='Fast, easy, and free BitTorrent client - shared library (mingw-w64)'

  cd "${srcdir}/${_realname}-${pkgver}"

  install -Dm644 libtransmission/libtransmission.a -t "${pkgdir}${MINGW_PREFIX}/lib"
  install -Dm644 libtransmission/*.h -t "${pkgdir}${MINGW_PREFIX}/include/transmission"

  install -Dm644 COPYING "${pkgdir}${MINGW_PREFIX}/share/licences/libtransmission/COPYING"
}

package_transmission-cli() {
  pkgdesc="Fast, easy, and free BitTorrent client - CLI tools, daemon and web client (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-curl"
           "${MINGW_PACKAGE_PREFIX}-libb64"
           "${MINGW_PACKAGE_PREFIX}-libevent"
           "${MINGW_PACKAGE_PREFIX}-libnatpmp"
           "${MINGW_PACKAGE_PREFIX}-miniupnpc")

  cd "${srcdir}/${_realname}-${pkgver}"

  for dir in daemon cli web utils; do
     make -C "$dir" DESTDIR="${pkgdir}" install
  done

  install -Dm644 COPYING "${pkgdir}${MINGW_PREFIX}/share/licences/transmission-cli/COPYING"
}

package_transmission-gtk() {
  pkgdesc="Fast, easy, and free BitTorrent client - GTK+ GUI (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-curl"
           "${MINGW_PACKAGE_PREFIX}-gtk3"
           "${MINGW_PACKAGE_PREFIX}-hicolor-icon-theme"
           "${MINGW_PACKAGE_PREFIX}-libb64"
           "${MINGW_PACKAGE_PREFIX}-libevent"
           "${MINGW_PACKAGE_PREFIX}-libnatpmp"
           "${MINGW_PACKAGE_PREFIX}-miniupnpc")
  optdepends=("${MINGW_PACKAGE_PREFIX}-libnotify: Desktop notification support"
              "${MINGW_PACKAGE_PREFIX}-transmission-cli: daemon and web support")

  cd "${srcdir}/${_realname}-${pkgver}"

  make -C gtk DESTDIR="${pkgdir}" install
  make -C po DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}${MINGW_PREFIX}/share/licences/transmission-gtk/COPYING"
}

package_transmission-qt() {
  pkgdesc='Fast, easy, and free BitTorrent client - Qt GUI (mingw-w64)'
  depends=("${MINGW_PACKAGE_PREFIX}-curl"
           "${MINGW_PACKAGE_PREFIX}-libb64"
           "${MINGW_PACKAGE_PREFIX}-libevent"
           "${MINGW_PACKAGE_PREFIX}-libnatpmp"
           "${MINGW_PACKAGE_PREFIX}-miniupnpc"
           "${MINGW_PACKAGE_PREFIX}-qt5-base")
  optdepends=("${MINGW_PACKAGE_PREFIX}-transmission-cli: daemon and web support")

  cd "${srcdir}/${_realname}-${pkgver}"

  make -C qt INSTALL_ROOT="${pkgdir}${MINGW_PREFIX}" install

  install -Dm644 qt/translations/*.qm -t "${pkgdir}${MINGW_PREFIX}/share/transmission-qt/translations"
  install -Dm644 qt/icons/transmission.png "${pkgdir}${MINGW_PREFIX}/share/pixmaps/transmission-qt.png"
  install -Dm644 qt/transmission-qt.desktop "${pkgdir}${MINGW_PREFIX}/share/applications/transmission-qt.desktop"

  install -Dm644 COPYING "${pkgdir}${MINGW_PREFIX}/share/licences/transmission-qt/COPYING"
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
