# Maintainer: Joel Holdsworth <jholdsworth@nvidia.com>

_realname=conan
pkgbase=mingw-w64-python-conan
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
provides=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
replaces=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=1.51.3
pkgrel=1
pkgdesc="A distributed, open source, C/C++ package manager (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=('spdx:MIT')
url="https://conan.io"
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-pyjwt>=1.4.0"
         "${MINGW_PACKAGE_PREFIX}-python-requests>=2.25"
         "${MINGW_PACKAGE_PREFIX}-python-urllib3>=1.26.6"
         "${MINGW_PACKAGE_PREFIX}-python-colorama>=0.3.3"
         "${MINGW_PACKAGE_PREFIX}-python-yaml>=3.11"
         "${MINGW_PACKAGE_PREFIX}-python-patch-ng>=1.17.4"
         "${MINGW_PACKAGE_PREFIX}-python-fasteners>=0.14.1"
         "${MINGW_PACKAGE_PREFIX}-python-six>=1.10.0"
         "${MINGW_PACKAGE_PREFIX}-python-node-semver>=0.6.1"
         "${MINGW_PACKAGE_PREFIX}-python-distro>=1.0.2"
         "${MINGW_PACKAGE_PREFIX}-python-pygments>=2.0"
         "${MINGW_PACKAGE_PREFIX}-python-tqdm>=4.28.1"
         "${MINGW_PACKAGE_PREFIX}-python-jinja>=3.0"
         "${MINGW_PACKAGE_PREFIX}-python-dateutil>=2.7.0"
         "${MINGW_PACKAGE_PREFIX}-python-bottle>=0.12.8"
         "${MINGW_PACKAGE_PREFIX}-python-pluginbase>=0.5")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/conan-io/conan/archive/${pkgver}.tar.gz"
        "0001-Loosened-node-semver-version-requirement.patch")
sha256sums=('a335d60cd6b5f5c2ed8584d64e486b7adc9e4db190eba72fb30d4b008860fe31'
            '116ffda81204699ea3dfdbbf471071e1a8c8dfcc17d2be970fe5e98d6657e377')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # Apply patches
  patch -Nbp1 -i "${srcdir}/0001-Loosened-node-semver-version-requirement.patch"
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}/${_realname}-${pkgver}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
   ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  # Remove build machine shebangs
  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*.py; do
    sed -i 's/\#\!.*//' $_f
  done

  install -Dm644 LICENSE.md "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/COPYING"
  install -Dm644 contributors.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/contributors.txt"
}
