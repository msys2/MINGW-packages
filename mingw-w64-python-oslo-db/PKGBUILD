# Maintainer: Some One <some.one@some.email.com>
#This value is here because some python package names have capital letters
#while there is a strong preference for lowercase letters in ArchLinux and
#MSYS packages.

_realname=oslo-db
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}"  "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=4.41.1
pkgrel=1
pkgdesc="OpenStack Database Pattern Library (mingw-w64)"
arch=('any')
url='https://pypi.python.org/pypi/oslo.db'
license=('LICENSE')
makedepends=("${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-pbr"
             "${MINGW_PACKAGE_PREFIX}-python2-pbr" 
             "${MINGW_PACKAGE_PREFIX}-python3-alembic"
             "${MINGW_PACKAGE_PREFIX}-python2-alembic"
             "${MINGW_PACKAGE_PREFIX}-python3-debtcollector"
             "${MINGW_PACKAGE_PREFIX}-python2-debtcollector"
             "${MINGW_PACKAGE_PREFIX}-python3-oslo-i18n"
             "${MINGW_PACKAGE_PREFIX}-python2-oslo-i18n"
             "${MINGW_PACKAGE_PREFIX}-python3-oslo-config"
             "${MINGW_PACKAGE_PREFIX}-python2-oslo-config"
             "${MINGW_PACKAGE_PREFIX}-python3-oslo-utils"
             "${MINGW_PACKAGE_PREFIX}-python2-oslo-utils"
             "${MINGW_PACKAGE_PREFIX}-python3-sqlalchemy"
             "${MINGW_PACKAGE_PREFIX}-python2-sqlalchemy"
             "${MINGW_PACKAGE_PREFIX}-python3-sqlalchemy-migrate"
             "${MINGW_PACKAGE_PREFIX}-python2-sqlalchemy-migrate"
             "${MINGW_PACKAGE_PREFIX}-python3-stevedore"
             "${MINGW_PACKAGE_PREFIX}-python2-stevedore")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python3-mock"
              "${MINGW_PACKAGE_PREFIX}-python2-mock"
              "${MINGW_PACKAGE_PREFIX}-python3-testrepository"
              "${MINGW_PACKAGE_PREFIX}-python2-testrepository"
              "${MINGW_PACKAGE_PREFIX}-python3-testresources"
              "${MINGW_PACKAGE_PREFIX}-python2-testresources"
              "${MINGW_PACKAGE_PREFIX}-python3-oslotest"
              "${MINGW_PACKAGE_PREFIX}-python2-oslotest"
              "${MINGW_PACKAGE_PREFIX}-python3-oslo-context"
              "${MINGW_PACKAGE_PREFIX}-python2-oslo-context" 
              "${MINGW_PACKAGE_PREFIX}-python2-unittest2")
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-$pkgver.tar.gz"::"https://github.com/openstack/oslo.db/archive/$pkgver.tar.gz")
sha512sums=('be5eadc9f1ea221f00ae288c6a28099b49ba84e1e5b833c83f9e40e768f0fc099ca4a333be77eb8db8282dcefd21c63191532590b35c3cdd96fd0d889588ec30')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"
  pushd "oslo.db-${pkgver}"
#    apply_patch_with_msg 0001-A-really-important-fix.patch \
#      0002-A-less-important-fix.patch
  popd 
  for builddir in python{2,3}-build-${CARCH}; do
    rm -rf ${builddir} | true
    cp -r "oslo.db-${pkgver}" "${builddir}"
  done
  sed -i 's/import unittest2/import unittest as unittest2/' python3-build-${CARCH}/oslo_db/tests/sqlalchemy/test_async_eventlet.py

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
  # set version for PBR
  export PBR_VERSION=$pkgver
}

# Note that build() is sometimes skipped because it's done in 
# the packages setup.py install for simplicity if you can do so.
# but sometimes, you want to do a check before install which would
# also trigger the build.
build() {
  for pver in {2,3}; do  
    msg "Python ${pver} build for ${CARCH}"  
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py build
  done  
}

check() {
  for pver in {2,3}; do
    msg "Python ${pver} test for ${CARCH}"
    cd "${srcdir}/python${pver}-build-${CARCH}"
    PYTHON=python${pver} ${MINGW_PREFIX}/bin/python${pver} setup.py testr || warning "tests failed"
  done
}

package_python3-oslo-db() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3"
            "${MINGW_PACKAGE_PREFIX}-python3-six"
           "${MINGW_PACKAGE_PREFIX}-python3-pbr"
           "${MINGW_PACKAGE_PREFIX}-python3-alembic"
           "${MINGW_PACKAGE_PREFIX}-python3-debtcollector"
           "${MINGW_PACKAGE_PREFIX}-python3-oslo-i18n"
           "${MINGW_PACKAGE_PREFIX}-python3-oslo-config" 
           "${MINGW_PACKAGE_PREFIX}-python3-oslo-utils"
           "${MINGW_PACKAGE_PREFIX}-python3-sqlalchemy"
           "${MINGW_PACKAGE_PREFIX}-python3-sqlalchemy-migrate"
           "${MINGW_PACKAGE_PREFIX}-python3-stevedore")

  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

}

package_python2-oslo-db() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2"
           "${MINGW_PACKAGE_PREFIX}-python2-six"
           "${MINGW_PACKAGE_PREFIX}-python2-pbr"
           "${MINGW_PACKAGE_PREFIX}-python2-alembic"
           "${MINGW_PACKAGE_PREFIX}-python2-debtcollector"
           "${MINGW_PACKAGE_PREFIX}-python2-oslo-i18n"
           "${MINGW_PACKAGE_PREFIX}-python2-oslo-config"
           "${MINGW_PACKAGE_PREFIX}-python2-oslo-utils"
           "${MINGW_PACKAGE_PREFIX}-python2-sqlalchemy"
           "${MINGW_PACKAGE_PREFIX}-python2-sqlalchemy-migrate"
           "${MINGW_PACKAGE_PREFIX}-python2-stevedore")

  cd "${srcdir}/python2-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
}

package_mingw-w64-i686-python2-oslo-db() {
  package_python2-oslo-db
}

package_mingw-w64-i686-python3-oslo-db() {
  package_python3-oslo-db
}

package_mingw-w64-x86_64-python2-oslo-db() {
  package_python2-oslo-db
}

package_mingw-w64-x86_64-python3-oslo-db() {
  package_python3-oslo-db
}
