# Maintainer:  Alexey Pavlov <alexpux@gmail.com>
# Contributor: Tim Stahlhut <stahta01@gmail.com>

_realname=wxPython
_wx_basever=3.2
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.2.0a1.dev5445+edf1cf07
pkgrel=1
pkgdesc="A wxWidgets GUI toolkit for Python (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
license=("custom:wxWindows")
url="https://www.wxpython.org/"
depends=("${MINGW_PACKAGE_PREFIX}-python"
         $( [[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]] || echo \
           "${MINGW_PACKAGE_PREFIX}-python-numpy" )
         "${MINGW_PACKAGE_PREFIX}-python-pillow"
         "${MINGW_PACKAGE_PREFIX}-python-six"
         "${MINGW_PACKAGE_PREFIX}-wxmsw${_wx_basever}")
makedepends=("${MINGW_PACKAGE_PREFIX}-doxygen"
             "${MINGW_PACKAGE_PREFIX}-python-attrdict3"
             "${MINGW_PACKAGE_PREFIX}-python-requests"
             "${MINGW_PACKAGE_PREFIX}-sip"
             "${MINGW_PACKAGE_PREFIX}-waf"
             "${MINGW_PACKAGE_PREFIX}-cc"
             # for waf
             "${MINGW_PACKAGE_PREFIX}-python-setuptools")
options=('strip' 'staticlibs' 'buildflags')
source=(#"https://files.pythonhosted.org/packages/b0/4d/80d65c37ee60a479d338d27a2895fb15bbba27a3e6bb5b6d72bb28246e99/${_realname}-${pkgver}.tar.gz"
        "https://wxpython.org/Phoenix/snapshot-builds/${_realname}-${pkgver}.tar.gz"
        "https://github.com/wxWidgets/wxWidgets/commit/4010140491a4db4067a7754e6cf2ea7dd71e2161.diff"
        "https://github.com/wxWidgets/Phoenix/commit/fd6b4814e4fc9bbef0e33e0c966739954a0d643a.diff"
        '201-Remove-obsolete-entries-from-Doxyfile.patch'
        '501-MSys2-fixes-for-waf-configure.patch')
noextract=("${_realname}-${pkgver}.tar.gz")
sha256sums=('218b0ba6accd982a0a88b53456718f19a4dec0cfc389dfca376a2616fb2d10b3'
            'db64fba34f71793d2c4665fd9f442a6c246131caee21bd04005608151ec5e930'
            'b89f3dca57091d7937b62f350432c932f622cd2ea5c4b5b5402abe48308a5409'
            '97b5c9dd353f2c570b7a940585868c1127d10a96a3442cd96706359410494aa8'
            '35f2a3ac7c693306f81322221fd2c132baf235c74f1ea2f873cdf8e5a01453d5')

prepare() {
  plain "Extracting ${_realname}-${pkgver}.tar.gz due to symlink(s) without pre-existing target(s)"
  cd "${srcdir}"
  [[ -d ${_realname}-${pkgver} ]] && rm -rf ${_realname}-${pkgver}
  tar zxf "${srcdir}/${_realname}-${pkgver}.tar.gz" || true

  cd "${srcdir}/${_realname}-${pkgver}"
  rm -f etg/{_,}webkit.py sip/gen/{_,}webkit.sip

  # wxPython possible bug fixes 1xx

  # backport changes from wxPython git repo
  patch -Np1 -i ${srcdir}/fd6b4814e4fc9bbef0e33e0c966739954a0d643a.diff

  # Revert some wxPython changes 3xx

  # MSys2 patches need to fix build problems
  # See https://github.com/wxWidgets/Phoenix/pull/2200
  patch -Np1 -i ${srcdir}/501-MSys2-fixes-for-waf-configure.patch

  # wxWidgets changes 2xx
  cd "${srcdir}/${_realname}-${pkgver}/ext/wxWidgets"
  # Reduce warnings
  patch -Np1 -i ${srcdir}/201-Remove-obsolete-entries-from-Doxyfile.patch
  # Fix encoding error; change file(s) to UTF8/ASCII.
  patch -Np1 -i ${srcdir}/4010140491a4db4067a7754e6cf2ea7dd71e2161.diff
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  export PYTHONDONTWRITEBYTECODE=1
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  WAF="{$MINGW_PREFIX}/bin/waf" \
  SIP="${MINGW_PREFIX}/bin/sip" \
  DOXYGEN="${MINGW_PREFIX}/bin/doxygen" \
  WX_CONFIG="${MINGW_PREFIX}/bin/wx-config-${_wx_basever}" \
    "${MINGW_PREFIX}/bin/python" build.py \
      --prefix="${MINGW_PREFIX}" --python="${MINGW_PREFIX}/bin/python.exe" \
      --release --use_syswx --no_msedge --nodoc --cairo -vv \
      --no_allmo --no_magic --regenerate_sysconfig \
        dox touch etg sip

  export CC_NAME=${CC}

  local _jobs=${MAKEFLAGS:--j1}
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  CXX_NAME=${CC_NAME} \
  CC=${MINGW_PREFIX}/bin/cc.exe \
  CXX=${MINGW_PREFIX}/bin/${CXX}.exe \
  LDFLAGS="${LDFLAGS} $(${MINGW_PREFIX}/bin/python-config --ldflags)" \
  PYTHON_CONFIG="${MINGW_PREFIX}/bin/python-config" \
    waf \
      --prefix="${MINGW_PREFIX}" --python="${MINGW_PREFIX}/bin/python.exe" \
      --color=yes --jobs=${_jobs#-j} \
      --wx_config="sh.exe ${MINGW_PREFIX}/bin/wx-config-${_wx_basever}" \
      --no_magic --nopyc --nopyo --nopycache \
        configure

  waf build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    "${MINGW_PREFIX}/bin/python" setup.py install \
      --prefix="${MINGW_PREFIX}" --root="${pkgdir}" --skip-build

  MSYS2_ARG_CONV_EXCL="-p" ${MINGW_PREFIX}/bin/python -m compileall \
    -o 0 -o 1 -q -s"${pkgdir}" -p"/" "${pkgdir}${MINGW_PREFIX}/lib/python"*

  # remove shebang line
  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*-script.py; do
    sed -e '1 { s/^#!.*$// }' -i ${_f}
  done

  install -vDm 644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname/LICENSE.txt"
}
