# Maintainer:  Alexey Pavlov <alexpux@gmail.com>
# Contributor: Tim Stahlhut <stahta01@gmail.com>

_realname=wxPython
_wx_basever=3.2
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.2.0
pkgrel=1
pkgdesc="A wxWidgets GUI toolkit for Python (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
license=('spdx:wxWindows')
url="https://www.wxpython.org/"
depends=("${MINGW_PACKAGE_PREFIX}-python"
         $( [[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]] || echo \
           "${MINGW_PACKAGE_PREFIX}-python-numpy" )
         "${MINGW_PACKAGE_PREFIX}-python-pillow"
         "${MINGW_PACKAGE_PREFIX}-python-six"
         "${MINGW_PACKAGE_PREFIX}-wxmsw${_wx_basever}")
makedepends=("${MINGW_PACKAGE_PREFIX}-doxygen"
             "${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-requests"
             "${MINGW_PACKAGE_PREFIX}-sip"
             "${MINGW_PACKAGE_PREFIX}-waf"
             "${MINGW_PACKAGE_PREFIX}-cc"
             # for waf
             "python" "python-setuptools")
options=('strip' 'staticlibs' 'buildflags')
source=(#"https://files.pythonhosted.org/packages/b0/4d/80d65c37ee60a479d338d27a2895fb15bbba27a3e6bb5b6d72bb28246e99/${_realname}-${pkgver}.tar.gz"
        "https://github.com/wxWidgets/Phoenix/releases/download/${_realname}-${pkgver}/${_realname}-${pkgver}.tar.gz"
        '201-Remove-obsolete-entries-from-Doxyfile.patch'
        '301-Revert-some-of-python-sip-changes.patch')
sha256sums=('03452bf02f182fae78c030ac12aadedb0c5a5b63257b887dfe5689f028730984'
            '97b5c9dd353f2c570b7a940585868c1127d10a96a3442cd96706359410494aa8'
            '50feb203bcbee757adb844c970ecadebe3dbbab2114cab88988b79464663347f')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  rm -f etg/{_,}webkit.py sip/gen/{_,}webkit.sip

  # wxPython possible bug fixes 1xx

  # Revert some wxPython changes 3xx
  patch -Np1 -i ${srcdir}/301-Revert-some-of-python-sip-changes.patch

  # backport wxPython git changes 4xx

  # wxWidgets changes 2xx
  cd "${srcdir}/${_realname}-${pkgver}/ext/wxWidgets"
  # Reduce warnings
  patch -Np1 -i ${srcdir}/201-Remove-obsolete-entries-from-Doxyfile.patch
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  export PYTHONDONTWRITEBYTECODE=1
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  WAF="{$MINGW_PREFIX}/bin/waf" \
  SIP="${MINGW_PREFIX}/bin/sip" \
  DOXYGEN="${MINGW_PREFIX}/bin/doxygen" \
  WX_CONFIG="${MINGW_PREFIX}/bin/wx-config-${_wx_basever}" \
  ${MINGW_PREFIX}/bin/python -m build \
    --prefix="${MINGW_PREFIX}" --python="${MINGW_PREFIX}/bin/python.exe" \
    --release --use_syswx --no_msedge --nodoc --cairo -vv \
    --no_allmo --no_magic --regenerate_sysconfig \
    dox touch etg sip

  local _jobs=${MAKEFLAGS:--j1}
  # Use MSYS2 Python because, with mingw-w64 Python, waf has issues using
  # the wx-config* shell script.
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  CC_NAME=${CC} CC="${MINGW_PREFIX}/bin/${CC}.exe" \
  CXX_NAME=${CXX} CXX="${MINGW_PREFIX}/bin/${CXX}.exe" \
  LDFLAGS="${LDFLAGS} $("${MINGW_PREFIX}/bin/python-config" --ldflags)" \
  PYTHON_CONFIG="${MINGW_PREFIX}/bin/python-config" \
  /usr/bin/python "${MINGW_PREFIX}/bin/waf" \
    --prefix="${MINGW_PREFIX}" --python="${MINGW_PREFIX}/bin/python.exe" \
    --no_msvc --check-c-compiler=${CC} --check-cxx-compiler=${CXX} \
    --color=yes --jobs=${_jobs#-j} \
    --wx_config="sh.exe ${MINGW_PREFIX}/bin/wx-config-${_wx_basever}" \
    --no_magic --nopyc --nopyo --nopycache \
    configure build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    "${MINGW_PREFIX}/bin/python" setup.py install \
      --prefix="${MINGW_PREFIX}" --root="${pkgdir}" --skip-build

  MSYS2_ARG_CONV_EXCL="-p" python -m compileall \
    -o 0 -o 1 -q -s"${pkgdir}" -p"/" "${pkgdir}${MINGW_PREFIX}/lib/python"*

  # remove shebang line
  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*-script.py; do
    sed -e '1 { s/^#!.*$// }' -i ${_f}
  done

  install -vDm 644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname/LICENSE.txt"
}
