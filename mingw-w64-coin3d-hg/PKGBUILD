# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=coin3d
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}-hg"
pkgver=r11346.ecd7b724798a
pkgrel=1
pkgdesc="High-level, retained-mode toolkit for effective 3D graphics development (mingw-w64)"
arch=('any')
license=('BSD')
depends=("${_mingw_suff}-simage"
		"${_mingw_suff}-bzip2"
		"${_mingw_suff}-expat"
		"${_mingw_suff}-openal"
		#"${_mingw_suff}-freeglut"
		"${_mingw_suff}-freetype"
		"${_mingw_suff}-fontconfig"
		"${_mingw_suff}-zlib")
makedepends=("mercurial" "${_mingw_suff}-cmake" "${_mingw_suff}-gcc")
provides=("${_mingw_suff}-${_realname}")
conflicts=("${_mingw_suff}-${_realname}")
url=('https://bitbucket.org/Coin3D/coin')
source=("${_realname}"::"hg+https://bitbucket.org/Alexpux/coin"
		hg+https://bitbucket.org/Alexpux/generalmsvcgeneration
		hg+https://bitbucket.org/Alexpux/boost-header-libs-full)
options=(!strip staticlibs)
md5sums=('SKIP'
         'SKIP'
         'SKIP')

pkgver() {
	cd "$srcdir/$_realname"
	printf "r%s.%s" "$(hg identify -n)" "$(hg identify -i)"
}

prepare() {
	cd ${srcdir}/${_realname}
}

build() {
	[[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
	mkdir ${srcdir}/build-${MINGW_CHOST}
	cd ${srcdir}/build-${MINGW_CHOST}
	../"${_realname}"/configure \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--enable-shared \
		--disable-static \
		--disable-html \
		--disable-debug \
		--enable-system-expat \
		--enable-3ds-import \
		--disable-dl-fontconfig \
		--disable-dl-freetype \
		--disable-dl-zlib \
		--disable-dl-libbzip2 \
		--disable-dl-simage \
		--disable-dl-openal \
		--with-simage=${MINGW_PREFIX} \
		--with-openal=${MINGW_PREFIX} \
		--with-fontconfig=${MINGW_PREFIX} \
		--with-freetype=${MINGW_PREFIX} \
		--with-bzip2=${MINGW_PREFIX} \
		--with-zlib=${MINGW_PREFIX} \
		--with-simage=${MINGW_PREFIX} \
		--with-glu=${MINGW_PREFIX}/${MINGW_CHOST} \
		--without-x
		
		#--with-spidermonkey=no
	make
}

package() {
	cd "${srcdir}/build-${MINGW_CHOST}"
	make DESTDIR=${pkgdir} install
}
