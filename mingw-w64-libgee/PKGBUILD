# Maintainer: Ignacio Casal Quinteiro <icq@gnome.org>

_realname=libgee
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.18.0
pkgrel=2
arch=('any')
pkgdesc="A collection library providing GObject-based interfaces and classes for commonly used data structures (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-glib2")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-vala"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection")
options=('strip' 'staticlibs')
license=("LGPL2+")
url="https://wiki.gnome.org/Projects/Libgee"
source=("http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver%.*}/${_realname}-${pkgver}.tar.xz")
sha256sums=('4ad99ef937d071b4883c061df40bfe233f7649d50c354cf81235f180b4244399')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  # Get proper unprefixed directories for gobject-introspection files
  autoreconf -fiv
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib

  make -j1
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install
}
