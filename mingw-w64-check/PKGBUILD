# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=check
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.9.14
pkgrel=2
pkgdesc="A unit testing framework for C (mingw-w64)"
arch=('any')
url="http://check.sourceforge.net/"
license=("LGPL")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
options=('strip' 'staticlibs')
source=(http://downloads.sourceforge.net/sourceforge/${_realname}/${_realname}-${pkgver}.tar.gz)
md5sums=('38263d115d784c17aa3b959ce94be8b8')

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
