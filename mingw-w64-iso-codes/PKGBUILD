# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=iso-codes
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.3
pkgrel=1
pkgdesc="Lists of the country, language, and currency names (mingw-w64)"
arch=('any')
license=('LGPL')
makedepends=("git")
url="https://salsa.debian.org/iso-codes-team/iso-codes"
_commit=43398a317371e309361ce43072603863cb2f57e1  # tags/iso-codes-4.3^0
source=("git+https://salsa.debian.org/iso-codes-team/iso-codes/#commit=${_commit}"
        #"https://salsa.debian.org/iso-codes-team/iso-codes/uploads/049ce6aac94d842be809f4063950646c/${_realname}-${pkgver}.tar.xz"
        #"https://salsa.debian.org/iso-codes-team/iso-codes/uploads/c3aec90f806c0b4abf916457b19746a5/${_realname}-${pkgver}.tar.xz.sig"
        )
sha256sums=('SKIP')
validpgpkeys=('D1CB8F39BC5DED24C5D2C78C1302F1F036EBEB19' #Dr. Tobias Quathamer <t.quathamer@mailbox.org>
              '1302F1F036EBEB19' #Dr. Tobias Quathamer <t.quathamer@mailbox.org>
              'F972A168A2703B34CC23E09FD4E5EDACC0143D2D') # Christian Perrier <christian.perrier@onera.fr>

prepare() {
  cd ${_realname}
  autoreconf -fi
}

build() {
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}
  ../${_realname}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST}

  make
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make DESTDIR="${pkgdir}" pkgconfigdir=${MINGW_PREFIX}/lib/pkgconfig install
}
