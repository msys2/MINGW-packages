# Maintainer: Nicolas Frattaroli <ovdev@fratti.ch>

_realname=pngcrush
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.7.84
pkgrel=1
pkgdesc="A tool for optimizing the compression of PNG files (mingw-w64)"
arch=('any')
url="http://pmt.sourceforge.net/pngcrush"
license=('custom')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" "${MINGW_PACKAGE_PREFIX}-libpng" "${MINGW_PACKAGE_PREFIX}-zlib")
source=("http://downloads.sourceforge.net/pmt/${_realname}-${pkgver}-nolib.tar.gz"
        LICENSE)
md5sums=('ee96d73c620f21a46c33abfa350ccfc9'
         'e6446a786d939c98fcada57f9ae6de1f')

prepare() {
  mv "${srcdir}/${_realname}-${pkgver}-nolib" "${srcdir}/${_realname}-build-${MINGW_CHOST}"
}

build() {
  cd "${srcdir}/${_realname}-build-${MINGW_CHOST}"
  CFLAGS+=" -DPNGCRUSH_COUNT_COLORS" make
}

package() {
  install -Dm755 ${srcdir}/${_realname}-build-${MINGW_CHOST}/${_realname} \
    "${pkgdir}"${MINGW_PREFIX}/bin/${_realname}
  install -Dm644 LICENSE \
    "$pkgdir"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
