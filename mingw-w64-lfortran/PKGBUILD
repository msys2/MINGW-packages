# Maintainer: ZUO Zhihua <zuo.zhihua@qq.com>

_realname=lfortran
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.60.0
pkgrel=1
pkgdesc="Modern interactive LLVM-based Fortran compiler. (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/lfortran/lfortran"
msys2_repository_url='https://github.com/lfortran/lfortran'
msys2_references=(
  'aur: lfortran'
)
license=('spdx:BSD-3-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-clang"
         $([[ ${MINGW_PACKAGE_PREFIX} != *-clang-* ]] || echo "${MINGW_PACKAGE_PREFIX}-libunwind")
         "${MINGW_PACKAGE_PREFIX}-llvm-libs"
         #"${MINGW_PACKAGE_PREFIX}-mlir"
         "${MINGW_PACKAGE_PREFIX}-zlib")
         #"${MINGW_PACKAGE_PREFIX}-zstd")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-llvm"
             "${MINGW_PACKAGE_PREFIX}-fmt"
             "${MINGW_PACKAGE_PREFIX}-rapidjson")
             #"re2c")
source=("${url}/releases/download/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "001-find-import-libraries.patch"
        "002-link-to-shared-llvm.patch"
        "003-dont-prepend-dot-slash.patch")
sha256sums=('6cd36a260fd24f7bba8817d1d21a8eb29e5bebbea4241bf7ce71572cd3c8f7ba'
            '14096e7fb84cada3807980734f05024a48205f47d4e805d353203d61a0109700'
            'f9a4923816797bbc2a09192352eded2ed027c727937b5f74ae31625835777986'
            '7da178402a24a7b01cd65081b11d2094c93264abac037dd62f689ac327ab88e3')
options=(debug !strip)

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/001-find-import-libraries.patch
  patch -p1 -i "${srcdir}"/002-link-to-shared-llvm.patch
  patch -p0 -i "${srcdir}"/003-dont-prepend-dot-slash.patch
}

build() {
  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  if [[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]]; then
    _extra_config+=("-DWITH_UNWIND=ON" "-DWITH_LIBUNWIND=ON" "-DWITH_LLVM_STACKTRACE=ON")
  # else
  #   _extra_config+=("-DWITH_BFD=ON")
  #   LDFLAGS+=" -lbfd"
  fi

  #LLVM_DIR="${MINGW_PREFIX}/opt/llvm-20/lib/cmake/llvm" \
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${_extra_config[@]}" \
      -DWITH_LSP=ON \
      -DWITH_FMT=ON \
      -DWITH_JSON=ON \
      -DWITH_LLVM=ON \
      -DWITH_MLIR=OFF \
      -DWITH_STACKTRACE=OFF \
      -DWITH_ZLIB=ON \
      -DWITH_ZSTD=OFF \
      -DUSE_DYNAMIC_ZLIB=ON \
      -S ${_realname}-${pkgver} \
      -B build-${MSYSTEM}

  "${MINGW_PREFIX}"/bin/cmake.exe --build build-${MSYSTEM}
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

#  "${MINGW_PREFIX}"/bin/cmake.exe --build . --target test
}

package() {
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install build-${MSYSTEM}

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/LICENSE \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
