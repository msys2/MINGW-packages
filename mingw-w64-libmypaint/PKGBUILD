# Maintainer: The MyPaint Team <mypaintopensource@gmail.com>

_realname=libmypaint
_realver=1.6.1
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
conflicts=("${MINGW_PACKAGE_PREFIX}-mypaint<1.3.0alpha")
pkgver=${_realver/-/}
pkgrel=3
pkgdesc="Brush engine used by MyPaint (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="http://mypaint.org"
license=('spdx:ISC')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             "${MINGW_PACKAGE_PREFIX}-glib2"
             "intltool"
             "git")
# autotools are required because several Makefile.am are modified
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-json-c")
options=('strip' '!debug')
source=("https://github.com/mypaint/${_realname}/releases/download/v${_realver}/${_realname}-${_realver}.tar.xz"
        "0001-fix-autoconf.patch")
sha256sums=('741754f293f6b7668f941506da07cd7725629a793108bb31633fb6c3eae5315f'
            '0ba7acddb4962f84ea817a497f41d39dd26f3829db532c056e0705ef751fcf11')

prepare() {
  cd ${_realname}-${_realver}

  # https://github.com/mypaint/libmypaint/issues/178
  patch -p1 -i "${srcdir}/0001-fix-autoconf.patch"

  autoreconf -fiv
}

build() {
  #export GI_SCANNER_DEBUG="save-temps"
  cd "${srcdir}"
  [[ -d build-${MSYSTEM} ]] && rm -rf build-${MSYSTEM}
  cp -rf ${_realname}-${_realver} build-${MSYSTEM}
  cd build-${MSYSTEM}


  PKG_CONFIG_PATH="${PKG_CONFIG_PATH}:/usr/share/pkgconfig" \
  ./configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --enable-shared \
    --enable-static

  make
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}
  make -j1 DESTDIR="${pkgdir}" install
  install -Dm644 ${srcdir}/${_realname}-${_realver}/COPYING "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
