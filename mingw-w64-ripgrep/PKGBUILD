# Maintainer: Christopher Degawa <ccom@randomderp.com>

_realname=ripgrep
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=14.0.3
pkgrel=1
pkgdesc='line-oriented search tool that recursively searches your current directory for a regex pattern (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/BurntSushi/ripgrep'
msys2_references=(
    'archlinux: ripgrep'
)
license=('spdx:MIT OR Unlicense')
depends=("${MINGW_PACKAGE_PREFIX}-pcre2")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
source=("${url}/archive/refs/tags/${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('f5794364ddfda1e0411ab6cad6dd63abe3a6b421d658d9fee017540ea4c31a0e')
noextract=("${_realname}-${pkgver}.tar.gz")

prepare() {
  cd "${srcdir}"
  tar -xzf "${_realname}-${pkgver}.tar.gz" || true

  cd "${srcdir}/${_realname}-${pkgver}"
  cargo fetch --locked
}

build() {
  cd "${_realname}-${pkgver}"

  cargo build --release --locked --features 'pcre2'
}

check() {
  cd "${_realname}-${pkgver}"

  cargo test --release --locked --features 'pcre2'
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  cargo install \
    --frozen \
    --offline \
    --no-track \
    --features 'pcre2' \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  mkdir -p "${pkgdir}${MINGW_PREFIX}/share/zsh/site-functions"
  target/release/rg --generate complete-zsh > "${pkgdir}${MINGW_PREFIX}/share/zsh/site-functions/_rg"

  mkdir -p "${pkgdir}${MINGW_PREFIX}/share/bash-completion/completions"
  target/release/rg --generate complete-bash > "${pkgdir}${MINGW_PREFIX}/share/bash-completion/completions/rg"

  mkdir -p "${pkgdir}${MINGW_PREFIX}/share/fish/vendor_completions.d"
  target/release/rg --generate complete-fish > "${pkgdir}${MINGW_PREFIX}/share/fish/vendor_completions.d/rg.fish"

  mkdir -p "${pkgdir}${MINGW_PREFIX}/share/man/man1"
  target/release/rg --generate man >  "${pkgdir}${MINGW_PREFIX}/share/man/man1/rg.1"

  install -Dm644 COPYING LICENSE-MIT UNLICENSE -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/"
}
