# Contributor: dragon-archer <dragon-archer@outlook.com>

_realname=pypdfium2-ctypesgen
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
_pkgver=1.1.1
pkgver=${_pkgver}.r901.78edc2c0
pkgrel=1
pkgdesc='Interface generator for Python ctypes. (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/pypdfium2-team/ctypesgen'
msys2_repository_url='https://github.com/pypdfium2-team/ctypesgen'
msys2_changelog_url='https://github.com/pypdfium2-team/ctypesgen/blob/pypdfium2/docs/CHANGELOG.md'
license=('spdx:BSD-2-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools-scm"
             'git')
options=('!strip')
_commit='78edc2c0df793456ed40c058701aba6b2c295ddc'
source=("${_realname}"::"git+https://github.com/pypdfium2-team/ctypesgen.git#commit=${_commit}")
sha256sums=('9439bfd86281222d6a1a81f6c51ad1a9978d9bfd71fcf64b0924bd518eb8df94')

build() {
  cp -r "${_realname}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  export SETUPTOOLS_SCM_PRETEND_VERSION=${_pkgver}

  "${MINGW_PREFIX}"/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    "${MINGW_PREFIX}"/bin/python -m installer --prefix="${MINGW_PREFIX}" \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
