# Maintainer: Ryuta Suzuki <oroppas@gmail.com>

_realname=openpyxl
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}"
         "${MINGW_PACKAGE_PREFIX}-python2-${_realname}")
pkgver=2.5.7
pkgrel=1
pkgdesc="A python library to read/write Excel 2007 xlsx/xlsm file (mingw-w64)"
url="https://openpyxl.readthedocs.io/"
arch=('any')
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-jdcal"
             "${MINGW_PACKAGE_PREFIX}-python2-jdcal"
             "${MINGW_PACKAGE_PREFIX}-python3-et-xmlfile"
             "${MINGW_PACKAGE_PREFIX}-python2-et-xmlfile")
_dtoken="ef/2b/d61297ec61caddfe2935459e6c4600f37f74ebf4b794c4d28863958715bd"
source=("https://pypi.python.org/packages/${_dtoken}/openpyxl-${pkgver}.tar.gz"
        "LICENSE")
sha256sums=('d3da4d6a78077d6f9fb1a1ec12d4aa500f7caa4661b8528538503b24ed72d632'
            '0c1a4c0d3ea3fe274145e211f4c2e7de6f1747e3a086610c4ae70aca2ec9d50c')

prepare() {
  cd "${srcdir}"
  for builddir in python{2,3}-build-${CARCH}; do
    rm -rf ${builddir} | true
    cp -r "${_realname}-${pkgver}" "${builddir}"
  done
}

build() {
  for pver in {2,3}; do  
    msg "Python ${pver} build for ${CARCH}"  
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py build
  done  
}

package_python3-openpyxl() {
  depends=(
    "${MINGW_PACKAGE_PREFIX}-python3-jdcal"
    "${MINGW_PACKAGE_PREFIX}-python3-et-xmlfile"
  )

  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 ${srcdir}/LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE
}

package_python2-openpyxl() {
  depends=(
    "${MINGW_PACKAGE_PREFIX}-python2-jdcal"
    "${MINGW_PACKAGE_PREFIX}-python2-et-xmlfile"
  )

  cd "${srcdir}/python2-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 ${srcdir}/LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE
}

package_mingw-w64-i686-python2-openpyxl() {
  package_python2-openpyxl
}

package_mingw-w64-i686-python3-openpyxl() {
  package_python3-openpyxl
}

package_mingw-w64-x86_64-python2-openpyxl() {
  package_python2-openpyxl
}

package_mingw-w64-x86_64-python3-openpyxl() {
  package_python3-openpyxl
}

# vim:set ts=2 sw=2 et:
