# Maintainer: Mario Emmenlauer <memmenlauer@biodataanalysis.de>

_realname=vigra
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.11.0
_pkgver=${pkgver//./-}
pkgrel=1
pkgdesc="vigra - Generic Programming for Computer Vision"
arch=('any')
url='https://ukoethe.github.io/vigra/'
license=('MIT X11')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-boost"
         "${MINGW_PACKAGE_PREFIX}-fftw"
         "${MINGW_PACKAGE_PREFIX}-hdf5"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libtiff"
         "${MINGW_PACKAGE_PREFIX}-openexr"
         "${MINGW_PACKAGE_PREFIX}-python2-numpy"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-python2-nose"
             "${MINGW_PACKAGE_PREFIX}-python2-numpy"
             "${MINGW_PACKAGE_PREFIX}-python2-sphinx")
options=('staticlibs' 'strip')
source=(${_realname}-${pkgver}.tar.gz::https://github.com/ukoethe/vigra/releases/download/Version-${_pkgver}/${_realname}-${pkgver}-src.tar.gz
        0001-vigra-numpy-detection.patch
        0002-vigra-dllexport.patch
        0003-vigra-cmath-std-namespace.patch)
sha256sums=('68617de347eae7d4700a8f66cd59ce31d6cd92ffb4a235b4df34c688673af5cb'
            '14c0dac7f057eabea30e2668ce60592fe318d4bf171da3ea225f9813ca660885'
            '053216c5ada67032dc4df9ca12212d7def9400e3844b8649a4d2c0dfb74c142e'
            'b70f6f2d3873188febf406cb2bceeac45a5c5f47f14f2b481ff3a9cdb66f5d51')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -p1 -i "${srcdir}/0001-vigra-numpy-detection.patch"
  patch -p1 -i "${srcdir}/0002-vigra-dllexport.patch"
  patch -p1 -i "${srcdir}/0003-vigra-cmath-std-namespace.patch"
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  cp -rf "${_realname}-${pkgver}" "build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake . \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -DCMAKE_BUILD_TYPE="Release" \
    -DWITH_BOOST_GRAPH=ON \
    -DWITH_VIGRANUMPY=ON \
    -DWITH_OPENEXR=ON

  make ${MAKEFLAGS}
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  #make check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" -j1 install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE.txt" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.txt"
}
