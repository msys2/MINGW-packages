# Contributor : Xin Sun <sun.simpson@gmail.com>

# TODO build documentation with doxygen

_realname=vigra
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=1.10.0.r486.4510b64
pkgrel=1
arch=('any')
url="http://hci.iwr.uni-heidelberg.de/vigra/"
license=('custom:MIT')
provides=("${MINGW_PACKAGE_PREFIX}-vigra")
pkgdesc="Computer vision library"
depends=(
  "${MINGW_PACKAGE_PREFIX}-libpng"
  "${MINGW_PACKAGE_PREFIX}-libtiff"
  "${MINGW_PACKAGE_PREFIX}-openexr"
  "${MINGW_PACKAGE_PREFIX}-gcc-libs"
  "${MINGW_PACKAGE_PREFIX}-hdf5"
  "${MINGW_PACKAGE_PREFIX}-fftw"
)
optdepends=(
  "${MINGW_PACKAGE_PREFIX}-boost-c++11: for python bindings"
  "${MINGW_PACKAGE_PREFIX}-python2-numpy: for python bindings"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cmake" 'sh' 'git'
  # documentation
  "doxygen" "${MINGW_PACKAGE_PREFIX}-python2-sphinx"
  # python2 binding
  "${MINGW_PACKAGE_PREFIX}-boost-c++11" "${MINGW_PACKAGE_PREFIX}-python2-numpy"
)
source=(
  "${_realname}"::"git+https://github.com/ukoethe/vigra.git"
  "vigra-mingw-win32.patch"
  "vigra-mingw-python2.patch"
  "vigra-bessel.patch"
)
md5sums=(
  'SKIP'
  'b2eb2d78459e7349f7b0555146cb17ec'
  'd8620710999f85e37a327eac33eb050a'
  'b430dd3ffdf97c211e7e2dd563c64bc4'
)

pkgver() {
  cd "${srcdir}/${_realname}"
  v="$(git describe --tags | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
  printf "%s" "${v#Version.}"
}

prepare() {
  cd "${srcdir}/${_realname}"
  patch -p1 -i ${srcdir}/vigra-mingw-win32.patch
  patch -p1 -i ${srcdir}/vigra-mingw-python2.patch
  patch -p1 -i ${srcdir}/vigra-bessel.patch
}

build() {
  CXXFLAGS+=" -std=gnu++11 -mnop-fun-dllimport"
  cd "${srcdir}"
  [[ -d "build-${MINGW_CHOST}" ]] && rm -rf "build-${MINGW_CHOST}"
  mkdir -p "build-${MINGW_CHOST}" && cd "build-${MINGW_CHOST}"
  ${MINGW_PREFIX}/bin/cmake \
    -Wno-dev \
    -G "MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}/${MINGW_PREFIX} \
    -DPYTHON_EXECUTABLE=${MINGW_PREFIX}/bin/python2 \
    -DWITH_OPENEXR=true \
    -DWITH_VIGRANUMPY=1 \
    -DPYTHON_SPHINX=${MINGW_PREFIX}/bin/sphinx-build2 \
    -DDOCINSTALL=share/doc \
    "../${_realname}"

  make -j1     # build serially in case GCC eats up the host memory
#  make -j1 doc  # build serially in case GCC eats up the host memory
}

# compiler segfault in C++11 mode
#check() {
#  cd ${srcdir}/build-${MINGW_CHOST}
#  make -j1 -k check
#}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make install

  # license
  install -D -m644 ../${_realname}/LICENSE.txt ${pkgdir}/${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
