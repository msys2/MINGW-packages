# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=pyqt6-sip
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=13.0.1
pkgrel=1
pkgdesc="The sip module support for PyQt6 (mingw-w64)"
arch=('any')
license=('GPL')
url="https://riverbankcomputing.com/software/pyqt"
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools")
source=(https://pypi.python.org/packages/source/P/PyQt6_sip/PyQt6_sip-${pkgver}.tar.gz)
sha256sums=('310d0c0efdbc086ef913f4ecdfaf8e62f2b70cadeacb42536bfe943c19709987')

prepare() {
  cd "${srcdir}"
  rm -rf python-build-${MINGW_CHOST} | true
  cp -r "PyQt6_sip-${pkgver}" "python-build-${MINGW_CHOST}"
}

build() {
  cd python-build-${MINGW_CHOST}
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py build
}

package(){
  cd "${srcdir}"/python-build-${MINGW_CHOST}
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
}
