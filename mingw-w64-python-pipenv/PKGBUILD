# Contributor: @ifurther <55025025+ifurther@users.noreply.github.com>

_realname=pipenv
pkgbase=mingw-w64-python-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-${_realname}
pkgver=v2022.1.8
pkgrel=1
pkgdesc="Python Development Workflow for Humans."
url="https://pipenv.pypa.io"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=('MIT')
depends=(
    ${MINGW_PACKAGE_PREFIX}-python
    ${MINGW_PACKAGE_PREFIX}-python-certifi
	${MINGW_PACKAGE_PREFIX}-python-setuptools
	${MINGW_PACKAGE_PREFIX}-python-pip
    ${MINGW_PACKAGE_PREFIX}-python-virtualenv
	${MINGW_PACKAGE_PREFIX}-python-virtualenv-clone
)
makedepends=(${MINGW_PACKAGE_PREFIX}-python-setuptools)
source=(https://github.com/pypa/${_realname}/archive/refs/tags/${pkgver}.tar.gz)
sha256sums=('6bf64140bf240689a84447e376030da766afee76ecd3a082fbf2dbdb26a170d7')

prepare() {
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname}-${pkgver/v/}" "python-build-${MSYSTEM}"
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE.txt"

  _PIPENV_COMPLETE=bash_source ${pkgdir}${MINGW_PREFIX}/bin/pipenv | install -Dm644 /dev/stdin "${pkgdir}${MINGW_PREFIX}/share/bash-completion/completions/pipenv"
  _PIPENV_COMPLETE=zsh_source  ${pkgdir}${MINGW_PREFIX}/bin/pipenv | install -Dm644 /dev/stdin "${pkgdir}${MINGW_PREFIX}/share/zsh/site-functions/_pipenv"
  /usr/bin/env _PIPENV_COMPLETE=fish_source ${pkgdir}${MINGW_PREFIX}/bin/pipenv | install -Dm644 /dev/stdin "${pkgdir}${MINGW_PREFIX}/share/fish/vendor_completions.d/pipenv.fish"
  
  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*-script.py*; do
    sed -e '1 { s|^#!.*$|#!/usr/bin/env pythonw.exe|g }' -i ${_f}
  done
}
