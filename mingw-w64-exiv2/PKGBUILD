# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>
# Contributor: Miloš Komarčević <miloskomarcevic@aim.com>

_realname=exiv2
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.28.6
pkgrel=2
pkgdesc="Exif/IPTC/Xmp C++ metadata library and tools (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://exiv2.org/'
msys2_repository_url="https://github.com/Exiv2/exiv2"
msys2_references=(
  'anitya: 769'
  'archlinux: exiv2'
  "cpe: cpe:/a:exiv2:exiv2"
  "cygwin: exiv2"
  "gentoo: media-gfx/exiv2"
)
license=('spdx:GPL-2.0-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-gettext-tools"
             "${MINGW_PACKAGE_PREFIX}-python")
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-brotli"
         "${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-expat"
         "${MINGW_PACKAGE_PREFIX}-gettext-runtime"
         "${MINGW_PACKAGE_PREFIX}-libiconv"
         "${MINGW_PACKAGE_PREFIX}-libinih"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread"
         "${MINGW_PACKAGE_PREFIX}-zlib")
source=("https://github.com/Exiv2/${_realname}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "https://patch-diff.githubusercontent.com/raw/Exiv2/exiv2/pull/3378.patch")
sha256sums=('9d481117b97afa5324bf3ea0ad79f5c2939ae94ba0d0f5f8eb23aee16d9ec58d'
            '82700106d060a449f24c68cb725b3f9045f3950f8b324b038b7b5af8d969bc11')

prepare() {
  cd "${_realname}-${pkgver}"

  patch -p1 -i "${srcdir}/3378.patch"
}

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  #shared
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      "${extra_config[@]}" \
      -DBUILD_SHARED_LIBS=ON \
      -DCMAKE_DLL_NAME_WITH_SOVERSION=ON \
      -DCONAN_AUTO_INSTALL=OFF \
      -DEXIV2_ENABLE_VIDEO=ON \
      -DEXIV2_ENABLE_NLS=ON \
      -DEXIV2_BUILD_SAMPLES=OFF \
      -DEXIV2_ENABLE_WEBREADY=ON \
      -DEXIV2_ENABLE_CURL=ON \
      -DPython3_EXECUTABLE=${MINGW_PREFIX}/bin/python.exe \
      -S "${_realname}-${pkgver}" \
      -B "build-shared-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/cmake --build "build-shared-${MSYSTEM}"

  #static
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      "${extra_config[@]}" \
      -DCMAKE_CXX_FLAGS="-DCURL_STATICLIB" \
      -DCMAKE_C_FLAGS="-DCURL_STATICLIB" \
      -DBUILD_SHARED_LIBS=OFF \
      -DCONAN_AUTO_INSTALL=OFF \
      -DEXIV2_ENABLE_VIDEO=ON \
      -DEXIV2_ENABLE_NLS=ON \
      -DEXIV2_BUILD_SAMPLES=OFF \
      -DEXIV2_ENABLE_WEBREADY=ON \
      -DEXIV2_ENABLE_CURL=ON \
      -DPython3_EXECUTABLE=${MINGW_PREFIX}/bin/python.exe \
      -S "${_realname}-${pkgver}" \
      -B "build-static-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/cmake --build "build-static-${MSYSTEM}"
}

package() {
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install "build-static-${MSYSTEM}"

  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install "build-shared-${MSYSTEM}"

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/COPYING "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
