# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=xerces-c
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.1.4
pkgrel=2
pkgdesc="A validating XML parser written in a portable subset of C++ (mingw-w64)"
arch=('any')
url="https://xerces.apache.org/xerces-c"
license=("APACHE")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" "${MINGW_PACKAGE_PREFIX}-icu") # "${MINGW_PACKAGE_PREFIX}-curl"
options=('staticlibs' 'strip')
source=("https://archive.apache.org/dist/xerces/c/3/sources/xerces-c-${pkgver}.tar.gz"
        001-no-undefined.patch)
sha256sums=('c98eedac4cf8a73b09366ad349cb3ef30640e7a3089d360d40a3dde93f66ecf6'
            'c2969902e9e7053ae1d8adbec86872baf87f2fc57bcd712d5550a0fcc3b45dd4')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-no-undefined.patch

  autoreconf -fiv
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  "${srcdir}"/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
