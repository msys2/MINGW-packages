# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=xerces-c
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=3.1.1
pkgrel=2
pkgdesc="A validating XML parser written in a portable subset of C++. (mingw-w64)"
arch=('any')
url="http://xerces.apache.org/xerces-c"
license=("APACHE")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config")
depends=("${_mingw_suff}-gcc-libs") # "${_mingw_suff}-curl" "${_mingw_suff}-icu"
options=('staticlibs' 'strip')
source=("http://mirrors.sonic.net/apache/xerces/c/3/sources/xerces-c-$pkgver.tar.gz")
md5sums=('6a8ec45d83c8cfb1584c5a5345cb51ae')

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  "${srcdir}"/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}
  make libxerces_c_la_LDFLAGS="-release 3.1 -no-undefined"
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install
  find "${pkgdir}${MINGW_PREFIX}" -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
}
