# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=xerces-c
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.1.2
pkgrel=2
pkgdesc="A validating XML parser written in a portable subset of C++ (mingw-w64)"
arch=('any')
url="https://xerces.apache.org/xerces-c"
license=("APACHE")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs") # "${MINGW_PACKAGE_PREFIX}-curl" "${MINGW_PACKAGE_PREFIX}-icu"
options=('staticlibs' 'strip')
source=("https://archive.apache.org/dist/xerces/c/3/sources/xerces-c-${pkgver}.tar.gz"
        001-no-undefined.patch)
sha256sums=('743bd0a029bf8de56a587c270d97031e0099fe2b7142cef03e0da16e282655a0'
            'c2969902e9e7053ae1d8adbec86872baf87f2fc57bcd712d5550a0fcc3b45dd4')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-no-undefined.patch

  autoreconf -fiv
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  "${srcdir}"/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
