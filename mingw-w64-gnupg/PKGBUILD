# Maintainer: Alexey Pavlov <alexpux@gmail.com>

# uncomment patch fix_seat_check_on_windows.patch to compile on windows
# requires unix2dos (installed using pacman -S dos2unix)

_realname=gnupg
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.4.18
pkgrel=1
pkgdesc="GNU Privacy Guard - a PGP replacement tool (mingw-w64)"
arch=('any')
groups=("${MINGW_PACKAGE_PREFIX}")
depends=("${MINGW_PACKAGE_PREFIX}-curl"
        "${MINGW_PACKAGE_PREFIX}-bzip2"
        "${MINGW_PACKAGE_PREFIX}-zlib"
        "${MINGW_PACKAGE_PREFIX}-libksba"
        "${MINGW_PACKAGE_PREFIX}-libgcrypt"
        "${MINGW_PACKAGE_PREFIX}-libassuan" 
        "${MINGW_PACKAGE_PREFIX}-libgnurx"
        "${MINGW_PACKAGE_PREFIX}-w32pth")
makedepends=("texinfo" "${MINGW_PACKAGE_PREFIX}-gcc")
license=('GPL')
url="http://www.gnupg.org/"
source=(ftp://ftp.gnupg.org/gcrypt/gnupg/gnupg-$pkgver.tar.bz2{,.sig}
        gnupg-1.4.15-libiconv-dll.patch
        fix_seat_check_on_windows.patch)
sha1sums=('41462d1a97f91abc16a0031b5deadc3095ce88ae'
          'SKIP'
          '2ea46a1c24a30016e21b9f16e324798ec2b54d98'
          'a8eb0dec2ca2d23d9fcb4efba9b09ef6724cf053')

prepare() {
  cd "${srcdir}"/gnupg-${pkgver}
  patch -p1 -i ${srcdir}/gnupg-1.4.15-libiconv-dll.patch
#  patch -p1 -i ../fix_seat_check_on_windows.patch
  
  #./autogen.sh
  # libtoolize --copy --force
  # aclocal -I m4 -I gl/m4
  # automake --add-missing
  # autoconf
}

build() {
  mkdir -p "${srcdir}/gnupg-${pkgver}-build-${CARCH}"
  cd "${srcdir}/gnupg-${pkgver}-build-${CARCH}"
  "${srcdir}"/gnupg-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST} \
      --libexecdir=${MINGW_PREFIX}/lib
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
  make check
}

package() {
  cd "${srcdir}/gnupg-${pkgver}-build-${CARCH}"
  make DESTDIR=${pkgdir} install
  # move conflicting files
  #mv ${pkgdir}${MINGW_PREFIX}/share/gnupg{,2}
}
