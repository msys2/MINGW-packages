# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=jasper
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.900.1
pkgrel=4
pkgdesc="A software-based implementation of the codec specified in the emerging JPEG-2000 Part-1 standard (mingw-w64)"
arch=('any')
url="http://www.ece.uvic.ca/~mdadams/jasper"
license=("custom:JasPer2.0")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config" "dos2unix")
depends=("${MINGW_PACKAGE_PREFIX}-libjpeg-turbo")
options=('staticlibs' 'strip' '!libtool')
source=("http://www.ece.uvic.ca/~mdadams/jasper/software/jasper-${pkgver}.zip"
        patch-libjasper-stepsizes-overflow.diff
        jpc_dec.c.patch
        jasper-1.900.1-CVE-2008-3520.patch
        jasper-1.900.1-CVE-2008-3522.patch
        jasper-pkgconfig.patch
        jasper-1.900.1-CVE-2011-4516-CVE-2011-4517-CERT-VU-887409.patch
        jasper-CVE-2014-9029.patch
        jasper-CVE-2014-8137.patch
        jasper-CVE-2014-8138.patch
        jasper-CVE-2014-8157.patch
        jasper-CVE-2014-8158.patch
        jasper-1.900.1-Coverity-BAD_SIZEOF.patch
        jasper-1.900.1-Coverity-CHECKED_RETURN.patch
        jasper-1.900.1-Coverity-FORWARD_NULL.patch
        jasper-1.900.1-Coverity-NULL_RETURNS.patch
        jasper-1.900.1-Coverity-RESOURCE_LEAK.patch
        jasper-1.900.1-Coverity-UNREACHABLE.patch
        jasper-1.900.1-Coverity-UNUSED_VALUE.patch
        jasper-1.900.1-mingw32.patch
        jasper-1.900.1-enable-shared.patch
        jasper-1.900.1-sleep.patch)
sha1sums=('9c5735f773922e580bf98c7c7dfda9bbed4c5191'
          'f298566fef08c8a589d072582112cd51c72c3983'
          'c1a0176a15210c0af14d85e55ce566921957d780'
          '2483dba925670bf29f531d85d73c4e5ada513b01'
          '0e7b6142cd9240ffb15a1ed7297c43c76fa09ee4'
          '2cd4bf95286ec1f8757023f216ba7b839b4ee923'
          '06a60d1ee02a2c20a93ba1fb8a04bc3d868216a6'
          'ecae5348b05e0fb304600dc4fecb834208c4c235'
          'bee436561698a339c680e6654252b8bf18643318'
          '6086e717af2f0a026f70e399e28fe115f08a8cc1'
          'aaf96946073d2ece35f3695e8cc7956b5cad9a1d'
          'e69b339de43d1dc2fbb98368cee3d20f76d35941'
          '184279e0684159462419c4f1dcf67b99e6842bb9'
          'c68961c9071a772cf1cbfec6effa961352e8bb52'
          '21d312eb4625050f39cfad0431b47ce225e3c536'
          '4f5bc89491670360bebf3b9d1b21a14c2b2f9faf'
          '643519f88b7293f2ed20efa8b6222cfe0151708f'
          'f8f7515303107d67f0e86bf42b26b458bfda1701'
          '551c5f809d8e9adb16819d73e636a01cae65c8d7'
          '2d64229bf71633ec7e1ba0c1d5116dda1fa72468'
          '54940e11d1b04964a0ed9329a8e1ffbc290353ad'
          'd009ecd9d1d0212bcedcf28f483db77ddd3bb1c7')

prepare() {
  cd "$srcdir/jasper-$pkgver"
  find . -type f -exec dos2unix {} \;

  patch -p1 -i "${srcdir}"/patch-libjasper-stepsizes-overflow.diff
  patch -p1 -i "${srcdir}"/jpc_dec.c.patch
  patch -p1 -i "${srcdir}"/jasper-1.900.1-CVE-2008-3520.patch
  patch -p1 -i "${srcdir}"/jasper-1.900.1-CVE-2008-3522.patch
  patch -p1 -i "${srcdir}"/jasper-pkgconfig.patch
  patch -p1 -i "${srcdir}"/jasper-1.900.1-CVE-2011-4516-CVE-2011-4517-CERT-VU-887409.patch
  patch -p1 -i "${srcdir}"/jasper-CVE-2014-9029.patch
  patch -p1 -i "${srcdir}"/jasper-CVE-2014-8137.patch
  patch -p1 -i "${srcdir}"/jasper-CVE-2014-8138.patch
  patch -p1 -i "${srcdir}"/jasper-CVE-2014-8157.patch
  patch -p1 -i "${srcdir}"/jasper-CVE-2014-8158.patch
  patch -p1 -i "${srcdir}"/jasper-1.900.1-Coverity-BAD_SIZEOF.patch
  patch -p1 -i "${srcdir}"/jasper-1.900.1-Coverity-CHECKED_RETURN.patch
  patch -p1 -i "${srcdir}"/jasper-1.900.1-Coverity-FORWARD_NULL.patch
  patch -p1 -i "${srcdir}"/jasper-1.900.1-Coverity-NULL_RETURNS.patch
  patch -p1 -i "${srcdir}"/jasper-1.900.1-Coverity-RESOURCE_LEAK.patch
  patch -p1 -i "${srcdir}"/jasper-1.900.1-Coverity-UNREACHABLE.patch
  patch -p1 -i "${srcdir}"/jasper-1.900.1-Coverity-UNUSED_VALUE.patch
  patch -p1 -i "${srcdir}"/jasper-1.900.1-mingw32.patch
  patch -p1 -i "${srcdir}"/jasper-1.900.1-enable-shared.patch
  patch -p1 -i "${srcdir}"/jasper-1.900.1-sleep.patch

  #libtoolize --copy --force
  #aclocal
  #automake --add-missing
  #autoconf

  autoreconf -fiv
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --enable-static \
    --disable-opengl

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
