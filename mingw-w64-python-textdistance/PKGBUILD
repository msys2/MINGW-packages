# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=textdistance
pkgbase=mingw-w64-python-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-${_realname}
pkgver=4.6.0
pkgrel=1
pkgdesc='Compute distance between sequences (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64' 'clang32')
license=('MIT')
url='https://github.com/life4/textdistance'
#depends=(
#    ${MINGW_PACKAGE_PREFIX}-python-pywin32-ctypes)
makedepends=(
    ${MINGW_PACKAGE_PREFIX}-python
    ${MINGW_PACKAGE_PREFIX}-python-setuptools)
source=("https://github.com/life4/${_realname}/archive/${pkgver}.tar.gz")
sha256sums=('8da297cafe0c0a99ed859ef80c6328b9235a365832c8bc17c6887d48998bee6f')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  sed -i "s|4.2.2|4.3.0|g" setup.py

  cd "${srcdir}"
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}"
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
