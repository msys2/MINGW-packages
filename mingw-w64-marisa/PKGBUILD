# Maintainer: Feng Shu <tumashu@163.com>

_realname=marisa
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.2.7
pkgrel=1
pkgdesc="Matching Algorithm with Recursively Implemented StorAge (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/s-yata/marisa-trie"
msys2_repository_url='https://github.com/s-yata/marisa-trie'
license=('spdx:BSD-2-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools")
source=("https://github.com/s-yata/marisa-trie/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
       "001-tools-makefile.patch")
sha256sums=('d4e0097d3a78e2799dfc55c73420d1a43797a2986a4105facfe9a33f4b0ba3c2'
            '7b0c45a90457fab8d6db68a9cc53c2edcc9d08f30f363ec25b7380587ebbaaa1')

prepare() {
  cd "marisa-trie-${pkgver}"
  patch -p0 -i "${srcdir}"/001-tools-makefile.patch
  autoreconf -fiv
}

build() {
  mkdir build-${MSYSTEM} && cd build-${MSYSTEM}

  local -a _extra_config
  if [[ ${CARCH} == x86_64 ]]; then
    _extra_config+=("--enable-sse3")
  fi
 
  ../marisa-trie-${pkgver}/configure \
    --enable-shared \
    "${_extra_config[@]}"

  make
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/marisa-trie-${pkgver}/COPYING.md \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
