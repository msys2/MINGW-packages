_realname=pragtical
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=3.3.0
pkgrel=1
pkgdesc='The practical and pragmatic code editor. (mingw-w64)'
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'archlinux: pragtical'
)
msys2_repository_url='https://github.com/pragtical/pragtical'
url='https://pragtical.dev/'
license=('spdx:MIT')
depends=(
  "${MINGW_PACKAGE_PREFIX}-freetype"
  "${MINGW_PACKAGE_PREFIX}-lua"
  "${MINGW_PACKAGE_PREFIX}-pcre2"
  "${MINGW_PACKAGE_PREFIX}-SDL2"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-meson"
  "${MINGW_PACKAGE_PREFIX}-ninja"
  "${MINGW_PACKAGE_PREFIX}-pkgconf"
)
source=("https://github.com/pragtical/pragtical/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "0001-pragtical-meson-fix-install.patch")
sha256sums=('5dcde10164ff769aa5c6eb8a91d517421f0c70e3ac87f2c6ed536b6cb6b509da'
            '2e3da169123c34e059d78e63557f5c6e0e89b1e3fd377b3af69a6e6c44928b82')

prepare() {
  cd "${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-pragtical-meson-fix-install.patch"
}

build() {
  MSYS2_ARG_CONV_EXCL="--prefix=" \
    "${MINGW_PREFIX}"/bin/meson.exe setup \
      --prefix="${MINGW_PREFIX}" \
      --wrap-mode=nodownload \
      --auto-features=enabled \
      --buildtype=plain \
      -Duse_system_lua=true \
      ${_realname}-${pkgver} \
      build-${MSYSTEM}

  "${MINGW_PREFIX}"/bin/meson.exe compile -C build-${MSYSTEM}
}

package() {
  "${MINGW_PREFIX}"/bin/meson.exe install -C build-${MSYSTEM} --destdir "${pkgdir}"
}
