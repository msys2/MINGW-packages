# Maintainer: Philipp Smirnov <psmirnov@airmail.cc>

_realname=gnatstudio
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
_gh_release=20230501
pkgver=24.0.r44097.ac9958c
pkgrel=1
_branch=24.0

pkgdesc="GNAT Studio is a powerful and lightweight IDE for Ada and SPARK (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
license=('GPL3')
url="https://github.com/AdaCore/gnatstudio"
makedepends=("${MINGW_PACKAGE_PREFIX}-gprbuild"
             "git")
depends=("${MINGW_PACKAGE_PREFIX}-gtkada"
         "${MINGW_PACKAGE_PREFIX}-gnatcoll-db2ada"
         # "${MINGW_PACKAGE_PREFIX}-gnatcoll-python3" # TODO: Now part of gnatcoll-bindings, may be separate
         "${MINGW_PACKAGE_PREFIX}-python-gobject"
         "${MINGW_PACKAGE_PREFIX}-python-pynput"
         )
# source=("${_realname}-cr-${_gh_release}.tar.gz::https://github.com/AdaCore/gnatstudio/archive/gnatstudio-cr-${_gh_release}.tar.gz")
source=("${_realname}-git::git+https://github.com/AdaCore/gnatstudio.git#branch=${_branch}"
        "001-Use-system-VSS-library-in-DAP-project.patch"
        "002-Honour-DESTDIR-in-installation-targets.patch"
        "003-Fix-collections.abc.MutableMapping-import-in-gs_utils-for-Python-3.10.patch")
sha256sums=('SKIP'
            'b4905ea6da11166a17bc21e3e91b31321eb42904cc4972231aab9305eadef2fe'
            '96ce63244806cf266d61a0794a7253a136a0d7cbf3184114b630e08ad313444d'
            '2cef5855fa26cf8fc368bc77c7de65e50cbe89247c45fe2a09e55f5d2881c77b')
# noextract=("${_realname}-cr-${_gh_release}.tar.gz")

pkgver() {
  cd ${_realname}-git
  printf "%s.r%s.%s" "$_branch" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

# prepare() {
#   # [[ -d "${srcdir}/gnatstudio-${_realname}-cr-${_gh_release}/" ]] && rm -rf "${srcdir}/gnatstudio-${_realname}-cr-${_gh_release}/"

#   # export MSYS=winsymlinks:lnk
#   # tar zxf "${_realname}-cr-${_gh_release}.tar.gz" || true

#   # cd "${srcdir}/gnatstudio-${_realname}-cr-${_gh_release}/"
# }

prepare() {
  cd "${srcdir}/${_realname}-git"
  patch -p0 -i "${srcdir}/001-Use-system-VSS-library-in-DAP-project.patch"
  patch -p0 -i "${srcdir}/002-Honour-DESTDIR-in-installation-targets.patch"
  patch -p0 -i "${srcdir}/003-Fix-collections.abc.MutableMapping-import-in-gs_utils-for-Python-3.10.patch"
}

build() {
  cd "${srcdir}/${_realname}-git"
  echo "${pkgver}" > "${srcdir}/${_realname}-git/VERSION.txt"
  LANG=C date "+%Y%m%d" > "${srcdir}/${_realname}-git/DATE.txt"
  ./configure --prefix="${MINGW_PREFIX}" --with-python=${MINGW_PREFIX}/bin/python
  make PROCESSORS=0 BUILD=Production PRJ_BUILD=Release LIBRARY_TYPE=relocatable
}

package() {
  cd "${srcdir}/${_realname}-git"
  make DESTDIR="${pkgdir}" install

  # Copy License Files
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/" COPYING*
}
