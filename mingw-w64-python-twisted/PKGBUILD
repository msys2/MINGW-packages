# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_pyname=Twisted
_realname=twisted
pkgbase=mingw-w64-python-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-${_realname}
pkgver=23.8.0
pkgrel=1
pkgdesc='An asynchronous networking framework written in Python (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64' 'clang32')
url='https://twistedmatrix.com'
license=('spdx:MIT')
depends=(
    ${MINGW_PACKAGE_PREFIX}-python
    ${MINGW_PACKAGE_PREFIX}-python-attrs
    ${MINGW_PACKAGE_PREFIX}-python-automat
    ${MINGW_PACKAGE_PREFIX}-python-constantly
    ${MINGW_PACKAGE_PREFIX}-python-hyperlink
    ${MINGW_PACKAGE_PREFIX}-python-incremental
    ${MINGW_PACKAGE_PREFIX}-python-twisted_iocpsupport
    ${MINGW_PACKAGE_PREFIX}-python-typing_extensions
    ${MINGW_PACKAGE_PREFIX}-python-zope.interface
)
makedepends=(${MINGW_PACKAGE_PREFIX}-python-build
             ${MINGW_PACKAGE_PREFIX}-python-installer
             ${MINGW_PACKAGE_PREFIX}-python-hatchling
             ${MINGW_PACKAGE_PREFIX}-python-hatch-fancy-pypi-readme)
options=(!strip)
source=("https://pypi.org/packages/source/${_pyname::1}/${_pyname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('3c73360add17336a622c0d811c2a2ce29866b6e59b1125fd6509b17252098a24')

prepare() {
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}"
}

build() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
