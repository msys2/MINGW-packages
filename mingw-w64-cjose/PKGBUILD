# Maintainer: Diego Sogari <diego.sogari@gmail.com>

_realname=cjose
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.6.2.2
pkgrel=1
pkgdesc="C library implementing the Javascript Object Signing and Encryption (JOSE) (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/OpenIDC/cjose"
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-check")
depends=("${MINGW_PACKAGE_PREFIX}-jansson"
         "${MINGW_PACKAGE_PREFIX}-openssl")
source=("https://github.com/OpenIDC/cjose/releases/download/v${pkgver}/cjose-${pkgver}.tar.gz"
        "001-cjose-0.6.1-fix-mingw-build.patch")
sha256sums=('2de8652b23ee3e15398176e8b2aaa513aa89635368f2748fbc304f8aefe910a4'
            'd43f20f5a2bd23ede21823fd1e5115f2247f8dcfaddd7092f8f0204d411db335')
noextract=("${_realname}-${pkgver}.tar.gz")

prepare() {
  cd "${srcdir}"
  rm -rf "${_realname}-${pkgver}"
  tar -xzvf "${_realname}-${pkgver}.tar.gz" --exclude lib

  cd "${_realname}-${pkgver}"
  patch -Np1 -i "${srcdir}/001-cjose-0.6.1-fix-mingw-build.patch"
  autoreconf --force --install
  sed -e "s|-Werror||g" -i src/Makefile*
}

build() {
  cp -r ${_realname}-${pkgver} build-${MSYSTEM} && cd build-${MSYSTEM}

  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --with-openssl=${MINGW_PREFIX} \
    --with-jansson=${MINGW_PREFIX}

  make
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"
  make test
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
}
