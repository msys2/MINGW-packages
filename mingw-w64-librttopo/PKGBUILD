# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.su>

_realname=librttopo
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.1.0
pkgrel=2
pkgdesc="RT Topology Library (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://gitea.osgeo.org/rttopo/librttopo"
msys2_repository_url='https://gitlab.com/rttopo/rttopo'
license=('spdx:GPL-2.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-geos")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools")
source=("https://gitlab.com/rttopo/rttopo/-/archive/librttopo-${pkgver}/rttopo-librttopo-${pkgver}.tar.bz2")
sha256sums=('4b28732c0322849c8754751a384ee3596d06ab316dfc57fe9bbe757c82a27efe')

prepare() {
  cd "${srcdir}/rttopo-${_realname}-${pkgver}"

  ./autogen.sh
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  ../rttopo-${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \

  make
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  make check
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/rttopo-${_realname}-${pkgver}/COPYING \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
