# Maintainer: Ray Donnelly <mingw.android@gmail.com>
# Maintainer: Joan Karadimov <joan.karadimov@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>
# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname='pywin32'
pkgbase=mingw-w64-python-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-${_realname}
url='https://sourceforge.net/projects/pywin32'
pkgdesc='Python for Windows Extensions (mingw-w64)'
license=('PSF')
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang32' 'clang64' 'clangarm64')
pkgver=301
pkgrel=3
makedepends=(
    ${MINGW_PACKAGE_PREFIX}-python
    ${MINGW_PACKAGE_PREFIX}-python-setuptools
    rsync)
source=("https://github.com/mhammond/${_realname}/archive/b${pkgver}.zip"
        001-compile-and-setup-fixes.patch
        002-com-extensions-fix.patch
        003-win32-extensions-fix.patch
        004-isapi-fix.patch
        005-swig-fix.patch
        006-mapi-headers-fix.patch
        010-workaround-broken-add-dll-dir.patch)
sha256sums=('b55811d60112aea58c58bbed2ffd92f7be6727cb2400764755c7e6cd9789d210'
            'ad391a53adde1f403e1305bd07fbc24beeb4ee7042af1998cf07e33e1199cfed'
            '23c990db894735cff4f102c028083f0176f4ecafd46dac5c6b1a886a2ce1beef'
            'bd25e7446e02b4e165a499f01dcc233efcebf9d1c1d95d4b23b87bf5d2865b67'
            'd1f07a91cae14e7554ff1cb6b0116cdc8264d08af9bc7e4b14629dd40cc93bde'
            '076cc45da1e5c44ee5530e72766a6aeac222b3db4117043761de5f1ac18f8aeb'
            'a04e4c8ee86606188c55349c4be755c72a9f03830d6f24828aba6bfa251e030d'
            '0168b0ea893cd252201dbde6baad6cfa4b382bc76ba65657606b6c76ce78262f')

prepare() {
    cd "${srcdir}/${_realname}-b${pkgver}"
    patch -p1 < "${startdir}"/001-compile-and-setup-fixes.patch
    patch -p1 < "${startdir}"/002-com-extensions-fix.patch
    patch -p1 < "${startdir}"/003-win32-extensions-fix.patch
    patch -p1 < "${startdir}"/004-isapi-fix.patch
    patch -p1 < "${startdir}"/005-swig-fix.patch
    patch -p1 < "${startdir}"/006-mapi-headers-fix.patch

    # FIXME: https://github.com/msys2-contrib/cpython-mingw/issues/48
    patch -p1 < "${startdir}"/010-workaround-broken-add-dll-dir.patch
}

build() {
    msg2 "Synchronizing build directory"
    rsync --recursive --times "${srcdir}/${_realname}-b${pkgver}"/* "${srcdir}/build-python-${CARCH}-${_realname}-${pkgver}"
    cd "${srcdir}/build-python-${CARCH}-${_realname}-${pkgver}"

    LIB="${MINGW_PREFIX}/lib:${MINGW_PREFIX}/${CARCH}-w64-mingw32/lib" \
    INCLUDE="${MINGW_PREFIX}/include:${MINGW_PREFIX}/${CARCH}-w64-mingw32/include" \
    ${MINGW_PREFIX}/bin/python setup.py build --skip-verstamp
}

package() {
    cd "${srcdir}/build-python-${CARCH}-${_realname}-${pkgver}"
    LIB="${MINGW_PREFIX}/lib:${MINGW_PREFIX}/${CARCH}-w64-mingw32/lib" \
    DISTUTILS_DEBUG=1 \
    MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python setup.py install \
        --prefix=${MINGW_PREFIX} \
        --root="${pkgdir}" \
        --optimize=1
}
