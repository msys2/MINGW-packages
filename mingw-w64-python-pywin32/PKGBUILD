# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=pywin32
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}"
         "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=219
pkgrel=1
pkgdesc='Python for Windows Extensions (mingw-w64)'
groups=("${MINGW_PACKAGE_PREFIX}")
arch=('any')
url='https://sourceforge.net/projects/pywin32'
license=('PSF')
source=("http://prdownloads.sourceforge.net/${_realname}/${_realname}-${pkgver}.zip"
        "0001-MinGW-w64-Import-distutils-mingwcompiler.patch")
sha1sums=('1eb8039f9e15f53f56a9e1b54f00cd2c0f63ff8e'
          '0b3883b57a9d2575c98629239ddb87e53b3f35d5')

prepare() {
  pushd $_realname-$pkgver
    patch -p1 -i "${srcdir}"/0001-MinGW-w64-Import-distutils-mingwcompiler.patch
  popd
  [ -d $_realname-py2-$pkgver-${CARCH} ] && rm -rf $_realname-py2-$pkgver-${CARCH}
  cp -a $_realname-$pkgver $_realname-py2-$pkgver-${CARCH}
  [ -d $_realname-$pkgver-${CARCH} ] && rm -rf $_realname-$pkgver-${CARCH}
  mv $_realname-$pkgver $_realname-$pkgver-${CARCH}
}

# check() {
#   cd "${srcdir}"/build-${CARCH}
#   make check
# }

package2() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools")
  cd $_realname-py2-$pkgver-${CARCH}

  DISTUTILS_DEBUG=1 \
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
                                --root="${pkgdir}" --optimize=1
}

package3() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools")
  cd $_realname-$pkgver-${CARCH}

  DISTUTILS_DEBUG=1 \
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup3.py install --prefix=${MINGW_PREFIX} \
                                --root="${pkgdir}" --optimize=1
}

package_mingw-w64-x86_64-python2-pywin32() {
  package2
}

package_mingw-w64-i686-python2-pywin32() {
  package2
}

package_mingw-w64-x86_64-python3-pywin32() {
  package3
}

package_mingw-w64-i686-python3-pywin32() {
  package3
}
