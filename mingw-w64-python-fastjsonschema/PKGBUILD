# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=fastjsonschema
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=2.15.3
pkgrel=1
pkgdesc="Fast JSON schema validator for Python (mingw-w64)"
arch=('any')
url="https://github.com/horejsek/python-fastjsonschema"
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=(BSD)
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools")
source=("python-${_realname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('94e59a36c0efbf75a1a790fed15b51c9876b81204a5184c0c89075c68b10e7ec')

build() {
  cd "${srcdir}"

  rm -rf "python-build-${MSYSTEM}" | true
  cp -r "python-${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  "${MINGW_PREFIX}"/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    "${MINGW_PREFIX}"/bin/python setup.py install --prefix="${MINGW_PREFIX}" \
      --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 README.rst -t "${pkgdir}${MINGW_PREFIX}/usr/share/doc/python-${_realname}/"
  install -Dm644 LICENSE -t "${pkgdir}${MINGW_PREFIX}/usr/share/licenses/python-${_realname}/"
}

