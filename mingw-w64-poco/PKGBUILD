# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=poco
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.6.0
pkgrel=2
pkgdesc="POrtable COmponents C++ Libraries (mingw-w64)"
arch=('any')
url="http://pocoproject.org"
license=("boost")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-expat"
         "${MINGW_PACKAGE_PREFIX}-libmariadbclient"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-pcre"
         "${MINGW_PACKAGE_PREFIX}-sqlite3"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' 'strip')
source=(http://pocoproject.org/releases/${_realname}-${pkgver}/${_realname}-${pkgver}-all.tar.gz
        001-WIN32-defines-instead-hardcoded-values.patch
        002-enable-C++11-if-compiler-supports-it.patch
        003-check-for-WIN64-and-enable-LLP64-schema.patch
        004-format-string-using-ostringstream.patch
        005-dont-compare-signed-unsigned.patch
        006-include-some-Win32-libs-to-compile.patch
        007-MinGW-doesnt-implement-wide-character-startup-module.patch
        008-Add-Mingw32-W64-configuration.patch
        009-Fix-compilation-on-WIN64-target.patch
        010-remove-warnings.patch
        011-cmake-mingw.patch
        012-find-mysql.patch
        013-POCO_WIN32_UTF8-redefinition.patch
        100-fix-mingw-config.patch)
md5sums=('4fed893d4ca57db98b0e10d82545232a'
         'dc6b7d75f579fa97b28ab6c07e62517e'
         'd57c5432708b13f5d1ccbd5c67a49996'
         '3b5dd43a3d6be5f6d2612d4f850d3b63'
         '221473673ab508c5ada2725e4a1fe466'
         '5be1be3d1e5b8b72c7efdc09aa1ceff7'
         'f5847e1ff54fba6ff87eb3620a8ed717'
         '6a821451f0ae40db2801becce3c67bdf'
         'c5fa0bacfade712a5c5cf706f3c99ee4'
         'f3aa357f69168abb2919ddb912664c1e'
         'cb90582b6bbae7b1231ad5cdd290eeb0'
         '840e76174514740463379ddb03611523'
         '50b56ac82b87697cf03f966c198cb6fd'
         '5c8fe24b1fc4a5f9c357eb65b35a49f6'
         'cee49c612b14edb3ae57d0e0b9329f3c')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}-all
  patch -p1 -i ${srcdir}/001-WIN32-defines-instead-hardcoded-values.patch
  patch -p1 -i ${srcdir}/002-enable-C++11-if-compiler-supports-it.patch
  patch -p1 -i ${srcdir}/003-check-for-WIN64-and-enable-LLP64-schema.patch
  patch -p1 -i ${srcdir}/004-format-string-using-ostringstream.patch
  patch -p1 -i ${srcdir}/005-dont-compare-signed-unsigned.patch
  patch -p1 -i ${srcdir}/006-include-some-Win32-libs-to-compile.patch
  patch -p1 -i ${srcdir}/007-MinGW-doesnt-implement-wide-character-startup-module.patch
  patch -p1 -i ${srcdir}/008-Add-Mingw32-W64-configuration.patch
  patch -p1 -i ${srcdir}/009-Fix-compilation-on-WIN64-target.patch
  patch -p1 -i ${srcdir}/010-remove-warnings.patch
  patch -p1 -i ${srcdir}/011-cmake-mingw.patch
  patch -p1 -i ${srcdir}/012-find-mysql.patch
  patch -p1 -i ${srcdir}/013-POCO_WIN32_UTF8-redefinition.patch
  patch -p1 -i ${srcdir}/100-fix-mingw-config.patch
}

build() {
  local _config=
  case "${CARCH}" in
    i686)
      _config=MinGW
    ;;

    x86_64)
      _config=MinGW32-W64
    ;;
  esac

  CXXFLAGS+=" -std=c++11"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DPOCO_UNBUNDLED=ON \
    -DENABLE_MONGODB=OFF \
    ../${_realname}-${pkgver}-all

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make destdir=${pkgdir} install
}
