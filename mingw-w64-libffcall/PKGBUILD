# Maintainer: Peter Budai <peterbudai at hotmail.com>

_realname=libffcall
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.0
pkgrel=1
pkgdesc="C library for implementing foreign function calls in embedded interpreters (mingw-w64)"
arch=('any')
url="http://www.gnu.org/software/libffcall/"
license=('GPL2')
source=("https://ftp.gnu.org/gnu/libffcall/${_realname}-${pkgver}.tar.gz")
sha256sums=('41aad2b449d90a5308e72267829db006dc9f9cc8736807082e1f2daef680196c')

prepare() {
  cd $srcdir/${_realname}-${pkgver}

}

build() {
  cd "$srcdir"/${_realname}-${pkgver}
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --with-pic \
    --enable-silent-rules

  make -j1
}

check() {
  cd "${srcdir}"/build-${CARCH}
  make check
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install DESTDIR="${pkgdir}"

  # Move HTMl docs to the doc folder
  cd "${pkgdir}"
  mkdir -p ${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}
  mv ${pkgdir}${MINGW_PREFIX}/share/html/* ${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/
  rm -rf ${pkgdir}${MINGW_PREFIX}/share/html
}
