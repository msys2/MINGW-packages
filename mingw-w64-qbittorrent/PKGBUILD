# Contributor: Chocobo1 <https://github.com/Chocobo1>

_realname=qbittorrent
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=4.0.2
pkgrel=1
pkgdesc="An advanced BitTorrent client programmed in C++, based on Qt toolkit and libtorrent-rasterbar. (mingw-w64)"
arch=('any')
url="https://qbittorrent.org/"
license=('custom' 'GPL')
depends=("${MINGW_PACKAGE_PREFIX}-qt5"
         "${MINGW_PACKAGE_PREFIX}-libtorrent-rasterbar")
optdepends=("${MINGW_PACKAGE_PREFIX}-python3: needed for torrent search tab")
source=(https://downloads.sourceforge.net/project/qbittorrent/qbittorrent/${_realname}-${pkgver}/${_realname}-${pkgver}.tar.xz{,.asc})
sha256sums=('09240274036be42328131602cd69e12bac909031b0b827c1168ae305d67bbac3'
            'SKIP')
validpgpkeys=('D8F3DA77AAC6741053599C136E4A2D025B7CC9A2')


prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # prepare env for mingw
  sed -i 's/unix:!macx:/unix|win32-g++:/g' "src/src.pro"
  sed -i 's/!haiku/#!haiku/g' "unixconf.pri"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ./configure --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} --host=${MINGW_CHOST} --target=${MINGW_CHOST} \
    --with-boost-system=boost_system-mt
  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  make INSTALL_ROOT=${pkgdir} install
  install -Dm644 "COPYING" "${pkgdir}${MINGW_PREFIX}/usr/share/licenses/${_realname}/COPYING"
}
