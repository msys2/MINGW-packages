# Contributor: Chocobo1 <https://github.com/Chocobo1>

_realname=qbittorrent
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=4.1.2
pkgrel=1
pkgdesc="An advanced BitTorrent client programmed in C++, based on Qt toolkit and libtorrent-rasterbar (mingw-w64)"
arch=('any')
url="https://qbittorrent.org/"
license=('custom' 'GPL')
depends=("${MINGW_PACKAGE_PREFIX}-boost"
         "${MINGW_PACKAGE_PREFIX}-qt5"
         "${MINGW_PACKAGE_PREFIX}-libtorrent-rasterbar"
         "${MINGW_PACKAGE_PREFIX}-zlib")
optdepends=("${MINGW_PACKAGE_PREFIX}-python3: needed for torrent search tab")
makedepends=("${MINGW_PACKAGE_PREFIX}-pkg-config")
source=(${_realname}-${pkgver}.tar.gz::https://github.com/qbittorrent/qBittorrent/archive/release-${pkgver}.tar.gz)
sha256sums=('ffc5210f3d2d112b4618f9fc2b8efa57684566edf63920beee03f28d66347803')
validpgpkeys=('D8F3DA77AAC6741053599C136E4A2D025B7CC9A2')

prepare() {
  cd "${srcdir}/${_realname}-release-${pkgver}"

  # prepare env for mingw
  sed -i 's/unix:!macx:/unix|win32-g++:/g' "src/src.pro"
  sed -i 's/!haiku/#!haiku/g' "unixconf.pri"
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  cp -rf ${_realname}-release-${pkgver} build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}

  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --with-boost-system=boost_system-mt
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"

  make INSTALL_ROOT=${pkgdir} install
  install -Dm644 "COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
