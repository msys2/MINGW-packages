# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=mold
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.32.0
pkgrel=2
pkgdesc="A Modern Linker (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/rui314/mold'
license=('spdx:MIT')
msys2_references=(
  'archlinux: mold'
)
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread-git"
         "${MINGW_PACKAGE_PREFIX}-tbb"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-zstd"
         "${MINGW_PACKAGE_PREFIX}-dlfcn")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-mold")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/rui314/mold/archive/v${pkgver}.tar.gz"
        "002-cmake-copy-instead-symlink.patch"
        "003-support-lto.patch::https://github.com/rui314/mold/pull/1287.patch")
sha256sums=('4b7e4146ea0f52be9adae8b417399f3676a041e65b55e3f25f088120d30a320b'
            '0c0c755132035defbe857977fd1c0dcd4251f4a241506f1f249ce5be3c534576'
            '79fb136fa1fb70af1def02b59d12fb459f8dc654ca2804db6406ad5cd8971652')
noextract=(${_realname}-${pkgver}.tar.gz)

prepare() {
  tar -xzf ${_realname}-${pkgver}.tar.gz || true
  cd "${srcdir}"/${_realname}-${pkgver}

  patch -p1 -i "${srcdir}"/002-cmake-copy-instead-symlink.patch
  # https://github.com/rui314/mold/pull/1287
  patch -p1 -i "${srcdir}"/003-support-lto.patch
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DCMAKE_INSTALL_LIBEXECDIR='lib' \
      "${_extra_config[@]}" \
      -DMOLD_USE_MIMALLOC=OFF \
      -DMOLD_USE_SYSTEM_TBB=ON \
      -DMOLD_USE_MOLD=ON \
      -DMOLD_LTO=ON \
      -DBUILD_TESTING=OFF \
      ../${_realname}-${pkgver}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  "${MINGW_PREFIX}"/bin/cmake -DBUILD_TESTING=ON ../${_realname}-${pkgver}
  "${MINGW_PREFIX}"/bin/cmake --build .
  "${MINGW_PREFIX}"/bin/ctest --output-on-failure
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .
}
