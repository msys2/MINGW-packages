# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=mold
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.31.0
pkgrel=3
pkgdesc="A Modern Linker (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/rui314/mold'
license=('spdx:MIT')
msys2_references=(
  'archlinux: mold'
)
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread-git"
         "${MINGW_PACKAGE_PREFIX}-tbb"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-zstd"
         "${MINGW_PACKAGE_PREFIX}-dlfcn")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/rui314/mold/archive/v${pkgver}.tar.gz"
        "001-cmake-enable-lto-support.patch"
        "002-cmake-copy-instead-symlink.patch"
        "003-enable-lto-support.patch")
sha256sums=('3DC3AF83A5D22A4B29971BFAD17261851D426961C665480E2CA294E5C74AA1E5'
            'a47588ed9df53bbda65aa6d2b87cd09315e5807fec9922ada14593a5414270b2'
            '0c0c755132035defbe857977fd1c0dcd4251f4a241506f1f249ce5be3c534576'
            '5512dd0d087fa1e08e3cade48f6c6785fe12a111765499daac6a81dc4e9a8d29')
noextract=(${_realname}-${pkgver}.tar.gz)

prepare() {
  tar -xzf ${_realname}-${pkgver}.tar.gz || true
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/001-cmake-enable-lto-support.patch
  patch -p1 -i "${srcdir}"/002-cmake-copy-instead-symlink.patch
  patch -p1 -i "${srcdir}"/003-enable-lto-support.patch
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DCMAKE_INSTALL_LIBEXECDIR='lib' \
      "${_extra_config[@]}" \
      -DMOLD_USE_MIMALLOC=OFF \
      -DMOLD_USE_SYSTEM_TBB=ON \
      -DMOLD_USE_MOLD=OFF \
      -DBUILD_TESTING=OFF \
      ../${_realname}-${pkgver}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  "${MINGW_PREFIX}"/bin/cmake -DBUILD_TESTING=ON ../${_realname}-${pkgver}
  "${MINGW_PREFIX}"/bin/cmake --build .
  "${MINGW_PREFIX}"/bin/ctest --output-on-failure
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .
}
