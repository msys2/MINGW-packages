# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=mold
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.40.4
pkgrel=1
pkgdesc="A Modern Linker (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/rui314/mold'
msys2_repository_url='https://github.com/rui314/mold'
license=('spdx:MIT')
msys2_references=(
  'anitya: 241732'
  'archlinux: mold'
  'gentoo: sys-devel/mold'
)
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread"
         "${MINGW_PACKAGE_PREFIX}-libblake3"
         "${MINGW_PACKAGE_PREFIX}-tbb"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-zstd"
         "${MINGW_PACKAGE_PREFIX}-dlfcn")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
source=("https://github.com/rui314/mold/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "002-cmake-copy-instead-symlink.patch")
sha256sums=('69414c702ec1084e1fa8ca16da24f167f549e5e11e9ecd5d70a8dcda6f08c249'
            '0c0c755132035defbe857977fd1c0dcd4251f4a241506f1f249ce5be3c534576')
noextract=(${_realname}-${pkgver}.tar.gz)

prepare() {
  tar -xzf ${_realname}-${pkgver}.tar.gz || true
  cd ${_realname}-${pkgver}

  patch -p1 -i "${srcdir}"/002-cmake-copy-instead-symlink.patch
}

build() {
  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DCMAKE_INSTALL_LIBEXECDIR='lib' \
      "${_extra_config[@]}" \
      -DMOLD_USE_MIMALLOC=OFF \
      -DMOLD_USE_SYSTEM_TBB=ON \
      -DMOLD_USE_MOLD=OFF \
      -DBUILD_TESTING=OFF \
      -S ${_realname}-${pkgver} \
      -B build-${MSYSTEM}

  cmake --build build-${MSYSTEM}
}

check() {
  cmake -DBUILD_TESTING=ON -S ${_realname}-${pkgver} -B build-${MSYSTEM}
  cmake --build build-${MSYSTEM}
  ctest --test-dir build-${MSYSTEM} --output-on-failure
}

package() {
  DESTDIR="${pkgdir}" cmake --install build-${MSYSTEM}
}
