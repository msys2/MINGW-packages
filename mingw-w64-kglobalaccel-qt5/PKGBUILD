# Maintainer (MSYS2): Ray Donnelly <mingw.android@gmail.com>

_variant=-static
#_variant=-shared
source "$(dirname ${BASH_SOURCE[0]})"/../mingw-w64-PKGBUILD-common/kde-frameworks5
_kde_f5_init_package "${_variant}" "kglobalaccel"
pkgver=5.12.0
pkgrel=1
arch=('any')
pkgdesc="Global desktop keyboard shortcuts (mingw-w64-qt5${_namesuff})"
license=('LGPL')
depends=('qt5-base' 'shared-mime-info' 'gamin')
makedepends=("${MINGW_PACKAGE_PREFIX}-extra-cmake-modules")
depends=()
groups=('kf5')
# install=${pkgname}.install
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${_basename}-${pkgver}.tar.xz")
md5sums=('93b2302fa2587bec638c3e619fac1bd5')

prepare() {
  mkdir -p build-${CARCH}${_variant}
}

build() {
  local -a extra_config
  cd build-${CARCH}${_variant}
  if [ "${_variant}" = "-static" ]; then
    extra_config+=( -DBUILD_SHARED_LIBS=NO )
    QT5_PREFIX=${MINGW_PREFIX}/qt5-static
    export PATH=${QT5_PREFIX}/bin:"$PATH"
  else
    QT5_PREFIX=${MINGW_PREFIX}
  fi

  cmake ../${_basename}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=${QT5_PREFIX} \
    -DLIB_INSTALL_DIR=lib \
    -DECM_MKSPECS_INSTALL_DIR=${QT5_PREFIX} \
    -DQt5_DIR=${QT5_PREFIX}/lib/cmake/Qt5 \
    -DBUILD_TESTING=OFF \
    -DECM_DIR=${MINGW_PREFIX}/share/ECM \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    "${extra_config[@]}" \
    -G'MSYS Makefiles' --debug-output
  make VERBOSE=1
}

package() {
  cd build-${CARCH}${_variant}
  make DESTDIR="${pkgdir}" install
}
