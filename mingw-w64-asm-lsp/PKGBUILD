# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=asm-lsp
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.9.0
pkgrel=1
pkgdesc="Language server for NASM/GAS/GO Assembly (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'aur: asm-lsp'
  'purl: pkg:cargo/asm-lsp'
)
msys2_repository_url='https://github.com/bergercookie/asm-lsp'
url='https://crates.io/crates/asm-lsp'
license=('spdx:BSD-2-Clause')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
source=("https://github.com/bergercookie/asm-lsp/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('863bdc5aad7039f7f355e09a2a759adbef6ea40b9efb3085f7bee4a382d79e11')

prepare() {
  cd "${_realname}-${pkgver}"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${_realname}-${pkgver}"

  cargo build --release --frozen
}

package() {
  cd "${_realname}-${pkgver}"

  install -Dm755 target/release/asm-lsp.exe "${pkgdir}${MINGW_PREFIX}/bin/asm-lsp.exe"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
