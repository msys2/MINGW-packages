# Maintainer: Mazhar Hussain <realmazharhussain@gmail.com>

_realname=gst-plugins-rs
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgdesc='GStreamer plugins written in Rust (mingw-w64)' 
url="https://gitlab.freedesktop.org/gstreamer/$_realname"
pkgver=0.10.6
pkgrel=1
license=('spdx:LGPL-3.0-or-later'
         'spdx:Apache-2.0'
         'spdx:MIT'
         'spdx:MPL-2.0')
depends=(${MINGW_PACKAGE_PREFIX}-gtk4
         ${MINGW_PACKAGE_PREFIX}-libsodium
         ${MINGW_PACKAGE_PREFIX}-gstreamer
         ${MINGW_PACKAGE_PREFIX}-dav1d
         ${MINGW_PACKAGE_PREFIX}-libwebp)
makedepends=(${MINGW_PACKAGE_PREFIX}-meson
             ${MINGW_PACKAGE_PREFIX}-pkgconf
             ${MINGW_PACKAGE_PREFIX}-rust
             ${MINGW_PACKAGE_PREFIX}-cargo-c
             ${MINGW_PACKAGE_PREFIX}-python-tomli)
source=("$url/-/archive/$_version/$_realname-$_version.tar.bz2")
sha256sums=('90af993b28f050e2d682582ca1bde440665e75a5fbaec6577cafc609e24f2d92')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
arch=(any)

build() {
  MSYS2_ARG_CONV_EXCL="--prefix=" \
  "${MINGW_PREFIX}"/bin/meson setup \
    --prefix="${MINGW_PREFIX}" \
    --auto-features=enabled \
    -D doc=disabled \
    -D audiofx=disabled \
    $_realname-$_version \
    build-${MSYSTEM}

  meson compile -C build-${MSYSTEM}
}

check() {
  meson test -C build-${MSYSTEM}
}

package() {
  meson install -C build-${MSYSTEM} --destdir="$pkgdir"
  install -Dm644 $_realname-$_version/LICENSE* -t "$pkgdir/$MINGW_PREFIX"/share/licenses/$_realname/
}
