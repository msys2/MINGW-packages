# Maintainer: Junjie Mao <eternal.n08@gmail.com>

_realname=ocaml-findlib
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.5.5
pkgrel=1
pkgdesc='OCaml library manager'
groups=("${MINGW_PACKAGE_PREFIX}")
arch=('any')
url='http://projects.camlcity.org/projects/findlib.html'
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-ocaml")
source=("http://download.camlcity.org/download/findlib-1.5.5.tar.gz")
sha1sums=('3221121c4b5adacc050aa361475eee59b2698afa')

env=${MINGW_PREFIX#/}
_pkg="findlib-${pkgver}"
_pkgsrc="${env}-${_pkg}"
noextract=(${_pkg}.tar.gz)
prepare() {
  cd $startdir/
  [ -d ${srcdir}/${_pkgsrc} ] && rm -rf ${srcdir}/${_pkgsrc}
  tar -xzf ${startdir}/${_pkg}.tar.gz -C ${srcdir}

  cd ${srcdir}
  mv ${_pkg} ${_pkgsrc}
  cd ${_pkgsrc}
}

build() {
  cd ${srcdir}/${_pkgsrc}
  export OCAMLLIB=${MINGW_PREFIX}/lib/ocaml
  ./configure -system win32 \
              -sitelib ${MINGW_PREFIX}/lib/ocaml \
              -mandir ${MINGW_PREFIX}/share/man \
              -no-camlp4
  make all -j1
  make opt -j1
}

check() {
  # The package does not include a testsuite
  plain "skip"
}

package() {
  cd ${srcdir}/${_pkgsrc}
  make install prefix=${pkgdir}
  [ -f LICENSE ] && install -Dm644 LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
