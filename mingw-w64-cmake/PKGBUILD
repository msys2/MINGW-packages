# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=cmake
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.8.1
pkgrel=2
pkgdesc="A cross-platform open-source make system (mingw-w64)"
arch=('any')
url="https://www.cmake.org/"
license=("MIT")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-qt5")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-expat"
         "${MINGW_PACKAGE_PREFIX}-jsoncpp"
         "${MINGW_PACKAGE_PREFIX}-libarchive"
         "${MINGW_PACKAGE_PREFIX}-libuv"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-rhash")
optdepends=("${MINGW_PACKAGE_PREFIX}-qt5: CMake Qt GUI")
options=('staticlibs') # '!strip' 'debug'
source=("https://www.cmake.org/files/v${pkgver%.*}/${_realname}-${pkgver}.tar.gz"
        "0001-Windows-Add-missing-stringapiset.h-include.patch"
        "0002-bootstrap-Set-DEFAULT_CODEPAGE-in-cmake_cxx_flags_En.patch"
        "0003-Disable-response-files-for-MSYS-Generator.patch"
        "0004-Implement-Qt5-static-plugin-support.patch"
        "0005-Do-not-install-Qt-bundle-in-cmake-gui.patch"
        "0006-pkg-config-Add-dont-define-prefix-when-PKG_CONFIG_WI.patch"
        "0007-Do-not-generate-import-libs-for-exes.patch"
        "0008-Output-line-numbers-in-callstacks.patch"
        "cm_codecvt.patch")
sha256sums=('ce5d9161396e06501b00e52933783150a87c33080d4bdcef461b5b7fd24ac228'
            '0d8557d7964a624d00c64ce390a4440d6d82119b45c1cd24ddeae3ee028dcf87'
            'cfffe9d255dd5d78f8d029878bb46f7adfb60ac4ec9564e8d478b5b6a52231b8'
            '6845069dc9428e09d837b0f260ec3dd477533f8b19812b6d44eb29e8f4561c29'
            'b3b9c83e55e29f39bc4bb0a8db476e898d85bb7aaf3e27cde433a7f9eae776bb'
            'f8886078cb61e2b3d2cb6d7fe9325290206e006d7a1f1423425078bd35b0023f'
            'c230783cdd1a32ddd3d0d938c63d70a24e23bc6947f90f22770d85be5af54c6a'
            'a3541d1272805fa11de0bb7cb19b37f55f2e0664410247c5cfab4ee93d2637a5'
            '9dc604b25b97ff612723c8955b27ab015d22af3e762851a01a934a656c8b80c5'
            '2d3e54836f93f0391fe805dfc9a895477ed7a304b9148bc8baf49062f117958d')

prepare() {
  cd ${_realname}-${pkgver}
  patch -Np1 -i "${srcdir}"/0001-Windows-Add-missing-stringapiset.h-include.patch
  patch -Np1 -i "${srcdir}"/0002-bootstrap-Set-DEFAULT_CODEPAGE-in-cmake_cxx_flags_En.patch
  patch -Np1 -i "${srcdir}"/0003-Disable-response-files-for-MSYS-Generator.patch
  patch -Np1 -i "${srcdir}"/0004-Implement-Qt5-static-plugin-support.patch
  patch -Np1 -i "${srcdir}"/0005-Do-not-install-Qt-bundle-in-cmake-gui.patch
  patch -Np1 -i "${srcdir}"/0006-pkg-config-Add-dont-define-prefix-when-PKG_CONFIG_WI.patch
  patch -Np1 -i "${srcdir}"/0007-Do-not-generate-import-libs-for-exes.patch
  patch -Np1 -i "${srcdir}"/0008-Output-line-numbers-in-callstacks.patch
  patch -Np1 -i "${srcdir}"/cm_codecvt.patch
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  MSYSTEM=MINGW \
    "${srcdir}"/${_realname}-${pkgver}/configure  \
    --prefix=${MINGW_PREFIX}                      \
    --system-libs                                 \
    --no-system-expat                             \
    --qt-gui                                      \
    --parallel=${NUMBER_OF_PROCESSORS}            \
    --server                                      \
    --mandir=share                                \
    --docdir=share/doc/cmake

  plain "Start building..."
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  ./bin/cmake.exe -DCMAKE_INSTALL_PREFIX:PATH=${pkgdir}${MINGW_PREFIX} -P cmake_install.cmake
}
