# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=cmake
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.3.2
pkgrel=3
pkgdesc="A cross-platform open-source make system (mingw-w64)"
arch=('any')
url="http://www.cmake.org/"
license=("MIT")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-qt5")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-expat"
         "${MINGW_PACKAGE_PREFIX}-jsoncpp"
         "${MINGW_PACKAGE_PREFIX}-libarchive"
         "${MINGW_PACKAGE_PREFIX}-zlib")
optdepends=("${MINGW_PACKAGE_PREFIX}-qt5: CMake Qt GUI")
options=('staticlibs') # '!strip' 'debug'
source=("http://www.cmake.org/files/v${pkgver%.*}/${_realname}-${pkgver}.tar.gz"
        "disable-response-files-for-msys.patch"
        "dont-install-bundle.patch"
        "qt5-static-plugin-support.patch"
        "pkg-config-without-prefix.patch"
        "do-not-generate-import-library-for-executables.patch")
md5sums=('5febbd11bcaac854a27eebaf4a124be2'
         '3b0eb07bb98ea9b8e8482e4936971013'
         'fb33ace70ba863159b2c7964a9661e1e'
         '1800f99cdae3abe0ae96b6bf7fa873df'
         'a55c5c6231fc953bad5600c3bb49667b'
         '3105d5c805111cd4db89a4c743ed18fe')

prepare() {
  cd ${_realname}-${pkgver}
  patch -Np1 -i ${srcdir}/disable-response-files-for-msys.patch
  patch -Np1 -i ${srcdir}/dont-install-bundle.patch
  patch -Np1 -i ${srcdir}/qt5-static-plugin-support.patch
  patch -Np1 -i ${srcdir}/pkg-config-without-prefix.patch
  patch -Np1 -i ${srcdir}/do-not-generate-import-library-for-executables.patch
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  MSYSTEM=MINGW "${srcdir}"/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --system-libs \
    --no-system-expat \
    --qt-gui \
    --parallel=3 \
    --mandir=share \
    --docdir=share/doc/cmake

  plain "Start building..."
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  ./bin/cmake.exe -DCMAKE_INSTALL_PREFIX:PATH=${pkgdir}${MINGW_PREFIX} -P cmake_install.cmake
  rm "${pkgdir}${MINGW_PREFIX}/cmake.org.html"
}
