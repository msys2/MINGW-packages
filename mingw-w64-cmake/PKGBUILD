# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=cmake
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=2.8.12.2
pkgrel=4
pkgdesc="A cross-platform open-source make system (mingw-w64)."
arch=('any')
url="http://www.cmake.org/"
license=("MIT")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config" "${_mingw_suff}-qt5")
depends=("${_mingw_suff}-gcc-libs"
    "${_mingw_suff}-curl"
    "${_mingw_suff}-expat"
    "${_mingw_suff}-libarchive"
    "${_mingw_suff}-zlib")
optdepends=("${_mingw_suff}-qt5: CMake Qt GUI")
options=('staticlibs' 'strip')
source=("http://www.cmake.org/files/v2.8/${_realname}-${pkgver}.tar.gz"
        "findfreetype.patch"
        "disable-response-files-for-msys.patch"
        "dont-install-bundle.patch")
md5sums=('17c6513483d23590cbce6957ec6d1e66'
         '90321de1d9d46cd8d6609d0509dbd7b0'
         'b715f7d6859ce82a0a1824592ab2a448'
         'fb33ace70ba863159b2c7964a9661e1e')

prepare() {
  cd ${_realname}-${pkgver}
  patch -Np1 -i ${srcdir}/findfreetype.patch
  patch -Np1 -i ${srcdir}/disable-response-files-for-msys.patch
  patch -Np1 -i ${srcdir}/dont-install-bundle.patch
}

build() {
  mkdir -p ${pkgdir}${MINGW_PREFIX}
  pushd ${MINGW_PREFIX} > /dev/null
  export PREFIX_LIB=`pwd -W`
  popd > /dev/null
  
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  MSYSTEM=MINGW "${srcdir}"/${_realname}-${pkgver}/configure \
    --prefix=${PREFIX_LIB} \
    --system-libs \
    --no-system-expat \
    --qt-gui \
    --parallel=3 \
    --mandir=share \
    --docdir=share/doc/cmake

  plain "Start building..."
  make
}

package() {
  mkdir -p "${pkgdir}${MINGW_PREFIX}"
  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null

  cd "${srcdir}/build-${MINGW_CHOST}"
  ./bin/cmake.exe -DCMAKE_INSTALL_PREFIX:PATH=${PREFIX_WIN} -P cmake_install.cmake
}
