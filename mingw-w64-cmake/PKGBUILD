# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=cmake
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.4.1
pkgrel=1
pkgdesc="A cross-platform open-source make system (mingw-w64)"
arch=('any')
url="https://www.cmake.org/"
license=("MIT")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-qt5")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-expat"
         "${MINGW_PACKAGE_PREFIX}-jsoncpp"
         "${MINGW_PACKAGE_PREFIX}-libarchive"
         "${MINGW_PACKAGE_PREFIX}-zlib")
optdepends=("${MINGW_PACKAGE_PREFIX}-qt5: CMake Qt GUI")
options=('staticlibs') # '!strip' 'debug'
source=("https://www.cmake.org/files/v${pkgver%.*}/${_realname}-${pkgver}.tar.gz"
        "disable-response-files-for-msys.patch"
        "dont-install-bundle.patch"
        "qt5-static-plugin-support.patch"
        "pkg-config-without-prefix.patch"
        "do-not-generate-import-library-for-executables.patch")
sha256sums=('d41462bdd80dc37f0d5608167b354bb3af8c068eee640be04c907154c5c113e2'
            '76565ffc20010b27e895af7919f35deedc7fd0b86ca8746bbc22329185ad57fa'
            'aa816b734bf91941403b25bc9d9fd1797675ce2b4a7311bdb6e3ecae38964c37'
            '5470eaf830aa43f37893d34af224915cc9be0c8b2d165a0115d7fd01881b9e69'
            'aa807e20e4a2902a8e6f3f36e1cd7d4b043d0df12f4fb0f4e6bd27d9d6842c0f'
            '8503536bfc6606e147c62cd7bc28f984911a04b7bf8164264c345eacbf68dc5b')

prepare() {
  cd ${_realname}-${pkgver}
  patch -Np1 -i ${srcdir}/disable-response-files-for-msys.patch
  patch -Np1 -i ${srcdir}/dont-install-bundle.patch
  patch -Np1 -i ${srcdir}/qt5-static-plugin-support.patch
  patch -Np1 -i ${srcdir}/pkg-config-without-prefix.patch
  patch -Np1 -i ${srcdir}/do-not-generate-import-library-for-executables.patch
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  MSYSTEM=MINGW "${srcdir}"/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --system-libs \
    --no-system-expat \
    --qt-gui \
    --parallel=3 \
    --mandir=share \
    --docdir=share/doc/cmake

  plain "Start building..."
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  ./bin/cmake.exe -DCMAKE_INSTALL_PREFIX:PATH=${pkgdir}${MINGW_PREFIX} -P cmake_install.cmake
}
