# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=cmake
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.10.1
pkgrel=1
pkgdesc="A cross-platform open-source make system (mingw-w64)"
arch=('any')
url="https://www.cmake.org/"
license=("MIT")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-qt5")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-expat"
         "${MINGW_PACKAGE_PREFIX}-jsoncpp"
         "${MINGW_PACKAGE_PREFIX}-libarchive"
         "${MINGW_PACKAGE_PREFIX}-libuv"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-rhash")
optdepends=("${MINGW_PACKAGE_PREFIX}-qt5: CMake Qt GUI")
options=('staticlibs') # '!strip' 'debug'
source=("https://www.cmake.org/files/v${pkgver%.*}/${_realname}-${pkgver}.tar.gz"
        "0001-Windows-Add-missing-stringapiset.h-include.patch"
        "0003-Disable-response-files-for-MSYS-Generator.patch"
        "0004-Implement-Qt5-static-plugin-support.patch"
        "0005-Do-not-install-Qt-bundle-in-cmake-gui.patch"
        "0006-pkg-config-Add-dont-define-prefix-when-PKG_CONFIG_WI.patch"
        "0007-Do-not-generate-import-libs-for-exes.patch"
        "0008-Output-line-numbers-in-callstacks.patch")
sha256sums=('7be36ee24b0f5928251b644d29f5ff268330a916944ef4a75e23ba01e7573284'
            '0d8557d7964a624d00c64ce390a4440d6d82119b45c1cd24ddeae3ee028dcf87'
            '6845069dc9428e09d837b0f260ec3dd477533f8b19812b6d44eb29e8f4561c29'
            '364279aa2e55426b714561be6d179ebf92d85907817c50b47535f4aa4b545a54'
            'f8886078cb61e2b3d2cb6d7fe9325290206e006d7a1f1423425078bd35b0023f'
            'c230783cdd1a32ddd3d0d938c63d70a24e23bc6947f90f22770d85be5af54c6a'
            'a3541d1272805fa11de0bb7cb19b37f55f2e0664410247c5cfab4ee93d2637a5'
            '9dc604b25b97ff612723c8955b27ab015d22af3e762851a01a934a656c8b80c5')


# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #


prepare() {
  cd ${_realname}-${pkgver}
  apply_patch_with_msg \
    0001-Windows-Add-missing-stringapiset.h-include.patch \
    0003-Disable-response-files-for-MSYS-Generator.patch \
    0004-Implement-Qt5-static-plugin-support.patch \
    0005-Do-not-install-Qt-bundle-in-cmake-gui.patch \
    0006-pkg-config-Add-dont-define-prefix-when-PKG_CONFIG_WI.patch \
    0007-Do-not-generate-import-libs-for-exes.patch \
    0008-Output-line-numbers-in-callstacks.patch
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  MSYSTEM=MINGW \
    "${srcdir}"/${_realname}-${pkgver}/configure  \
    --prefix=${MINGW_PREFIX}                      \
    --system-libs                                 \
    --no-system-expat                             \
    --qt-gui                                      \
    --parallel=${NUMBER_OF_PROCESSORS}            \
    --server                                      \
    --mandir=share                                \
    --docdir=share/doc/cmake

# Fix path references in cmake-3.8.2/Tests/CMakeTests/ImplicitLinkInfoTest.cmake.in
# This used to be in the msys patch but I feel it's better to do it here so that GCC
# could be updated the script will still work.
  local _gcc=$(gcc --version | grep ^gcc | sed 's/^.* //g')
#  sed -e "s|C:/some-mingw/|/usr/|g" \
#      -e "s|/some-mingw/|/usr/|g" \
#      -e "s|/libexec/|/lib/|g" \
#      -e "s|/mingw32/3.4.5|/$(uname -m)-pc-msys/${_gcc}|g" \
#      -e "s|/usr/lib/crt2.o|/usr/lib/crt0.o -L/usr/lib/w32api/|g" \
#      -e "s|-lmingw32|-lmsys-2.0|g" \
#      -i Tests/CMakeTests/ImplicitLinkInfoTest.cmake.in

  plain "Start building..."
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  ./bin/cmake.exe -DCMAKE_INSTALL_PREFIX:PATH=${pkgdir}${MINGW_PREFIX} -P cmake_install.cmake
}
