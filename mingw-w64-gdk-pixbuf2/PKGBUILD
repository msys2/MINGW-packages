# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gdk-pixbuf2
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=2.30.6
pkgrel=1
pkgdesc="An image loading library (mingw-w64)"
arch=('any')
url="http://www.gtk.org"
license=("LGPL")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config" "${_mingw_suff}-gobject-introspection")
depends=("${_mingw_suff}-gcc-libs"
    "${_mingw_suff}-glib2>=2.37.2"
    "${_mingw_suff}-jasper"
    "${_mingw_suff}-libpng"
    "${_mingw_suff}-libjpeg-turbo"
    "${_mingw_suff}-libtiff")
options=('strip' 'staticlibs')
install=${_realname}-${CARCH}.install
source=("http://download.gnome.org/sources/gdk-pixbuf/${pkgver%.*}/gdk-pixbuf-$pkgver.tar.xz"
    0002-A-hack-to-fix-export-symbols.mingw.patch
    0003-use-free-software-libs-on.mingw.patch
    0004-give-cc-to-gir-scanner.all.patch)
sha256sums=('43394fa4adfb49fa4d662fdc6f43f7f25a9f914509528f57720b18487b0a7f5a'
            '31c64c6f783ca0f48bc6857451bb68da4e132f7857d6462cf16c5971c64d7f57'
            '02c29eae558c0297e59422d2159230d65ba5d781c02f3cd39937e76b24f02369'
            'bce33b7f4b156ef256285381d28bcf910a8437c4fd668d90b25e5901cc1dcff3')

prepare() {
  cd ${srcdir}/gdk-pixbuf-${pkgver}
  patch -p1 -i ${srcdir}/0002-A-hack-to-fix-export-symbols.mingw.patch
  patch -p1 -i ${srcdir}/0003-use-free-software-libs-on.mingw.patch
  patch -p1 -i ${srcdir}/0004-give-cc-to-gir-scanner.all.patch
  
  autoreconf -fi
}

build() {
    CFLAGS+=" -mms-bitfields"
    CXXFLAGS+=" -mms-bitfields"
    mkdir -p "${srcdir}/build-${MINGW_CHOST}"
    cd "${srcdir}/build-${MINGW_CHOST}"
    ${srcdir}/gdk-pixbuf-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --enable-static \
      --enable-shared \
    --enable-introspection
    make
}

package() {
    cd "${srcdir}/build-${MINGW_CHOST}"
    make DESTDIR="$pkgdir" install
    find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    #rm -r "${pkgdir}${MINGW_PREFIX}/share"
}
