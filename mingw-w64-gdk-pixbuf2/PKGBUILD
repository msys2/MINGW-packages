# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

_realname=gdk-pixbuf2
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.31.1
pkgrel=4
pkgdesc="An image loading library (mingw-w64)"
arch=('any')
url="http://www.gtk.org"
license=(LGPL2)
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config" "${MINGW_PACKAGE_PREFIX}-gobject-introspection")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
        "${MINGW_PACKAGE_PREFIX}-glib2>=2.37.2"
        "${MINGW_PACKAGE_PREFIX}-jasper"
        "${MINGW_PACKAGE_PREFIX}-libpng"
        "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
        "${MINGW_PACKAGE_PREFIX}-libtiff")
options=('strip' 'staticlibs')
install=${_realname}-${CARCH}.install
source=("http://download.gnome.org/sources/gdk-pixbuf/${pkgver%.*}/gdk-pixbuf-$pkgver.tar.xz"
        0001-Use-a-regex-to-properly-export-the-symbols.patch
        0001-Give-priority-to-the-modules-using-the-open-source-l.patch
        0002-gdk-pixbuf-introspection.patch)
sha256sums=('25a75e3c61dac11e6ff6416ad846951ccafac6486b1c6a1bfb0b213b99db52cd'
            'e8d278e30c44e973e14e3c61e8ab195621d6a9a402e0da557db4616955ca4543'
            '8f8b3882b3bbc6d32e99230ab188a5749cea57c37b699e371cb7f6096a1ec712'
            'dd496d66f0a6b369410fb4039e116ac7d6c2808c97998932a1f8f70b58f88ce2')

prepare() {
  cd ${srcdir}/gdk-pixbuf-${pkgver}
  patch -p1 -i ${srcdir}/0001-Use-a-regex-to-properly-export-the-symbols.patch
  patch -p1 -i ${srcdir}/0001-Give-priority-to-the-modules-using-the-open-source-l.patch
  patch -p1 -i ${srcdir}/0002-gdk-pixbuf-introspection.patch

  autoreconf -fi
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ${srcdir}/gdk-pixbuf-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-static \
    --enable-shared \
    --enable-introspection \
    --with-included-loaders
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  
  #get rid of "_utf8" sufix in function names
  sed -i 's/\(<constructor\|method name="\)\(.*\)\(_utf8"\)/\1\2"/' gdk-pixbuf/GdkPixbuf-2.0.gir
  g-ir-compiler gdk-pixbuf/GdkPixbuf-2.0.gir > gdk-pixbuf/GdkPixbuf-2.0.typelib
  
  make DESTDIR="$pkgdir" install
  find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
  install -Dm644 "${srcdir}/gdk-pixbuf-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
