# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=moor
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.10.3
pkgrel=1
pkgdesc='Terminal pager program (mingw-w64)'
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://github.com/walles/moor'
msys2_repository_url='https://github.com/walles/moor'
msys2_references=(
  'anitya: 283347'
  'aur: moor'
  'gentoo: sys-apps/moor'
  'purl: pkg:golang/github.com/walles/moor/v2'
)
license=('spdx:BSD-2-Clause')
makedepends=("${MINGW_PACKAGE_PREFIX}-go" "${MINGW_PACKAGE_PREFIX}-cc")
provides=("${MINGW_PACKAGE_PREFIX}-moar")
conflicts=("${MINGW_PACKAGE_PREFIX}-moar")
replaces=("${MINGW_PACKAGE_PREFIX}-moar")
options=('!strip')
source=("https://github.com/walles/moor/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('ad5cd21d4260fe2bb86a0b2f25511b810efd95f195a39969049a94350af3dd88')

build() {
    export GOOS=windows
    export GOROOT=${MINGW_PREFIX}/lib/go
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GO_LDFLAGS="-s -w"
    export GOFLAGS="-trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external"
    case "${CARCH}" in
      x86_64)
        GOFLAGS+=" -buildmode=pie"
        ;;
    esac

    cd "${_realname}-${pkgver}"
    go build -ldflags="-s -w -X main.versionString=${pkgver}" ./cmd/moor
}

check(){
    cd "${_realname}-${pkgver}"
    go test ./... || warning "Tests failed"
}

package() {
    cd "${_realname}-${pkgver}"
    install -Dm755 "moor.exe" "${pkgdir}${MINGW_PREFIX}/bin/moor.exe"
    install -Dm644 "moor.1" "${pkgdir}${MINGW_PREFIX}/share/man/man1/moor.1"
    install -Dm644 "LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
