# Maintainer: Chris Gorman <chrisjohgorman@gmail.com>

_realname=pyaml
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=23.12.0
pkgrel=2
pkgdesc="PyYAML-based mdule to produce pretty and readble YAML-serialized data (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://pypi.python.org/pypi/pyaml'
license=('WTFPL')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-yaml")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build" 
	     "${MINGW_PACKAGE_PREFIX}-python-installer"
	     "${MINGW_PACKAGE_PREFIX}-python-setuptools"
	     "${MINGW_PACKAGE_PREFIX}-python-wheel")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-unidecode")
options=('!strip')
source=("https://pypi.org/packages/source/p/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('ce6f648efdfb1b3a5579f8cedb04facf0fa1e8f64846b639309b585bb322b4e5')

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}"
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  msg "Python install for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 COPYING -t "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/"
}
