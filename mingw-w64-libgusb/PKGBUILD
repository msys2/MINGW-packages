# Maintainer: Ignacio Casal Quinteiro <icq@gnome.org>

_realname=libgusb
_tarballname=gusb
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.2.6
pkgrel=1
arch=('any')
pkgdesc="GLib wrapper around libusb1 (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-libusb"
         "${MINGW_PACKAGE_PREFIX}-glib2")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-vala"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection")
options=('strip' 'staticlibs')
license=("LGPL")
url="https://github.com/hughsie/libgusb"
source=("https://github.com/hughsie/${_realname}/archive/${_tarballname}_${pkgver//./_}.tar.gz")
sha256sums=('d996e8b8adc8438fb7a19bbfae25a0d6b71e06b3ac40274baf6005a4befa2d41')

prepare() {
  cd "${srcdir}"/${_realname}-${_tarballname}_${pkgver//./_}
  gtkdocize
  autoreconf -f -i
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${_tarballname}_${pkgver//./_}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --enable-introspection

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install
}
