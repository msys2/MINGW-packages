# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=oslo-utils
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}"  "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=3.37.1
pkgrel=1
pkgdesc="Oslo Utility library (mingw-w64)"
arch=('any')
url='https://www.oslo-utils.org/'
license=('Apache')
makedepends=("${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools" 
             "${MINGW_PACKAGE_PREFIX}-python3-pbr" 
             "${MINGW_PACKAGE_PREFIX}-python2-pbr"
             "${MINGW_PACKAGE_PREFIX}-python2-funcsigs"
             "${MINGW_PACKAGE_PREFIX}-python3-iso8601"
             "${MINGW_PACKAGE_PREFIX}-python2-iso8601"
             "${MINGW_PACKAGE_PREFIX}-python3-oslo-i18n" 
             "${MINGW_PACKAGE_PREFIX}-python2-oslo-i18n"
             "${MINGW_PACKAGE_PREFIX}-python2-monotonic"
             "${MINGW_PACKAGE_PREFIX}-python3-pytz" 
             "${MINGW_PACKAGE_PREFIX}-python2-pytz"
             "${MINGW_PACKAGE_PREFIX}-python3-netaddr"
             "${MINGW_PACKAGE_PREFIX}-python2-netaddr"
             "${MINGW_PACKAGE_PREFIX}-python3-netifaces"
             "${MINGW_PACKAGE_PREFIX}-python2-netifaces"
             "${MINGW_PACKAGE_PREFIX}-python3-debtcollector"
             "${MINGW_PACKAGE_PREFIX}-python2-debtcollector")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python3-oslotest" 
              "${MINGW_PACKAGE_PREFIX}-python2-oslotest"
              "${MINGW_PACKAGE_PREFIX}-python3-ddt" 
              "${MINGW_PACKAGE_PREFIX}-python2-ddt"
              "${MINGW_PACKAGE_PREFIX}-python3-testrepository"
              "${MINGW_PACKAGE_PREFIX}-python2-testrepository")
source=("${_realname}-$pkgver.tar.gz"::"https://github.com/openstack/oslo.utils/archive/$pkgver.tar.gz")
sha512sums=('d656a59bf3ec1283009951dbf05676a01fa5b54e19588e356c384e8e0e06177f5f8f546135317fa2d874333a493a576e97d7bdde41dadd546edc1c8d646832a2')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"
  pushd "oslo.utils-${pkgver}"
#    apply_patch_with_msg 0001-A-really-important-fix.patch \
#      0002-A-less-important-fix.patch
  popd 
  for builddir in python{2,3}-build-${CARCH}; do
    rm -rf ${builddir} | true
    cp -r "oslo.utils-${pkgver}" "${builddir}"
  done
  # Use python 3's standard monotonic function
  sed -i '/monotonic/d' python3-build-${CARCH}/requirements.txt
  sed -i 's/from monotonic import monotonic/from time import monotonic/' python3-build-${CARCH}/oslo_utils/timeutils.py

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
  export PBR_VERSION=$pkgver
}

# Note that build() is sometimes skipped because it's done in 
# the packages setup.py install for simplicity if you can do so.
# but sometimes, you want to do a check before install which would
# also trigger the build.
build() {
  for pver in {2,3}; do  
    msg "Python ${pver} build for ${CARCH}"  
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py build
  done  
}

check() {
  msg "Python 3 build for ${CARCH}"
  cd "${srcdir}/python3-build-${CARCH}"
  ${MINGW_PREFIX}/bin/stestr run

  msg "Python 2 build for ${CARCH}"
  cd "${srcdir}/python2-build-${CARCH}"
  PYTHON=python2 ${MINGW_PREFIX}/bin/stestr2 run
}

package_python3-oslo-utils() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3")

  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}

package_python2-oslo-utils() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2")

  cd "${srcdir}/python2-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE"
}

package_mingw-w64-i686-python2-oslo-utils() {
  package_python2-oslo-utils
}

package_mingw-w64-i686-python3-oslo-utils() {
  package_python3-oslo-utils
}

package_mingw-w64-x86_64-python2-oslo-utils() {
  package_python2-oslo-utils
}

package_mingw-w64-x86_64-python3-oslo-utils() {
  package_python3-oslo-utils
}
