# Maintainer: Miloš Komarčević <miloskomarcevic@aim.com>

_realname=webp-pixbuf-loader
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.2.5
pkgrel=1
pkgdesc="WebP GDK Pixbuf Loader library (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://github.com/aruiz/webp-pixbuf-loader"
license=('spdx:LGPL-2.0-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-meson")
depends=("${MINGW_PACKAGE_PREFIX}-libwebp"
         "${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/aruiz/webp-pixbuf-loader/archive/${pkgver}.tar.gz"
        "https://patch-diff.githubusercontent.com/raw/aruiz/webp-pixbuf-loader/pull/74.patch")
sha256sums=('e1b76c538a1d3b3fc41323d044c7c84365ab9bd5ab3dcc8de7efb0c7dc2f206b'
            '6c75ed9fac4fd22ff1eed46f5fb771b0f56e6ffce28f3a5d08ca2bb6c09d73c6')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  # https://github.com/aruiz/webp-pixbuf-loader/pull/74
  patch -Np1 -i "${srcdir}"/74.patch
}

build() {
  [[ -d "${srcdir}/build-${MSYSTEM}" ]] && rm -rf "${srcdir}/build-${MSYSTEM}"
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/meson.exe \
      --prefix=${MINGW_PREFIX} \
      --wrap-mode=nodownload \
      --auto-features=enabled \
      --buildtype=plain \
      "../${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/meson.exe compile
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/meson.exe install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE.LGPL-2" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
