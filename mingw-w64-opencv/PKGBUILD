# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=opencv
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=2.4.8
pkgrel=1
pkgdesc="Open Source Computer Vision Library (mingw-w64)"
arch=('any')
url="http://opencv.org/"
depends=(
    #"${_mingw_suff}-gst-plugins-base0.10"
    "${_mingw_suff}-libjpeg"
    "${_mingw_suff}-libpng"
    "${_mingw_suff}-libtiff"
    "${_mingw_suff}-jasper"
    "${_mingw_suff}-ffmpeg"
    "${_mingw_suff}-openexr"
    #"${_mingw_suff}-intel-tbb"
    "${_mingw_suff}-zlib"
    #"${_mingw_suff}-gtkglext"
    )
makedepends=("${_mingw_suff}-cmake"
    "${_mingw_suff}-qt5"
    "${_mingw_suff}-python2-numpy"
    "${_mingw_suff}-python2-sphinx"
    "${_mingw_suff}-eigen3"
    #"${_mingw_suff}-gtk2"
    )
optdepends=(
    "${_mingw_suff}-eigen3"
    "${_mingw_suff}-qt5"
    #'libcl: For coding with OpenCL'
    "${_mingw_suff}-python2-numpy: Python 2.x interface")
source=("http://downloads.sourceforge.net/opencvlibrary/${_realname}-$pkgver.zip"
    'pkgconfig.patch'
    'fsh.patch'
    'change-layout-mingw.patch'
    'fix-install-python.patch'
    'use-system-libraries.mingw.patch')
md5sums=('50cc1433b3654074206f5b3dbfd49848'
         'c7cea48ed7d4f729ebdb9673bac41bd3'
         'c597598d142dd34d0eb4af7d6e9779d8'
         '332dbe3ea5d0e8482a8348df310b8870'
         'b29a30b35fcb8442c54288bb2c6c683c'
         '0e8cee1d377747176ffa7cd721db61e4')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # hack-fix pkg-config mess
  # see https://bugs.archlinux.org/task/32430
  # and http://code.opencv.org/issues/1925
  patch -Np1 -i "$srcdir/pkgconfig.patch"

  # hack-fix folder naming inconsistency that they won't fix
  # see http://code.opencv.org/issues/2512
  # and https://bugs.archlinux.org/task/32342
  patch -Np1 -i "$srcdir/fsh.patch"

  patch -Np1 -i "$srcdir/change-layout-mingw.patch"
  patch -Np1 -i "$srcdir/fix-install-python.patch"
  patch -Np1 -i "$srcdir/use-system-libraries.mingw.patch"
  # no longer including docs, see https://bugs.archlinux.org/task/34185
  # python2 compatibility for generating (html) docs
  #sed -i 's/sphinx-build/sphinx-build2/' cmake/OpenCVDetectPython.cmake
}

build() {
  mkdir -p ${pkgdir}${MINGW_PREFIX}
  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null

  pushd ${MINGW_PREFIX} > /dev/null
  export PREFIX_DEPS=`pwd -W`
  popd > /dev/null

  # SSE only available from Pentium 3 onwards (i686 is way older)
  [[ "$CARCH" = 'i686' ]] && \
    _cmakeopts=('-D ENABLE_SSE=OFF'
      '-DENABLE_SSE2=OFF'
      '-DENABLE_SSE3=OFF')

  # all x64 CPUs support SSE2 but not SSE3
  [[ "$CARCH" = 'x86_64' ]] && _cmakeopts+=('-DENABLE_SSE3=OFF')

  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX:PATH=${PREFIX_WIN} \
    -DWITH_OPENCL=OFF \
    -DWITH_OPENGL=ON \
    -DWITH_TBB=ON \
    -DWITH_XINE=OFF \
    -DBUILD_WITH_DEBUG_INFO=OFF \
    -DBUILD_TESTS=OFF \
    -DBUILD_PERF_TESTS=OFF \
    -DBUILD_EXAMPLES=ON \
    -DINSTALL_C_EXAMPLES=ON \
    -DINSTALL_PYTHON_EXAMPLES=ON \
    -DWITH_GTK=OFF \
    -DWITH_GSTREAMER=OFF \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DWITH_QT=ON \
    ${_cmakeopts[@]} \
    ../${_realname}-${pkgver}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 install

  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/opencv.pc
}
