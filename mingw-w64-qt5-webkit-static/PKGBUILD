# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=qt5-webkit-static
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
_pkgver=5.212.0-alpha4
pkgver=${_pkgver//-/}
pkgrel=1
pkgdesc="Webkit module for Qt5 (static) (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url="https://github.com/qtwebkit/qtwebkit/wiki"
msys2_repository_url="https://github.com/qtwebkit/qtwebkit"
msys2_references=(
  'archlinux: qt5-webkit'
)
license=('spdx:Qt-GPL-exception-1.0 AND GPL-3.0-or-later AND LGPL-2.1-or-later AND LGPL-3.0-only AND GFDL-1.3-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ruby"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-gperf"
             "${MINGW_PACKAGE_PREFIX}-icu"
             "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
             "${MINGW_PACKAGE_PREFIX}-libpng"
             "${MINGW_PACKAGE_PREFIX}-libxml2"
             "${MINGW_PACKAGE_PREFIX}-libxslt"
             "${MINGW_PACKAGE_PREFIX}-qt5-static"
             "${MINGW_PACKAGE_PREFIX}-sqlite3")
source=(https://github.com/qtwebkit/qtwebkit/releases/download/qtwebkit-${_pkgver}/qtwebkit-${_pkgver}.tar.xz
        0003-qtwebkit-mfence-mingw.patch
        0006-python-output-unix-line-endings.patch
        0008-fix-using-msys-perl.patch
        0011-mingw-posix-layout-files.patch
        0014-disable-asm-win64.patch
        0015-use-proper-import-suffix-mingw.patch
        0016-separate-debug-into-bin.patch
        0202-install-private-headers-and-modules.patch
        0300-fix-generating-module-files.patch
        0501-build-gcc9.patch
        0502-clang-compat.patch
        0503-python3.patch
        0504-qwebinspector-crash-workaround.patch
        0601-missing-include.patch
        0801-suppress-some-warnings.patch
        0802-properly-link-with-system-libs.patch
        0803-search-for-proper-prl-filename.patch
        0804-use-executable_allocator_demand.patch
        0805-libxml2-updates.patch
        0806-codegen-python3-literal-syntax.patch
        0807-deprecated-pure-parser.patch
        0808-remove-bogus-defines.patch
        0809-non-literal-format-string.patch
        0810-webkit-static-no-export.patch
        0811-remove-user-defined-copy.patch
        0812-final-class-inheritance.patch
        0813-logical-expression-operands.patch
        0814-cachevalidation-string-concatenation.patch
        0815-missing-override.patch
        0816-printing-size_t-correctly.patch
        0901-minimal-private-libs.patch
        d92b11fea65364fefa700249bd3340e0cd4c5b31.patch
        6e18844035d9c5e25d770ed817b858c2423d894b.patch
        4e1f209f21c3958e5a8d3a0646dccf9e429ca7e4.patch)
sha256sums=('9ca126da9273664dd23a3ccd0c9bebceb7bb534bddd743db31caf6a5a6d4a9e6'
            'b7100bc8c27ef6abbdb704848224c6a4f8ffea665fe80c2292b64950b1c5bdaa'
            '02d0eb76207c4c2d36a50291976f8cc141fcff030c4b77c399402cd10a789b99'
            '9ef33dc05e5652362e10d34d75fd2cf9e7952c1c99306eb2402367401f86770b'
            'a1ef305a81b4e1b70290d4c3f70bee545f06c17736fe77abde474dba0df8c5c7'
            'c39b65235a61217bf4197285d21765a47a91d2e8c267f4b777c982d56d32b408'
            'bcc0579204aa0bb0210c3b78396c27db166df3f371a808cba87ef4c3ffce9f54'
            '38850d42f3f35c96a66df404c1efbbe82b00c848988a5e6649deaee00a8b727c'
            'bc65b6e2bca1d8b304b063a4783dbcb845f0554202c7e2035f2cdca9b165d8ea'
            '60d7fd05a7c09649fff61f73d4573f20a95b7cd3ce822598f430232678542517'
            'a75eceeb8ea71583d3a717c330bc49591417c464c862c945cc471624cf061b4c'
            '17bfe3bf5dde380b16198d9f63c7f74032c1fa7e8530047625f0f413d365a722'
            '08031fdddd5e0ce66d5ca8343d0eac80345795a9c2a30610a67ca10acbb3f5a1'
            '491826ff49965c007137e95164fde7d28bd1eb24cf929406c5c6748259d54ea6'
            '83e9ffd3e74f6b533d5a3f5929e7918d31da690c66407cd1119f50d178fa94a0'
            '2ba56abdc75e319d1e7d4781bc9dc58c6fab4913409f3807826e2b4be1e863cd'
            'ee0bc5d559f1db2a1ed08a5809f43ffed0ad766c68511842224342dafeb90119'
            '8fc79d516b59acbd83b30e90b9b040e9a2d170176eff9644dd15f37eab7f0494'
            '8f5fb143a9dd976dd5167491b2167542e51ee59156fa67bc0cf0227ef8b11a76'
            '73c5df6e9cba1d8581a22151c54c5fc12618de13301e6b705e619d1674dc6de8'
            '6200acb5a9bdbae33dcf2cc4c48f4285bed682795a35f0dde578fbaa4081af0b'
            'da54a0159eb5f8d018519985810debf9076589cfccf86758087b9397a6d679e4'
            'd3e9f05cb5d46eca6cb3901687aa26facf64c29188781dd8ef2126bfee43b739'
            '194218d799986c00fcc2b23e47ad6e0759b9e5419997283efa418b78ce67c381'
            'ff7f258af074036e15035a4c34b3c52c7de802a0fff41e3d78ac0c6ad5814086'
            '04d0f780fd9473aefd02b6222ad3c7a40ed52c58a97fc2ec1075ab58199ee17f'
            'e1f0291fa6e9d2c54b31c2b06871f28b6a8f2929409850436fb4e37562b6c03d'
            'cbe411abc6a9c73ebe556709bc614afdfff521a81c529836f8d30770300a13cd'
            'ac746f8ef1eaf62fcce037e0b1349df6e0b9eea22ed17a10b7eb1b879b5b46ff'
            '368576a1c06c8aeca1efd8947c59b6a172f963e2b9ef53e4bcd4321923f84ba6'
            'c24a75361f94522ec48ee0104832ca7e7ec5182bc42c2b61176b44799fc4b676'
            '8b779722fec7c9b699752c4f09c3c58bff35b9d686db3de70c513bf99ace293d'
            'cc5a2b762426e9cda5a3ae056bba266b5f775ee75c0590913839c255d5f10db0'
            '49d4b19885806f82bff65b09e575488de176e63d91fad4545fda520aec96af82'
            '8e5f2d183c05d740d5eb1c3a191796bfb43743a462c5a0341f8171202f048af7')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd qtwebkit-${_pkgver}

  apply_patch_with_msg \
    0003-qtwebkit-mfence-mingw.patch \
    0006-python-output-unix-line-endings.patch \
    0008-fix-using-msys-perl.patch \
    0011-mingw-posix-layout-files.patch \
    0015-use-proper-import-suffix-mingw.patch \
    0014-disable-asm-win64.patch \
    0016-separate-debug-into-bin.patch

  # Upstream patches
  apply_patch_with_msg \
    0202-install-private-headers-and-modules.patch

  apply_patch_with_msg \
    0300-fix-generating-module-files.patch \
    0501-build-gcc9.patch \
    0502-clang-compat.patch \
    0503-python3.patch \
    0504-qwebinspector-crash-workaround.patch \
    0601-missing-include.patch

  # Fix build with bison 3.7+
  apply_patch_with_msg \
    d92b11fea65364fefa700249bd3340e0cd4c5b31.patch \
    6e18844035d9c5e25d770ed817b858c2423d894b.patch \
    4e1f209f21c3958e5a8d3a0646dccf9e429ca7e4.patch

  apply_patch_with_msg \
    0801-suppress-some-warnings.patch \
    0802-properly-link-with-system-libs.patch \
    0803-search-for-proper-prl-filename.patch \
    0804-use-executable_allocator_demand.patch \
    0805-libxml2-updates.patch \
    0806-codegen-python3-literal-syntax.patch \
    0807-deprecated-pure-parser.patch \
    0808-remove-bogus-defines.patch \
    0809-non-literal-format-string.patch \
    0810-webkit-static-no-export.patch \
    0811-remove-user-defined-copy.patch \
    0812-final-class-inheritance.patch \
    0813-logical-expression-operands.patch \
    0814-cachevalidation-string-concatenation.patch \
    0815-missing-override.patch \
    0816-printing-size_t-correctly.patch

  apply_patch_with_msg \
    0901-minimal-private-libs.patch
}

build() {
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  export PATH="${MINGW_PREFIX}/qt5-static/bin:${MINGW_PREFIX}/qt5-static/lib:${PATH}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake -Wno-dev \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX}/qt5-static \
    "${_extra_config[@]}" \
    -DPORT=Qt \
    -DQT_BUNDLED_JPEG=ON \
    -DQT_BUNDLED_PNG=ON \
    -DUSE_THIN_ARCHIVES=OFF \
    ../qtwebkit-${_pkgver}

  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  cd build-${MSYSTEM}

  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .

  # Remove unused libs
  rm -f ${pkgdir}${MINGW_PREFIX}/qt5-static/lib/{libANGLESupport,libWTF}.a

  install -Dm755 bin/QtTestBrowser.exe ${pkgdir}${MINGW_PREFIX}/qt5-static/bin/QtTestBrowser.exe
}
