# Maintainer: David Demelier <markand@malikania.fr

_realname=vera++
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3.0
pkgrel=8
pkgdesc="A programmable tool for verification, analysis and transformation of C++ source code"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://bitbucket.org/verateam/vera"
license=('spdx:BSL-1.0')
depends=("${MINGW_PACKAGE_PREFIX}-tcl"
         "${MINGW_PACKAGE_PREFIX}-boost"
         "${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake")
source=(https://bitbucket.org/verateam/vera/downloads/${_realname}-${pkgver}.tar.gz
        0001-fix_python_detection_issues.patch)
sha256sums=('9415657a09438353489db10ca860dd6459e446cfd9c649a1a2e02268da66f270'
            '6e801c0f612962b2de75a17de4651e4f769032b35975cb3bdec5defa00697a69')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Np0 -i "${srcdir}/0001-fix_python_detection_issues.patch"
}

build() {
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DVERA_LUA=Off \
    -DVERA_USE_SYSTEM_BOOST=ON \
    -DPYTHON_EXECUTABLE=${MINGW_PREFIX}/bin/python \
    ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  cd build-${MSYSTEM}
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .
}
