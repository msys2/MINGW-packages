# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=mesa
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=10.5.6
pkgrel=1
pkgdesc="Open-source implementation of the OpenGL specification (mingw-w64)"
arch=('any')
depends=("")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-clang"
             "scons"
             "libxml2-python"
             "python2-mako")
url="http://www.mesa3d.org"
license=('MIT')
options=('staticlibs' 'strip')
source=(ftp://ftp.freedesktop.org/pub/mesa/${pkgver}/${_realname}-${pkgver}.tar.xz{,.sig}
        001-extra-libs.patch)
md5sums=('7a845c50fc8aaae4c9373e795c1f491d'
         'SKIP'
         '3eaea880ab48f851c392c2106a616e21')

case ${MINGW_CHOST} in
  i686*)
    _mach=x86
  ;;
  x86_64*)
    _mach=x86_64
  ;;
esac

prepare() {
  cd ${srcdir}/${_realname}-$pkgver
  patch -p1 -i ${srcdir}/001-extra-libs.patch
}

build() {
  cd ${srcdir}/${_realname}-$pkgver

  LLVM=${MINGW_PREFIX} \
  CFLAGS="-march=core2 -pipe -D_USE_MATH_DEFINES" \
  CXXFLAGS="-march=core2 -pipe -std=c++11 -D_USE_MATH_DEFINES" \
  LDFLAGS="-static -s" \
  scons build=release platform=windows toolchain=mingw machine=${_mach} libgl-gdi #mesagdi
}

package() {
  cd ${srcdir}/${_realname}-$pkgver

  mkdir -p ${pkgdir}${MINGW_PREFIX}/bin
  #mkdir -p ${pkgdir}${MINGW_PREFIX}/lib/mesa
  local _dll_files=( $(find ${srcdir}/${_realname}-$pkgver -type f -name "*.dll") )
  cp -f ${_dll_files[@]} ${pkgdir}${MINGW_PREFIX}/bin/
  #local _dlla_files=( $(find ${srcdir}/Mesa-$pkgver -type f -name "*.a") )
  #cp -f ${_dlla_files[@]} ${pkgdir}${MINGW_PREFIX}/lib/mesa/
}
