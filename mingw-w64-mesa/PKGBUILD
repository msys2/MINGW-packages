# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=mesa
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=19.3.0rc3
fpkgver=19.3.0-rc3
pkgrel=1
pkgdesc="Open-source implementation of the OpenGL specification (mingw-w64)"
arch=('any')
makedepends=("${MINGW_PACKAGE_PREFIX}-llvm"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-zlib"
             "${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-python3-mako"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")
optdepends=("${MINGW_PACKAGE_PREFIX}-opengl-man-pages: for the OpenGL API man pages")
url="https://www.mesa3d.org/"
license=('MIT')
options=('staticlibs' 'strip')
source=(https://mesa.freedesktop.org/archive/${_realname}-${fpkgver}.tar.xz{,.sig}
        001-extra-libs.patch
        osmesa.pc
        llvmwrapgen.sh)
sha256sums=('3a1d6da04e0378609dc2a1f65c62f88ffbc533ce8373fb5fbbb372fc0ffc0b10'
            'SKIP'
            'bc9bb5013ac80ded47ad164ae1ef58cc9a39784eb4bf61e8c7d654bb273b05a9'
            'fdf26548336cc7e5700560c6636a87ffa63daa3048fa94cf4a4a0b50890c9327'
            '2c8ed04df376158ed045f5035cc4f98762aa93565c2bf52d8ffb06cc215f6a34')
validpgpkeys=('8703B6700E7EE06D7A39B8D6EDAE37B02CEB490D') # Emil Velikov <emil.l.velikov@gmail.com>
validpgpkeys+=('946D09B5E4C9845E63075FF1D961C596A7203456') # Andres Gomez <tanty@igalia.com>
validpgpkeys+=('E3E8F480C52ADD73B278EE78E1ECBE07D7D70895') # Juan Antonio Su√°rez Romero (Igalia, S.L.) <jasuarez@igalia.com>"
validpgpkeys+=('A5CC9FEC93F2F837CB044912336909B6B25FADFA') # Juan A. Suarez Romero <jasuarez@igalia.com>
validpgpkeys+=('71C4B75620BC75708B4BDB254C95FAAB3EB073EC') # Dylan Baker <dylan@pnwbakers.com>

noextract=(${_realname}-${fpkgver}.tar.xz)

case ${MINGW_CHOST} in
  i686*)
    _mach=x86
  ;;
  x86_64*)
    _mach=x86_64
  ;;
esac

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  [[ -d ${srcdir}/${_realname}-${fpkgver} ]] && rm -rf ${srcdir}/${_realname}-${fpkgver}
  tar -xJf ${srcdir}/${_realname}-${fpkgver}.tar.xz -C ${srcdir} || true

  cd ${srcdir}/${_realname}-${fpkgver}
  ${srcdir}/llvmwrapgen.sh

# Disable this patch.  llvm-config does not return the proper
# values and we probably don't need to call it anyway.

#  patch -p1 -i ${srcdir}/001-extra-libs.patch
}

buildcmd(){
  ${MINGW_PREFIX}/bin/meson ./build/windows-${_mach} --default-library=static --buildtype=release -Dllvm=true --backend=ninja -Dosmesa=gallium -Dbuild-tests=true
  ${MINGW_PREFIX}/bin/ninja -C ./build/windows-${_mach}
}

build() {
  cd ${srcdir}/${_realname}-${fpkgver}
  CFLAGS="-march=core2 -pipe" \
  CXXFLAGS="-march=core2 -pipe" \
  LDFLAGS="-static -s" \
  PROCESSOR_ARCHITECTURE="${CARCH}" \
  buildcmd
}

package() {
  cd ${srcdir}/${_realname}-${fpkgver}
  mkdir -p ${pkgdir}${MINGW_PREFIX}/bin
  mkdir -p ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig
  mkdir -p ${pkgdir}${MINGW_PREFIX}/include/mesa/GL

  cp -f ${srcdir}/${_realname}-${fpkgver}/build/windows-${_mach}/src/gallium/targets/libgl-gdi/opengl32.dll ${pkgdir}${MINGW_PREFIX}/bin/
  cp -f ${srcdir}/${_realname}-${fpkgver}/build/windows-${_mach}/src/gallium/targets/graw-gdi/graw.dll ${pkgdir}${MINGW_PREFIX}/bin/
  cp -f ${srcdir}/${_realname}-${fpkgver}/build/windows-${_mach}/src/gallium/targets/graw-null/graw_null.dll ${pkgdir}${MINGW_PREFIX}/bin/
  cp -f ${srcdir}/${_realname}-${fpkgver}/build/windows-${_mach}/src/gallium/targets/graw-null/libgraw_util.a ${pkgdir}${MINGW_PREFIX}/lib/
  cp -f ${srcdir}/${_realname}-${fpkgver}/build/windows-${_mach}/src/gallium/targets/osmesa/osmesa.dll ${pkgdir}${MINGW_PREFIX}/bin/
  cp -f ${srcdir}/${_realname}-${fpkgver}/build/windows-${_mach}/src/gallium/state_trackers/osmesa/libosmesa_st.a ${pkgdir}${MINGW_PREFIX}/lib/

  cp -f ${srcdir}/${_realname}-${fpkgver}/include/GL/gl.h ${pkgdir}${MINGW_PREFIX}/include/mesa/GL/
  cp -f ${srcdir}/${_realname}-${fpkgver}/include/GL/osmesa.h ${pkgdir}${MINGW_PREFIX}/include/mesa/GL/
  cp -f ${srcdir}/osmesa.pc ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/
  # fixup our .pc file with valid info
  sed -e "s|/mingw64|${MINGW_PREFIX}|g" \
    -e "s|8|${fpkgver}|g" \
    -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/osmesa.pc
}
