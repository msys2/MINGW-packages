# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gettext
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=0.18.3.2
pkgrel=2
arch=('any')
groups=("${_mingw_suff}")
pkgdesc="GNU internationalization library (mingw-w64)"
depends=("${_mingw_suff}-gcc-libs" "${_mingw_suff}-libiconv") # "${_mingw_suff}-termcap"
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-ncurses")
options=('strip' 'staticlibs')
license=('GPL' 'LGPL')
url="http://www.gnu.org/software/gettext/"
source=("http://ftp.gnu.org/pub/gnu/${_realname}/${_realname}-$pkgver.tar.gz"{,.sig}
    00-relocatex-libintl-0.18.3.1.patch
    120-Fix-Woe32-link-errors-when-compiling-with-O0.patch
    04-mingw-script-slash-fix.mingw.patch
    05-always-use-libintl-vsnprintf.mingw.patch
    06-dont-include-ctype-after-gnulibs-wctype.mingw.patch
    07-fix-asprintf-conflict.mingw.patch)
md5sums=('241aba309d07aa428252c74b40a818ef'
         'SKIP'
         '91921076ca88a28db98af7189408f469'
         '9fa1ff735793384e30e79f2670b29afd'
         '1d359f5ee347c2c0597d1a3d9a3770d7'
         '27852a388b8cf38188dc392c244230ff'
         'f69747f43f279b8a81286cfe5916b82f'
         '3ebccf730ec3377b068027eb2283afb2')

prepare() {
  cd "$srcdir/${_realname}-$pkgver"
  patch -p1 -i ${srcdir}/00-relocatex-libintl-0.18.3.1.patch
  patch -p0 -i ${srcdir}/04-mingw-script-slash-fix.mingw.patch
  patch -p0 -i ${srcdir}/05-always-use-libintl-vsnprintf.mingw.patch
  patch -p0 -i ${srcdir}/06-dont-include-ctype-after-gnulibs-wctype.mingw.patch
  patch -p0 -i ${srcdir}/07-fix-asprintf-conflict.mingw.patch
  patch -p1 -i ${srcdir}/120-Fix-Woe32-link-errors-when-compiling-with-O0.patch
  
  # libtoolize --automake --copy --force
  # ./autogen.sh --quick --skip-gnulib
}

build()
{
  CFLAGS+=" -D__USE_MINGW_ANSI_STDIO=1 -fexceptions --param=ssp-buffer-size=4"
  CXXFLAGS+=" -D__USE_MINGW_ANSI_STDIO=1 -fexceptions --param=ssp-buffer-size=4"

  mkdir -p $srcdir/build-${MINGW_CHOST} && cd $srcdir/build-${MINGW_CHOST}
  # export lt_cv_deplibs_check_method='pass_all'
  ${srcdir}/${_realname}-$pkgver/configure \
    --prefix=${MINGW_PREFIX} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --disable-java \
    --disable-native-java \
    --disable-csharp \
    --enable-static \
    --enable-threads=win32 \
    --without-emacs \
    --disable-openmp \
    --without-cvs \
    --without-git \
    --with-included-libcroco \
    --with-included-libunistring \
    --with-included-libxml \
    --with-included-glib \
    --with-libncurses-prefix=${MINGW_PREFIX}
  make
}

package() {
  cd $srcdir/build-${MINGW_CHOST}
  make DESTDIR="$pkgdir" install

  # Drop some useless tools & files
  #rm -rf "${pkgdir}${MINGW_PREFIX}/lib/gettext"
  rm -rf "${pkgdir}${MINGW_PREFIX}/share/man"
  rm -rf "${pkgdir}${MINGW_PREFIX}/share/info"
}
