# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libvorbis
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.3.4
pkgrel=1
pkgdesc="Vorbis codec library (mingw-w64)"
arch=('any')
url="http://xiph.org"
license=('custom')
depends=("${_mingw_suff}-libogg" "${_mingw_suff}-gcc-libs")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config")
options=('strip' 'staticlibs')
source=(http://downloads.xiph.org/releases/vorbis/${_realname}-${pkgver}.tar.gz)
md5sums=('8851c593a52d1ef9c526d95174873852')

build() {
	mkdir -p "${srcdir}/build-${MINGW_CHOST}"
	cd "${srcdir}/build-${MINGW_CHOST}"
	"${srcdir}"/${_realname}-${pkgver}/configure \
		--enable-shared \
		--enable-static \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--target=${MINGW_CHOST}
	make
}

package() {
	cd ${srcdir}/build-${MINGW_CHOST}  
	make DESTDIR="$pkgdir" install
	rm -r ${pkgdir}${MINGW_PREFIX}/share/doc
}
