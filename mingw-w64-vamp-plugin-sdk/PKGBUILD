# Maintainer: Jeff Hubbard <musikernel@gmail.com>

_realname=vamp-plugin-sdk
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.10.0
pkgrel=4
pkgdesc="Vamp plugins extract descriptive information from audio data (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://www.vamp-plugins.org/"
msys2_repository_url="https://github.com/vamp-plugins/vamp-plugin-sdk"
license=("spdx:BSD-3-Clause AND MIT")
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-libsndfile")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools")
source=("https://github.com/vamp-plugins/vamp-plugin-sdk/releases/download/vamp-plugin-sdk-v${pkgver%.*}/${_realname}-${pkgver}.tar.gz"
        "01-mingw-shared.patch"
        "02-clang.patch")
sha256sums=('aeaf3762a44b148cebb10cde82f577317ffc9df2720e5445c3df85f3739ff75f'
            'b3525265a6104403baadf2239a06a73da556eb894d956274d863bf6e434c8e83'
            '30a92217663b9a7755a1c9af5db7f00309f1d0dc4d3abb38712c46e8bbcb4c5c')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}"/01-mingw-shared.patch

  if [[ $MINGW_PACKAGE_PREFIX == *-clang-* ]]; then
    patch -p1 -i "${srcdir}"/02-clang.patch
  fi
}

build() {
  [[ -d "build-${MSYSTEM}" ]] && rm -rf "build-${MSYSTEM}"
  cp -rf "${_realname}-${pkgver}" "build-${MSYSTEM}"
  cd build-${MSYSTEM}

  CFLAGS+=" -D_USE_MATH_DEFINES"
  CPPFLAGS+=" -D_USE_MATH_DEFINES"
  ./configure \
    --prefix=${MINGW_PREFIX}

  make -j1
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}

  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/COPYING \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
