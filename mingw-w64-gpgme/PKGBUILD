# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gpgme
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.4.3
pkgrel=1
pkgdesc="A C wrapper library for GnuPG"
arch=('any')
url="http://www.gnupg.org/related_software/gpgme/"
license=('LGPL')
depends=("${_mingw_suff}-libgpg-error"
    "${_mingw_suff}-w32pth"
    "${_mingw_suff}-gnupg")
options=('!emptydirs')
source=(ftp://ftp.gnupg.org/gcrypt/${_realname}/${_realname}-${pkgver}.tar.bz2{,.sig})
md5sums=('334e524cffa8af4e2f43ae8afe585672'
         'SKIP')

build() {
  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"
  "${srcdir}"/gpgme-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
  --libexecdir=${MINGW_PREFIX}/lib \
  --disable-fd-passing \
  --disable-static \
    --disable-gpgsm-test
  make
}

check() {
  cd "${srcdir}/build-${CARCH}"
  make check
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR=${pkgdir} install
}
