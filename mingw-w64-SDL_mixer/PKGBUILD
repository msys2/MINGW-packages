# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=SDL_mixer
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=1.2.12
pkgrel=1
pkgdesc="A simple multi-channel audio mixer (mingw-w64)"
arch=('any')
url="http://www.libsdl.org/projects/SDL_mixer/"
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-SDL"
         "${MINGW_PACKAGE_PREFIX}-libvorbis"
         "${MINGW_PACKAGE_PREFIX}-libmikmod"
         "${MINGW_PACKAGE_PREFIX}-smpeg"
         )
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-fluidsynth")
optdepends=("${MINGW_PACKAGE_PREFIX}-fluidsynth: MIDI software synth, replaces built-in timidity")
source=(http://www.libsdl.org/projects/SDL_mixer/release/${_realname}-${pkgver}.tar.gz
        mikmod1.patch
        mikmod2.patch
        fluidsynth-volume.patch
        double-free-crash.patch)
md5sums=('e03ff73d77a55e3572ad0217131dc4a1'
         'c335d4ff1fe86950d1fc730745c9c96d'
         'd13d98fef8ec51167a370fa7976a7a7f'
         'eceb06e3a8b31fee300d7f33478401d4'
         'b38c1b23751ca95631badeb9327d085e')
noextract=(${_realname}-${pkgver}.tar.gz)

prepare() {
  [[ -d ${srcdir}/${_realname}-${pkgver} ]] && rm -rf ${srcdir}/${_realname}-${pkgver}
  tar -xzf ${srcdir}/${_realname}-${pkgver}.tar.gz -C ${srcdir} || true
  
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -Np1 -i ${srcdir}/mikmod1.patch
  patch -Np1 -i ${srcdir}/mikmod2.patch
  patch -Np1 -i ${srcdir}/fluidsynth-volume.patch
  patch -Np1 -i ${srcdir}/double-free-crash.patch

  #sed -e "/CONFIG_FILE_ETC/s|/etc/timidity.cfg|/etc/timidity++/timidity.cfg|" \
  #    -e "/DEFAULT_PATH/s|/etc/timidity|/etc/timidity++|" \
  #    -e "/DEFAULT_PATH2/s|/usr/local/lib/timidity|/usr/lib/timidity|" \
  #    -i timidity/config.h
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --enable-static

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}/"

  make DESTDIR="${pkgdir}/" install
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" \
    "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
