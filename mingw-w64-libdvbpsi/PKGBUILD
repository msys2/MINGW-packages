# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libdvbpsi
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3.0
pkgrel=1
pkgdesc="A library designed for decoding and generation of MPEG TS and DVB PSI tables (mingw-w64)"
arch=('any')
url="https://www.videolan.org/developers/libdvbpsi.html"
license=("LGPL2.1")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
options=('staticlibs' 'strip')
source=("https://download.videolan.org/pub/${_realname}/${pkgver}/${_realname}-${pkgver}.tar.bz2")
sha256sums=('a2fed1d11980662f919bbd1f29e2462719e0f6227e1a531310bd5a706db0a1fe')

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-release

  make SUBDIRS=src
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make SUBDIRS=src DESTDIR="${pkgdir}" install
}
