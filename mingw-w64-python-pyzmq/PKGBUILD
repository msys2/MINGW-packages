# Maintainer: Patrick Stewart <patstew@gmail.com>

_realname=pyzmq
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}" "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=17.1.2
pkgrel=1
pkgdesc="Python bindings for zeromq, written in Cython (mingw-w64)"
arch=('any')
url="http://www.zeromq.org/bindings:python"
license=('LGPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-python2-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-cython2"
             "${MINGW_PACKAGE_PREFIX}-cython"
             "${MINGW_PACKAGE_PREFIX}-zeromq")
depends=("${MINGW_PACKAGE_PREFIX}-zeromq")

source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/zeromq/${_realname}/archive/v${pkgver}.tar.gz"
        "001-mingw-python.patch")
sha256sums=('77a32350440e321466b1748e6063b34a8a73768b62cb674e7d799fbc654b7c45'
            'b53a9b6712f1665d398689d45a3981a6f63d4e54664aa2b0b6e83edea11f1a1b')
prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/001-mingw-python.patch

  cd ${srcdir}
  cp -a ${_realname}-${pkgver} py2zmq-${pkgver}
}

all_build() {
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/${1} setup.py install \
    --prefix=${MINGW_PREFIX} --root=${pkgdir} --optimize=1 --zmq=${MINGW_PREFIX}

  find "${pkgdir}/" -name "*.pyc" -delete
  find "${pkgdir}/" -type d -empty -delete

  rm -rf build

  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  # fix absolute paths in config files
  find "${pkgdir}${MINGW_PREFIX}" -name "*.json" -exec sed -e "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" -i {} \;
}

package_python3-pyzmq() {
  local interpreter=python3
  depends=("${MINGW_PACKAGE_PREFIX}-python3" "${MINGW_PACKAGE_PREFIX}-zeromq")

  cd "${srcdir}/${_realname}-${pkgver}"

  sed -i 's|except socket.error, e:|except socket.error as e:|' zmq/eventloop/ioloop.py
  sed -i 's|except gevent.Timeout, t:|except gevent.Timeout as t:|' zmq/green/core.py

  all_build ${interpreter}
}

package_python2-pyzmq() {
  local interpreter=python2
  depends=("${MINGW_PACKAGE_PREFIX}-python2" "${MINGW_PACKAGE_PREFIX}-zeromq")

  cd "${srcdir}/py2zmq-${pkgver}"

  rm -f ./buildutils/include_win32/stdint.h

  all_build ${interpreter}
}

package_mingw-w64-i686-python2-pyzmq() {
  package_python2-pyzmq
}

package_mingw-w64-i686-python3-pyzmq() {
  package_python3-pyzmq
}

package_mingw-w64-x86_64-python2-pyzmq() {
  package_python2-pyzmq
}

package_mingw-w64-x86_64-python3-pyzmq() {
  package_python3-pyzmq
}
