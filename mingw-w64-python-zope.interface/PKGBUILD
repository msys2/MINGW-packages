# Contributor: Frederic Wang <fred.wang@free.fr>

_realname=zope.interface
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
provides=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
replaces=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=5.2.0
pkgrel=2
pkgdesc='Interfaces for Python (mingw-w64)'
url=' https://github.com/zopefoundation/zope.interface'
license=('ZPL 2.1')
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-cc")
_dtoken="ac/8a/657532df378c2cd2a1fe6b12be3b4097521570769d4852ec02c24bd3594e"
source=("${_realname}-${pkgver}.tar.gz::https://github.com/zopefoundation/zope.interface/archive/${pkgver}.tar.gz")
sha256sums=('2abe865ee27fd1faadaf4a292d2cbd126cb7d3bc0d3ac275f4dff7bb9bf73539')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}"

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE.txt" "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
