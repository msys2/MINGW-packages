# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_font=inconsolatalgc
_realname=ttf-inconsolata-lgc-nerd
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgdesc="Patched font Inconsolata LGC from nerd fonts library (mingw-w64)"
pkgver=3.0.2
pkgrel=1
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://github.com/ryanoasis/nerd-fonts'
_urlbase="${url}/releases/download/v${pkgver}"
license=('spdx:OFL-1.1')
options=('!debug' '!strip')
groups=("${MINGW_PACKAGE_PREFIX}-nerd-fonts")
source=("${_font}-${pkgver}.tar.xz::${_urlbase}/InconsolataLGC.tar.xz")
sha256sums=('eabc980d00bdc4d0e27d25b67685d429c3c744b398daeab495668eef42014203')
noextract=("${_font}-${pkgver}.tar.xz")

prepare() {
  rm -rf ${_font}
  mkdir ${_font}
  bsdtar xf ${_font}-${pkgver}.tar.xz -C ${_font}
}

package() {
  install -Dm644 ${_font}/*.ttf -t "${pkgdir}${MINGW_PREFIX}/share/fonts/TTF"
  install -Dm644 ${_font}/LICENSE -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/"
}
