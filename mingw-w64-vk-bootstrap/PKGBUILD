# Maintainer: Felix Xing (Felixaszx) <felixaszx@outlook.com>

_realname=vk-bootstrap
# This commit contains Vulkan headers 1.4.318
_commit=20c6010a365b77c9c28c61a1e95b31e162a4c577
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.4.318
pkgrel=1
pkgdesc="A utility library that jump starts initialization of Vulkan (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://charles-lunarg.github.io/vk-bootstrap/"
msys2_repository_url="https://github.com/charles-lunarg/vk-bootstrap"
license=("spdx:MIT")
depends=("${MINGW_PACKAGE_PREFIX}-vulkan-headers")
makedepends=("git"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-vulkan-headers")
source=("${_realname}"::"git+https://github.com/charles-lunarg/vk-bootstrap.git#commit=${_commit}")
sha256sums=('0b448608b740aa7e3ddaeac990a7f8f8c78a8b34b5c0951ea184ba6346cf05db')

build() {
  cd ${_realname}
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

# For some reasons -DBUILD_SHARED_LIBS=ONs does not work, it will only build static lib
# This is an upstream issue
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DBUILD_SHARED_LIBS=ON \
      -DVK_BOOTSTRAP_TEST=OFF \
      -DVK_BOOTSTRAP_VULKAN_HEADER_DIR="${MSYSTEM}/include" \
      "${extra_config[@]}" \
      -B "build-${MSYSTEM}"

  cmake --build "build-${MSYSTEM}"
}

package() {
  cd ${_realname}
  DESTDIR="${pkgdir}" cmake --install "build-${MSYSTEM}"

  install -Dm644 "${srcdir}/${_realname}/LICENSE.txt" \
    "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
