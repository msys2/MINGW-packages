# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=podman
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=4.9.3
pkgrel=1
pkgdesc='Tool for running OCI-based containers in pods (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/containers/podman"
license=('spdx:Apache-2.0')
makedepends=(
    "${MINGW_PACKAGE_PREFIX}-go"
    "${MINGW_PACKAGE_PREFIX}-cc"
    "${MINGW_PACKAGE_PREFIX}-python"
    "git"
)
options=('!strip')
_GV_VERSION="0.7.3"  # See GV_VERSION in Makefile
source=("https://github.com/containers/podman/archive/v$pkgver/${_realname}-${pkgver}.tar.gz"
        "https://github.com/containers/gvisor-tap-vsock/archive/v${_GV_VERSION}/gvisor-tap-vsock-${_GV_VERSION}.tar.gz")
sha256sums=('37afc5bba2738c68dc24400893b99226c658cc9a2b22309f4d7abe7225d8c437'
            '851ed29b92e15094d8eba91492b6d7bab74aff4538dae0c973eb7d8ff48afd8a')
noextract=("${_realname}-${pkgver}.tar.gz")

prepare() {
    echo "Extracting ${_realname}-${pkgver}.tar.gz ..."
    tar -xzf ${_realname}-${pkgver}.tar.gz | true
    rm -rf build-${MSYSTEM}
    cp -r ${_realname}-${pkgver} build-${MSYSTEM}

    cp -r "gvisor-tap-vsock-${_GV_VERSION}" "build-proxy-${MSYSTEM}"
}

build() {
    export GOOS=windows
    export GOROOT=${MINGW_PREFIX}/lib/go
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GO_LDFLAGS="-s -w"
    export GOFLAGS="-trimpath -modcacherw -ldflags=-linkmode=external"
    case "${CARCH}" in
      i686|x86_64)
        GOFLAGS+=" -buildmode=pie"
        ;;
    esac

    cd  "${srcdir}/build-proxy-${MSYSTEM}"
    make win-gvproxy win-sshproxy

    cd "${srcdir}/build-${MSYSTEM}"
    # parallel make breaks markdown processing
    make -j1 podman-remote CGO_ENABLED=1
    make -j1 docker-docs CGO_ENABLED=1
}

package() {
    cd "build-${MSYSTEM}"

    # copy the proxy binaries where install.remote expects them
    cp "${srcdir}/build-proxy-${MSYSTEM}/bin/"* "./bin/windows"

    make install.remote install.docker-full install.man install.completions DESTDIR="$pkgdir" PREFIX=${MINGW_PREFIX}
    rm -Rf "${pkgdir}${MINGW_PREFIX}/lib"
    sed -i "s|/usr/bin/||g" "${pkgdir}${MINGW_PREFIX}/bin/docker"
}
