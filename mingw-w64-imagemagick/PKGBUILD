# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=imagemagick
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=6.8.9.0
pkgrel=1
pkgdesc="An image viewing/manipulation program (mingw-w64)"
arch=('any')
url="http://www.imagemagick.org/"
license=("custom")
makedepends=("${_mingw_suff}-gcc"
    "${_mingw_suff}-pkg-config"
    "${_mingw_suff}-pango"
    "${_mingw_suff}-cairo"
    "${_mingw_suff}-openexr"
    "${_mingw_suff}-librsvg"
    "${_mingw_suff}-libxml2"
    "${_mingw_suff}-jasper"
    "${_mingw_suff}-libpng"
    "${_mingw_suff}-libtiff"
    "${_mingw_suff}-libwebp")
depends=("${_mingw_suff}-libtool"
    "${_mingw_suff}-djvulibre"
    "${_mingw_suff}-lcms2"
    "${_mingw_suff}-fontconfig"
    "${_mingw_suff}-freetype"
    "${_mingw_suff}-bzip2"
    "${_mingw_suff}-zlib"
    "${_mingw_suff}-liblqr"
    "${_mingw_suff}-fontconfig"
    "${_mingw_suff}-xz"
    "${_mingw_suff}-glib2"
    "${_mingw_suff}-fftw"
    "${_mingw_suff}-openjpeg2"
    #"${_mingw_suff}-perl"
    )
optdepends=("${_mingw_suff}-ghostscript: for Ghostscript support"
      "${_mingw_suff}-openexr: for OpenEXR support"
      "${_mingw_suff}-librsvg: for SVG support"
      "${_mingw_suff}-libxml2: for XML support"
      "${_mingw_suff}-jasper: for JPEG-2000 support"
      "${_mingw_suff}-libpng: for PNG support"
      "${_mingw_suff}-libtiff: for PNG support"
      "${_mingw_suff}-libwebp: for WEBP support")
options=('staticlibs' 'strip')
source=(ftp://ftp.sunet.se/pub/multimedia/graphics/ImageMagick/ImageMagick-${pkgver%.*}-${pkgver##*.}.tar.xz{,.asc}
    ImageMagick-6.8.8.1-mingw.patch
    winpath.patch)
md5sums=('d89b4b5db15cf8fb1486cec1067244ae'
         'SKIP'
         '7ecd727d1d9baad5f9b41ebcb2facdbe'
         'b0b634231da667d7472522fc08fd3519')

prepare() {
  cd ImageMagick-${pkgver%.*}-${pkgver##*.}
  patch -p1 -i ${srcdir}/ImageMagick-6.8.8.1-mingw.patch
  patch -p1 -i ${srcdir}/winpath.patch
  autoreconf -fi
}

build() {
  export lt_cv_deplibs_check_method='pass_all'
  cd ImageMagick-${pkgver%.*}-${pkgver##*.}
  mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}  
  [[ $CARCH = "i686" ]] && EXTRAOPTS="--with-gcc-arch=i686"
  [[ $CARCH = "x86_64" ]] && EXTRAOPTS="--with-gcc-arch=x86-64"
  ../configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --with-modules \
    --enable-hdri \
    --with-wmf \
    --with-openexr \
    --with-xml \
    --with-lcms2 \
    --with-openjp2 \
    --with-lqr \
    --with-webp \
    --with-rsvg \
    --with-gslib \
    --without-gvc \
    --with-djvu \
    --without-autotrace \
    --without-jbig \
    --without-fpx \
    --without-dps \
    --with-fftw \
    --without-perl \
    --without-x \
    $EXTRAOPTS
    make
  
    #--enable-opencl \
    #--with-gslib \
    #--with-gs-font-dir=/usr/share/fonts/Type1 
    #--with-perl-options="INSTALLDIRS=vendor"    
}

package() {
  cd ImageMagick-${pkgver%.*}-${pkgver##*.}/build-${MINGW_CHOST}
  make -j1 DESTDIR="${pkgdir}" install
  #make DESTDIR="${pkgdir}" install-data-html
  install -Dm644 ${srcdir}/ImageMagick-${pkgver%.*}-${pkgver##*.}/LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/imagemagick/LICENSE"
  install -Dm644 ${srcdir}/ImageMagick-${pkgver%.*}-${pkgver##*.}/NOTICE "${pkgdir}${MINGW_PREFIX}/share/licenses/imagemagick/NOTICE"

  #rm -r "${pkgdir}${MINGW_PREFIX}/share"
}
