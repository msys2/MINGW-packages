# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=imagemagick
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=6.8.9.10
pkgrel=1
pkgdesc="An image viewing/manipulation program (mingw-w64)"
arch=('any')
url="http://www.imagemagick.org/"
license=("custom")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-pkg-config"
            "${MINGW_PACKAGE_PREFIX}-pango"
            "${MINGW_PACKAGE_PREFIX}-cairo"
            "${MINGW_PACKAGE_PREFIX}-openexr"
            "${MINGW_PACKAGE_PREFIX}-librsvg"
            "${MINGW_PACKAGE_PREFIX}-libxml2"
            "${MINGW_PACKAGE_PREFIX}-jasper"
            "${MINGW_PACKAGE_PREFIX}-libpng"
            "${MINGW_PACKAGE_PREFIX}-libtiff"
            "${MINGW_PACKAGE_PREFIX}-libwebp"
            "${MINGW_PACKAGE_PREFIX}-ghostscript")
depends=("${MINGW_PACKAGE_PREFIX}-libtool"
        "${MINGW_PACKAGE_PREFIX}-djvulibre"
        "${MINGW_PACKAGE_PREFIX}-lcms2"
        "${MINGW_PACKAGE_PREFIX}-fontconfig"
        "${MINGW_PACKAGE_PREFIX}-freetype"
        "${MINGW_PACKAGE_PREFIX}-bzip2"
        "${MINGW_PACKAGE_PREFIX}-zlib"
        "${MINGW_PACKAGE_PREFIX}-liblqr"
        "${MINGW_PACKAGE_PREFIX}-fontconfig"
        "${MINGW_PACKAGE_PREFIX}-xz"
        "${MINGW_PACKAGE_PREFIX}-glib2"
        "${MINGW_PACKAGE_PREFIX}-fftw"
        "${MINGW_PACKAGE_PREFIX}-openjpeg2"
        #"${MINGW_PACKAGE_PREFIX}-perl"
        )
optdepends=("${MINGW_PACKAGE_PREFIX}-ghostscript: for Ghostscript support"
      "${MINGW_PACKAGE_PREFIX}-openexr: for OpenEXR support"
      "${MINGW_PACKAGE_PREFIX}-librsvg: for SVG support"
      "${MINGW_PACKAGE_PREFIX}-libxml2: for XML support"
      "${MINGW_PACKAGE_PREFIX}-jasper: for JPEG-2000 support"
      "${MINGW_PACKAGE_PREFIX}-libpng: for PNG support"
      "${MINGW_PACKAGE_PREFIX}-libtiff: for PNG support"
      "${MINGW_PACKAGE_PREFIX}-libwebp: for WEBP support")
options=('staticlibs' 'strip' '!debug' 'libtool')
source=(ftp://ftp.sunet.se/pub/multimedia/graphics/ImageMagick/ImageMagick-${pkgver%.*}-${pkgver##*.}.tar.xz{,.asc}
        ImageMagick-6.8.8.1-mingw.patch
        001-relocate.patch
        winpath.patch)
md5sums=('d6f23aed68b766d421843a03ae038273'
         'SKIP'
         'c95a23e8562dbab4e6b0e5407da25209'
         '7821087a00c459c0d3482508fdc1e25e'
         'b0b634231da667d7472522fc08fd3519')

prepare() {
  cd ImageMagick-${pkgver%.*}-${pkgver##*.}
  patch -p1 -i ${srcdir}/ImageMagick-6.8.8.1-mingw.patch
  patch -p1 -i ${srcdir}/001-relocate.patch
  #patch -p1 -i ${srcdir}/winpath.patch
  autoreconf -fi
}

build() {
  export lt_cv_deplibs_check_method='pass_all'
  [[ $CARCH = "i686" ]] && EXTRAOPTS="--with-gcc-arch=i686"
  [[ $CARCH = "x86_64" ]] && EXTRAOPTS="--with-gcc-arch=x86-64"
  
  #cd ImageMagick-${pkgver%.*}-${pkgver##*.}
  mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}
  ../ImageMagick-${pkgver%.*}-${pkgver##*.}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-deprecated \
    --enable-hdri \
    --with-djvu \
    --with-fftw \
    --with-gslib \
    --without-gvc \
    --with-lcms2 \
    --with-lqr \
    --with-modules \
    --with-openexr \
    --with-openjp2 \
    --with-rsvg \
    --with-webp \
    --with-wmf \
    --with-xml \
    --without-autotrace \
    --without-dps \
    --without-fpx \
    --without-jbig \
    --without-ltdl \
    --without-perl \
    --without-x \
    $EXTRAOPTS

  make

    #--enable-opencl \
    #--with-gslib \
    #--with-gs-font-dir=/usr/share/fonts/Type1
    #--with-perl-options="INSTALLDIRS=vendor"
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make -j1 DESTDIR="${pkgdir}" install

  #find . -name "*.xml" -exec sed -i "s/${MINGW_PREFIX}/newWord/g" '{}' \;

  install -Dm644 ${srcdir}/ImageMagick-${pkgver%.*}-${pkgver##*.}/LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
  install -Dm644 ${srcdir}/ImageMagick-${pkgver%.*}-${pkgver##*.}/NOTICE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/NOTICE"
}
