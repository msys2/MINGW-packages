# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=imagemagick
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=7.0.3.5
pkgrel=2
pkgdesc="An image viewing/manipulation program (mingw-w64)"
arch=('any')
url="https://www.imagemagick.org/"
license=("custom")
makedepends=("${MINGW_PACKAGE_PREFIX}-cairo"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-ghostscript"
             "${MINGW_PACKAGE_PREFIX}-librsvg"
             "${MINGW_PACKAGE_PREFIX}-libxml2"
             "${MINGW_PACKAGE_PREFIX}-openexr"
             "${MINGW_PACKAGE_PREFIX}-pango"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
            )
depends=("${MINGW_PACKAGE_PREFIX}-bzip2"
         "${MINGW_PACKAGE_PREFIX}-djvulibre"
         "${MINGW_PACKAGE_PREFIX}-fftw"
         "${MINGW_PACKAGE_PREFIX}-fontconfig"
         "${MINGW_PACKAGE_PREFIX}-freetype"
         "${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-jasper"
         "${MINGW_PACKAGE_PREFIX}-jbigkit"
         "${MINGW_PACKAGE_PREFIX}-libraqm"
         "${MINGW_PACKAGE_PREFIX}-lcms2"
         "${MINGW_PACKAGE_PREFIX}-liblqr"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libtiff"
         "${MINGW_PACKAGE_PREFIX}-libtool"
         "${MINGW_PACKAGE_PREFIX}-libwebp"
         "${MINGW_PACKAGE_PREFIX}-openjpeg2"
         #"${MINGW_PACKAGE_PREFIX}-perl"
         "${MINGW_PACKAGE_PREFIX}-xz"
         "${MINGW_PACKAGE_PREFIX}-zlib"
        )
optdepends=("${MINGW_PACKAGE_PREFIX}-ghostscript: for Ghostscript support"
            "${MINGW_PACKAGE_PREFIX}-openexr: for OpenEXR support"
            "${MINGW_PACKAGE_PREFIX}-librsvg: for SVG support"
            "${MINGW_PACKAGE_PREFIX}-libxml2: for XML support"
            #"${MINGW_PACKAGE_PREFIX}-jasper: for JPEG-2000 support"
            #"${MINGW_PACKAGE_PREFIX}-libpng: for PNG support"
            #"${MINGW_PACKAGE_PREFIX}-libtiff: for PNG support"
            #"${MINGW_PACKAGE_PREFIX}-libwebp: for WEBP support"
            )
options=('staticlibs' 'strip' '!debug' 'libtool')
source=(https://www.imagemagick.org/download/ImageMagick-${pkgver%.*}-${pkgver##*.}.tar.xz{,.asc}
        ImageMagick-7.0.1.3-mingw.patch
        001-7.0.1.3-relocate.patch
        002-7.0.3.1-build-fixes.patch
        003-7.0.1.8-cplusplus-fixes.patch
        004-7.0.3.5-build-fixes.patch)
sha256sums=('fbf920be8ac523cf459d133c5694a284916fe8289fe26400a773ee7eaa9a27fb'
            'SKIP'
            '9e945a313f5da2962978ff7026c0cf93c0365afaf14c0f0e3223ce7917d81d2c'
            '919a1bccea333c525f24a96e3229f5a85e0261da907e6f983d05af0c5b8bdddf'
            '77330cc7da6dffbe09be59ccec134e4ba630c9e8a90f8ad43ee606e7a0553708'
            'f628755b79753c00a22471a9562f5abcba67702f2156b38aa0e9b83d3644960a'
            'b3471be1bd0ad4e0756ab206be53770e540e7242ca90f33bd1692f8ab6b53dc2')
#Lexie Parsimoniae (ImageMagick code signing key) <lexie.parsimoniae@imagemagick.org>
validpgpkeys=('D8272EF51DA223E4D05B466989AB63D48277377A')

prepare() {
  cd ImageMagick-${pkgver%.*}-${pkgver##*.}
  rm -f MagickCore/pathtools{.c,.h} > /dev/null 2>&1 || true
  patch -p1 -i ${srcdir}/ImageMagick-7.0.1.3-mingw.patch
  patch -p1 -i ${srcdir}/001-7.0.1.3-relocate.patch
  patch -p1 -i ${srcdir}/002-7.0.3.1-build-fixes.patch
  patch -p1 -i ${srcdir}/003-7.0.1.8-cplusplus-fixes.patch
  patch -p1 -i ${srcdir}/004-7.0.3.5-build-fixes.patch
  autoreconf -fi
}

build() {
  export lt_cv_deplibs_check_method='pass_all'
  [[ $CARCH = "i686" ]] && EXTRAOPTS="--with-gcc-arch=i686"
  [[ $CARCH = "x86_64" ]] && EXTRAOPTS="" #EXTRAOPTS="--with-gcc-arch=x86-64"

  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  # We can't use Magic++ portion due to a bug:
  # https://github.com/ImageMagick/ImageMagick/issues/188
  mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}
  ../ImageMagick-${pkgver%.*}-${pkgver##*.}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-deprecated \
    --enable-legacy-support \
    --enable-hdri \
    --with-djvu \
    --with-fftw \
    --with-gslib \
    --without-gvc \
    --with-lcms2 \
    --with-lqr \
    --with-modules \
    --with-openexr \
    --with-openjp2 \
    --enable-opencl \
    --with-rsvg \
    --with-webp \
    --with-wmf \
    --with-xml \
    --without-autotrace \
    --without-dps \
    --without-fpx \
    --with-jbig \
    --without-ltdl \
    --without-perl \
    --without-x \
    --with-lraqm \
    --with-magick-plus-plus \
    --with-windows-font-dir=c:/Windows/fonts \
    $EXTRAOPTS \
    CFLAGS="${CFLAGS}" CPPFLAGS="${CPPFLAGS}" LDFLAGS="${LDFLAGS}"

  if check_option "debug" "y"; then
    MAKE_VERBOSE="V=1"
  fi
  make ${MAKE_VERBOSE}

    #--enable-opencl \
    # --with-gs-font-dir=${MINGW_PREFIX}/ \
    # --with-dejavu-font-dir= \
    #--with-gslib \
    #--with-gs-font-dir=/usr/share/fonts/Type1
    #--with-perl-options="INSTALLDIRS=vendor"
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  if check_option "debug" "y"; then
    MAKE_VERBOSE="V=1"
  fi
  make -j1 DESTDIR="${pkgdir}" install ${MAKE_VERBOSE}

  #find . -name "*.xml" -exec sed -i "s/${MINGW_PREFIX}/newWord/g" '{}' \;

  install -Dm644 ${srcdir}/ImageMagick-${pkgver%.*}-${pkgver##*.}/LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
  install -Dm644 ${srcdir}/ImageMagick-${pkgver%.*}-${pkgver##*.}/NOTICE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/NOTICE"
}
