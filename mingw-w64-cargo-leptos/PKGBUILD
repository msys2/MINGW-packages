# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_basename=leptos
_realname=cargo-${_basename}
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.2.21
pkgrel=2
pkgdesc='Build tool for Leptos (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://leptos.dev/'
msys2_repository_url='https://github.com/leptos-rs/cargo-leptos'
msys2_references=(
  'archlinux: cargo-leptos'
)
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-binaryen"
         "${MINGW_PACKAGE_PREFIX}-cargo-generate"
         "${MINGW_PACKAGE_PREFIX}-rust-wasm")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
source=("${msys2_repository_url}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('3f6f7f0a28b33653387c7c7d0b1aa5b50b4a2343009540c28e9a3c63ad36a091')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # update psm to fix aarch64 build issue
  cargo update -p psm@0.1.23 --precise 0.1.24
  "${MINGW_PREFIX}/bin/cargo.exe" fetch --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cp -r "${_realname}-${pkgver}" "build-${MSYSTEM}"
  cd "build-${MSYSTEM}"

  "${MINGW_PREFIX}/bin/cargo.exe" build \
    --release \
    --frozen
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  "${MINGW_PREFIX}/bin/cargo.exe" test \
    --release \
    --frozen
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  install -Dm755 "target/release/${_realname}.exe" "${pkgdir}${MINGW_PREFIX}/bin/${_realname}.exe"

  cd "${srcdir}/build-${MSYSTEM}"

  install -Dm644 "README.md" "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/README.md"
  install -Dm644 "LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
