# Maintainer: Haroogan <Haroogan@gmail.com>
# Maintainer: Oscar Fuentes <ofv@wanadoo.es>

_realname=emacs
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=28.0.91
pkgrel=1
pkgdesc="The extensible, customizable, self-documenting, real-time display editor (mingw-w64)"
url="https://www.gnu.org/software/emacs/"
license=('spdx:GPL-3.0-only')
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
depends=(
    "${MINGW_PACKAGE_PREFIX}-cairo"
    "${MINGW_PACKAGE_PREFIX}-giflib"
    "${MINGW_PACKAGE_PREFIX}-gmp"
    "${MINGW_PACKAGE_PREFIX}-gnutls"
    "${MINGW_PACKAGE_PREFIX}-harfbuzz"
    "${MINGW_PACKAGE_PREFIX}-imagemagick" # not detected even with '--with-imagemagick'
    "${MINGW_PACKAGE_PREFIX}-jansson"
    "${MINGW_PACKAGE_PREFIX}-lcms2"
    "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
    "${MINGW_PACKAGE_PREFIX}-libpng"
    "${MINGW_PACKAGE_PREFIX}-librsvg"
    "${MINGW_PACKAGE_PREFIX}-libtiff"
    "${MINGW_PACKAGE_PREFIX}-libxml2"
    "${MINGW_PACKAGE_PREFIX}-xpm-nox"
    "${MINGW_PACKAGE_PREFIX}-zlib"
)
makedepends=(
    "${MINGW_PACKAGE_PREFIX}-autotools"
    "${MINGW_PACKAGE_PREFIX}-cc"
    "${MINGW_PACKAGE_PREFIX}-pkg-config"
)
# Don't zip info files because the built-in info reader uses gzip to
# decompress them. gzip is not available as a mingw binary.
options=('strip' '!zipman')
source=("https://github.com/emacs-mirror/${_realname}/archive/${_realname}-${pkgver}.tar.gz"
        "001-fix-cc-target-for-clang.patch"
        "002-mingw-build-fix.patch"
        "003-mingw-no-aligned-alloc.patch")
sha256sums=('98b1466a4681215d1d945e39fe3c3907ae262ced3c1d27786d502713b4ff4b60'
            '75e52d33215182cef2893d2edbc88875ef730478309a8bc3dfb03f560e8cd5b9'
            '3498d5e299b2a921edafb14b947a76b6f8547c0cf524464355b32bd37b1005fa'
            'f732d3cb66506621c60307ea9b27487e7ff1188ed9c64302f3819665cb8f9518')
prepare() {
  cd emacs-${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-fix-cc-target-for-clang.patch
  patch -p1 -i ${srcdir}/002-mingw-build-fix.patch
  patch -p1 -i ${srcdir}/003-mingw-no-aligned-alloc.patch

  #LLD_HACK: lld doesn't support '-Wl,-heap'
  if [[ $MSYSTEM == CLANG* ]]; then
    sed -e "s| -Wl,-heap,0x00100000||g" -i configure.ac
  fi

  autoreconf -fi
}

build() {
  [[ -d build-${MSYSTEM} ]] && rm -rf build-${MSYSTEM}
  cp -r emacs-${_realname}-${pkgver} build-${MSYSTEM}

  cd build-${MSYSTEM}
  ./configure \
    --prefix="${MINGW_PREFIX}" \
    --build="${MINGW_CHOST}" \
    --with-wide-int \
    --with-modules \
    --without-pop \
    --without-compress-install

  make
}

package() {
  cd build-${MSYSTEM}
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}

# TODO:
# Patch `shell-file-name' default in the C source code similarly to
# `source-directory'.
