# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=wit-bindgen
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.53.1
pkgrel=1
pkgdesc="A language binding generator for WebAssembly interface types (mingw-w64)"
url="https://github.com/bytecodealliance/wit-bindgen"
msys2_repository_url="https://github.com/bytecodealliance/wit-bindgen"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
license=('spdx:Apache-2.0 WITH LLVM-exception AND Apache-2.0 AND MIT')
depends=("${MINGW_PACKAGE_PREFIX}-rust")
makedepends=('git')
source=("git+https://github.com/bytecodealliance/wit-bindgen.git#tag=v${pkgver}")
sha256sums=('67d002671ac013a47248981313e31b4785bc788ec1339cd388a3be347e0e5f58')

prepare() {
  cd ${_realname}
  cargo fetch \
    --locked \
    --target "${RUST_CHOST}" \
    --config='net.git-fetch-with-cli=true'
}

build() {
  cd ${_realname}
  cargo build --release --frozen
}

check() {
  cd ${_realname}
  cargo test --release --frozen
}

package() {
  cd ${_realname}
  install -Dm755 target/release/${_realname} -t "${pkgdir}${MINGW_PREFIX}"/bin
  install -Dm644 LICENSE-* -t "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}
}
