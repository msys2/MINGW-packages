# Maintainer: Jeff Hubbard <musikernel@gmail.com>

_realname=ladspa-sdk
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.15
pkgrel=1
pkgdesc="Linux Audio Developer's Simple Plugin API (LADSPA) SDK (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64')
url="https://www.ladspa.org/"
license=("LGPL")
source=("https://ladspa.org/download/ladspa_sdk_${pkgver}.tgz")
sha256sums=('4229959b09d20c88c8c86f4aa76427843011705df22d9c28b38359fd1829fded')
noextract=(ladspa_sdk_${pkgver}.tgz)

prepare() {
  [[ -d ${srcdir}/ladspa_sdk_${pkgver} ]] && rm -rf ${srcdir}/ladspa_sdk_${pkgver}
  tar -xzf ${srcdir}/ladspa_sdk_${pkgver}.tgz -C ${srcdir} || true
}

package() {
  cd ladspa_sdk_${pkgver}
  install -Dm0644 src/ladspa.h "${pkgdir}/${MINGW_PREFIX}/include/ladspa.h"
}
