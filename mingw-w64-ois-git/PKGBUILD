# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=ois
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
_base_ver=1.4.0
pkgver=1.4.0.68.af406d9
pkgrel=1
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
replaces="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgdesc="Object Oriented Input System (mingw-w64)"
arch=('any')
url="https://sourceforge.net/projects/wgois"
license=('custom:zlib/libpng')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
options=('strip' '!libtool' 'staticlibs')
makedepends=('autoconf' 'automake' 'libtool' "${MINGW_PACKAGE_PREFIX}-gcc" "git")
source=("${_realname}"::"git+https://github.com/Alexpux/Object-oriented-Input-System--OIS-.git#branch=devel")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_realname}"
  printf "%s.%s.%s" "${_base_ver}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${srcdir}/${_realname}

  libtoolize --copy --force
  aclocal ${ACLOCAL_FLAGS}
  autoheader
  automake --include-deps --add-missing --foreign --copy
  autoconf
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}

  ../${_realname}/configure \
     --prefix=${MINGW_PREFIX} \
     --build=${MINGW_CHOST} \
     --host=${MINGW_CHOST} \
     --enable-shared=yes \
     --enable-static=yes

  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install
}
