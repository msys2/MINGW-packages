# Maintainer: Mazhar Hussain <realmazharhussain@gmail.com>

_realname=blueprint-compiler
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=0.6.0
pkgrel=1
pkgdesc='A markup language for GTK user interfaces (mingw-w64)' 
url='https://jwestman.pages.gitlab.gnome.org/blueprint-compiler/'
license=('spdx:LGPL-3.0-or-later')
arch=(any)
options=(strip staticlibs)
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
depends=(${MINGW_PACKAGE_PREFIX}-python-gobject)
makedepends=(${MINGW_PACKAGE_PREFIX}-meson)
source=(https://gitlab.gnome.org/jwestman/blueprint-compiler/-/archive/v$pkgver/blueprint-compiler-v$pkgver.tar.gz)
sha256sums=('c9e3652b66803c1de6a24b71f4fa4638b32260b4b04b93c2f3d958e1ce2a175e')

build() {
  cd "$srcdir"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
  "${MINGW_PREFIX}"/bin/meson setup \
    --prefix="${MINGW_PREFIX}" \
    $_realname-v$pkgver \
    build-${MINGW_CHOST}

  meson compile -C build-${MINGW_CHOST}
}

check() {
  cd "$srcdir"
  meson test -C build-${MINGW_CHOST}
}

package() {
  cd "$srcdir"
  meson install -C build-${MINGW_CHOST} --destdir="$pkgdir"
}
