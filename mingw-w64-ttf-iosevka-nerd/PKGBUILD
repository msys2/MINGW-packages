# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_font=iosevka
_realname=ttf-${_font}-nerd
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgdesc="Patched font Iosevka from nerd fonts library (mingw-w64)"
pkgver=3.0.2
pkgrel=1
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://github.com/ryanoasis/nerd-fonts'
_urlbase="${url}/releases/download/v${pkgver}"
license=('spdx:OFL-1.1')
options=('!debug' '!strip')
groups=("${MINGW_PACKAGE_PREFIX}-nerd-fonts")
source=("${_font}-${pkgver}.tar.xz::${_urlbase}/Iosevka.tar.xz")
sha256sums=('3958b94bd48e57c389d95ab27bd4a9b69164bc2466a092bc760644ac97abd9af')
noextract=("${_font}-${pkgver}.tar.xz")

prepare() {
  rm -rf ${_font}
  mkdir ${_font}
  bsdtar xf ${_font}-${pkgver}.tar.xz -C ${_font}
}

package() {
  install -Dm644 iosevka/*.ttf -t "${pkgdir}${MINGW_PREFIX}/share/fonts/TTF"
  install -Dm644 iosevka/LICENSE.md -t "${pkgdir}${MINGW_PREFIX}/share/licenses/ttf-iosevka-nerd/"
}
