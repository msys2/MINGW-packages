# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=three-merge
pkgbase=mingw-w64-python-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-${_realname}
pkgver=0.1.1
pkgrel=1
pkgdesc='Simple library for merging two strings with respect to a base one (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=('BSD')
url='https://github.com/spyder-ide/three-merge'
depends=(
    ${MINGW_PACKAGE_PREFIX}-python-diff-match-patch)
makedepends=(
    ${MINGW_PACKAGE_PREFIX}-python
    ${MINGW_PACKAGE_PREFIX}-python-setuptools)
source=("https://github.com/spyder-ide/${_realname}/archive/v${pkgver}.tar.gz")
sha256sums=('b6ab2231fd349cdf03be5be04f28c1d59d5a475f7396dcf8fd8b0597981d113e')

prepare() {
  cd "${srcdir}"
  rm -rf python-build-${CARCH} | true
  cp -r "${_realname}-${pkgver}" "python-build-${CARCH}"
}

build() {
  msg "Python build for ${CARCH}"
  cd "${srcdir}/python-build-${CARCH}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
