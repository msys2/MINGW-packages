# Maintainer: Frederic Wang <fred.wang@free.fr>

_realname=imagesize
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}" "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=0.7.1
pkgrel=2
pkgdesc="Getting image size from png/jpeg/jpeg2000/gif file (mingw-w64)"
url='https://github.com/shibukawa/imagesize_py'
license=('MIT')
arch=('any')
makedepends=("${MINGW_PACKAGE_PREFIX}-python2-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools")
_dtoken="53/72/6c6f1e787d9cab2cc733cf042f125abec07209a58308831c9f292504e826"
source=("https://pypi.python.org/packages/${_dtoken}/${_realname}-${pkgver}.tar.gz")
sha256sums=('0ab2c62b87987e3252f89d30b7cedbec12a01af9274af9ffa48108f2c13c6062')

prepare() {
  cd ${srcdir}
  cp -r ${_realname}-${pkgver} build-python2
  cp -r ${_realname}-${pkgver} build-python3
}

package_python3-imagesize() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3")
  cd ${srcdir}/build-python3
  ${MINGW_PREFIX}/bin/python3 setup.py build
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}"
}

package_python2-imagesize() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2")
  cd ${srcdir}/build-python2
  ${MINGW_PREFIX}/bin/python2 setup.py build
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}"
}

package_mingw-w64-i686-python2-imagesize() {
  package_python2-imagesize
}

package_mingw-w64-i686-python3-imagesize() {
  package_python3-imagesize
}

package_mingw-w64-x86_64-python2-imagesize() {
  package_python2-imagesize
}

package_mingw-w64-x86_64-python3-imagesize() {
  package_python3-imagesize
}
