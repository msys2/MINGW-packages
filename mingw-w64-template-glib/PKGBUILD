# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=template-glib
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.26.1
pkgrel=1
arch=('any')
pkgdesc="A templating library for GLib (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-gobject-introspection")
makedepends=("${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "gtk-doc"
             "flex"
             "bison"
             "autoconf-archive")
options=('!strip' 'debug' 'staticlibs')
license=("LGPL 2.1")
url="https://www.gnome.org/"
source=(https://download.gnome.org/sources/${_realname}/${pkgver%.*}/${_realname}-${pkgver}.tar.xz
        0001-win.patch)
sha256sums=('f0c2dd055f3077df785af017e6810e7c79e94258ef0c292a09ec3ac268af4c77'
            'eacd82af9463c64900544a33aa6b3e6f975f0665798228e1a7487a304cf1995c')

prepare() {
  cd ${_realname}-${pkgver}

  patch -p1 -i ${srcdir}/0001-win.patch
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  meson \
    --buildtype plain \
    -Dwith_introspection=false \
    ../${_realname}-${pkgver}

  ninja
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  DESTDIR=${pkgdir}${MINGW_PREFIX} ninja install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
