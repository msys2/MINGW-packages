# Maintainer: Alexey Kasatkin <alexeikasatkin@gmail.com>

_realname=geos
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.4.2
pkgrel=3
pkgdesc="C++ port of the Java Topology Suite (mingw-w64)"
arch=('any')
url="http://trac.osgeo.org/geos/"
license=('LGPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('strip' '!emptydirs')
source=(http://download.osgeo.org/geos/geos-$pkgver.tar.bz2
        01-fix-buildsystem.patch)
sha256sums=('15e8bfdf7e29087a957b56ac543ea9a80321481cef4d4f63a7b268953ad26c53'
            '89cd30338e6d29bfb0b1f50565b6ac32f85a4e327fbcfa025f20e51af4abae27')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/01-fix-buildsystem.patch
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTS=ON \
    -DGEOS_ENABLE_INLINE=OFF \
    ../${_realname}-${pkgver}
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make install
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  ctest
}
