# Maintainer: Wu, Zhenyu <wuzhenyu@ustc.edu>

_crate="svg2pdf-cli"
_realname="svg2pdf"
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.11.0
pkgrel=1
pkgdesc='The command line interface for svg2pdf. (mingw-w64)'
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://crates.io/crates/svg2pdf-cli'
license=('Apache-2.0' 'MIT')

depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")

source=("$_crate-0.11.0.tar.gz::https://crates.io/api/v1/crates/svg2pdf-cli/0.11.0/download")
sha512sums=('86333caf20b0ccf19f50d78bfa7a7e267a75ba51876fd201c70cc85097ba3f7246a6f8a932524844647199d676675a3faee6cb603964ee1e74f871a4ddbde3a4')

prepare() {
  cd "${srcdir}/${_crate}-${pkgver}"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${srcdir}/${_crate}-${pkgver}"

  cargo build --release --frozen
}

check() {
  cd "${srcdir}/${_crate}-${pkgver}"

  cargo test --release --frozen
}

package() {
  cd "${srcdir}/${_crate}-${pkgver}"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"
}
