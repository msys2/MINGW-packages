# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=mpv
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.7.2
pkgrel=1
pkgdesc="Video player based on MPlayer/mplayer2 (mingw-w64)"
url="http://mpv.io"
arch=('any')
license=('GPL')
depends=("${MINGW_PACKAGE_PREFIX}-ffmpeg"
         "${MINGW_PACKAGE_PREFIX}-lcms2"
         "${MINGW_PACKAGE_PREFIX}-libdvdread"
         #"${MINGW_PACKAGE_PREFIX}-libcdio-paranoia"
         "${MINGW_PACKAGE_PREFIX}-enca"
         "${MINGW_PACKAGE_PREFIX}-libguess"
         "${MINGW_PACKAGE_PREFIX}-mpg123"
         "${MINGW_PACKAGE_PREFIX}-lua"
         "${MINGW_PACKAGE_PREFIX}-libdvdnav")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "python"
             "perl"
             "pkg-config")
source=(https://github.com/mpv-player/${_realname}/archive/v$pkgver.tar.gz)
md5sums=('5081934235bf1d531364f59e5f718024')

prepare() {
  cd ${_realname}-${pkgver}
  /usr/bin/python3 ./bootstrap.py
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  sed -i 's:bin/env python:bin/env python3:' waf

  DEST_OS=win32 \
  TARGET=${MINGW_CHOST} \
  PKG_CONFIG=/usr/bin/pkg-config \
  CC=gcc PERL=/usr/bin/perl \
  AR=${MINGW_PREFIX}/bin/ar \
  WINDRES=${MINGW_PREFIX}/bin/windres \
  /usr/bin/python3 ./waf configure \
    --prefix=${MINGW_PREFIX} \
    --check-c-compiler=gcc \
    --enable-libmpv-shared \
    --disable-client-api-examples

  /usr/bin/python3 ./waf build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  export PYTHON=${MINGW_PREFIX}/bin/python3
  /usr/bin/python3 ./waf install --destdir="${pkgdir}"
  
  #rm -rf ${pkgdir}${MINGW_PREFIX}/lib
  mv ${pkgdir}${MINGW_PREFIX}/bin ${pkgdir}${MINGW_PREFIX}/lib
  mkdir -p ${pkgdir}${MINGW_PREFIX}/bin
  mv ${pkgdir}${MINGW_PREFIX}/lib/*.exe ${pkgdir}${MINGW_PREFIX}/bin/
  mv ${pkgdir}${MINGW_PREFIX}/lib/*.dll ${pkgdir}${MINGW_PREFIX}/bin/
}
