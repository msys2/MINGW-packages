# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=wiiuse
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.15.7
pkgrel=1
pkgdesc="Feature complete cross-platform Wii Remote access library (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://github.com/wiiuse/wiiuse'
msys2_repository_url='https://github.com/wiiuse/wiiuse'
license=('spdx:GPL-3.0-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-doxygen")
source=("https://github.com/wiiuse/wiiuse/archive/${pkgver}/${_realname}-${pkgver}.tar.gz"
        "0001-fix-cmakelists.patch")
sha256sums=('d16dbe3b38e3c1dbe3e9a2c5b0a32a801710da2aca66581500ef2b98eba1d8ff'
            '116204ffbdfb83e9a074e74980bdfe7fed0e1b5ead95209ca6e536851f7c77bc')

prepare() {
  cd "${_realname}-${pkgver}"

  patch -Np1 -i "../0001-fix-cmakelists.patch"
}

build() {
  declare -a _cmake_opts=(
    -GNinja
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}"
    -DBUILD_EXAMPLE=OFF
    -DBUILD_EXAMPLE_SDL=OFF
    -DINSTALL_EXAMPLES=OFF
  )

  if check_option "debug" "n"; then
    _cmake_opts+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _cmake_opts+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      "${_cmake_opts[@]}" \
      -DBUILD_SHARED_LIBS=OFF \
      -S ${_realname}-${pkgver} \
      -B build-${MSYSTEM}-static

  cmake --build build-${MSYSTEM}-static

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      "${_cmake_opts[@]}" \
      -DBUILD_SHARED_LIBS=ON \
      -S ${_realname}-${pkgver} \
      -B build-${MSYSTEM}-shared

  cmake --build build-${MSYSTEM}-shared
}

package() {
  DESTDIR="${pkgdir}" cmake --install build-${MSYSTEM}-static
  DESTDIR="${pkgdir}" cmake --install build-${MSYSTEM}-shared

  install -Dm644 "${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
