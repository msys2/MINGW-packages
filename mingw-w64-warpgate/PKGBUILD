# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=warpgate
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.19.1
pkgrel=1
pkgdesc="Fully transparent SSH, HTTPS, MySQL and Postgres bastion/PAM that doesn't need additional client-side software (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://warpgate.null.page'
msys2_repository_url='https://github.com/warp-tech/warpgate'
license=('spdx:Apache-2.0')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-nodejs"
             "${MINGW_PACKAGE_PREFIX}-just")
source=("${msys2_repository_url}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('dedc9abad305007e55aa87afd43cf6371d34e1f64c001d554abe6ff7f05a99a1')

prepare() {
  cd "${_realname}-${pkgver}"

  just npm ci
  cargo fetch --locked --target "${RUST_CHOST}"
}

build() {
  cd "${_realname}-${pkgver}"

  just npm run build
  cargo build \
    --release \
    --frozen \
    -p warpgate \
    --all-features
}

check() {
  cd "${_realname}-${pkgver}"

  cargo test \
    --release \
    --frozen \
    -p warpgate \
    --all-features
}

package() {
  cd "${_realname}-${pkgver}"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path warpgate \
    --all-features \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
