# Maintainer: Wu, Zhenyu <whenyu@ustc.edu>

_realname=gojq
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.12.16
pkgrel=1
pkgdesc="Pure Go implementation of jq (mingw-w64)"
arch=(any)
mingw_arch=('mingw32' 'mingw64' 'ucrt32' 'ucrt64' 'clang32' 'clang64' 'clangarm64')
url="https://github.com/itchyny/gojq"
license=('MIT')
# LTO renders GO BUILDID unreproducible: https://gcc.gnu.org/bugzilla/show_bug.cgi?id=108534
options=(!lto)
source=("${url}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('80365ef7dd7935296d42b98c79b51723a01d3c332501098485bebb4b9a89eb13')

prepare() {
  cd "${_realname}-${pkgver}"
  mkdir -p build
}

build() {
  cd "${_realname}-${pkgver}"
	export CGO_LDFLAGS="$LDFLAGS"
	export CGO_CFLAGS="$CFLAGS"
	export CGO_CPPFLAGS="$CPPFLAGS"
	export CGO_CXXFLAGS="$CXXFLAGS"

	export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
	go build -o build \
		"./cmd/$_realname"
}

package() {
  cd "${_realname}-${pkgver}"
  install -Dm 755 "build/$_realname.exe" -t "${pkgdir}/$MINGW_PREFIX/bin"
  install -Dm 644 LICENSE  -t "${pkgdir}/$MINGW_PREFIX/share/licenses/${_realname}"
}

# vim:set ts=2 sw=2 et:
