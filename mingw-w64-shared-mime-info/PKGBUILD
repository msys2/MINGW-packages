# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

_realname=shared-mime-info
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.9
pkgrel=1
pkgdesc="Freedesktop.org Shared MIME Info (mingw-w64)"
arch=('any')
license=(GPL2)
depends=("${MINGW_PACKAGE_PREFIX}-libxml2" "${MINGW_PACKAGE_PREFIX}-glib2")
makedepends=('intltool' "${MINGW_PACKAGE_PREFIX}-pkg-config" "${MINGW_PACKAGE_PREFIX}-gettext")
install=${_realname}-${CARCH}.install
url="https://freedesktop.org/Software/shared-mime-info"
source=(https://people.freedesktop.org/~hadess/${_realname}-${pkgver}.tar.xz
        program.manifest)
sha256sums=('5c0133ec4e228e41bdf52f726d271a2d821499c2ab97afd3aa3d6cf43efcdc83'
            'f93191ad3e217ef9939cf16885ffe50ce920a0b639e183e46415f10df4b4db72')
options=(!makeflags)

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  autoreconf -fiv
}

build() {
  mkdir ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
  "${srcdir}/${_realname}-${pkgver}"/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-update-mimedb \
    --disable-default-make-check

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -k check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
  [[ ${CARCH} == i686 ]] && {
    _arch=x86
  } || {
    _arch=amd64
  }
  install -Dm644 ${srcdir}/program.manifest ${pkgdir}${MINGW_PREFIX}/bin/update-mime-database.exe.manifest
  sed -e "s|%PROGNAME%|GNU.update-mime-database|g" \
      -e "s|%ARCH%|${_arch}|g" \
      -i ${pkgdir}${MINGW_PREFIX}/bin/update-mime-database.exe.manifest
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
