
_realname=tree-sitter-lua
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.2.0
pkgrel=1
pkgdesc='Lua grammar for tree-sitter (mingw-64)'
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'archlinux: tree-sitter-lua'
)
url='https://github.com/tree-sitter-grammars/tree-sitter-lua'
msys2_repository_url='https://github.com/tree-sitter-grammars/tree-sitter-lua'
license=('spdx:MIT')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-tree-sitter"
)
source=("https://github.com/tree-sitter-grammars/${_realname}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('6c41227cd0a59047b19d31f0031d4d901f08bfd78d6fc7f55c89e5b8374c794e')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  tree-sitter generate src/grammar.json
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  # To get around Windows not being supported
  OS='unknown' make CC='cc' LINKSHARED='-shared' PREFIX="${MINGW_PREFIX}" PARSER_URL=$url
}

package() {
  install -d "${pkgdir}${MINGW_PREFIX}/lib/tree_sitter"
  cd "${srcdir}/${_realname}-${pkgver}"
  OS='unknown' make DESTDIR="${pkgdir}" PREFIX="${MINGW_PREFIX}" install

  # To add library to neovim runtime path
  mkdir -p "${pkgdir}${MINGW_PREFIX}/lib/nvim/parser"
  cp "${pkgdir}${MINGW_PREFIX}/lib/lib${_realname}.so" "${pkgdir}${MINGW_PREFIX}/lib/nvim/parser/${_realname#tree-sitter-}.so"

  install -Dm644 LICENSE.md "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.md"
}
