# Maintainer: Oscar Fuentes <ofv@wanadoo.es>

_realname=ctags
pkgname="${MINGW_PACKAGE_PREFIX}-universal-${_realname}-git"
pkgver=r2611.44688c7
pkgrel=1
pkgdesc="A maintained Ctags implementation (mingw-w64)"
arch=('any')
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
url="https://github.com/universal-ctags/ctags"
license=("GPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" "${MINGW_PACKAGE_PREFIX}-libiconv")
options=('staticlibs' 'strip')
source=("git+https://github.com/universal-ctags/ctags.git")
md5sums=('SKIP')

pkgver() {
  cd "${_realname}"
  printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ${srcdir}/${_realname}
  autoreconf -fiv
  mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}
  ../${_realname}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --disable-etags \
    --disable-external-sort \
    --enable-iconv
  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make prefix="${pkgdir}/${MINGW_PREFIX}" install
}
