# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=tree-sitter-markdown
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.5.2
pkgrel=1
pkgdesc='Markdown grammar for tree-sitter (mingw-w64)'
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://github.com/tree-sitter-grammars/tree-sitter-markdown'
msys2_repository_url='https://github.com/tree-sitter-grammars/tree-sitter-markdown'
msys2_references=(
  'archlinux: tree-sitter-markdown'
)
license=('spdx:MIT')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-tree-sitter"
  "${MINGW_PACKAGE_PREFIX}-nodejs"
)
source=("${_realname}-${pkgver}.tar.gz::${msys2_repository_url}/releases/download/v${pkgver}/${_realname}.tar.gz"
        "fix-cmake-install.patch")
sha256sums=('e566f53ee7f0bc1bd3c3775e74e29a78b80db84bc663951eec3615ca55626fac'
            'd65baa285b7948099d12e3f75f07f4745361794b648f4ae9da320e226b6d13f0')
noextract=("${_realname}-${pkgver}.tar.gz")

prepare() {
  mkdir -p "${_realname}-${pkgver}"
  tar -xf "${_realname}-${pkgver}.tar.gz" -C "${_realname}-${pkgver}" || true

  # https://github.com/tree-sitter-grammars/tree-sitter-markdown/pull/219
  patch -d "${_realname}-${pkgver}" -p1 -i ../fix-cmake-install.patch
}

build() {
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX" \
    cmake \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -Wno-dev \
      -DCMAKE_BUILD_TYPE=None \
      -DCMAKE_SHARED_LIBRARY_SUFFIX_C=.so \
      -DCMAKE_INSTALL_BINDIR=lib \
      -S "${_realname}-${pkgver}" \
      -B "build-${MSYSTEM}"

  cmake --build "build-${MSYSTEM}"
}

package() {
  DESTDIR="${pkgdir}" cmake --install "build-${MSYSTEM}"

  # To add library to neovim runtime path
  mkdir -p "${pkgdir}${MINGW_PREFIX}/lib/nvim/parser"
  cp "${pkgdir}${MINGW_PREFIX}/lib/lib${_realname}.so" "${pkgdir}${MINGW_PREFIX}/lib/nvim/parser/${_realname#tree-sitter-}.so"
  cp "${pkgdir}${MINGW_PREFIX}/lib/lib${_realname}-inline.so" "${pkgdir}${MINGW_PREFIX}/lib/nvim/parser/${_realname#tree-sitter-}_inline.so"

  install -Dm644 "${_realname}-${pkgver}"/LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
