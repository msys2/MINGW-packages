
_realname=tree-sitter-markdown
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.3.2
pkgrel=1
pkgdesc='Markdown grammar for tree-sitter (mingw-64)'
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'archlinux: tree-sitter-markdown'
)
url='https://github.com/tree-sitter-grammars/tree-sitter-markdown'
msys2_repository_url='https://github.com/tree-sitter-grammars/tree-sitter-markdown'
license=('spdx:MIT')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-tree-sitter"
)
source=("https://github.com/tree-sitter-grammars/${_realname}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('5dac48a6d971eb545aab665d59a18180d21963afc781bbf40f9077c06cb82ae5')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}/tree-sitter-markdown"
  tree-sitter generate --no-bindings src/grammar.json

  cd "../tree-sitter-markdown-inline"
  tree-sitter generate --no-bindings src/grammar.json
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  # To get around Windows not being supported
  OS='unknown' make CC='cc' LINKSHARED='-shared' PREFIX="${MINGW_PREFIX}" PARSER_URL=$url
}

package() {
  install -d "${pkgdir}${MINGW_PREFIX}/lib/tree_sitter"
  cd "${srcdir}/${_realname}-${pkgver}"
  OS='unknown' make DESTDIR="${pkgdir}" PREFIX="${MINGW_PREFIX}" install

  # To add library to neovim runtime path
  mkdir -p "${pkgdir}${MINGW_PREFIX}/lib/nvim/parser"
  cp "${pkgdir}${MINGW_PREFIX}/lib/lib${_realname}.so" "${pkgdir}${MINGW_PREFIX}/lib/nvim/parser/${_realname#tree-sitter-}.so"
  cp "${pkgdir}${MINGW_PREFIX}/lib/lib${_realname}-inline.so" "${pkgdir}${MINGW_PREFIX}/lib/nvim/parser/${_realname#tree-sitter-}_inline.so"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
