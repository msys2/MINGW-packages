# Maintainer (MSYS2): Ray Donnelly <mingw.android@gmail.com>
# Contributor (MSYS2): Mateusz Miku≈Ça <mati865@gmail.com>

_variant=-${KF5_VARIANT:-shared}
source "$(dirname ${BASH_SOURCE[0]})"/../mingw-w64-PKGBUILD-common/kde-frameworks5
_kde_f5_init_package "${_variant}" "kemoticons"
pkgver=5.25.0
pkgrel=1
arch=('any')
pkgdesc="Support for emoticons and emoticons themes (mingw-w64-qt5${_namesuff})"
license=('LGPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-extra-cmake-modules")
depends=()
_kde_f5_add_depends "${_variant}" "${MINGW_PACKAGE_PREFIX}-qt5${_namesuff}"
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${_basename}-${pkgver}.tar.xz")
sha256sums=('072bdcfb2437f25e2d4bcc02bef7d80de200953bdd7f822c2649c005a40abb9d')

prepare() {
  mkdir -p build-${CARCH}${_variant}

  # repalce old QStandardPaths functions with our new *Msys functions
  cd "${srcdir}"/${_basename}-${pkgver}
  find . -type f -name "*.cpp*" -print0 | xargs -0 sed -i -e "s/QStandardPaths::locateAll(/QStandardPaths::locateAllMsys(/g" \
                                                          -e "s/QStandardPaths::locate(/QStandardPaths::locateMsys(/g" \
                                                          -e "s/QStandardPaths::standardLocations(/QStandardPaths::standardLocationsMsys(/g" \
                                                          -e "s/QStandardPaths::writableLocation(/QStandardPaths::writableLocationMsys(/g"
}

build() {
  local -a extra_config
  cd build-${CARCH}${_variant}
  if [ "${_variant}" = "-static" ]; then
    extra_config+=( -DBUILD_SHARED_LIBS=NO )
    QT5_PREFIX=${MINGW_PREFIX}/qt5-static
    export PATH=${QT5_PREFIX}/bin:"$PATH"
  else
    QT5_PREFIX=${MINGW_PREFIX}
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake ../${_basename}-${pkgver} \
      -DCMAKE_BUILD_TYPE=$(_kde_f5_CMAKE_BUILD_TYPE) \
      -DCMAKE_INSTALL_PREFIX=${QT5_PREFIX} \
      -DKDE_INSTALL_LIBDIR=lib \
      -DBUILD_TESTING=OFF \
      "${extra_config[@]}" \
      -G'MSYS Makefiles'
  make
}

package() {
  cd build-${CARCH}${_variant}
  make DESTDIR="${pkgdir}" install
}
