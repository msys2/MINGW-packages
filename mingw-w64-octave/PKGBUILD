_realname=octave
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=9.1.0
pkgrel=1
pkgdesc="GNU Octave: Interactive programming environment for numerical computations (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://www.octave.org"
msys2_repository_url="https://github.com/gnu-octave/octave"
msys2_references=(
  'archlinux: octave'
)
license=('spdx:GPL-3.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         $([[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]] || echo "${MINGW_PACKAGE_PREFIX}-gcc-libgfortran")
         "${MINGW_PACKAGE_PREFIX}-fontconfig"
         "${MINGW_PACKAGE_PREFIX}-freetype"
         "${MINGW_PACKAGE_PREFIX}-libiconv"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread-git"
         "${MINGW_PACKAGE_PREFIX}-omp"
         "${MINGW_PACKAGE_PREFIX}-openblas"
         "${MINGW_PACKAGE_PREFIX}-pcre2"
         "${MINGW_PACKAGE_PREFIX}-readline"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         $([[ ${CARCH} == i686 ]] && echo \
           "${MINGW_PACKAGE_PREFIX}-qt5-tools" || echo \
           "${MINGW_PACKAGE_PREFIX}-arpack" \
           "${MINGW_PACKAGE_PREFIX}-curl" \
           "${MINGW_PACKAGE_PREFIX}-fftw" \
           "${MINGW_PACKAGE_PREFIX}-ghostscript" \
           "${MINGW_PACKAGE_PREFIX}-gl2ps" \
           "${MINGW_PACKAGE_PREFIX}-glpk" \
           "${MINGW_PACKAGE_PREFIX}-graphicsmagick" \
           "${MINGW_PACKAGE_PREFIX}-hdf5" \
           "${MINGW_PACKAGE_PREFIX}-libsndfile" \
           "${MINGW_PACKAGE_PREFIX}-qhull" \
           "${MINGW_PACKAGE_PREFIX}-qrupdate" \
           "${MINGW_PACKAGE_PREFIX}-qscintilla-qt6" \
           "${MINGW_PACKAGE_PREFIX}-qt6-tools" \
           "${MINGW_PACKAGE_PREFIX}-suitesparse" \
           "${MINGW_PACKAGE_PREFIX}-sundials")
        )
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-fc"
             "${MINGW_PACKAGE_PREFIX}-gperf"
             $([[ ${CARCH} == i686 ]] || echo \
               "${MINGW_PACKAGE_PREFIX}-gnuplot" \
               "${MINGW_PACKAGE_PREFIX}-fltk" \
               "${MINGW_PACKAGE_PREFIX}-portaudio" \
               "${MINGW_PACKAGE_PREFIX}-rapidjson")
            )
checkdepends=("unzip"
              "zip")
optdepends=('texinfo: for help-support in Octave'
            "unzip: for decompressing .zip archives"
            "zip: for compressing .zip archives")
if [[ ${CARCH} != i686 ]]; then
  optdepends+=("${MINGW_PACKAGE_PREFIX}-epstool: eps output with tight bounding box"
               "${MINGW_PACKAGE_PREFIX}-fltk: alternative plotting and dialogs"
               "${MINGW_PACKAGE_PREFIX}-gnuplot: alternative plotting"
               "${MINGW_PACKAGE_PREFIX}-portaudio: audio support")
fi
source=(https://ftp.gnu.org/gnu/octave/octave-$pkgver.tar.xz{,.sig}
        "0002-mk-doc-cache-path.patch"
        "0003-no-community-support.patch"
        "0005-makeinfo-perl.patch")
validpgpkeys=('DBD9C84E39FE1AAE99F04446B05F05B75D36644B')  # John W. Eaton
sha256sums=('ed654b024aea56c44b26f131d31febc58b7cf6a82fad9f0b0bf6e3e9aa1a134b'
            'SKIP'
            'aa5bd559d9774abc0f0c930a606762d1e452cc90278d16d8ae83561c0cae3bf8'
            '313daf7bf0080e4d645a601dedc36208de466af51ffbc74afc21e8505544e45d'
            '3f6756b39547d996955762b71ce6845a8ebfde6bad92a3a1a6d72fd43df5973d')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Np1 -i "${srcdir}/$_patch"
  done
}

prepare() {
  cd "${_realname}-${pkgver}"

  apply_patch_with_msg \
    0002-mk-doc-cache-path.patch \
    0005-makeinfo-perl.patch

  if [[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]]; then
    apply_patch_with_msg \
      0003-no-community-support.patch

    # We need the patched libtool with Flang specific changes.
    autoreconf -fiv
  fi

}

build() {
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  declare -a _extra_config
  if [[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]]; then
    _extra_config+=(# Clang seems to require a different set of dllexport
                    # attributes than GCC.  For the time being, export all
                    # symbols (including those from gnulib and other static
                    # libraries linked into the Octave libraries).  Doing that
                    # might cause issues in some cases.  But solving this
                    # without breaking GCC support might be difficult.
                    "--disable-lib-visibility-flags"
                    # Libtool still needs some hints how to handle LLVM Flang:
                    "--enable-fortran-calling-convention=gfortran"
                    "ac_cv_f77_compiler_gnu=yes"
                    "lt_cv_prog_gnu_ld=yes"
                    "F77=${MINGW_PREFIX}/bin/flang")
    # The headers from some dependent libraries (graphicsmagick++?) cause a
    # torrent of warnings about deprecated declarations.  Sometimes to the
    # point that the build crashes with garbled characters in the terminal
    # output for some reason.  Just calling `make all -j2` again in the build
    # tree lets the compilation finish successfully.
    # Disable those warnings as a work-around.
    CXXFLAGS+=" -Wno-deprecated-declarations -Wno-ignored-attributes"
  fi

  # The configure step sometimes hangs for MINGW32 for currently unknown reasons.
  # Add a timeout of 30 minutes (well over what should be needed) to unblock
  # the build system in that case.
  timeout 30m \
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --sbindir=${MINGW_PREFIX}/bin \
    --enable-shared \
    --disable-static \
    --enable-relocate-all \
    --enable-link-all-dependencies \
    "${_extra_config[@]}" \
    gl_cv_have_weak=no \
    octave_cv_fftw3_threads_lib="-lfftw3_omp" \
    octave_cv_fftw3f_threads_lib="-lfftw3f_omp" \
    JAVA_HOME=""

  PERL5SHELL="bash -l -c" make all
}

check() {
  cd "${srcdir}"/build-${MSYSTEM}
  make check || true
}

package() {
  make -C "build-${MSYSTEM}" DESTDIR="${pkgdir}" install

  # Remove full path reference
  local _PREFIX_WIN="$(cygpath -wm ${MINGW_PREFIX})"
  for pcfile in "${pkgdir}${MINGW_PREFIX}"/lib/pkgconfig/*.pc; do
    sed -s "s|${_PREFIX_WIN}|${MINGW_PREFIX}|g" -i "${pcfile}"
  done

  install -D -m644 "${srcdir}"/${_realname}-${pkgver}/COPYING "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
