# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=coin-or-coinutils
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.11.10
pkgrel=1
pkgdesc="COIN-OR collection of utility classes. (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64')
url='https://github.com/coin-or/CoinUtils'
msys2_references=(
  'archlinux: coin-or-coinutils'
)
license=('spdx:EPL-2.0')
groups=("${MINGW_PACKAGE_PREFIX}-coin-or")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-bzip2"
         "${MINGW_PACKAGE_PREFIX}-coin-or-data-sample"
         "${MINGW_PACKAGE_PREFIX}-glpk"
         "${MINGW_PACKAGE_PREFIX}-openblas"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-fc")
source=("${url}/archive/releases/${pkgver}/${_realname}-${pkgver}.tar.gz"
        001-fix-build-on-mingw.patch
        002-fix-build-with-clang.patch)
sha256sums=('80c7c215262df8d6bd2ba171617c5df844445871e9891ec6372df12ccbe5bcfd'
            '0e78c010ece6f73ac99068d1783bf6ea83fcf9992b85f71be78081a92cab0251'
            'd84af994d8c8c3f757022d3c2cd5a8906eb572158d3fa1663b521eb16f19ef48')

prepare() {
  cd CoinUtils-releases-${pkgver}
  patch -p1 -i ${srcdir}/001-fix-build-on-mingw.patch

  if [[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]]; then
    patch -p1 -i ${srcdir}/002-fix-build-with-clang.patch
  fi
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  export lt_cv_deplibs_check_method='pass_all'

  if [[ ${MINGW_PACKAGE_PREFIX} != *-clang-* ]]; then
    export F77=${MINGW_PREFIX}/bin/gfortran.exe
  else
    export F77=${MINGW_PREFIX}/bin/flang.exe
  fi

  CXXFLAGS+=" -Wno-register" \
  ../"CoinUtils-releases-${pkgver}"/configure \
    --prefix="${MINGW_PREFIX}" \
    --enable-shared \
    --disable-static \
    --with-blas-lib='-lopenblas' \
    --with-lapack-lib='' \
    --with-glpk-lib='-lglpk' \
    --enable-dependency-linking

  make
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  make check
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  make install DESTDIR="${pkgdir}"

  install -Dm644 "${srcdir}/CoinUtils-releases-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
