# Maintainer: David Grayson <davidegrayson@gmail.com>

_realname=win7appid
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.1
pkgrel=2
pkgdesc='Windows 7 (and up) shortcut application id tool'
arch=('any')
url='https://code.google.com/p/win7appid'
license=('custom')
makedepends=(
    "gzip"
    "${MINGW_PACKAGE_PREFIX}-gcc"
)
depends=()
options=('strip')
source=(
    "https://win7appid.googlecode.com/svn/trunk/Win7AppId.cpp"
    "win7appid.1"
    "LICENSE.txt"
)

sha256sums=(
    'e6b5d9e5586d1a1e0c7911b951e00a964a80b94db21e74cdc327e523fecb9fa3'
    'SKIP'
    'SKIP'
)

prepare() {
    gzip < win7appid.1 > win7appid.1.gz
}

build() {
  mkdir -p "build-${MINGW_CHOST}"
  cd "build-${MINGW_CHOST}"

  g++ -O1 -municode -DWIN32_LEAN_AND_MEAN=1 -DUNICODE -D_UNICODE \
    ../*.cpp -lole32 \
    -o win7appid.exe
}

package() {
  r="${pkgdir}${MINGW_PREFIX}"
  mkdir -p "${r}/share/licenses/${_realname}" \
    "${r}/share/man/man1" "${r}/bin"

  install -m644 LICENSE.txt -t "${r}/share/licenses/${_realname}"
  install -m644 win7appid.1.gz -t "${r}/share/man/man1"

  cd "build-${MINGW_CHOST}"
  install -m755 win7appid.exe -t "${r}/bin"
}
