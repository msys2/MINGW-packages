# Maintainer: d0p1 <contact@d0p1.eu>

_realname=liboqs
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.10.1
pkgrel=1
pkgdesc="C library for prototyping and experimenting with quantum-resistant cryptography (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64')
url='https://openquantumsafe.org/'
msys2_repository_url="https://github.com/open-quantum-safe/liboqs"
license=('spdx:MIT')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-ninja"
  "${MINGW_PACKAGE_PREFIX}-cc"
)
checkdepends=(
  "${MINGW_PACKAGE_PREFIX}-python-pytest"
  "${MINGW_PACKAGE_PREFIX}-python-pytest-xdist"
  "${MINGW_PACKAGE_PREFIX}-python-yaml"
)
source=("https://github.com/open-quantum-safe/liboqs/archive/refs/tags/${pkgver}.tar.gz"
        "0001-build-shared.patch")
sha256sums=('00ca8aba65cd8c8eac00ddf978f4cac9dd23bb039f357448b60b7e3eed8f02da'
            '23e0ea762e58214a3cf709f99c3a3e8f457cd35b0a5e763671d21318fd0ae189')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -Np1 -i "${srcdir}"/0001-build-shared.patch
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DCMAKE_DLL_NAME_WITH_SOVERSION=ON \
      -DBUILD_SHARED_LIBS=ON \
      ../${_realname}-${pkgver}

  cmake --build .
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  PATH="$(pwd)/bin:$PATH" \
    cmake --build . --target run_tests
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" cmake --install .

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE.txt" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
