# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=docbook-xsl
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.79.2
pkgrel=3
pkgdesc='XML stylesheets for Docbook-xml transformations (mingw-w64)'
arch=('any')
license=('custom')
url='http://docbook.org/'
depends=("${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-libxslt"
         "${MINGW_PACKAGE_PREFIX}-docbook-xml")
install=${_realname}-${CARCH}.install
source=("https://github.com/docbook/xslt10-stylesheets/releases/download/release/${pkgver}/docbook-xsl-${pkgver}.tar.bz2"
        "0001-add-back-old-uri-rewrite.patch"
        "0002-fix-infinity.patch")
sha256sums=('316524ea444e53208a2fb90eeb676af755da96e1417835ba5f5eb719c81fa371'
            'ab61e0a29e6129afd2b6147de6acfe6f4fa3bfabe692553c07684d2c31378290'
            '4a27dd468bb803290b73aeb3a0fde190a1897b97945b59eaf39d8535bb6332a5')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  patch -p1 -i ${srcdir}/0001-add-back-old-uri-rewrite.patch
  patch -p1 -i ${srcdir}/0002-fix-infinity.patch
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}

  _pkgroot=${pkgdir}${MINGW_PREFIX}/share/xml/docbook/xsl-stylesheets-${pkgver}

    install -dm755 ${_pkgroot}
  install -m644 VERSION VERSION.xsl ${_pkgroot}

  for fn in assembly common eclipse epub epub3 fo highlighting html htmlhelp javahelp lib \
                  manpages params profiling roundtrip template website xhtml xhtml-1_1 xhtml5; do
    install -dm755 ${_pkgroot}/${fn}
    install -m644 ${fn}/*.{xml,xsl,dtd,ent} ${_pkgroot}/${fn} || true  # ignore missing files
  done

  install -Dm644 catalog.xml ${_pkgroot}/catalog.xml
  install -dm755 ${pkgdir}${MINGW_PREFIX}/etc/xml
  install -Dm644 COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
