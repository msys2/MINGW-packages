# Maintainer: Wu, Zhenyu <wuzhenyu@ustc.edu>

_realname=typst-lsp
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.13.0
pkgrel=1
pkgdesc='language server for Typst (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt32' 'ucrt64' 'clang32' 'clang64' 'clangarm64')
url="https://github.com/nvarner/$_realname"
license=(Apache MIT)
depends=(${MINGW_PACKAGE_PREFIX}-gcc-libs
         ${MINGW_PACKAGE_PREFIX}-curl
         ${MINGW_PACKAGE_PREFIX}-openssl)
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
source=("$url/archive/v$pkgver/$_realname-$pkgver.tar.gz")
sha256sums=('860d56653b719402736b00ac9bc09e5e418dea2577cead30644252e85ab5d1a1')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  cargo add time@=0.3.36
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  cargo build --release --frozen
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  cargo test --release --frozen
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE-* -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
}
