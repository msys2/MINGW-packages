# Maintainer: @naveen521kk on Github <naveen521kk@gmail.com>

_realname=github-cli
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.28.0
pkgrel=1
pkgdesc='The GitHub CLI (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://github.com/cli/cli"
license=('spdx:MIT')
depends=("winpty")
makedepends=("${MINGW_PACKAGE_PREFIX}-go" "${MINGW_PACKAGE_PREFIX}-cc" "git")
optdepends=("git: To interact with repositories")
options=('!strip')
source=("$_realname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "gh"
        "0001-make-fix-target-name.patch::https://github.com/cli/cli/commit/3d1b40db772e6db83be8ee83cfa254b4a668c300.patch")
sha256sums=('cf3c0fb7f601d717d8b5177707a197c49fd426f5dc3c9aa52a932e96ba7166af'
            '9ee5f2b44b7e9aa751508f02c1020e341e0212a9aa146b7428eb5ffea310be27'
            '107fd97b55e4b206d74d8d262e46d9221ba96d0bac33bc336b209773ad5b06d4')

prepare() {
  cd "cli-$pkgver"
  patch -p1 -i "${srcdir}/0001-make-fix-target-name.patch"
}

build() {
    cd "cli-$pkgver"
    export GOOS=windows
    export GOROOT=${MINGW_PREFIX}/lib/go
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GO_LDFLAGS="-s -w"
    export GOFLAGS="-trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external"
    case "${CARCH}" in
      i686|x86_64)
        GOFLAGS+=" -buildmode=pie"
        ;;
    esac

    make GH_VERSION="v$pkgver" bin/gh.exe manpages
    bin/gh.exe completion -s bash | install -Dm644 /dev/stdin bash-completion/completions/gh
    bin/gh.exe completion -s zsh | install -Dm644 /dev/stdin zsh/site-functions/_gh
    bin/gh.exe completion -s fish | install -Dm644 /dev/stdin fish/vendor_completions.d/gh.fish
}

check(){
    cd "cli-$pkgver"
    make test
}

package() {
    cd "cli-$pkgver"
    install -Dm755 "${srcdir}/gh" "${pkgdir}${MINGW_PREFIX}/bin/gh"
    install -Dm755 "bin/gh.exe" "${pkgdir}${MINGW_PREFIX}/bin/gh.exe"
    cp -r share/ "${pkgdir}${MINGW_PREFIX}/"
    install -Dm644 "LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
    install -Dm644 "README.md" "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/README.md"
    install -Dm644 bash-completion/completions/gh "${pkgdir}${MINGW_PREFIX}/share/bash-completion/completions/gh"
    install -Dm644 zsh/site-functions/_gh "${pkgdir}${MINGW_PREFIX}/share/zsh/site-functions/_gh"
    install -Dm644 fish/vendor_completions.d/gh.fish "${pkgdir}${MINGW_PREFIX}/share/fish/vendor_completions.d/gh.fish"
}
