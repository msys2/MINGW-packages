# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=protobuf-c
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.1.0
pkgrel=1
pkgdesc="Protocol Buffers implementation in C (mingw-w64)"
arch=('any')
url="https://github.com/protobuf-c/protobuf-c"
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-protobuf")
groups=("${MINGW_PACKAGE_PREFIX}")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('staticlibs' 'strip')
source=("$url/releases/download/v$pkgver/${_realname}-$pkgver.tar.gz")
md5sums=('884a6e7deb4a2f7b3cc81a4dcabe514a')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
}

build() {
  [[ -d "$srcdir/build-${MINGW_CHOST}" ]] && rm -rf "$srcdir/build-${MINGW_CHOST}"
  mkdir build-${MINGW_CHOST}
  cd "$srcdir/build-${MINGW_CHOST}"

  ../${_realname}-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST}
  make
}

check() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make check
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/LICENSE "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSE
}
