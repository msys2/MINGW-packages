# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=protobuf-c
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.5.0
pkgrel=5
pkgdesc="Protocol Buffers implementation in C (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/protobuf-c/protobuf-c"
msys2_references=(
  "cpe: cpe:/a:protobuf-c_project:protobuf-c"
)
license=('spdx:BSD-2-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-abseil-cpp"
         "${MINGW_PACKAGE_PREFIX}-protobuf")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools")
source=("$url/releases/download/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "https://patch-diff.githubusercontent.com/raw/protobuf-c/protobuf-c/pull/711.patch")
sha256sums=('7b404c63361ed35b3667aec75cc37b54298d56dd2bcf369de3373212cc06fd98'
            '2712342334ea793118e777d25599f57d5000f18b63c9a3bac19f24ee1ce8cc15')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  # Fix build against protobuf-26
  patch -p1 -i ${srcdir}/711.patch
  autoreconf -fiv
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  ../${_realname}-${pkgver}/configure \
      --prefix=${MINGW_PREFIX}

  make
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"
  make check || true
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/LICENSE "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSE
}
