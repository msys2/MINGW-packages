# Maintainer: James Duley <jagduley gmail>

_realname=osgearth
pkgbase=mingw-w64-${_realname}-git
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}-git" "${MINGW_PACKAGE_PREFIX}-${_realname}-debug-git")
pkgver=r7923.978cc0b
pkgrel=1
arch=('any')
license=('LGPL')
url="http://osgearth.org"
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "git")
options=('staticlibs' 'strip')
source=("${_realname}"::"git+https://github.com/gwaldron/osgearth.git"
        "0001-Fixed-crash-due-to-probable-GCC-5.3.0-optimizer-bug.patch")
md5sums=('SKIP'
         '77f2817d9506a3bff7f5962159a27df1')

pkgver() {
  cd "$srcdir/$_realname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${srcdir}/${_realname}
  patch -p1 -i ${srcdir}/0001-Fixed-crash-due-to-probable-GCC-5.3.0-optimizer-bug.patch
}

build() {
  _pkgver=${_realname}-${pkgver} # How it's tagged in git

  export FREETYPE_DIR=${MINGW_PREFIX}

  for builddir in {Release,Debug}-${MINGW_CHOST}; do
    [[ -d ${builddir} ]] && rm -rf ${builddir}
    mkdir -p ${builddir}
    pushd ${builddir}
      [[ "${builddir%-${MINGW_CHOST}}" == "Release" ]] && {
        local _opts=""
      } || {
        local _opts=""
      }

      msg "Building ${builddir%-${MINGW_CHOST}} version..."

      MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
      ${MINGW_PREFIX}/bin/cmake.exe \
        -G"MSYS Makefiles" \
        -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
        -DCMAKE_BUILD_TYPE=${builddir%-${MINGW_CHOST}} \
        ${_opts} \
        ../${_realname}

      make
    popd
  done
}

package_release() {
  pkgdesc="A terrain rendering toolkit for OpenSceneGraph (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-OpenSceneGraph"
           "${MINGW_PACKAGE_PREFIX}-gdal")
  provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
  conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")

  cd Release-${MINGW_CHOST}
  make DESTDIR=${pkgdir} -j1 install
}

package_debug() {
  pkgdesc="A terrain rendering toolkit for OpenSceneGraph (debug) (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-${_realname}-git=$pkgver"
           "${MINGW_PACKAGE_PREFIX}-OpenSceneGraph-debug")
  provides=("${MINGW_PACKAGE_PREFIX}-${_realname}-debug")
  conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}-debug")
  options=('staticlibs' '!strip')

  cd Debug-${MINGW_CHOST}
  make DESTDIR=${pkgdir} -j1 install

  rm -rf ${pkgdir}${MINGW_PREFIX}/include
  rm -rf ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig
  rm -rf ${pkgdir}${MINGW_PREFIX}/share
}

package_mingw-w64-i686-osgearth-git() {
  package_release
}

package_mingw-w64-i686-osgearth-debug-git() {
  package_debug
}

package_mingw-w64-x86_64-osgearth-git() {
  package_release
}

package_mingw-w64-x86_64-osgearth-debug-git() {
  package_debug
}
