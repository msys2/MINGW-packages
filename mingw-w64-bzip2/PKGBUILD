# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

_realname=bzip2
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.1.0
pkgrel=3
_commit='66c46b8c9436613fd81bc5d03f63a61933a4dcc3'
pkgdesc="A high-quality data compression program (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://sourceware.org/bzip2/"
msys2_repository_url='https://sourceware.org/git/?p=bzip2.git'
msys2_references=(
  "cpe: cpe:/a:bzip:bzip2"
)
license=("custom")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "git")
source=("bzip2-${pkgver}::git+https://gitlab.com/bzip2/bzip2#commit=$_commit"
        "bzip2-1.1.0-progress.all.patch"
        "cmake-dual-libs.patch")
sha256sums=('60912dfdba992b5ac539aa44179f383cd61ae435bfed903562d985aeec572c85'
            '0c67171b43ded338160694cabcdab8b40cfdd18321de26a462796087e2e2fbef'
            '4f1ca2064ce685d79c624b74fc9dfd18a0c3d0015836e9380f71fe8873b585a3')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -p1 -i "$srcdir/"bzip2-1.1.0-progress.all.patch
  patch -p1 -i "$srcdir/"cmake-dual-libs.patch
}

build() {
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  mkdir -p "${srcdir}/shared-${MSYSTEM}" && cd "${srcdir}/shared-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    ${extra_config} \
    -DBUILD_SHARED_LIBS=ON \
    -DENABLE_APP=ON \
    -DENABLE_TESTS=ON \
    -DCMAKE_VERBOSE_MAKEFILE=ON \
    "../${_realname}-${pkgver}"

  cmake --build .
  
    mkdir -p "${srcdir}/static-${MSYSTEM}" && cd "${srcdir}/static-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    ${extra_config} \
    -DBUILD_SHARED_LIBS=OFF \
    -DENABLE_APP=ON \
    -DENABLE_TESTS=ON \
    "../${_realname}-${pkgver}"

  cmake --build .
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"
  make -k check
}

package() {
  cd "${srcdir}/shared-${MSYSTEM}"
  DESTDIR="${pkgdir}" cmake --install .
  
  cd "${srcdir}/static-${MSYSTEM}"
  DESTDIR="${pkgdir}" cmake --install .

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
