# Maintainer: David Macek <david.macek.0@gmail.com>

# todo check if unix directory is interesting
# todo install docs

_realname=putty
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.70
pkgrel=1
pkgdesc="A free telnet/SSH client (mingw-w64)"
arch=('any')
url="https://www.chiark.greenend.org.uk/~sgtatham/putty/"
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
optdepends=("${MINGW_PACKAGE_PREFIX}-putty-ssh: ssh wrapper for plink")
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}-git"
          "${MINGW_PACKAGE_PREFIX}-${_realname}-svn")
source=("https://the.earth.li/~sgtatham/putty/latest/${_realname}-${pkgver}.tar.gz"
        '0001-Fixes-for-various-compilation-warnings.patch'
        '0003-linedraw.patch')
sha256sums=('bb8aa49d6e96c5a8e18a057f3150a1695ed99a24eef699e783651d1f24e7b0be'
            '0c7528518319e3b514d0b8c929c8d73bca6c8e3a1a5ece4ab2329bca994a8aa5'
            '344e01c8032b258532c2edd2eb468adceab7c90f73ca38171487d9fa696a112e')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/0001-Fixes-for-various-compilation-warnings.patch
  patch -p1 -i "${srcdir}"/0003-linedraw.patch

  mkdir -p "${srcdir}"/${_realname}-build-${CARCH}
  cp -r "${srcdir}"/${_realname}-${pkgver}/* "${srcdir}"/${_realname}-build-${CARCH}
}

build() {
  cd "${srcdir}"/${_realname}-build-${CARCH}

  ./mkfiles.pl
  cd windows
  if [ "${CARCH}" = "x86_64" ]; then
    sed -i 's/x86/amd64/' *.mft
  fi
  make -f Makefile.mgw
}

package() {
  cd "${srcdir}"/${_realname}-build-${CARCH}/windows
  install -D -m644 ../LICENCE "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSE
  for f in putty puttygen pageant plink pscp psftp puttytel
  do
    install -D -m755 ${f}.exe "${pkgdir}"/${MINGW_PREFIX}/bin/${f}.exe
  done
}
