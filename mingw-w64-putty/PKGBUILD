# Maintainer: David Macek <david.macek.0@gmail.com>

# todo fix or explain: -DDEBUG -DWIN32S_COMPAT -D_NO_OLDNAMES -DNO_MULTIMON -DNO_HTMLHELP -DNO_SECUREZEROMEMORY
# todo check if unix directory is interesting
# todo install docs

_realname=putty
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.67
pkgrel=1
pkgdesc="A free telnet/SSH client (mingw-w64)"
arch=('any')
url="http://www.chiark.greenend.org.uk/~sgtatham/putty/"
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
optdepends=("${MINGW_PACKAGE_PREFIX}-putty-ssh: ssh wrapper for plink")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}-git"
          "${MINGW_PACKAGE_PREFIX}-${_realname}-svn")
source=("https://the.earth.li/~sgtatham/putty/latest/${_realname}-${pkgver}.tar.gz"
        '0001-fixes2.patch'
        '0003-linedraw.patch')
sha256sums=('80192458e8a46229de512afeca5c757dd8fce09606b3c992fbaeeee29b994a47'
            '33762b7858c18c2f4a891cdb4a77da893aa23fb7b1053f1146e51af6cb235a8c'
            '344e01c8032b258532c2edd2eb468adceab7c90f73ca38171487d9fa696a112e')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/0001-fixes2.patch
  patch -p1 -i "${srcdir}"/0003-linedraw.patch
  mkdir -p "${srcdir}"/${_realname}-build-${CARCH}
  cp -r "${srcdir}"/${_realname}-${pkgver}/* "${srcdir}"/${_realname}-build-${CARCH}
}

build() {
  cd "${srcdir}"/${_realname}-build-${CARCH}

  ./mkfiles.pl
  cd windows
  if [ "${CARCH}" = "x86_64" ]; then
    sed -i 's/x86/amd64/' *.mft
  fi
  make -f Makefile.cyg
}

package() {
  cd "${srcdir}"/${_realname}-build-${CARCH}/windows
  install -D -m644 ../LICENCE "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSE
  for f in putty puttygen pageant plink pscp psftp puttytel
  do
    install -D -m755 ${f}.exe "${pkgdir}"/${MINGW_PREFIX}/bin/${f}.exe
  done
}
