# Maintainer: David Macek <david.macek.0@gmail.com>

# todo check if unix directory is interesting
# todo install docs

_realname=putty
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.71
pkgrel=1
pkgdesc="A free telnet/SSH client (mingw-w64)"
arch=('any')
url="https://www.chiark.greenend.org.uk/~sgtatham/putty/"
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
optdepends=("${MINGW_PACKAGE_PREFIX}-putty-ssh: ssh wrapper for plink")
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}-git"
          "${MINGW_PACKAGE_PREFIX}-${_realname}-svn")
source=("https://the.earth.li/~sgtatham/putty/latest/${_realname}-${pkgver}.tar.gz"
        '0001-Fixes-for-various-compilation-warnings.patch')
sha256sums=('2f931ce2f89780cc8ca7bbed90fcd22c44515d2773f5fa954069e209b48ec6b8'
            '23c0d83c482c34230e23ba15b87df9916244bfc248ec084fb90221688362f160')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/0001-Fixes-for-various-compilation-warnings.patch

  mkdir -p "${srcdir}"/${_realname}-build-${CARCH}
  cp -r "${srcdir}"/${_realname}-${pkgver}/* "${srcdir}"/${_realname}-build-${CARCH}
}

build() {
  cd "${srcdir}"/${_realname}-build-${CARCH}

  ./mkfiles.pl
  cd windows
  if [ "${CARCH}" = "x86_64" ]; then
    sed -i 's/x86/amd64/' *.mft
  fi
  make -f Makefile.mgw
}

package() {
  cd "${srcdir}"/${_realname}-build-${CARCH}/windows
  install -D -m644 ../LICENCE "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSE
  for f in putty puttygen pageant plink pscp psftp puttytel
  do
    install -D -m755 ${f}.exe "${pkgdir}"/${MINGW_PREFIX}/bin/${f}.exe
  done
}
