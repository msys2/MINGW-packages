# Maintainer: David Macek <david.macek.0@gmail.com>

# todo fix or explain: -DDEBUG -DWIN32S_COMPAT -D_NO_OLDNAMES -DNO_MULTIMON -DNO_HTMLHELP -DNO_SECUREZEROMEMORY
# todo check if unix directory is interesting
# todo install docs

_realname=putty
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-svn"
_base_ver=0.63
pkgver=0.63.r10297
pkgrel=2
pkgdesc="A free telnet/SSH client (mingw-w64)"
arch=('any')
url="http://www.chiark.greenend.org.uk/~sgtatham/putty/"
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "subversion")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
optdepends=("${MINGW_PACKAGE_PREFIX}-putty-ssh: ssh wrapper for plink")
source=("$_realname::svn+svn://svn.tartarus.org/sgt/putty"
        '0001-fixes.patch'
	'0002-casts.patch'
	'0003-linedraw.patch')
md5sums=('SKIP'
         '5b5065e5bd2b8421196c068d428bfb57'
	 '1e4f574c0a0c6dfc8ec676f93fa109e7'
	 '2f7c232dd89a30cbe6e8b7dec329cda4')

pkgver() {
  cd "${srcdir}"/${_realname}
  local ver="$(svnversion)"
  printf "%s.r%s" "${_base_ver}" "${ver//[[:alpha:]]}"
}

prepare() {
  cd "${srcdir}"/${_realname}
  patch -p0 -i "${srcdir}"/0001-fixes.patch
  patch -p1 -i "${srcdir}"/0002-casts.patch
  patch -p1 -i "${srcdir}"/0003-linedraw.patch
}

build() {
  cd "${srcdir}"/${_realname}

  ./mkfiles.pl
  cd windows
  if [ "${CARCH}" = "x86_64" ]; then
    sed -i 's/x86/amd64/' *.mft
  fi
  make -f Makefile.cyg
}

package() {
  cd "${srcdir}"/${_realname}/windows
  install -D -m644 ../LICENCE "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSE
  for f in putty.exe puttygen.exe pageant.exe plink.exe pscp.exe psftp.exe puttytel.exe
  do
    install -D -m755 $f "${pkgdir}"/${MINGW_PREFIX}/bin/$f
  done
}
