# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=soqt

pkgbase=mingw-w64-${_realname}-hg
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-hg"
pkgver=r1943.a5b300353523
pkgrel=1
pkgdesc="The glue between Coin & Qt (mingw-w64)"
arch=('any')
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-coin3d" "${MINGW_PACKAGE_PREFIX}-qt5")
makedepends=("mercurial" "${MINGW_PACKAGE_PREFIX}-cmake" "${MINGW_PACKAGE_PREFIX}-gcc")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
url='https://bitbucket.org/Coin3D/dime'
source=("${_realname}"::"hg+https://bitbucket.org/Alexpux/soqt"
    "hg+https://bitbucket.org/Alexpux/generalmsvcgeneration"
    "hg+https://bitbucket.org/Alexpux/soanydata"
    "hg+https://bitbucket.org/Alexpux/sogui"
    )
options=(!strip staticlibs !makeflags)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd "$srcdir/$_realname"
  printf "r%s.%s" "$(hg identify -n)" "$(hg identify -i)"
}

prepare() {
  cd ${srcdir}/${_realname}
}

build() {
  #export lt_cv_deplibs_check_method='pass_all'
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
  ../"${_realname}"/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --disable-static \
    --enable-optimization \
    --disable-debug \
    --disable-loadlibrary \
    --with-qt=${MINGW_PREFIX} \
    --with-coin=${MINGW_PREFIX} \
    --without-x \
    --disable-dependency-tracking
  LC_ALL=C make -j1
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install
}
