#Maintainer: Ma, Leon <akml@163.com>

_realname=global
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=6.6.2
pkgrel=2
pkgdesc="GNU GLOBAL source code tagging system (mingw-w64)"
arch=('any')
url='https://www.gnu.org/software/global'
license=('GPL3')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-ncurses")

source=("https://ftp.gnu.org/gnu/${_realname}/${_realname}-${pkgver}.tar.gz"
	'001-global-6.6.2-mingw64.txt'
	)
sha256sums=('ca1dc15e9f320983e4d53ccb947ce58729952728273fdf415ab309ea2c0cd7fa'
            '2bb92dd9c93ebd26913b3cd768a5f2f0a7f5d6367a19f05323f9b89ec84e492e')

_global_srcdir=${_realname}-${pkgver}

prepare() {
  cd ${_global_srcdir}

  patch -p1 -i ${srcdir}/001-global-6.6.2-mingw64.txt
  autoconf

  rm -rf "${srcdir}"/build-${CARCH}
}

build() {
  cd ${srcdir} && mkdir build-${CARCH} && cd build-${CARCH}
  export CPPFLAGS="-I${MINGW_PREFIX}/include/ncurses"

  ../${_global_srcdir}/configure \
    --prefix=${MINGW_PREFIX} \
    --with-ncurses=${MINGW_PREFIX}

  make
}

check() {
  cd "${srcdir}"/build-${CARCH}
  make check
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install DESTDIR="${pkgdir}"
}
