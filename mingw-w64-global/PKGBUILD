#Maintainer: Ma, Leon <akml@163.com>

_realname=global
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=6.5.3
pkgrel=1
pkgdesc="GNU GLOBAL source code tagging system (mingw-w64)"
arch=('any')
url='https://www.gnu.org/software/global'
license=('GPL3')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-ncurses")

source=("https://ftp.gnu.org/gnu/${_realname}/${_realname}-${pkgver}.tar.gz"
        'glo653.txt' #Jason Hood <jadoxa@yahoo.com.au>, http://adoxa.altervista.org/global
        '002-fix-sigset_t-undefined.txt'
		)
sha256sums=('336f91f1d4a84469bc37a0dc7e9dc7cde9154cf677bb1bb5cd220c9b41b80302'
            'a10d36f47a5f9b335efd0a5f56bda555617619b16446d9b4f59ec46942fbe59d'
            '06e2d891f174f93187b754c149f48bbed01c5719d693439197f00ea8a2e8fe29'
			)

_global_srcdir=${_realname}-${pkgver}

prepare() {
  cd ${_global_srcdir}
  patch -p1 -i ${srcdir}/glo653.txt

  patch -p1 -i ${srcdir}/002-fix-sigset_t-undefined.txt

  rm -rf "${srcdir}"/build-${CARCH}
}

build() {
  cd ${srcdir} && mkdir build-${CARCH} && cd build-${CARCH}
  export CPPFLAGS="-I${MINGW_PREFIX}/include/ncurses"

  ../${_global_srcdir}/configure \
    --prefix=${MINGW_PREFIX}

  make
}

check() {
  cd "${srcdir}"/build-${CARCH}
  make check
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install DESTDIR="${pkgdir}"
}
