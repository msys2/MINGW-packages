#Maintainer: Ma, Leon <akml@163.com>

_realname=global
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=6.6.2
pkgrel=1
pkgdesc="GNU GLOBAL source code tagging system (mingw-w64)"
arch=('any')
url='https://www.gnu.org/software/global'
license=('GPL3')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-ncurses")

source=("https://ftp.gnu.org/gnu/${_realname}/${_realname}-${pkgver}.tar.gz"
	'001-global-6.6.2-mingw64.txt'
	)
sha256sums=('43c64711301c2caf40dc56d7b91dd03d2b882a31fa31812bf20de0c8fb2e717f'
	    '620f533f198c52488f2c3563921b4a0554dbc3d4689830dddbc45482c96c9bac'
	    )

_global_srcdir=${_realname}-${pkgver}

prepare() {
  cd ${_global_srcdir}

  patch -p1 -i ${srcdir}/001-global-6.6.2-mingw64.txt
  autoconf

  rm -rf "${srcdir}"/build-${CARCH}
}

build() {
  cd ${srcdir} && mkdir build-${CARCH} && cd build-${CARCH}
  export CPPFLAGS="-I${MINGW_PREFIX}/include/ncurses"

  ../${_global_srcdir}/configure \
    --prefix=${MINGW_PREFIX} \
    --with-ncurses=${MINGW_PREFIX}

  make
}

check() {
  cd "${srcdir}"/build-${CARCH}
  make check
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install DESTDIR="${pkgdir}"
}
