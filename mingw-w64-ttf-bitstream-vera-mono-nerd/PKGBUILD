# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_font=bitstreamveramono
_realname=ttf-bitstream-vera-mono-nerd
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgdesc="Patched font Bitstream Vera Mono from nerd fonts library (mingw-w64)"
pkgver=3.0.2
pkgrel=1
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://github.com/ryanoasis/nerd-fonts'
_urlbase="${url}/releases/download/v${pkgver}"
license=('spdx:Bitstream-Vera')
options=('!debug' '!strip')
groups=("${MINGW_PACKAGE_PREFIX}-nerd-fonts")
source=("${_font}-${pkgver}.tar.xz::${_urlbase}/BitstreamVeraSansMono.tar.xz")
sha256sums=('89f422c10bf0d32b255f15a6a409b932587498e8836508c4c064fbbc4c466f72')
noextract=("${_font}-${pkgver}.tar.xz")

prepare() {
  rm -rf ${_font}
  mkdir ${_font}
  bsdtar xf ${_font}-${pkgver}.tar.xz -C ${_font}
}

package() {
  install -Dm644 ${_font}/*.ttf -t "${pkgdir}${MINGW_PREFIX}/share/fonts/TTF"
  install -Dm644 ${_font}/"Bitstream Vera License.txt" -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/"
}
