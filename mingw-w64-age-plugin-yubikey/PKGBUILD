# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=age-plugin-yubikey
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.5.0
pkgrel=1
pkgdesc='Yubikey plugin for age (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/str4d/age-plugin-yubikey"
msys2_repository_url='https://github.com/str4d/age-plugin-yubikey'
license=("spdx:Apache-2.0 OR MIT")
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-pkgconf")
optdepends=("${MINGW_PACKAGE_PREFIX}-age: for use with age")
_archive="${_realname}-${pkgver}"
source=("${url}/archive/v${pkgver}/${_archive}.tar.gz")
sha256sums=('65807403f0098569a473ffa76302b205da148a7f46b61fd331b8e323959978ba')

prepare() {
  cd "${_archive}"
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd "${_archive}"
  cargo build --frozen --release --all-features
}

check() {
  cd "${_archive}"
  cargo test --frozen --all-features
}

package() {
  cd "${_archive}"
  install -Dm0755 "target/release/${_realname}.exe" "${pkgdir}${MINGW_PREFIX}/bin/${_realname}.exe"
  install -Dm0644 LICENSE-MIT "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE-MIT"
}
