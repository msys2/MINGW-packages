# Contributor: Rafa≈Ç Brzegowy <rafal.brzegowy@yahoo.com>

_realname=similarity_measures
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.4.0
pkgrel=3
pkgdesc="Quantify the difference between two arbitrary curves (mingw64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/cjekel/similarity_measures"
msys2_repository_url='https://github.com/cjekel/similarity_measures'
msys2_changelog_url='https://github.com/cjekel/similarity_measures/blob/master/CHANGELOG.md'
msys2_references=(
  'purl: pkg:pypi/similaritymeasures'
)
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-numpy"
         "${MINGW_PACKAGE_PREFIX}-python-matplotlib"
         "${MINGW_PACKAGE_PREFIX}-python-scipy")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools")
source=("https://pypi.org/packages/source/${_realname::1}/${_realname/_/}/${_realname/_/}-${pkgver}.tar.gz")
sha256sums=('52032dd11ea3c0f94158e17df16f8d512101d6de0d31a2db42193f1434720572')

build() {
  cp -r "${_realname/_/}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
