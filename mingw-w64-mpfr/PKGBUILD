# Maintainer: Alexey Pavlov <Alexpux@gmail.com>
# Contributor: Karlson2k (Evgeny Grin) <k2k@narod.ru>

_realname=mpfr
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
_pkgver=3.1.4
_patchlevel=p3
pkgver=${_pkgver}
if [ -n "${_patchlevel}" ]; then
  pkgver+=.${_patchlevel}
fi
pkgrel=3
pkgdesc="Multiple-precision floating-point library (mingw-w64)"
arch=('any')
license=('LGPL')
options=('staticlibs')
depends=("${MINGW_PACKAGE_PREFIX}-gmp")
makedepends=("${MINGW_PACKAGE_PREFIX}-gmp")
source=(http://www.mpfr.org/mpfr-current/${_realname}-${_pkgver}.tar.xz{,.asc}
        mpfr-${_pkgver}-${_patchlevel}.patch)
sha256sums=('761413b16d749c53e2bfd2b1dfaa3b027b0e793e404b90b5fbaeef60af6517f5'
            'SKIP'
            '164847a4513a1a1647e2ec62f8bdbd4745d4bc0e26fc6f324439f28325f04d84')

prepare() {
  cd "${srcdir}/${_realname}-${_pkgver}"
  [ -n "${_patchlevel}" ] && patch -p1 -i $srcdir/${_realname}-${_pkgver}-${_patchlevel}.patch
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}

  ../${_realname}-${_pkgver}/configure \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --enable-static \
    --disable-shared \
    --with-gmp=${MINGW_PREFIX}

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
