# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=mpfr
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
_pkgver=3.1.3
_patchlevel=p0
pkgver=${_pkgver}.${_patchlevel}
pkgrel=2
pkgdesc="Multiple-precision floating-point library (mingw-w64)"
arch=('any')
license=('LGPL')
options=('staticlibs')
depends=("${MINGW_PACKAGE_PREFIX}-gmp")
makedepends=("${MINGW_PACKAGE_PREFIX}-gmp")
source=(http://www.mpfr.org/mpfr-current/${_realname}-${_pkgver}.tar.xz{,.asc})
sha256sums=('6835a08bd992c8257641791e9a6a2b35b02336c8de26d0a8577953747e514a16'
            'SKIP')

prepare() {
  cd "${srcdir}/${_realname}-${_pkgver}"
  #patch -p1 -i $srcdir/${_realname}-${_pkgver}-${_patchlevel}.patch
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}

  ../${_realname}-${_pkgver}/configure \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --enable-static \
    --disable-shared \
    --with-gmp=${MINGW_PREFIX}

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
