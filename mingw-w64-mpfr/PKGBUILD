# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=mpfr
_mingw_suff=mingw-w64-${CARCH}
pkgname=("${_mingw_suff}-${_realname}")
_pkgver=3.1.2
_patchlevel=p5
pkgver=${_pkgver}.${_patchlevel}
pkgrel=1
pkgdesc="Multiple-precision floating-point library"
arch=('any')
url="http://www.mpfr.org/"
groups=("${_mingw_suff}")
license=('LGPL')
options=('staticlibs')
depends=("${_mingw_suff}-gmp>=5.0")
makedepends=("${_mingw_suff}-gmp>=5.0")
source=(http://www.mpfr.org/mpfr-current/mpfr-${_pkgver}.tar.xz{,.asc}
        mpfr-${_pkgver}-${_patchlevel}.patch)
md5sums=('e3d203d188b8fe60bb6578dd3152e05c'
         'SKIP'
         'e19c1d8db2cde4991ab52c8c20d16185')

prepare() {
  cd "${srcdir}/${_realname}-${_pkgver}"
  patch -p1 -i $srcdir/${_realname}-${_pkgver}-${_patchlevel}.patch
}

build() {
  cd "${srcdir}/${_realname}-${_pkgver}"

  ./configure --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
  --enable-static \
  --disable-shared \
  --with-gmp=${MINGW_PREFIX}
  make
}

check() {
  cd "${srcdir}/${_realname}-${_pkgver}"
  make check
}

package() {
  cd "${srcdir}/${_realname}-${_pkgver}"
  make DESTDIR="${pkgdir}" install
}
