# Maintainer: Alexey Pavlov <Alexpux@gmail.com>
# Contributor: Karlson2k (Evgeny Grin) <k2k@narod.ru>

_realname=mpfr
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
_pkgver=4.2.0
pkgver=${_pkgver}.p9
pkgrel=1
pkgdesc="Multiple-precision floating-point library (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
license=('spdx:LGPL-3.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gmp")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools")
source=(https://ftp.gnu.org/gnu/mpfr/${_realname}-${_pkgver}.tar.xz{,.sig}
        patches.diff) # wget https://www.mpfr.org/mpfr-4.2.0/allpatches -O patches.diff
sha256sums=('06a378df13501248c1b2db5aa977a2c8126ae849a9d9b7be2546fb4a9c26d993'
            'SKIP'
            '06da0b2a56e365f7e74ac241926f61d2574a0754548ff8116e9ddee0dabacda0')
validpgpkeys=('A534BE3F83E241D918280AEB5831D11A0D4DB02A')

prepare() {
  cd "${srcdir}/${_realname}-${_pkgver}"

  patch -p1 -i ${srcdir}/patches.diff
  autoreconf -fiv
}

build() {
  [[ -d build-${MSYSTEM} ]] && rm -rf build-${MSYSTEM}
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  ../${_realname}-${_pkgver}/configure \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --disable-static \
    --enable-shared \
    --with-gmp=${MINGW_PREFIX}

  make
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"
  make check
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
}
