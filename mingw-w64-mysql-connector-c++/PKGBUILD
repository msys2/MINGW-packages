
_realname=mysql-connector-c++
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=9.6.0
pkgrel=1
pkgdesc="A MySQL database connector for C++ (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url="https://dev.mysql.com/doc/connector-cpp/${pkgver%.*}/en/" 
msys2_repository_url="https://github.com/mysql/mysql-connector-cpp"
msys2_references=(
  'aur: mysql-connector-c++'
)
license=("spdx:GPL-2.0-only")
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-lz4"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-zlib-ng"
         "${MINGW_PACKAGE_PREFIX}-zstd")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-doxygen")

source=("https://dev.mysql.com/get/Downloads/Connector-C++/${_realname}-${pkgver}-src.zip"
        001-noarchive-openssl-deps.patch
        002-install-locations.patch
        003-no-wholearchive-dll.patch)
sha256sums=('0b4697fd97d23e9e88ef22b7c5d6672ca6ee6378cc8ad3209e336224fab95c09'
            '4ab93335988b7fca8e77b06338a57012727c327f9c6ffc82dcb05ba97722f800'
            '8f747e0b20abbbf768f80d68cf3c75fc51f487d624e27919953ec0f4f65a858f'
            '25d431fd98793ce13827288944f44689e7be38b0174d367af147a8560f6616fa')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}-src"

  find . -name "CMake*" -type f -exec sed -i 's/CMAKE_MINIMUM_REQUIRED.*\(VERSION .*\)/CMAKE_MINIMUM_REQUIRED\(VERSION 3\.15)/I' {} \;

  apply_patch_with_msg \
    001-noarchive-openssl-deps.patch \
    002-install-locations.patch \
    003-no-wholearchive-dll.patch
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}-shared" && cd "${srcdir}/build-${MSYSTEM}-shared"

  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  CXXFLAGS+=" -Wno-unknown-pragmas -Wno-pragmas" \
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=;-DINSTALL_DOC_DIR=" \
  ${MINGW_PREFIX}/bin/cmake \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_INSTALL_LIBDIR="lib" \
    -DINSTALL_DOC_DIR=${MINGW_PREFIX}/share/doc/mysql-concpp \
    -GNinja \
    "${_extra_config[@]}" \
    -DBUNDLE_DEPENDENCIES=OFF \
    -DWITH_PROTOBUF=ON \
    -DWITH_LZ4=system \
    -DWITH_SSL=system \
    -DWITH_ZLIB=system \
    -DWITH_ZSTD=system \
    -DWITH_DOC=ON \
    ../${_realname}-${pkgver}-src

    ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}-shared"

  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install .

  install -vDm 644 "${srcdir}"/${_realname}-${pkgver}-src/LICENSE.txt -t "${pkgdir}${MINGW_PREFIX}/share/licenses/mysql-concpp/"
  install -vDm 644 "${srcdir}"/${_realname}-${pkgver}-src/{README.txt,README.md} -t "${pkgdir}${MINGW_PREFIX}/share/doc/mysql-concpp/"
  install -d "${pkgdir}${MINGW_PREFIX}/share/doc/mysql-concpp/html"
  cp -r "${srcdir}/build-${MSYSTEM}-shared/doc/html/"* "${pkgdir}${MINGW_PREFIX}/share/doc/mysql-concpp/html/"
  chmod -R 644 "${pkgdir}${MINGW_PREFIX}/share/doc/mysql-concpp/html/"
}
