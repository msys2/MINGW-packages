# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=gmpy2
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}"  "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=2.1.0a4
pkgrel=1
pkgdesc="Provides C-coded Python modules for fast multiple-precision arithmetic. (mingw-w64)"
arch=('any')
url='https://github.com/aleaxit/gmpy'
license=('LGPL2.1')
makedepends=("${MINGW_PACKAGE_PREFIX}-mpc"
             "${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools")
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/aleaxit/gmpy/archive/${_realname}-${pkgver/.0/}.tar.gz")
noextract=("${_realname}-$pkgver.tar.gz")
sha256sums=('747404f0a6a92f87767a5c1ef593f1a2fd1c9b2b180b70a5f77b4e5fa7db088b')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"
# bsd tar does not handle the symlinks in the distribution file
# at least if you don't have admin privilleges.  This is just a workaround
# for that.
  tar zxf "${_realname}-${pkgver}.tar.gz" || true
  pushd "gmpy-${_realname}-${pkgver/.0/}"
  cp src/gmpy2.h gmpy2/gmpy2.h
  cp src/gmpy2.pxd gmpy2/gmpy2.pxd
  popd
  for builddir in python{2,3}-build-${CARCH}; do
    rm -rf ${builddir} | true
    cp -r "gmpy-${_realname}-${pkgver/.0/}" "${builddir}"
  done
  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

# Note that build() is sometimes skipped because it's done in 
# the packages setup.py install for simplicity if you can do so.
# b#ut sometimes, you want to do a check before install which would
# also trigger the build.
build() {
  for pver in {2,3}; do  
    msg "Python ${pver} build for ${CARCH}"  
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py build
  done  
}

check() {
  for pver in {2,3}; do
    msg "Python ${pver} test for ${CARCH}"
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py check
  done
}

package_python3-gmpy2() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3" "${MINGW_PACKAGE_PREFIX}-mpc")

  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -d -m 755 ${pkgdir}${MINGW_PREFIX}/share/doc/python3-${_realname}
  install -m 644 -t ${pkgdir}${MINGW_PREFIX}/share/doc/python3-${_realname} docs/*
}

package_python2-gmpy2() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2" "${MINGW_PACKAGE_PREFIX}-mpc")

  cd "${srcdir}/python2-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -d -m 755 ${pkgdir}${MINGW_PREFIX}/share/doc/python2-${_realname}
  install -m 644 -t ${pkgdir}${MINGW_PREFIX}/share/doc/python2-${_realname} docs/*
}

package_mingw-w64-i686-python2-gmpy2() {
  package_python2-gmpy2
}

package_mingw-w64-i686-python3-gmpy2() {
  package_python3-gmpy2
}

package_mingw-w64-x86_64-python2-gmpy2() {
  package_python2-gmpy2
}

package_mingw-w64-x86_64-python3-gmpy2() {
  package_python3-gmpy2
}
