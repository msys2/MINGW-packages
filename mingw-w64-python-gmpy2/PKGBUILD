# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=gmpy2
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}"  "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=2.1.0a2
pkgrel=1
pkgdesc="Provides C-coded Python modules for fast multiple-precision arithmetic. (mingw-w64)"
arch=('any')
url='https://github.com/aleaxit/gmpy'
license=('LGPL2.1')
validpgpkeys=('gpg_KEY')
makedepends=("${MINGW_PACKAGE_PREFIX}-mpc"
             "${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools")
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-$pkgver.tar.gz"::"https://github.com/aleaxit/gmpy/archive/${_realname}-$pkgver.tar.gz"
        "gmpy2-cython.patch"::"https://github.com/aleaxit/gmpy/commit/cb0d591e.patch")

sha256sums=('54702d5da8a5e0b074c9e34da49737027205f3aaf034d22d8b049adb553169a9'
            '457e29bc4441a868eabd5a1f2a45316d13b96e949c2616237f2700e5d3d5bf6c')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"
  pushd "gmpy-${_realname}-${pkgver}"
  apply_patch_with_msg gmpy2-cython.patch
  popd
  for builddir in python{2,3}-build-${CARCH}; do
    rm -rf ${builddir} | true
    cp -r "gmpy-${_realname}-${pkgver}" "${builddir}"
  done
  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

# Note that build() is sometimes skipped because it's done in 
# the packages setup.py install for simplicity if you can do so.
# b#ut sometimes, you want to do a check before install which would
# also trigger the build.
build() {
  for pver in {2,3}; do  
    msg "Python ${pver} build for ${CARCH}"  
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py build
  done  
}

check() {
  for pver in {2,3}; do
    msg "Python ${pver} test for ${CARCH}"
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py check
  done
}

package_python3-gmpy2() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3" "${MINGW_PACKAGE_PREFIX}-mpc")

  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -d -m 755 $pkgdir${MINGW_PREFIX}//share/doc/python3-${_realname}
  install -m 644 -t $pkgdir${MINGW_PREFIX}//share/doc/python3-${_realname} docs/*
}

package_python2-gmpy2() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2" "${MINGW_PACKAGE_PREFIX}-mpc")

  cd "${srcdir}/python2-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -d -m 755 $pkgdir${MINGW_PREFIX}//share/doc/python2-${_realname}
  install -m 644 -t $pkgdir${MINGW_PREFIX}//share/doc/python2-${_realname} docs/*


}

package_mingw-w64-i686-python2-gmpy2() {
  package_python2-gmpy2
}

package_mingw-w64-i686-python3-gmpy2() {
  package_python3-gmpy2
}

package_mingw-w64-x86_64-python2-gmpy2() {
  package_python2-gmpy2
}

package_mingw-w64-x86_64-python3-gmpy2() {
  package_python3-gmpy2
}
