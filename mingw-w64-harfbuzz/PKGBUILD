# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=harfbuzz
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=0.9.26
pkgrel=3
pkgdesc="OpenType text shaping engine (mingw-w64)"
arch=('any')
url="http://www.freedesktop.org/wiki/Software/HarfBuzz"
license=("MIT")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config" "${_mingw_suff}-icu" "${_mingw_suff}-gobject-introspection")
depends=("${_mingw_suff}-gcc-libs" "${_mingw_suff}-glib2" "${_mingw_suff}-freetype" "${_mingw_suff}-cairo")
options=('strip' 'staticlibs')
optdepends=("${_mingw_suff}-icu: harfbuzz-icu")
source=("http://www.freedesktop.org/software/harfbuzz/release/${_realname}-${pkgver}.tar.bz2")
sha256sums=('5ded7dc3d15fb5c9230d7a224b9256b67b35fde30193b4c3a4c3f6b13110e257')

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  "${srcdir}"/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --enable-static \
    --enable-introspection
  make
}

check(){
  cd "${srcdir}/build-${MINGW_CHOST}"
  make check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install
  find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
}
