# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Andreas Baumgaertner <andreas.baumgaertner@tisco.at>

_realname=isl

pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
pkgver=0.14.1
pkgrel=1
pkgdesc="Library for manipulating sets and relations of integer points bounded by linear constraints"
arch=('any')
url="http://freecode.com/projects/isl"
groups=("${MINGW_PACKAGE_PREFIX}")
#depends=("${MINGW_PACKAGE_PREFIX}-gmp")
makedepends=("${MINGW_PACKAGE_PREFIX}-gmp" 
             "git")
options=('staticlibs')
license=('MIT')
source=("${_realname}"::"git://repo.or.cz/isl.git#tag=isl-0.14.1"
        "isl-0.14.1-no-undefined.patch")
md5sums=('SKIP'
         '1baeffd5df12e3e2c0feda4c636ff05a')

prepare() {
  cd "$srcdir/${_realname}"
  patch -p1 -i ${srcdir}/isl-0.14.1-no-undefined.patch
  
  autoreconf -fi
}

build() {
  cd "$srcdir/${_realname}"
  ./configure --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --enable-static \
    --disable-shared \
    --with-gmp-prefix=${MINGW_PREFIX}
  make
}

check() {
  cd "$srcdir/${_realname}"
  make check
}

package() {
  cd "${srcdir}/${_realname}"
  make DESTDIR="${pkgdir}" install
}
