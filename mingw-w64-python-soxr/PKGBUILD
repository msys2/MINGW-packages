# Contributor: dragon-archer <dragon-archer@outlook.com>

_realname=soxr
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.0.0
pkgrel=2
pkgdesc='Fast and high quality sample-rate conversion library for Python. (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'archlinux: python-soxr'
  'purl: pkg:pypi/soxr'
)
url='https://github.com/dofuuz/python-soxr'
license=('spdx:LGPL-2.1-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-libsoxr"
         "${MINGW_PACKAGE_PREFIX}-python-numpy")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools-scm"
             "${MINGW_PACKAGE_PREFIX}-python-scikit-build-core"
             "${MINGW_PACKAGE_PREFIX}-nanobind"
             "${MINGW_PACKAGE_PREFIX}-cc")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-pytest")
options=('!strip')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('e07ee6c1d659bc6957034f4800c60cb8b98de798823e34d2a2bba1caa85a4509')

build() {
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}

  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  "${MINGW_PREFIX}/bin/python" -m build --wheel --skip-dependency-check --no-isolation \
    -C cmake.define.USE_SYSTEM_LIBSOXR=ON
}

check() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  # Needs manual install
  mkdir _install
  "${MINGW_PREFIX}/bin/python" -m installer --prefix= --destdir=_install dist/*.whl

  cd _install/lib/python*/site-packages/

  PYTHONPATH=. ${MINGW_PREFIX}/bin/pytest "${srcdir}/python-build-${MSYSTEM}/tests"
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    "${MINGW_PREFIX}/bin/python" -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE.txt"
  install -Dm644 COPYING.LGPL "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/COPYING.LGPL"
}
