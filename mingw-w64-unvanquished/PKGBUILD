# ArchLinux (AUR):
# Maintainer: Viech <viech unvanquished net>
# Contributor: Gereon Schomber
# Contributor: Martin F. Schumann
# MSYS2:
# Maintainer: Mateusz Miku≈Ça <mati865@gmail.com>

_realname=unvanquished
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.50.0
pkgrel=1

_gitver="archlinux/${pkgver}-${pkgrel}"
_unvanquished="${_realname/u/U}-${_gitver/\//-}"

_naclsdkver="4"
if test "$CARCH" == "x86_64"; then
	_naclsdkname=mingw64
else
	_naclsdkname=mingw32
fi
_naclsdk="${_naclsdkname}-${_naclsdkver}"

pkgdesc='A team-based, fast-paced, fps/rts hybrid game which pits aliens against humans. Monthly alpha release. (mingw-w64)'
arch=('any')
url='http://www.unvanquished.net'
license=('GPL3')
depends=("${MINGW_PACKAGE_PREFIX}-unvanquished-data>=${pkgver}"
         "${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-freetype"
         "${MINGW_PACKAGE_PREFIX}-geoip"
         "${MINGW_PACKAGE_PREFIX}-glew"
         "${MINGW_PACKAGE_PREFIX}-gmp"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-libogg"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libtheora"
         "${MINGW_PACKAGE_PREFIX}-libvorbis"
         "${MINGW_PACKAGE_PREFIX}-libwebp"
         "${MINGW_PACKAGE_PREFIX}-nettle"
         "${MINGW_PACKAGE_PREFIX}-openal"
         "${MINGW_PACKAGE_PREFIX}-opus"
         "${MINGW_PACKAGE_PREFIX}-opusfile"
         "${MINGW_PACKAGE_PREFIX}-SDL2"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake")
provides=('unvanquished')
conflicts=('unvanquished-git')
options=('emptydirs' '!strip')
backup=("${MINGW_PREFIX:1}/etc/conf.d/unvanquished.conf"
        "${MINGW_PREFIX:1}/etc/unvanquished/server.cfg"
        "${MINGW_PREFIX:1}/etc/unvanquished/maprotation.cfg")
install="${_realname}-${CARCH}.install"
source=("https://github.com/Unvanquished/Unvanquished/archive/${_gitver}.tar.gz"
        'unvanquished-i686.install'
				'unvanquished-x86_64.install'
				"https://dl.unvanquished.net/deps/${_naclsdk}.zip")

prepare() {
	cd "${srcdir}"

  # Use MSYS2 libs
  rm -rf "${_naclsdk}/bin" "${_naclsdk}/include" "${_naclsdk}/lib"

  [[ -d "${_unvanquished}/daemon/external_deps/${_naclsdkname}" ]] \
    && rm -rf "${_unvanquished}/daemon/external_deps/${_naclsdkname}"
	ln -sfr "${_naclsdk}" -t "${_unvanquished}/daemon/external_deps"

	# Use /mingw{32,64} paths
	for file in "${srcdir}/${_unvanquished}"/archlinux/*.{conf,sh}; do
		sed -e "s/\/usr/\\${MINGW_PREFIX}/g" \
				-e "s/\/var/\\${MINGW_PREFIX}\/var/g" \
				-i $file
	done
}

build() {
	cd "${srcdir}/${_unvanquished}"

  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -G'MSYS Makefiles' \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DBUILD_CGAME=OFF \
      -DBUILD_SGAME=OFF \
      ../${_unvanquished}
	make
}

package() {
	# create installation directories

	install -dm 755 "${pkgdir}${MINGW_PREFIX}"/etc/conf.d \
                  "${pkgdir}${MINGW_PREFIX}"/etc/unvanquished \
                  "${pkgdir}${MINGW_PREFIX}"/bin \
                  "${pkgdir}${MINGW_PREFIX}"/lib/systemd/system \
                  "${pkgdir}${MINGW_PREFIX}"/lib/unvanquished \
                  "${pkgdir}${MINGW_PREFIX}"/share/applications \
                  "${pkgdir}${MINGW_PREFIX}"/share/icons/hicolor/128x128/apps \
                  "${pkgdir}${MINGW_PREFIX}"/share/licenses/unvanquished \
                  "${pkgdir}${MINGW_PREFIX}"/share/unvanquished/pkg \
                  "${pkgdir}${MINGW_PREFIX}"/var/lib/unvanquished-server/config \
                  "${pkgdir}${MINGW_PREFIX}"/var/lib/unvanquished-server/game

	# install content
	cd "${srcdir}/${_unvanquished}"

	install -m 644 debian/unvanquished.png "${pkgdir}${MINGW_PREFIX}/share/icons/hicolor/128x128/apps/"
	install -m 644 COPYING.txt             "${pkgdir}${MINGW_PREFIX}/share/licenses/unvanquished/"

	cd "${srcdir}"/build-${CARCH}

	install -m 755 daemon                  "${pkgdir}${MINGW_PREFIX}/lib/unvanquished/"
	install -m 755 daemonded               "${pkgdir}${MINGW_PREFIX}/lib/unvanquished/"
	install -m 755 daemon-tty              "${pkgdir}${MINGW_PREFIX}/lib/unvanquished/"
	install -m 755 irt_core-x86*.nexe      "${pkgdir}${MINGW_PREFIX}/lib/unvanquished/"
	# install -m 755 nacl_helper_bootstrap   "${pkgdir}/${MINGW_PREFIX}/lib/unvanquished/"
	install -m 755 nacl_loader             "${pkgdir}${MINGW_PREFIX}/lib/unvanquished/"

	# install starters and dedicated server config
	cd "${srcdir}/${_unvanquished}/archlinux"

	install -m 755 unvanquished.sh         "${pkgdir}${MINGW_PREFIX}/bin/unvanquished"
	install -m 755 unvanquished-tty.sh     "${pkgdir}${MINGW_PREFIX}/bin/unvanquished-tty"
	install -m 644 unvanquished.conf       "${pkgdir}${MINGW_PREFIX}/etc/conf.d/"
	# install -m 644 unvanquished.service    "${pkgdir}/usr/lib/systemd/system/"
	# install -m 644 unvanquished.desktop    "${pkgdir}/usr/share/applications/"
	install -m 644 configs/maprotation.cfg "${pkgdir}${MINGW_PREFIX}/etc/unvanquished/"
	install -m 644 configs/server.cfg      "${pkgdir}${MINGW_PREFIX}/etc/unvanquished/"

	# setup server home directory
	# moved to install file
	# cd "${pkgdir}${MINGW_PREFIX}/var/lib/unvanquished-server/config"
	#
	# MSYS='winsymlinks:lnk' ln -s ../../../..${MINGW_PREFIX}/etc/unvanquished/server.cfg .
	#
	# cd "${pkgdir}${MINGW_PREFIX}/var/lib/unvanquished-server/game"
	#
	# MSYS='winsymlinks:lnk' ln -s ../../../..${MINGW_PREFIX}/etc/unvanquished/maprotation.cfg .
}

sha256sums=('a4bd28ff55fe481d9e22f6209ef6ae647642bbcaca6653f6968172ec2fc2fcdf'
            'c65a44f13e5d757cc280ce35d723d56f27c34256ff8569143cbbc0d17c3dc464'
						'78cba91d087f5ca2ce2bda3fbe8cfdd13ce53ca404b1057b7c16063d59d4fe27')
if test "$CARCH" == "x86_64"; then
	sha256sums+=('507918feff75dd22894fb60750fd8112446d537d33bf5173353d351e2256e32b')
else
	sha256sums+=('85595e3b6ebdd936c711ab9c892a3f442e58412dbc7e4937a26aeba631f3bf9f')
fi
