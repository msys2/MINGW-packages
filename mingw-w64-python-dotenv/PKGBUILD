# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=dotenv
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.2.1
pkgrel=3
pkgdesc="Get and set values in your .env file in local and production servers (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/theskumar/python-dotenv/"
msys2_references=(
  'archlinux: python-dotenv'
  'gentoo: dev-python/python-dotenv'
  'purl: pkg:pypi/python-dotenv'
)
license=('spdx:BSD-3-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-click")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python-build"
  "${MINGW_PACKAGE_PREFIX}-python-installer"
  "${MINGW_PACKAGE_PREFIX}-python-setuptools"
)
options=("!strip")
source=("https://github.com/theskumar/python-dotenv/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('d9eebba53ff89f06bf99f2c87d2abef2b9da0790dc4b082ce1f9c5572f2cb1d0')

build() {
  cd "python-${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "python-${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
