# Maintainer: Peter Budai <peterbudai at hotmail dot com>

_realname=libgphoto2
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.5.12
pkgrel=1
pkgdesc="libgphoto2 is a free, redistributable, ready to use set of digital camera software applications for Unix-like system. (mingw-w64)"
arch=('any')
url="http://gphoto.org/proj/libgphoto2/"
license=("LGPL")
depends=("${MINGW_PACKAGE_PREFIX}-libsystre"
         "${MINGW_PACKAGE_PREFIX}-libjpeg"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-libgd"
         "${MINGW_PACKAGE_PREFIX}-libexif"
         "${MINGW_PACKAGE_PREFIX}-libusb")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-libtool")
source=(#https://sourceforge.net/projects/gphoto/files/libgphoto/${pkgver}/${_realname}-${pkgver}.tar.gz{,.asc}
        ${_realname}-${pkgver}.tar.gz::https://github.com/gphoto/libgphoto2/archive/${_realname}-${pkgver//./_}-release.tar.gz
        libgphoto2-gp_system_filename-fix.patch
        libgphoto2-use-pkg-config-for-gd.patch) 
sha256sums=('82118e8abaf54100c6be1d41489f9a26d951c7a6194db6f11efa60274a6f53a7'
            '27c558cdf88bee6ce1f3f564d6dfbc0b78809c33b98d20c9e58e40e335d7b0dd'
            '16036711f9c656e3988bc3c7b3e5209db3ad1163048cc7a0139e3e882d52f7c2')
validpgpkeys=('7C4AFD61D8AAE7570796A5172209D6902F969C95') # Marcus Meissner 

prepare() {
  cd "${srcdir}/${_realname}-${_realname}-${pkgver//./_}-release"
  patch -Np1 -i "${srcdir}/libgphoto2-gp_system_filename-fix.patch"
  patch -Np1 -i "${srcdir}/libgphoto2-use-pkg-config-for-gd.patch"
  autoreconf -fiv
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  ../${_realname}-${_realname}-${pkgver//./_}-release/configure \
    --prefix="${MINGW_PREFIX}"      \
    --build=${MINGW_CHOST}          \
    --host=${MINGW_CHOST}           \
    --with-gdlib=auto
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install

  # Remove unused udev helper
  rm -rf "${pkgdir}/${MINGW_PREFIX}/lib/udev"

  # Remove recursive symlink
  rm -rfv "${pkgdir}/${MINGW_PREFIX}/include/gphoto2/gphoto2"  

  # install profile script
  mkdir -p "${pkgdir}${MINGW_PREFIX}"/etc/profile.d

cat > ${pkgdir}${MINGW_PREFIX}/etc/profile.d/${_realname}.sh <<EOF
export CAMLIBS=${MINGW_PREFIX}/lib/${_realname}/${pkgver}
export IOLIBS=${MINGW_PREFIX}/lib/${_realname}_port/0.12.0
EOF

}
