# Maintainer: @naveen521kk on Github, Naveen M K <naveen521kk@gmail.com>

_perlname=DateTime-Locale
_realname="${_perlname,,}"
pkgbase=mingw-w64-perl-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-perl-${_realname}")
pkgver=1.33
pkgrel=1
pkgdesc="Localization support for DateTime.pm (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://metacpan.org/release/${_perlname}"
groups=("${MINGW_PACKAGE_PREFIX}-perl-modules")
provides=(
  "${MINGW_PACKAGE_PREFIX}-perl-datetime-locale-base"
  "${MINGW_PACKAGE_PREFIX}-perl-datetime-locale-catalog"
  "${MINGW_PACKAGE_PREFIX}-perl-datetime-locale-data"
  "${MINGW_PACKAGE_PREFIX}-perl-datetime-locale-fromdata"
  "${MINGW_PACKAGE_PREFIX}-perl-datetime-locale-util"
)
depends=(
  "${MINGW_PACKAGE_PREFIX}-perl"
  "${MINGW_PACKAGE_PREFIX}-perl-warnings"
  "${MINGW_PACKAGE_PREFIX}-perl-file-spec"
  "${MINGW_PACKAGE_PREFIX}-perl-list-util"
  "${MINGW_PACKAGE_PREFIX}-perl-specio-declare"
  "${MINGW_PACKAGE_PREFIX}-perl-file-sharedir"
  "${MINGW_PACKAGE_PREFIX}-perl-exporter"
  "${MINGW_PACKAGE_PREFIX}-perl-storable"
  "${MINGW_PACKAGE_PREFIX}-perl-strict"
  "${MINGW_PACKAGE_PREFIX}-perl-specio-library-string"
  "${MINGW_PACKAGE_PREFIX}-perl-params-validationcompiler"
  "${MINGW_PACKAGE_PREFIX}-perl-namespace-autoclean"
  "${MINGW_PACKAGE_PREFIX}-perl-dist-checkconflicts"
  "${MINGW_PACKAGE_PREFIX}-perl-carp"
)
options=('!emptydirs')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-cc"
  $([[ ${MINGW_PACKAGE_PREFIX} != *-clang-* ]] || \
    echo "${MINGW_PACKAGE_PREFIX}-gcc-compat")
)
license=('perl_5')
source=(
  'https://cpan.metacpan.org/authors/id/D/DR/DROLSKY/DateTime-Locale-1.33.tar.gz'
  'patchmakefile.py'
)
sha256sums=('52bfefad9ff39c4d8fd1f5e78642e38b8dcd33363f337d8e89bf19e6ad185cb9'
            '00fe5ced42aa635714909a8eba1347a6c132011a9bce68e9c75b27ebd64628f2')

build() {
  cd "$srcdir/${_perlname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL
  export pkgdir=$pkgdir
  python $srcdir/patchmakefile.py
  mingw32-make
}

package() {
  cd "$srcdir/${_perlname}-${pkgver}"
  mingw32-make DESTDIR="${pkgdir}/" install
}
