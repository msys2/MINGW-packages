# $Id$
# Contributor: Jevgeny Krasovsky <jkrasovsky@gmail.com>

_realname=qrencode
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.0.2
pkgrel=1
pkgdesc="C library for encoding data in a QR Code symbol (mingw-w64)"
arch=(any)
depends=("${MINGW_PACKAGE_PREFIX}-libpng")
makedepends=(${MINGW_PACKAGE_PREFIX}-SDL)
url="http://megaui.net/fukuchi/works/qrencode/index.en.html"
license=('GPL')
source=(http://megaui.net/fukuchi/works/${_realname}/${_realname}-${pkgver}.tar.bz2)
sha256sums=('c9cb278d3b28dcc36b8d09e8cad51c0eca754eb004cb0247d4703cb4472b58b4')

build() {
  rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
  install -Dm0644 ${srcdir}/${_realname}-${pkgver}/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
