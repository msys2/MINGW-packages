# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=qbs
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3.2
pkgrel=1
_pkgfqn="${_realname}-${pkgver}.src"
pkgdesc='Qt Build Suite'
url='http://qt-project.org'
arch=('any')
depends=("${MINGW_PACKAGE_PREFIX}-qt5")
makedepends=("git")
options=('docs' 'staticlibs') # '!strip' 'debug')
source=("http://download.qt-project.org/official_releases/qbs/${pkgver}/${_pkgfqn}.tar.gz"
        #${_realname}-${pkgver}::"git://gitorious.org/qt-labs/qbs.git#tag=v$pkgver"
        "manifest"
        "0001-add-hostosinfo.h-to-installed-headers.patch"
        "0002-WIP-Xcode-generator.patch"
        "0003-add-generator-header-installs.patch"
        "0005-shared-vs-static-extension.patch"
        "0006-probe-pretend-msys-is-mingw.patch")
sha256sums=('03ce02bc1e5d6985e79318a7e49b95052166de5fe464c9149ce2828368292a9f'
            '838098b25a8044176b3139b4003594570c62a8d64f5470fbbd769f3bf44e0855'
            'b791b90bd4e49fd0188167c308a3da1773ddc6740d88c385e4f5d811ef0bb117'
            '6a4a18fbd710f35ba2515b793b07471fd9d4d954e169e4f5ee6067a22c13f8a3'
            '036c5452f19eebfc3c39982b47191e4c4e7ae2ba0e0b23dc818105189f54b441'
            'd6193c98284e3b228a5f2143e95da53768d08d6b229fe71fda6941de2f8f0409'
            'b96dfb92a385cdecfb7795b17292694942f50183613e40564cbe545556a090d6')

prepare() {
  cd ${_realname}-${pkgver}
  [ -d src/plugins/generator ] && rm -rf src/plugins/generator
  rm -rf src/lib/corelib/api/generator.*
  rm -rf src/lib/corelib/tools/generat*
  patch -p1 < "${srcdir}"/0001-add-hostosinfo.h-to-installed-headers.patch
  patch -p1 < "${srcdir}"/0002-WIP-Xcode-generator.patch
  patch -p1 < "${srcdir}"/0003-add-generator-header-installs.patch
  patch -p1 < "${srcdir}"/0005-shared-vs-static-extension.patch
  patch -p1 < "${srcdir}"/0006-probe-pretend-msys-is-mingw.patch
}

build() {
local _config
if check_option "debug" "n"; then
  _config="release"
else
  _config="debug"
fi
  cd ${srcdir}/${_realname}-${pkgver}
  [ -d build-${CARCH} ] && rm -rf build-${CARCH}
  mkdir build-${CARCH}
  cd build-${CARCH}
  ${MINGW_PREFIX}/bin/qmake.exe ../qbs.pro \
    CONFIG+=${_config} \
    CONFIG+=qbs_enable_project_file_updates
  make
  make docs
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}/build-${CARCH}
  make INSTALL_ROOT=${pkgdir}${MINGW_PREFIX} install
  make INSTALL_ROOT=${pkgdir}${MINGW_PREFIX} install_docs

  rm -f ${pkgdir}${MINGW_PREFIX}/lib/*.dll

  for file in qbs-setup-madde-platforms qbs-setup-qt; do
    cp ${srcdir}/manifest ${pkgdir}${MINGW_PREFIX}/bin/${file}.exe.manifest
  done
}

