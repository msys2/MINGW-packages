# Maintainer (ArchLinux): Andrea Scarpino <andrea@archlinux.org>
# Contributor (ArchLinux): Tobias Powalowski <tpowa@archlinux.org>
# Maintainer (MSYS2): Mateusz Miku≈Ça <mati865@gmail.com>

_realname=strigi
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.7.8
pkgrel=1
pkgdesc="Fast crawling desktop search engine with Qt GUI"
arch=('any')
url='http://strigi.sourceforge.net/'
license=('GPL2')
depends=("${MINGW_PACKAGE_PREFIX}-dbus"
         "${MINGW_PACKAGE_PREFIX}-exiv2"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-bzip2")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake")
optdepends=('kdegraphics-strigi-analyzer: strigi analyzers for various graphics file'
            'kdesdk-strigi-analyzers: strigi analyzers for KDE SDK')
source=("http://www.vandenoever.info/software/${_realname}/${_realname}-${pkgver}.tar.bz2"
        'gcc48.patch'
        'fix-gcc6-detection.patch')
sha256sums=('43e461a625acdefa59bf58cc9d10aebda9eba920dfbe800fc77e6dcbf1eff989'
            'a38f3eeb4f158f505b39b6fab62614053951c22613adad65ebdcd23db13591b2'
            '78eb745986015248bc7cdd2f987604659983ce4096575550fdb4eaf90d8c111d')

prepare() {
  [[ -d "${srcdir}/build-${CARCH}" ]] && rm -rf "${srcdir}/build-${CARCH}"
  mkdir -p "${srcdir}/build-${CARCH}"

  cd ${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/fix-gcc6-detection.patch
  
  cd libstreams
  patch -p1 -i "${srcdir}"/gcc48.patch
}

build() {
  cd "${srcdir}/build-${CARCH}"
  
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake ../${_realname}-${pkgver} \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_SKIP_RPATH=ON \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DENABLE_INOTIFY=ON \
      -DENABLE_LOG4CXX=OFF \
      -DENABLE_FAM=OFF \
      -DENABLE_CLUCENE=OFF \
      -DENABLE_CLUCENE_NG=OFF \
      -DENABLE_FFMPEG=OFF \
      -DENABLE_QT4=OFF \
      -G'MSYS Makefiles'
  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install
}
