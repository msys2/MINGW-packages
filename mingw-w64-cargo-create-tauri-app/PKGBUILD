# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_basename=create-tauri-app
_realname=cargo-${_basename}
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.3.0
pkgrel=1
pkgdesc="Rapidly scaffold out a new tauri app project (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/tauri-apps/create-tauri-app"
license=('spdx:MIT OR Apache-2.0')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
source=("${url}/archive/${_basename}-v${pkgver}/${_basename}-${_basename}-v${pkgver}.tar.gz")
sha256sums=('b68bb11596f383de1888e70a4db3ab90c0c2870111f3cda13471bee092931c22')

prepare() {
  cd "${srcdir}/${_basename}-${_basename}-v${pkgver}"

  local _target="${CARCH}-pc-windows-gnu"
  if [[ $MINGW_PACKAGE_PREFIX == *-clang-aarch64 ]]; then
    _target="${CARCH}-pc-windows-gnullvm"
  fi
  "${MINGW_PREFIX}/bin/cargo.exe" fetch --locked --target "${_target}"
}

build() {
  cp -r "${_basename}-${_basename}-v${pkgver}" "build-${MSYSTEM}"
  cd "build-${MSYSTEM}"

  "${MINGW_PREFIX}/bin/cargo.exe" build \
    --release \
    --frozen
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  "${MINGW_PREFIX}/bin/cargo.exe" test \
    --release \
    --frozen
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  install -Dm755 "target/release/${_realname}.exe" "${pkgdir}${MINGW_PREFIX}/bin/${_realname}.exe"
  install -Dm644 "README.md" "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/README.md"
  install -Dm644 "LICENSE_MIT" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE_MIT"
  install -Dm644 "LICENSE_APACHE-2.0" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE_APACHE-2.0"
}
