# Maintainer: Zoltan Gyarmati <mr.zoltan.gyarmati@gmail.com>

_realname=bitwise
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.50
pkgrel=1
pkgdesc="Terminal-based bit manipulator (ncurses)"
url="https://github.com/mellowcandle/bitwise"
msys2_repository_url='https://github.com/mellowcandle/bitwise'
license=("spdx:GPL-3.0-only")
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')

depends=("${MINGW_PACKAGE_PREFIX}-ncurses"
         "${MINGW_PACKAGE_PREFIX}-readline")

makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
	"${MINGW_PACKAGE_PREFIX}-binutils"
	"${MINGW_PACKAGE_PREFIX}-autotools")

options=('strip')

source=("${_realname}-${pkgver}.tar.gz::https://github.com/mellowcandle/${_realname}/archive/refs/tags/v${pkgver}.tar.gz"
        "0001-Add-some-fixes-to-enable-build-on-msys2-under-Window.patch")

sha512sums=('1fdf449c385fb01674c23a7dacc4ce3c1c95330d061a7c0bfa319cd4d2ed76948cadf806cebef119220b420c33afe7f6b333844e6cd81dcc2887af6b26f84657'
            '9a28a4f141afb5c56d201a6bf70871171c64cf6a5c8434437f7040a13265c813258926b1917798a0ff933cf85e39ba117df3184724d34221e1f0f3082c7e7b84')

# Prevent automatic extraction to fix NTFS collision issues
# as the tarball has a README and a README.md file
noextract=("${_realname}-${pkgver}.tar.gz")

prepare() {
  cd "${srcdir}"
  bsdtar -xvf "${_realname}-${pkgver}.tar.gz"  --no-same-owner --no-same-permissions --exclude README
  cd "${_realname}-${pkgver}"
  patch -p1 < "${srcdir}/0001-Add-some-fixes-to-enable-build-on-msys2-under-Window.patch"
  ./bootstrap.sh
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  ./configure --prefix="${MINGW_PREFIX}"
  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}

