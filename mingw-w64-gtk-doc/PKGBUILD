# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=gtk-doc
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.29
pkgrel=1
pkgdesc="Documentation tool for public library API (mingw-w64)"
arch=('any')
url="https://www.gtk.org/gtk-doc/"
license=("GPL" "FDL")
options=(staticlibs strip)
depends=(
    "${MINGW_PACKAGE_PREFIX}-docbook-xsl"
    "${MINGW_PACKAGE_PREFIX}-docbook-xml"
    "${MINGW_PACKAGE_PREFIX}-libxslt"
    "${MINGW_PACKAGE_PREFIX}-python3"
    )
makedepends=(
    "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
    "itstool"
    )
url="https://www.gtk.org/gtk-doc/"
source=(https://download.gnome.org/sources/${_realname}/${pkgver}/${_realname}-${pkgver}.tar.xz
        pyscript2exe.py)
sha256sums=('14578e002496567276d310a62c9ffd6c56ee8806ce5079ffb0b81c4111f586b1'
            'f68b24932b3365c4098c04eeaeaf87275ceec29694b3f0597c431bbcf4f913a3')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
}

build() {
  export MSYS2_ARG_CONV_EXCL="-//OASIS"

  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  export PYTHON=${MINGW_PREFIX}/bin/python3
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --enable-static \
    --with-highlight=no \
    --with-xml-catalog=${MINGW_PREFIX}/etc/xml/catalog

  make

  for name in check fixxref mkdb mkhtml mkman mkpdf rebase scan scangobj ; do
    sed -s "s|'${MINGW_PREFIX}|sys.prefix + '|g" -i "gtkdoc-${name}"
  done

  echo -e "import sys\n$(cat gtkdoc/config.py)" > gtkdoc/config.py

  sed -s "s|'${MINGW_PREFIX}|sys.prefix + '|g" -i "gtkdoc/config.py"
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"

  make DESTDIR="$pkgdir" install

  for name in check fixxref mkdb mkhtml mkman mkpdf rebase scan scangobj depscan; do
    ${MINGW_PREFIX}/bin/python3 \
      "${srcdir}/pyscript2exe.py" "${pkgdir}${MINGW_PREFIX}/bin/gtkdoc-${name}"
  done
}
