# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=gtk-doc

pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.26
pkgrel=3
pkgdesc="Documentation tool for public library API (mingw-w64)"
arch=('any')
url="https://www.gtk.org/gtk-doc/"
license=("GPL" "FDL")
options=(staticlibs strip)
depends=(
    "${MINGW_PACKAGE_PREFIX}-docbook-xsl"
    "${MINGW_PACKAGE_PREFIX}-docbook-xml"
    "${MINGW_PACKAGE_PREFIX}-libxslt"
    "${MINGW_PACKAGE_PREFIX}-python3"
    "${MINGW_PACKAGE_PREFIX}-python3-six"
    )
makedepends=(
    "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
    "itstool"
    )
url="https://www.gtk.org/gtk-doc/"
source=(https://download.gnome.org/sources/${_realname}/${pkgver}/${_realname}-${pkgver}.tar.xz
        pyscript2exe.py
        0001-Always-open-files-in-text-mode-and-always-use-utf-8.patch
        0002-fixxref-rebase-Don-t-depend-on-glib-being-installed.patch
        win-fixes.patch)
sha256sums=('bff3f44467b1d39775e94fad545f050faa7e8d68dc6a31aef5024ba3c2d7f2b7'
            'f68b24932b3365c4098c04eeaeaf87275ceec29694b3f0597c431bbcf4f913a3'
            '02c7544fb2d84cd4fb7884a2521239008f75c35c085b3d1da06aa73d2a456296'
            '32e6c1f17ff6182c0f480e930beeb3bdc5ad8fb6bf4339b0838b9abc58680584'
            'bab8527690d529300b85154bd51b8118e0dfe3db1afec51d52d815066000b2e5')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # https://bugzilla.gnome.org/show_bug.cgi?id=787862
  patch -Np1 -i "${srcdir}"/0001-Always-open-files-in-text-mode-and-always-use-utf-8.patch
  # https://bugzilla.gnome.org/show_bug.cgi?id=787768
  patch -Np1 -i "${srcdir}"/0002-fixxref-rebase-Don-t-depend-on-glib-being-installed.patch
  patch -Np1 -i "${srcdir}"/win-fixes.patch
}

build() {
  export MSYS2_ARG_CONV_EXCL="-//OASIS"

  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  export PYTHON=${MINGW_PREFIX}/bin/python3
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --enable-static \
    --with-highlight=no \
    --with-xml-catalog=${MINGW_PREFIX}/etc/xml/catalog

  make

  for name in check fixxref mkdb mkhtml mkman mkpdf rebase scan scangobj ; do
    sed -s "s|'${MINGW_PREFIX}|sys.prefix + '|g" -i "gtkdoc-${name}"
  done

  echo -e "import sys\n$(cat gtkdoc/config.py)" > gtkdoc/config.py

  sed -s "s|'${MINGW_PREFIX}|sys.prefix + '|g" -i "gtkdoc/config.py"
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"

  make DESTDIR="$pkgdir" install

  for name in check fixxref mkdb mkhtml mkman mkpdf rebase scan scangobj depscan; do
    ${MINGW_PREFIX}/bin/python3 \
      "${srcdir}/pyscript2exe.py" "${pkgdir}${MINGW_PREFIX}/bin/gtkdoc-${name}"
  done
}
