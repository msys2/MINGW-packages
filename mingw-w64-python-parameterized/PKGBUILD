# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=parameterized
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
provides=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
replaces=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=0.7.1
pkgrel=1
pkgdesc="Parameterized testing with any Python test framework (mingw-w64)"
arch=('any')
url='https://github.com/wolever/parameterized'
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-mock"
              "${MINGW_PACKAGE_PREFIX}-python-nose")
options=('staticlibs' 'strip' '!debug')
source=("https://pypi.io/packages/source/p/parameterized/parameterized-${pkgver}.tar.gz")
sha512sums=('331752b6bf27f88921d39567a58900c0e78f1740056d93755fc7e4528a032207f81e4938302c27cdfd82fe2ad455c01100d91da5f222a11e4bcf0210de68d9c2')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  ${MINGW_PREFIX}/bin/python setup.py nosetests
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
