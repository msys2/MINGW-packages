_realname=libzen
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.4.41
pkgrel=1
pkgdesc="A C++ utility library (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url="https://mediaarea.net"
license=("spdx:Zlib")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-autotools"
  "${MINGW_PACKAGE_PREFIX}-cc"
)
depends=(
  "${MINGW_PACKAGE_PREFIX}-gcc-libs"
)

source=("https://mediaarea.net/download/source/${_realname}/${pkgver}/${_realname}_${pkgver}.tar.bz2")
sha256sums=('eb237d7d3dca6dc6ba068719420a27de0934a783ccaeb2867562b35af3901e2d')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

prepare() {
  cd "${srcdir}/ZenLib/Project/GNU/Library"

  ./autogen.sh
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  ../ZenLib/Project/GNU/Library/configure \
    --prefix="${MINGW_PREFIX}" \
    --host="${MINGW_CHOST}" \
    --build="${MINGW_CHOST}" \
    --enable-shared \
    --disable-static
  
  # Fix for out-of-source build
  INCLUDES="-I../ZenLib/Source" \
    mingw32-make
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  make DESTDIR="${pkgdir}" install

  install -vDm 644 "${srcdir}/build-${MSYSTEM}/.libs/libzen.lai" -t "${pkgdir}${MINGW_PREFIX}/lib/"
  install -vDm 644 "${srcdir}/Zenlib/License.txt" -t "${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname/"

  install -vDm 644 "${srcdir}/Zenlib/"{History.txt,README.md} -t "${pkgdir}${MINGW_PREFIX}/share/doc/$_realname/"
}
