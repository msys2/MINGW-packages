# Maintainer: Carlos Antunes <cmantunes@gmail.com>
# Contributed by: nanodude

_realname=gdb
_target=arm-none-eabi
_download_version=7-2018q2

pkgbase=mingw-w64-${_target}-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_target}-${_realname}")
pkgver=8.1.0
pkgrel=1
pkgdesc="GNU Tools for ARM Embedded Processors - GDB (mingw-w64)"
arch=('any')
url="https://launchpad.net/gcc-arm-embedded"
license=('GPL' 'Various')
groups=("${MINGW_PACKAGE_PREFIX}-${_target}-toolchain")
depends=("${MINGW_PACKAGE_PREFIX}-readline"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('staticlibs' '!debug' 'strip')
source=("gcc-arm-embedded-${_download_version}.tar.bz2"::"https://developer.arm.com/-/media/Files/downloads/gnu-rm/7-2018q2/gcc-arm-none-eabi-7-2018-q2-update-src.tar.bz2?revision=bf2e4a67-c08c-49d5-b97d-17757cc8a77e")
sha256sums=('965c5755edffbab0320736285d17de3f4189bee1801099a17f6c9aeb0218acf0')
noextract=("gcc-arm-embedded-${_download_version}.tar.bz2")

prepare() {
  # Extract packages from archive
  rm -rf ${srcdir}/tmp
  mkdir ${srcdir}/tmp && cd ${srcdir}/tmp
  tar  xvf ../../gcc-arm-embedded-${_download_version}.tar.bz2 --strip-components=2

  # Copy and extract GDB package
  cp gdb.tar.bz2 ${srcdir}
  cd ${srcdir}
  tar -xf gdb.tar.bz2 --checkpoint=100
}

build() {
  # Build GDB
  cd $srcdir
  rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}
  ../${_realname}/configure \
    --build="${MINGW_CHOST}" \
    --host="${MINGW_CHOST}" \
    --target="${_target}" \
    --prefix="${MINGW_PREFIX}" \
    --disable-werror \
    --disable-nls \
    --with-system-readline
  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make -j1 DESTDIR=${pkgdir} install

  # Remove files that conflict with arm-none-eabi-binutils
  rm -rf ${pkgdir}${MINGW_PREFIX}/share/man/man1
  rm -rf ${pkgdir}${MINGW_PREFIX}/arm-none-eabi
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/arm-none-eabi-addr2line.exe
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/arm-none-eabi-ar.exe
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/arm-none-eabi-as.exe
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/arm-none-eabi-c++filt.exe
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/arm-none-eabi-elfedit.exe
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/arm-none-eabi-gprof.exe
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/arm-none-eabi-ld.bfd.exe
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/arm-none-eabi-ld.exe
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/arm-none-eabi-nm.exe
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/arm-none-eabi-objcopy.exe
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/arm-none-eabi-objdump.exe
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/arm-none-eabi-ranlib.exe
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/arm-none-eabi-readelf.exe
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/arm-none-eabi-size.exe
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/arm-none-eabi-strings.exe
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/arm-none-eabi-strip.exe

  # Remove files that conflict with host
  rm -rf "${pkgdir}${MINGW_PREFIX}/share/info"
  rm -rf "${pkgdir}${MINGW_PREFIX}/share/gdb"
  rm -rf "${pkgdir}${MINGW_PREFIX}/include/gdb/jit-reader.h"
  rm -rf "${pkgdir}${MINGW_PREFIX}/lib/libiberty.a"
}

