# Maintainer: Michael Rans <rans@email.com>

_realname=espeak-ng
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.52+f3fcd1d
pkgrel=1
pkgdesc="A text-to-speech synthesizer for Linux, Windows, Android and other operating systems"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/espeak-ng/espeak-ng"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc" "${MINGW_PACKAGE_PREFIX}-cmake" "${MINGW_PACKAGE_PREFIX}-ninja" "git" "patch")
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs" "${MINGW_PACKAGE_PREFIX}-pcaudiolib")
_commit=0d451f8c1c6ae837418b823bd9c4cbc574ea9ff5
source=("git+https://github.com/mcarans/espeak-ng#commit=${_commit}"
		"MSYS2_MinGW64.patch")
sha256sums=('c6956dc62d0441b27bc81a373dee60905d688c708373b41270f89dd33fea30c0'
            '170e2b290ba2430f098270c13a41447056c09806c939e6d46c5f350f87cbf91a')

pkgver() {
  cd "$srcdir/$_gitname"
  # Use short hash
  printf "%s" "1.52+$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$_realname"
  patch -p1 -i ${srcdir}/MSYS2_MinGW64.patch
}

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi
 
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  cmake \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DBUILD_SHARED_LIBS=YES \
	-DHAVE_PTHREAD=yes \
	-DUSE_LIBPCAUDIO=yes \
    ${extra_config[@]} \
    -S ${_realname} \
    -B build-${MSYSTEM}

  cmake --build build-${MSYSTEM}

}

check() {
  cd "${srcdir}/build-${MSYSTEM}"
}

package() {
  DESTDIR="${pkgdir}" cmake --install build-${MSYSTEM}
  cd ${pkgdir}${MINGW_PREFIX}/include/espeak
  echo "#include \"../espeak-ng/speak_lib.h\"" > speak_lib.h
  
  install -Dm644 "${srcdir}/${_realname}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING" 
}
