# Maintainer: Marcelo Duarte https://github.com/marcelotduarte

_realname=cabarchive
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.2.5
pkgrel=1
pkgdesc="A pure-python library for creating and extracting cab files (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/hughsie/python-cabarchive"
msys2_repository_url='https://github.com/hughsie/python-cabarchive'
msys2_references=(
  'purl: pkg:pypi/cabarchive'
)
license=('spdx:LGPL')
depends=(
  "${MINGW_PACKAGE_PREFIX}-python"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python-build"
  "${MINGW_PACKAGE_PREFIX}-python-installer"
  "${MINGW_PACKAGE_PREFIX}-python-setuptools"
)
options=(!strip)
source=(
  "https://pypi.org/packages/source/${_realname::1}/${_realname/-/_}/${_realname/-/_}-${pkgver}.tar.gz"
)
sha256sums=(
  'b508b1cad890857e55f3f7a2027ca7bedb9dc05de5eff07f8289f9546200a7aa'
)

prepare() {
  cd "$srcdir"
  rm -rf python-build-${MSYSTEM} | true

  cp -r "${_realname//_/-}-$pkgver" "python-build-${MSYSTEM}"
}

build() {
  cd "python-build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}

