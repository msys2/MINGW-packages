# Contributor: star-hengxing <hengxing9029@outlook.com>

_realname=clip
pkgbase=mingw-w64-${_realname,,}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname,,}")
pkgver=1.11
pkgrel=1
pkgdesc="Library to copy/retrieve content to/from the clipboard/pasteboard. (mingw-w64)"
arch=(any)
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/dacap/clip"
msys2_repository_url='https://github.com/dacap/clip'
license=(spdx:MIT)
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
source=(${url}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz)
noextract=("${_realname}-${pkgver}.tar.gz")
sha256sums=('047d43f837adffcb3a26ce09fd321472615cf35a18e86418d789b70d742519dc')

prepare() {
  rm -rf ${_realname}-${pkgver}
  tar -xzf ${_realname}-${pkgver}.tar.gz || true
}

build() {
  mkdir -p "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G Ninja \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    ${extra_config[@]} \
    -DCLIP_TESTS=OFF \
    -DCLIP_EXAMPLES=OFF \
    -DCLIP_TESTS=OFF \
    -DCLIP_EXAMPLES=OFF \
    -DCLIP_ENABLE_IMAGE=ON \
    -DCLIP_ENABLE_LIST_FORMATS=ON \
    ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/LICENSE.txt -t "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname,,}/
}
