# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=gnome-latex
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.28.1
pkgrel=2
arch=('any')
pkgdesc="LaTeX editor designed for the GNOME desktop (mingw-w64)"
options=(strip staticlibs)
depends=("${MINGW_PACKAGE_PREFIX}-gsettings-desktop-schemas"
         "${MINGW_PACKAGE_PREFIX}-gtk3"
         "${MINGW_PACKAGE_PREFIX}-gtksourceview4"
         "${MINGW_PACKAGE_PREFIX}-gspell"
         "${MINGW_PACKAGE_PREFIX}-tepl"
         "${MINGW_PACKAGE_PREFIX}-libgee")
makedepends=("itstool"
             "${MINGW_PACKAGE_PREFIX}-vala"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection")
license=("GPL 2")
url="https://www.gnome.org"
install=${_realname}-${CARCH}.install
source=(https://download.gnome.org/sources/${_realname}/${pkgver%.*}/${_realname}-${pkgver}.tar.xz
        001-win.patch)
sha256sums=('02105e81765388faa9f75ccb43b745b1d66bfb14285b7855e6a7809e7f0c88fc'
            'a8ceaef88a506ac6172ed13454008c253e88e9c625c432ef0a336df911c9ff78')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  patch -p1 -b -V simple -i ${srcdir}/001-win.patch

  autoreconf -sf
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --enable-introspection \
    --disable-dconf-migration

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"

  make DESTDIR=${pkgdir} install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
