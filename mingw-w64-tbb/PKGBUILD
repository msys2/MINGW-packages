# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=oneTBB
pkgbase=mingw-w64-tbb
pkgname=("${MINGW_PACKAGE_PREFIX}-tbb")
pkgver=2021.5.0
pkgrel=2
arch=(any)
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://software.intel.com/en-us/oneapi/onetbb'
license=("custom")
pkgdesc='oneAPI Threading Building Blocks (mingw-w64)'
conflicts=("${MINGW_PACKAGE_PREFIX}-intel-tbb")
#replaces=("${MINGW_PACKAGE_PREFIX}-intel-tbb")
#provides=("${MINGW_PACKAGE_PREFIX}-intel-tbb")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         $([[ "${MSYSTEM}" == "CLANGARM64" ]] || echo "${MINGW_PACKAGE_PREFIX}-hwloc"))
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cc")
source=("https://github.com/oneapi-src/${_realname}/archive/v${pkgver}.tar.gz"
        001-fix-build-with-mingw.patch
        002-do-not-copy-lib.patch::https://github.com/oneapi-src/oneTBB/commit/f5de7b03.patch
        003-fix-win32-defs.patch
        004-support-mingw-aarch64.patch)
sha256sums=('e5b57537c741400cf6134b428fc1689a649d7d38d9bb9c1b6d64f092ea28178a'
            '1e342350d8655a7930a4ba7e28f654296cdd8f99233c957398f86d66f8a31e3e'
            'd58684d258afd03e6158402bff7bba06bba2c64fc9f32887be7cb565ddd21de3'
            'cd533959fcabe4c4f5452ec349cb25f048b96c8b8e5c560f786553daefd19390'
            '0f28cdc8a90e8dc3870cd584cf9f5f683e0a35696fe4dd816d1f6b8b5ea39de5')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  # https://github.com/oneapi-src/oneTBB/pull/618 for both 001 and 003
  patch -p1 -i ${srcdir}/001-fix-build-with-mingw.patch
  patch -p1 -i ${srcdir}/003-fix-win32-defs.patch

  patch -p1 -i ${srcdir}/002-do-not-copy-lib.patch
  patch -p1 -i ${srcdir}/004-support-mingw-aarch64.patch
}

build() {
  [[ -d "${srcdir}"/build-${MSYSTEM} ]] && rm -rf "${srcdir}"/build-${MSYSTEM}
  mkdir -p "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

  if check_option "debug" "n"; then
    _build_type="Release"
  else
    _build_type="Debug"
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -GNinja \
    -DCMAKE_BUILD_TYPE=${_build_type} \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DTBB_TEST=OFF \
    -DTBB_STRICT=OFF \
    -DBUILD_SHARED_LIBS=ON \
    ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  DESTDIR=${pkgdir} ${MINGW_PREFIX}/bin/cmake --install build-${MSYSTEM}
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
