# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libarchive
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=3.1.2
pkgrel=2
pkgdesc="library that can create and read several streaming archive formats (mingw-w64)"
arch=('any')
url="http://libarchive.googlecode.com"
license=("BSD")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-libgnurx" "${_mingw_suff}-pkg-config")
depends=(${_mingw_suff}-gcc-libs
		${_mingw_suff}-bzip2
		${_mingw_suff}-expat
		${_mingw_suff}-lzo2
		${_mingw_suff}-nettle
		${_mingw_suff}-openssl
		${_mingw_suff}-libiconv
		${_mingw_suff}-xz
		${_mingw_suff}-zlib)
options=('!libtool' 'strip')
source=("http://libarchive.org/downloads/libarchive-$pkgver.tar.gz"
		"0001-mtree-fix-line-filename-length-calculation.patch"
		'libarchive-3.1.3-CVE-2013-0211_read_buffer_overflow.patch')
md5sums=('efad5a503f66329bb9d2f4308b5de98a'
         '9727baf88b928417d5d1269891b1209a'
         '6954a585c309dc548d1f2b05f2f3cfcb')

prepare() {
  cd "$srcdir/libarchive-$pkgver"
  patch -Np1 -i "$srcdir/0001-mtree-fix-line-filename-length-calculation.patch"
  patch -Np1 -i "$srcdir/libarchive-3.1.3-CVE-2013-0211_read_buffer_overflow.patch"
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ${srcdir}/${_realname}-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
	--with-expat \
	--without-xml2
	
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install
  #rm -r "${pkgdir}${MINGW_PREFIX}/share"
}
