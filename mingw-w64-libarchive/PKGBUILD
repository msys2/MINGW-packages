# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libarchive
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.1.2
pkgrel=7
pkgdesc="library that can create and read several streaming archive formats (mingw-w64)"
arch=('any')
url="http://www.libarchive.org"
license=("BSD")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=(${MINGW_PACKAGE_PREFIX}-gcc-libs
         ${MINGW_PACKAGE_PREFIX}-bzip2
         ${MINGW_PACKAGE_PREFIX}-expat
         ${MINGW_PACKAGE_PREFIX}-libiconv
         ${MINGW_PACKAGE_PREFIX}-lzo2
         ${MINGW_PACKAGE_PREFIX}-libsystre
         ${MINGW_PACKAGE_PREFIX}-nettle
         ${MINGW_PACKAGE_PREFIX}-openssl
         ${MINGW_PACKAGE_PREFIX}-xz
         ${MINGW_PACKAGE_PREFIX}-zlib)
options=('!libtool' 'strip')
source=("http://libarchive.org/downloads/${_realname}-${pkgver}.tar.gz"
        "0001-mtree-fix-line-filename-length-calculation.patch"
        'libarchive-3.1.3-CVE-2013-0211_read_buffer_overflow.patch')
sha256sums=('eb87eacd8fe49e8d90c8fdc189813023ccc319c5e752b01fb6ad0cc7b2c53d5e'
            '2e7a80bbed769e3c14be562623bc755b16eec3f3c97fd09400bc4bc442454bdb'
            '2e390980be2bce0da109847e61164e38fb41f5ad203ddbf7f0426bad4d16b980')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Np1 -i "$srcdir/0001-mtree-fix-line-filename-length-calculation.patch"
  patch -Np1 -i "$srcdir/libarchive-3.1.3-CVE-2013-0211_read_buffer_overflow.patch"
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --with-expat \
    --without-xml2

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
