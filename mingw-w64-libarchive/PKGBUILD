# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libarchive
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.2.2
pkgrel=1
pkgdesc="library that can create and read several streaming archive formats (mingw-w64)"
arch=('any')
url="http://www.libarchive.org"
license=("BSD")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=(${MINGW_PACKAGE_PREFIX}-gcc-libs
         ${MINGW_PACKAGE_PREFIX}-bzip2
         ${MINGW_PACKAGE_PREFIX}-expat
         ${MINGW_PACKAGE_PREFIX}-libiconv
         ${MINGW_PACKAGE_PREFIX}-lz4
         ${MINGW_PACKAGE_PREFIX}-lzo2
         ${MINGW_PACKAGE_PREFIX}-libsystre
         ${MINGW_PACKAGE_PREFIX}-nettle
         ${MINGW_PACKAGE_PREFIX}-openssl
         ${MINGW_PACKAGE_PREFIX}-xz
         ${MINGW_PACKAGE_PREFIX}-zlib)
#makedepends=("${MINGW_PACKAGE_PREFIX}-cmake")
options=('!libtool' 'strip')
source=("http://libarchive.org/downloads/${_realname}-${pkgver}.tar.gz"
         0001-libarchive-3.2.2-bcrypt-fix.patch)
sha256sums=('691c194ee132d1f0f7a42541f091db811bc2e56f7107e9121be2bc8c04f1060f'
            'd51d1154cf75e73bdd7a2d140b54b3ea35827cb0cfe384eedfc963bf174f2689')

prepare() {
  cd "$srcdir"/${_realname}-${pkgver}
  #patch -p1 -i ${srcdir}/0001-libarchive-3.2.2-bcrypt-fix.patch
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --with-expat \
    --without-xml2

  make
}

#one test seems to crash
#check() {
#  cd "${srcdir}"/build-${MINGW_CHOST}
#  make test
#}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
