# Maintainer: Antoine Lambert <antoine.lambert33@gmail.com>

_realname=tulip
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
_pre=
_ver=5.0.0
pkgver=${_ver}${_pre}
pkgrel=1
pkgdesc="Large graphs analysis, drawing and visualization framework"
arch=('any')
url="https://tulip.labri.fr/"
license=('LGPL')
options=('strip' 'staticlibs')
source=("https://sourceforge.net/projects/auber/files/tulip/${_realname}-${_ver}${_pre}/${_realname}-${_ver}${_pre}_src.tar.gz"
        "001-fix-tulip-python-install.patch"
        "002-fix-undo-redo-crash.patch")
sha256sums=('18aa79c0302d683947d51f87cbed24ac53a571784a1d9c32aa0b92d7b506b80f'
            '947d47098637d06d77a1dce46641c386c93bc3c09b3c8af4f7b53dad9dbc0559'
            'c6401d8b7edc74cce03c83d82b8c68bbdcb69b5eedb2240b965d43ac62afbf41')

depends=("${MINGW_PACKAGE_PREFIX}-yajl"
         "${MINGW_PACKAGE_PREFIX}-qhull"
         "${MINGW_PACKAGE_PREFIX}-freetype"
         "${MINGW_PACKAGE_PREFIX}-glew"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libjpeg"
         "${MINGW_PACKAGE_PREFIX}-qt5"
         "${MINGW_PACKAGE_PREFIX}-quazip"
         "${MINGW_PACKAGE_PREFIX}-qtwebkit"
         "${MINGW_PACKAGE_PREFIX}-python3"
         )
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-doxygen"
             "${MINGW_PACKAGE_PREFIX}-python3-sphinx"
             # needed for sphinx for Python 3 to work
             "${MINGW_PACKAGE_PREFIX}-python3-chardet"
             )

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/001-fix-tulip-python-install.patch
  patch -p1 -i ${srcdir}/002-fix-undo-redo-crash.patch
}
			 
build() {
  rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_NEED_RESPONSE=ON \
    -DTULIP_FIXUP_BUNDLE=OFF \
    -DTULIP_USE_QT5=ON \
    -DPYTHON_EXECUTABLE=${MINGW_PREFIX}/bin/python3.exe \
    -DTULIP_PYTHON_SYSTEM_INSTALL=ON \
    ../${_realname}-${_ver}${_pre}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install
}
