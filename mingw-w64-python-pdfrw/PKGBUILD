# Contributor: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=pdfrw
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}" "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=0.3
pkgrel=1
pkgdesc="Convert restructured text to PDF via reportlab (mingw-w64)"
arch=('any')
url='https://github.com/pmaupin/${_realname}'
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools")
options=('staticlibs' 'strip' '!debug')
source=(${_realname}-${pkgver}.tar.gz::"https://github.com/pmaupin/${_realname}/archive/v${pkgver}.tar.gz")
sha256sums=('4716a4b9ba85bf93897523001d1df555ce1fcc320bd795073ac137ea3ab6b62e')


prepare() {  
  cd "$srcdir"/  
  for builddir in python{2,3}-build; do  
    rm -rf $builddir | true
    cp -r "${_realname}-${pkgver}" "${builddir}"
  done
} 

check() {  
  for pver in {2,3}; do  
    msg "Python ${pver} test for ${CARCH}"  
    cd "${srcdir}/python${pver}-build"  
    ${MINGW_PREFIX}/bin/python${pver} setup.py check  
  done  
}

package_python3-pdfrw() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3")

  cd ${srcdir}/python3-build  
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1

  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE.txt"
}

package_python2-pdfrw() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2")

  cd ${srcdir}/python2-build
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1

  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE.txt"
}

package_mingw-w64-i686-python2-pdfrw() {
  package_python2-pdfrw
}

package_mingw-w64-i686-python3-pdfrw() {
  package_python3-pdfrw
}

package_mingw-w64-x86_64-python2-pdfrw() {
  package_python2-pdfrw
}

package_mingw-w64-x86_64-python3-pdfrw() {
  package_python3-pdfrw
}
