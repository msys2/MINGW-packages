# Maintainer: Diego Sogari <diego.sogari@gmail.com>

_realname=libavro
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.11.3
pkgrel=2
pkgdesc="The Apache Avro data serialization system (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'aur: avro-c'
)
url="https://github.com/apache/avro"
license=('spdx:Apache-2.0')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-asciidoc"
             "${MINGW_PACKAGE_PREFIX}-source-highlight")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-boost"
         "${MINGW_PACKAGE_PREFIX}-jansson"
         "${MINGW_PACKAGE_PREFIX}-snappy"
         "${MINGW_PACKAGE_PREFIX}-xz"
         "${MINGW_PACKAGE_PREFIX}-zlib")
source=("https://github.com/apache/avro/archive/release-${pkgver}/${_realname}-${pkgver}.tar.gz"
        "001-libavro-1.10.0-fix-mingw-build.patch"
        "002-libavro-1.10.0-add-threadsafe-option.patch"
        "003-libavro-fix-pkgconfig-file.patch")
sha256sums=('da377ac1cf8b91458bf702cbcfb214eecb5c399b267f0ca9c0aade6cabaf126e'
            '5fe8808455773299305f68e715c8936612e83970cde7f32237f708882460e83f'
            '346e37dc9adb52739e672483ee895299da6e00121ae0061beb2df3063234d62b'
            '0dd04ae3557fc2bee2e738d1285d494f0dc1685707d98aeb28c98fc4036b66e4')

_apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -p1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${srcdir}/avro-release-${pkgver}"

  _apply_patch_with_msg \
    001-libavro-1.10.0-fix-mingw-build.patch \
    002-libavro-1.10.0-add-threadsafe-option.patch \
    003-libavro-fix-pkgconfig-file.patch
}

build() {
  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  CFLAGS+=" -Wno-incompatible-pointer-types"

  for lang in c c++
  do
    MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      "${_extra_config[@]}" \
      -DTHREADSAFE=ON \
      -S avro-release-${pkgver}/lang/${lang} \
      -B build-${MSYSTEM}-${lang}

    ${MINGW_PREFIX}/bin/cmake --build build-${MSYSTEM}-${lang}
  done
}

check() {
  for lang in c c++
  do
    cd "${srcdir}/build-${MSYSTEM}-${lang}"
    ${MINGW_PREFIX}/bin/ctest .
  done
}

package() {
  for lang in c c++
  do
    cd "${srcdir}/build-${MSYSTEM}-${lang}"
    DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .
  done
  rm "${pkgdir}${MINGW_PREFIX}"/include/avro/{msinttypes,msstdint}.h
}
