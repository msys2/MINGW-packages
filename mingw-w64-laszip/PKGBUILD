# Maintainer: Diego Sogari <diego.sogari@falker.com.br>

_realname=laszip
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.4.3
pkgrel=2
pkgdesc="Free and lossless LiDAR compression (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://www.laszip.org/"
license=('LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
source=(https://github.com/LASzip/LASzip/releases/download/${pkgver}/laszip-src-${pkgver}.tar.gz
        "${_realname}-3.1.1.patch"
        "laszip-3.4.3-mingw-utf8.patch")
sha256sums=('53f546a7f06fc969b38d1d71cceb1862b4fc2c4a0965191a0eee81a57c7b373d'
            'c3d98f0bb85bb78e8cd31e1578799ed2ebda67b567a96c07d22b6eb2c9f208ef'
            '1edee63baedcd020e9cbeb2e4d52741b97f8a029ee52d48199f4930308c59c60')

prepare() {
  cd "${srcdir}/${_realname}-src-${pkgver}"
  patch -Np1 -i "${srcdir}/${_realname}-3.1.1.patch"
  patch -Np1 -i "${srcdir}/laszip-3.4.3-mingw-utf8.patch"
}

build() {
  [[ -d "${srcdir}/build-${MSYSTEM}" ]] && rm -rf "${srcdir}/build-${MSYSTEM}"
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    "../${_realname}-src-${pkgver}"

  cmake --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  DESTDIR="${pkgdir}" cmake --install .
}
