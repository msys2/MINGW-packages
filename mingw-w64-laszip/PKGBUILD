# Maintainer: Diego Sogari <diego.sogari@falker.com.br>

_realname=laszip
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.1.1
pkgrel=2
pkgdesc="Free and lossless LiDAR compression (mingw-w64)"
arch=('any')
url="https://www.laszip.org/"
license=('LGPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-cmake")
source=(https://github.com/LASzip/LASzip/releases/download/${pkgver}/laszip-src-${pkgver}.tar.gz
        "${_realname}-3.1.1.patch"
        "laszip-3.1.1-win-utf8-paths.patch")
sha256sums=('53307e39072318b0175787cf0ed445de12efe0d6b4ccae66829a9a475f3aca19'
            'c3d98f0bb85bb78e8cd31e1578799ed2ebda67b567a96c07d22b6eb2c9f208ef'
            '227c39023b72471638a29d69270532af7f0c98acde86b8a0e5bae742d614b46e')

prepare() {
  cd "${srcdir}/${_realname}-src-${pkgver}"
  patch -Np1 -i "${srcdir}/${_realname}-3.1.1.patch"
  patch -Np1 -i "${srcdir}/laszip-3.1.1-win-utf8-paths.patch"
}

build() {
  rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    "../${_realname}-src-${pkgver}"

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
