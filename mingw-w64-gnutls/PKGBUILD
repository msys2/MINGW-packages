# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gnutls
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.4.0
pkgrel=1
pkgdesc="A library which provides a secure layer over a reliable transport layer (mingw-w64)"
arch=('any')
license=('GPL3' 'LGPL2.1')
url="http://www.gnutls.org/"
options=('!zipman')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
        "${MINGW_PACKAGE_PREFIX}-libtasn1"
        "${MINGW_PACKAGE_PREFIX}-gmp"
        "${MINGW_PACKAGE_PREFIX}-zlib"
        "${MINGW_PACKAGE_PREFIX}-nettle>=3.1"
        "${MINGW_PACKAGE_PREFIX}-p11-kit>=0.23.1"
        "${MINGW_PACKAGE_PREFIX}-libgnurx"
        #"${MINGW_PACKAGE_PREFIX}-unbound"
        )

makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
# Need remove "autogen" package before building.
source=(ftp://ftp.gnutls.org/gcrypt/gnutls/v${pkgver%.*}/${_realname}-${pkgver}.tar.xz{,.sig}
        0001-add-missing-define.patch
        0002-fix-duplicate-entries.patch
        0004-fix-gtkdoc.all.patch
        0005-fix-strtok-conflict.mingw.patch)
md5sums=('aa015c2666b031044edfb01b01980d84'
         'SKIP'
         '807a79513358a9f4123f62111f1eed59'
         '516c3b60e5c2878a0af9f243fd4dac30'
         '86cfab6e7c2ccfcfd1dad9d5024627e3'
         '77287dc835afbb8cc4e9c4aa6b7adb7b')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/0001-add-missing-define.patch
  patch -p1 -i ${srcdir}/0002-fix-duplicate-entries.patch
  #patch -p1 -i ${srcdir}/0004-fix-gtkdoc.all.patch
  #patch -p1 -i ${srcdir}/0005-fix-strtok-conflict.mingw.patch

  WANT_AUTOMAKE=latest autoreconf -fi -I m4
}

build() {
  # Workaround for localtime_r functions
  CFLAGS+=" -D_POSIX_C_SOURCE"
  CXXFLAGS+=" -D_POSIX_C_SOURCE"

  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --with-zlib \
    --enable-shared --disable-static \
    --enable-cxx \
    --enable-nls \
    --disable-rpath \
    --disable-gtk-doc \
    --with-libiconv-prefix=${MINGW_PREFIX} \
    --with-libregex-libs=-lsystre \
    --enable-local-libopts \
    --disable-guile \
    --disable-libdane \
    --disable-tests \
    gl_cv_double_slash_root=yes
  
  make
}

check() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make -k check || /bin/true
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install

  find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
}
