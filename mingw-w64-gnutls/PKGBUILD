# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gnutls
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=3.2.12
pkgrel=1
pkgdesc="A library which provides a secure layer over a reliable transport layer"
arch=('any')
license=('GPL3' 'LGPL2.1')
url="http://www.gnutls.org/"
options=('!zipman')
depends=("${_mingw_suff}-gcc"
		"${_mingw_suff}-libtasn1"
		"${_mingw_suff}-libgcrypt"
		"${_mingw_suff}-gmp"
		"${_mingw_suff}-zlib"
		"${_mingw_suff}-nettle"
		"${_mingw_suff}-p11-kit"
		"${_mingw_suff}-libgnurx"
		"${_mingw_suff}-libgpg-error"
		"${_mingw_suff}-unbound")
makedepends=("${_mingw_suff}-gcc")
source=(ftp://ftp.gnutls.org/gcrypt/gnutls/v3.2/${_realname}-${pkgver}.tar.xz{,.sig}
		0004-fix-gtkdoc.all.patch
		0005-fix-strtok-conflict.mingw.patch)
md5sums=('f507365940de8f095e1d867c6f0842f6'
         'SKIP'
         '86cfab6e7c2ccfcfd1dad9d5024627e3'
         '77287dc835afbb8cc4e9c4aa6b7adb7b')

prepare() {
	cd "${srcdir}/${_realname}-${pkgver}"
	patch -p1 -i ${srcdir}/0004-fix-gtkdoc.all.patch
	patch -p1 -i ${srcdir}/0005-fix-strtok-conflict.mingw.patch

	autoreconf -fi
}

build() {
	mkdir -p "${srcdir}/build-${MINGW_CHOST}"
	cd "${srcdir}/build-${MINGW_CHOST}"
	../${_realname}-${pkgver}/configure \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--target=${MINGW_CHOST} \
		--with-zlib \
		--enable-shared --disable-static \
		--enable-cxx \
		--enable-nls \
		--disable-rpath \
		--disable-gtk-doc \
		--with-libiconv-prefix=${MINGW_PREFIX} \
		--with-libregex-libs=-lgnurx \
		--with-unbound-root-key-file=${MINGW_PREFIX}/etc/unbound/root.key \
		--disable-guile \
		gl_cv_double_slash_root=yes
	make -j1
}

check() {
	cd ${srcdir}/build-${MINGW_CHOST}
	make -k check || /bin/true
}

package() {
	cd ${srcdir}/build-${MINGW_CHOST}
	make DESTDIR="${pkgdir}" install

	find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
	#rm -r ${pkgdir}${MINGW_PREFIX}/share/{info,man}
}
