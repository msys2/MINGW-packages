# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=clazy
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.17
pkgrel=2
pkgdesc="Qt oriented code checker based on clang framework (mingw-w64)"
arch=('any')
mingw_arch=('clang64' 'clangarm64')
url="https://www.kdab.com/"
msys2_repository_url="https://invent.kde.org/sdk/clazy"
license=('spdx:GPL-2.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-clang-libs"
         "${MINGW_PACKAGE_PREFIX}-llvm-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-clang"
             "${MINGW_PACKAGE_PREFIX}-clang-tools-extra"
             "${MINGW_PACKAGE_PREFIX}-llvm")
source=("https://invent.kde.org/sdk/clazy/-/archive/v${pkgver}/clazy-v${pkgver}.tar.bz2"
        001-fix-linking.patch)
sha256sums=('276fe1e952fe7a0dd530ded31fdbc226706df36ea7541d05c74e3dbfa31b7ab0'
            '5fbde0f37f8d217d1da28d93ef78dd2c297ccc9042e01d0ca8053ac4d8f7b292')
validpgpkeys=(949014B23D24354DFD548E5457416A0ADCEF0EFE) # Sergio Martins <sergio.martins@kdab.com>

prepare() {
  cd "${srcdir}/${_realname}-v${pkgver}"
  patch -p1 -i ${srcdir}/001-fix-linking.patch
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe -Wno-dev \
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    "${_extra_config[@]}" \
    "../${_realname}-v${pkgver}"

  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}
  install -Dm644 "${srcdir}/${_realname}-v${pkgver}"/LICENSES/*.* \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/
}
