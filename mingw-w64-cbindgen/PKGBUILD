# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=cbindgen
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.29.2
pkgrel=1
pkgdesc="A project for generating C bindings from Rust code (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://github.com/mozilla/cbindgen'
msys2_repository_url='https://github.com/mozilla/cbindgen'
msys2_changelog_url='https://github.com/mozilla/cbindgen/blob/main/CHANGES'
msys2_references=(
  'archlinux: cbindgen'
  'gentoo: dev-util/cbindgen'
  'purl: pkg:cargo/cbindgen'
)
license=('spdx:MPL-2.0')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
checkdepends=("${MINGW_PACKAGE_PREFIX}-cmake"
              "${MINGW_PACKAGE_PREFIX}-cython"
              "${MINGW_PACKAGE_PREFIX}-python")
source=("${url}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('c7d4d610482390c70e471a5682de714967e187ed2f92f2237c317a484a8c7e3a')

prepare() {
  cd "${_realname}-${pkgver}"
  rm rust-toolchain.toml

  cargo fetch --locked --target "${RUST_CHOST}"
}

build() {
  cd "${_realname}-${pkgver}"

  cargo build --release --frozen
}

check() {
  cd "${_realname}-${pkgver}"

  cargo test --release --frozen
}

package() {
  cd "${_realname}-${pkgver}"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
