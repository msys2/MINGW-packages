# Maintainer (ArchLinux): Felix Yan <felixonmars@archlinux.org>
# Contributor (ArchLinux): Andrea Scarpino <andrea@archlinux.org>
# Contributor (ArchLinux): Pierre Schmitz <pierre@archlinux.de>
# Maintainer (MSYS2): Ray Donnelly <mingw.android@gmail.com>
# Contributor (MSYS2): Mateusz Miku≈Ça <mati865@gmail.com>

_variant=-${KF5_VARIANT:-shared}
source "$(dirname ${BASH_SOURCE[0]})"/../mingw-w64-PKGBUILD-common/kde-frameworks5
_kde_f5_init_package "${_variant}" "oxygen-icons"
# Split svg icons because of teir size
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}"
         "${MINGW_PACKAGE_PREFIX}-${_basename}-svg-qt5")
pkgver=5.24.0
pkgrel=1
arch=('any')
pkgdesc="The Oxygen Icon Theme (mingw-w64-qt5${_namesuff})"
license=('LGPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-extra-cmake-modules")
depends=()
_kde_f5_add_depends "${_variant}" "${MINGW_PACKAGE_PREFIX}-qt5${_namesuff}"
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${_basename}5-${pkgver}.tar.xz")
sha256sums=('959b84144288346a48a857c45994ab2629be7dddc753b077118780f997bcfab0')
noextract=(${_basename}5-${pkgver}.tar.xz)

prepare() {
  #[[ -d "${srcdir}/${_basename}-${pkgver}" ]] && rm -rf "${srcdir}/${_basename}-${pkgver}"
  plain "Upacking the archive..."
  MSYS='winsymlinks:lnk' tar -xf ${_basename}5-${pkgver}.tar.xz -C ${srcdir}
  
  # Replace links with files
  [[ -d "${srcdir}/unlinked-${_basename}5-${pkgver}" ]] && rm -rf "${srcdir}/unlinked-${_basename}5-${pkgver}"
  plain "Creating unlinked-${_basename5}-${pkgver} with dereferenced symlinks..."
  cp -arL ${_basename}5-${pkgver} unlinked-${_basename}5-${pkgver}

  mkdir -p build-${CARCH}${_variant}
}

build() {
  local -a extra_config
  cd build-${CARCH}${_variant}
  if [ "${_variant}" = "-static" ]; then
    extra_config+=( -DBUILD_SHARED_LIBS=NO )
    QT5_PREFIX=${MINGW_PREFIX}/qt5-static
    export PATH=${QT5_PREFIX}/bin:"$PATH"
  else
    QT5_PREFIX=${MINGW_PREFIX}
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=;-DECM_MKSPECS_INSTALL_DIR=" \
    cmake ../unlinked-${_basename}5-${pkgver} \
      -DCMAKE_BUILD_TYPE=$(_kde_f5_CMAKE_BUILD_TYPE) \
      -DCMAKE_INSTALL_PREFIX=${QT5_PREFIX} \
      "${extra_config[@]}" \
      -G'MSYS Makefiles'
  make
}

package_oxygen-icons() {
  groups=('kf5')

  cd build-${CARCH}${_variant}
  make DESTDIR="$pkgdir" install
}

package_oxygen-icons-svg() {
  pkgdesc="The Oxygen Icon Theme (Scalable Vector Graphics)"

  cd unlinked-${_basename}5-${pkgver}
  find scalable -type f ! -name '*.sh' -exec \
    install -D -m644 "{}" "${pkgdir}${MINGW_PREFIX}/share/icons/oxygen/{}" \;
}

package_mingw-w64-i686-oxygen-icons-qt5() {
  package_oxygen-icons
}

package_mingw-w64-i686-oxygen-icons-svg-qt5() {
  package_oxygen-icons-svg
}

package_mingw-w64-x86_64-oxygen-icons-qt5() {
  package_oxygen-icons
}

package_mingw-w64-x86_64-oxygen-icons-svg-qt5() {
  package_oxygen-icons-svg
}
