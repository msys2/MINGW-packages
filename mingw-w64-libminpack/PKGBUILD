# Maintainer: luau-project <luau.project@gmail.com>

_realname=libminpack
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=19961126+dfsg1.5
_pkgver=19961126+dfsg1-5
pkgrel=1
pkgdesc="A software for solving nonlinear equations and nonlinear least squares problems (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://salsa.debian.org/science-team/minpack'
license=('LicenseRef-BSD-4-Clause-Modified')

_fortran_compiler_pkg=""
_fortran_compiler=""
depends=()
_LDFLAGS=""

if [[ ${MSYSTEM} = CLANG* ]] || [[ ${MSYSTEM} = MSYS ]];
then
  _fortran_compiler_pkg="flang"
  _fortran_compiler="flang"

  # -pipe on LDFLAGS defined at /etc/makepkg_mingw.conf
  # causes flang compiler to fail on configure.
  # So, we erase -pipe from LDFLAGS here
  _LDFLAGS=$(echo "${LDFLAGS}" | sed -En "s/\-pipe//p")
else
  _fortran_compiler_pkg="gcc-fortran"
  _fortran_compiler="gfortran"
  depends+=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
            "${MINGW_PACKAGE_PREFIX}-libwinpthread-git"
            "${MINGW_PACKAGE_PREFIX}-gcc-libgfortran")
  _LDFLAGS="${LDFLAGS}"
fi

makedepends=("${MINGW_PACKAGE_PREFIX}-${_fortran_compiler_pkg}")

source=("${url}/-/archive/debian/${_pkgver}/minpack-debian-${_pkgver}.tar.gz")

sha256sums=('a0be0155e812753871097b2032e62c9cc96abe5e8c906b932a9f18f862998063')

build() {
  cd "${srcdir}/minpack-debian-${_pkgver}"
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"
  
  "${srcdir}/minpack-debian-${_pkgver}"/configure \
    F77="${_fortran_compiler}" \
    LDFLAGS="${_LDFLAGS}" \
    --prefix="${MINGW_PREFIX}" \
    --build="${MINGW_CHOST}" \
    --host="${MINGW_CHOST}" \
    --target="${MINGW_CHOST}" \
    --enable-static \
    --enable-shared
  
  # Building a shared library while allowing undefined symbols is not allowed.
  # Thus, we work around the problem following
  # the advice contained at https://stackoverflow.com/a/62232265
  sed -i.bak -e "s/\(allow_undefined=\)yes/\1no/" libtool
  
  if [ "${_fortran_compiler}" = "flang" ]
  then
    # The part '-Xlinker --out-implib -Xlinker \$lib' on libtool
    # does not allow the link phase to run properly
    # on flang. Then we replace it with '-Wl,--out-implib=\$lib'
    # to pass arguments properly to the linker.
    sed -i.bak2 -e "s/\-Xlinker \-\-out\-implib \-Xlinker \\\\\$lib/\-Wl,\-\-out\-implib\=\\\\\$lib/" libtool
  fi

  make

}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  make install DESTDIR="${pkgdir}"

  install -Dm644 "${srcdir}/minpack-debian-${_pkgver}/debian/copyright" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
