# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=qoi
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=r331.30d15d7
pkgrel=1
_commit='30d15d79b7726b977cd889151cc5cd6b17742f8f'
pkgdesc="The 'Quite OK Image' format for fast, lossless image compression (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://qoiformat.org/'
msys2_repository_url='https://github.com/phoboslab/qoi'
msys2_references=(
  'aur: qoi-headers-git'
)
license=('spdx:MIT')
makedepends=("git")
source=("git+https://github.com/phoboslab/qoi.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd "${_realname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

package() {
  cd "${_realname}"
  install -Dm644 qoi.h "${pkgdir}${MINGW_PREFIX}"/include/qoi.h
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
