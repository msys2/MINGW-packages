# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=cliff
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}"  "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=2.15.0
pkgrel=1
pkgdesc="Command Line Interface Formulation Framewor (mingw-w64)"
arch=('any')
url='https://docs.openstack.org/cliff/latest/'
license=('Apache')
makedepends=('git' 
           "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
           "${MINGW_PACKAGE_PREFIX}-python2-setuptools" 
           "${MINGW_PACKAGE_PREFIX}-python3-pbr" 
           "${MINGW_PACKAGE_PREFIX}-python2-pbr"
           "${MINGW_PACKAGE_PREFIX}-python3-cmd2"
           "${MINGW_PACKAGE_PREFIX}-python2-cmd2"
           "${MINGW_PACKAGE_PREFIX}-python3-prettytable"
           "${MINGW_PACKAGE_PREFIX}-python2-prettytable"
           "${MINGW_PACKAGE_PREFIX}-python3-pyparsing"
           "${MINGW_PACKAGE_PREFIX}-python2-pyparsing"
           "${MINGW_PACKAGE_PREFIX}-python3-stevedore"
           "${MINGW_PACKAGE_PREFIX}-python2-stevedore"
           "${MINGW_PACKAGE_PREFIX}-python2-unicodecsv" 
           "${MINGW_PACKAGE_PREFIX}-python3-yaml"
           "${MINGW_PACKAGE_PREFIX}-python2-yaml")
#checkdepends=("${MINGW_PACKAGE_PREFIX}-python3-testrepository"
#              "${MINGW_PACKAGE_PREFIX}-python2-testrepository" 
#              "${MINGW_PACKAGE_PREFIX}-python3-mock" 
#              "${MINGW_PACKAGE_PREFIX}-python2-mock"
#              "${MINGW_PACKAGE_PREFIX}-python3-sphinx"
#              "${MINGW_PACKAGE_PREFIX}-python2-sphinx"
#              "${MINGW_PACKAGE_PREFIX}-python3-oslosphinx"
#              "${MINGW_PACKAGE_PREFIX}-python2-oslosphinx"
#              "${MINGW_PACKAGE_PREFIX}-python3-testscenarios"
#              "${MINGW_PACKAGE_PREFIX}-python2-testscenarios")
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-$pkgver.tar.gz::https://github.com/openstack/cliff/archive/${pkgver}.tar.gz")
sha512sums=('ca1d8cef8eb8f89cedeb3689baf227edbbc15c494cd730b892081fbcaf4b0f4d11792e9899d4b9c3ae9329940b2eca5a92814c1aa99faf7f5f88dc5d6b703f84')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"
#  pushd "${_realname}-${pkgver}"
#    apply_patch_with_msg 0001-A-really-important-fix.patch \
#      0002-A-less-important-fix.patch
#  popd 
  for builddir in python{2,3}-build-${CARCH}; do
    rm -rf ${builddir} | true
    cp -r "${_realname}-${pkgver}" "${builddir}"
  done
  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
  export PBR_VERSION=$pkgver
}

# Note that build() is sometimes skipped because it's done in 
# the packages setup.py install for simplicity if you can do so.
# but sometimes, you want to do a check before install which would
# also trigger the build.
build() {
  for pver in {2,3}; do  
    msg "Python ${pver} build for ${CARCH}"  
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py build
  done  
}

#check() {
#  for pver in {2,3}; do
#    msg "Python ${pver} test for ${CARCH}"
#    cd "${srcdir}/python${pver}-build-${CARCH}"
#    PYTHON=${MINGW_PREFIX}/bin/python${pver} 
#       ${MINGW_PREFIX}/bin/python${pver} setup.py testr
#  done
#}

package_python3-cliff() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3-six"
           "${MINGW_PACKAGE_PREFIX}-python3-pbr"
           "${MINGW_PACKAGE_PREFIX}-python3-cmd2"
           "${MINGW_PACKAGE_PREFIX}-python3-prettytable"
           "${MINGW_PACKAGE_PREFIX}-python3-pyparsing"
           "${MINGW_PACKAGE_PREFIX}-python3-stevedore"
           "${MINGW_PACKAGE_PREFIX}-python3-yaml")

  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}

package_python2-cliff() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2"
           "${MINGW_PACKAGE_PREFIX}-python2-six" 
           "${MINGW_PACKAGE_PREFIX}-python2-pbr" 
           "${MINGW_PACKAGE_PREFIX}-python2-cmd2" 
           "${MINGW_PACKAGE_PREFIX}-python2-prettytable"
           "${MINGW_PACKAGE_PREFIX}-python2-pyparsing"
           "${MINGW_PACKAGE_PREFIX}-python2-stevedore"
           "${MINGW_PACKAGE_PREFIX}-python2-unicodecsv"
           "${MINGW_PACKAGE_PREFIX}-python2-yaml")

  cd "${srcdir}/python2-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE"
}

package_mingw-w64-i686-python2-cliff() {
  package_python2-cliff
}

package_mingw-w64-i686-python3-cliff() {
  package_python3-cliff
}

package_mingw-w64-x86_64-python2-cliff() {
  package_python2-cliff
}

package_mingw-w64-x86_64-python3-cliff() {
  package_python3-cliff
}
