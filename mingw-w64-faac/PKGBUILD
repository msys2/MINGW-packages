# Maintainer: Zach Bacon <11doctorwhocanada@gmail.com>

_realname=faac
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.28
pkgrel=1
pkgdesc='FAAC is an AAC audio encoder (mingw-w64)'
groups=("${MINGW_PACKAGE_PREFIX}")
arch=('any')
url='http://www.audiocoding.com/faac.html'
license=('GPL2')
source=("http://downloads.sourceforge.net/${_realname}/${_realname}-${pkgver}.tar.gz"
		"make-shared.patch")
md5sums=('80763728d392c7d789cde25614c878f6'
		 'aa5048e1b2c960329d716a50fe1cfeaa')

prepare() {
  cd $srcdir/${_realname}-${pkgver}
  patch -Np1 -i ${srcdir}/make-shared.patch
}

build() {
  cd "$srcdir"/${_realname}-${pkgver}
  [ -d "${srcdir}"/build-${CARCH} ] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared \
    --without-mp4v2
  make
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install DESTDIR="${pkgdir}"
}
