# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=wezterm
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
_basever=20240203.110809.5046fc22
_commit=05343b387085842b434d267f91b6b0ec157e4331
pkgver=20240203.110809.5046fc22+r8564+05343b387
pkgrel=1
pkgdesc="A GPU-accelerated cross-platform terminal emulator and multiplexer (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url="https://wezfurlong.org/wezterm/"
msys2_repository_url='https://github.com/wez/wezterm'
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-libgit2"
         "${MINGW_PACKAGE_PREFIX}-libssh2")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-zstd"
             "${MINGW_PACKAGE_PREFIX}-openssl"
             'git')
source=("git+${msys2_repository_url}.git#commit=${_commit}"
        "mingw-build.patch")
sha256sums=('92e49c2f6cbca8b7025a9911772abe0b8566c80a63cf1fda1beedfef66c0bc8f'
            '7cc2ff9d4214d7fc51799964513b3b4a1d285d9ae36c44e6503f8cc6195336e9')

pkgver() {
  cd "${_realname}"

  printf "%s+r%s+%s" "${_basever}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${_realname}"

  patch -Np1 -i "${srcdir}/mingw-build.patch"

  git submodule update --init --recursive
  cargo fetch \
    --locked \
    --target "${RUST_CHOST}" \
    --config='net.git-fetch-with-cli=true'
}

build() {
  cd "${_realname}"

  export LIBGIT2_NO_VENDOR=1
  export OPENSSL_NO_VENDOR=1
  export ZSTD_SYS_USE_PKG_CONFIG=1
  export LIBSSH2_SYS_USE_PKG_CONFIG=1
  export WINAPI_NO_BUNDLED_LIBRARIES=1
  cargo build --release --frozen --features distro-defaults
}

package() {
  cd "${_realname}"

  install -Dm755 target/release/wezterm{,-gui,-mux-server} -t "${pkgdir}${MINGW_PREFIX}/bin"
  install -Dm755 target/release/strip-ansi-escapes -t "${pkgdir}${MINGW_PREFIX}/bin"
  install -Dm644 LICENSE.md -t "${pkgdir}${MINGW_PREFIX}/share/licenses/wezterm"
  install -Dm644 assets/shell-completion/bash "${pkgdir}${MINGW_PREFIX}/share/bash-completion/completions/wezterm"
  install -Dm644 assets/shell-completion/fish "${pkgdir}${MINGW_PREFIX}/share/fish/vendor_completions.d/wezterm.fish"
  install -Dm644 assets/shell-completion/zsh "${pkgdir}${MINGW_PREFIX}/share/zsh/site-functions/_wezterm"
}
