# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=gitui
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.25.2
pkgrel=1
pkgdesc='Blazing fast terminal-ui for git written in Rust (mingw-w64)'
msys2_references=(
  'archlinux: gitui'
)
url='https://github.com/extrawurst/gitui'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-libgit2" "${MINGW_PACKAGE_PREFIX}-libssh2")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-openssl")
source=("https://github.com/extrawurst/gitui/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('5a67d526e22533952a747cb34eb2430a1340dd3139f60a785f579bba4a6aa5ed')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  export GIT_DIR=/dev/null
  export LIBZ_SYS_STATIC=0
  export OPENSSL_NO_VENDOR=1
  export LIBGIT2_NO_VENDOR=1
  export LIBSSH2_SYS_USE_PKG_CONFIG=1
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  cargo build \
    --frozen \
    --release \
    --no-default-features \
    --features "ghemoji regex-fancy trace-libgit"
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  cargo test \
    --frozen \
    --release \
    --no-default-features \
    --features "ghemoji regex-fancy trace-libgit"
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  cargo install \
    --frozen \
    --offline \
    --no-track \
    --no-default-features \
    --features "ghemoji regex-fancy trace-libgit" \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 {KEY_CONFIG,THEMES}.md -t "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}"
  install -Dm644 LICENSE.md "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.md"
}
