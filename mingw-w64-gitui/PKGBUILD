# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=gitui
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.26.3
pkgrel=3
pkgdesc='Blazing fast terminal-ui for git written in Rust (mingw-w64)'
msys2_references=(
  'anitya: 187553'
  'archlinux: gitui'
  'purl: pkg:cargo/gitui'
)
url='https://github.com/extrawurst/gitui'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-libssh2"
         "${MINGW_PACKAGE_PREFIX}-libgit2")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-openssl"
             'git')
source=("git+https://github.com/extrawurst/gitui.git#tag=v${pkgver}")
sha256sums=('b149059b08ce83ad35df027d9a98582a4a2da8b4690caf10690a3999176af8b2')

_env() {
  export OPENSSL_NO_VENDOR=1
  export LIBGIT2_NO_VENDOR=1
  export PKG_CONFIG_ALL_DYNAMIC=1
  export LIBSSH2_SYS_USE_PKG_CONFIG=1
}

prepare() {
  cd "${_realname}"

  # bump git2 crate for libgit2 1.8+ support
  git cherry-pick -n 423b5ff3d13c30dba7ac3045fe31cb2e60a87a60
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${_realname}"

  _env
  cargo build \
    --frozen \
    --release \
    --no-default-features \
    --features "ghemoji regex-fancy trace-libgit"
}

check() {
  cd "${_realname}"

  _env
  cargo test \
    --frozen \
    --release \
    --no-default-features \
    --features "ghemoji regex-fancy trace-libgit"
}

package() {
  cd "${_realname}"

  _env
  cargo install \
    --frozen \
    --offline \
    --no-track \
    --no-default-features \
    --features "ghemoji regex-fancy trace-libgit" \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 {KEY_CONFIG,THEMES}.md -t "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}"
  install -Dm644 LICENSE.md "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.md"
}
