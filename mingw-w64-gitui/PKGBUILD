# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=gitui
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.25.1
pkgrel=1
pkgdesc='Blazing fast terminal-ui for git written in Rust (mingw-w64)'
msys2_references=(
  'archlinux: gitui'
)
url='https://github.com/extrawurst/gitui'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-libgit2")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-openssl")
source=("https://github.com/extrawurst/gitui/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('78d31ba66de1521477aef1642c798a385106ff4858f59e79775ba08a694d0ae4')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  local _target="${CARCH}-pc-windows-gnu"
  if [[ $MINGW_PACKAGE_PREFIX == *-clang-aarch64 ]]; then
    _target="${CARCH}-pc-windows-gnullvm"
  fi
  cargo fetch --locked --target "${_target}"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  OPENSSL_NO_VENDOR=1 \
  GIT_DIR=/dev/null \
  cargo build \
    --frozen \
    --release \
    --no-default-features \
    --features "ghemoji regex-fancy trace-libgit"
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  OPENSSL_NO_VENDOR=1 \
  GIT_DIR=/dev/null \
  cargo test \
    --frozen \
    --release \
    --no-default-features \
    --features "ghemoji regex-fancy trace-libgit"
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  OPENSSL_NO_VENDOR=1 \
  GIT_DIR=/dev/null \
  cargo install \
    --frozen \
    --offline \
    --no-track \
    --no-default-features \
    --features "ghemoji regex-fancy trace-libgit" \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 {KEY_CONFIG,THEMES}.md -t "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}"
  install -Dm644 LICENSE.md "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.md"
}
