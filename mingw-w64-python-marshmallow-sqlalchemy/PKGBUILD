# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=marshmallow-sqlalchemy
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.4.1
pkgrel=1
pkgdesc='SQLAlchemy integration with marshmallow (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://marshmallow-sqlalchemy.readthedocs.io/'
msys2_references=(
  'archlinux: python-marshmallow-sqlalchemy'
  'purl: pkg:pypi/marshmallow-sqlalchemy'
)
msys2_repository_url='https://github.com/marshmallow-code/marshmallow-sqlalchemy/'
msys2_changelog_url='https://marshmallow-sqlalchemy.readthedocs.io/en/latest/changelog.html'
msys2_documentation_url='https://marshmallow-sqlalchemy.readthedocs.io/'
msys2_issue_tracker_url='https://github.com/marshmallow-code/marshmallow-sqlalchemy/issues'
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-marshmallow"
         "${MINGW_PACKAGE_PREFIX}-python-sqlalchemy")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-flit-core")
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname/-/_}-${pkgver}.tar.gz")
sha256sums=('b4aa964356d00e178bdb8469a28daa9022b375ff4f5c04f8e2b9aafe1e65c529')

build() {
  cp -r "${_realname/-/_}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
