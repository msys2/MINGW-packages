# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=marshmallow-sqlalchemy
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.30.0
pkgrel=1
pkgdesc='SQLAlchemy integration with marshmallow (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'archlinux: python-marshmallow-sqlalchemy'
  'pypi: marshmallow-sqlalchemy'
)
msys2_repository_url='https://github.com/marshmallow-code/marshmallow-sqlalchemy/'
msys2_changelog_url='https://marshmallow-sqlalchemy.readthedocs.io/en/latest/changelog.html'
msys2_documentation_url='https://marshmallow-sqlalchemy.readthedocs.io/'
msys2_issue_tracker_url='https://github.com/marshmallow-code/marshmallow-sqlalchemy/issues'
url='https://marshmallow-sqlalchemy.readthedocs.io/'
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python-marshmallow"
         "${MINGW_PACKAGE_PREFIX}-python-sqlalchemy")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python-wheel")
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('29ad0a4fd1b4a1e52dcb07f9673d284a6b0795141916cc2169d4ee9a5d007347')

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
