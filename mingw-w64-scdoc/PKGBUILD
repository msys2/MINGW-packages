# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=scdoc
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.11.2
pkgrel=3
license=('spdx:MIT')
pkgdesc='Tool for generating roff manual pages (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://git.sr.ht/~sircmpwn/scdoc/'
makedepends=("${MINGW_PACKAGE_PREFIX}-cc")
source=("${_realname}-$pkgver.tar.gz"::"https://git.sr.ht/~sircmpwn/${_realname}/archive/$pkgver.tar.gz"
        "fix-clang-error.patch::https://git.sr.ht/~sircmpwn/scdoc/commit/1923fb61cd71182f05d029f280d056b21f66e8a0.patch"
        parsedate.patch)
sha512sums=('3fbf1a74b222dd88410636124e4b6ca73f7e77f67d512cf430a878fefcaa5c6c13a1e9f6c0c9654de15353f94bb1bd528665acebc2293bebb325501d1eb6cda3'
            '7e6786913b541ef72c0914b777aa3a47643540bc6cdc6a2c48191fad35311c4372c7e10a2805029a288dfeed546aac223c143afbe5f6b788384365782210a3e1'
            '564019dbc2f4dd07ac7758294da4c051eec3f8bf7c595591a99ed9e76a2fd807d016c646c2eca1ab60abdbfc2b332fdfd287c1ee18211443b6b71404e7ad4665')

prepare() {
  cd "$srcdir/${_realname}-$pkgver"
  patch -Np1 -i "${srcdir}"/fix-clang-error.patch
  patch -Np1 -i "${srcdir}"/parsedate.patch
  rm -rf "$srcdir/build-${MSYSTEM}"
  cp -r "$srcdir/${_realname}-$pkgver" "$srcdir/build-${MSYSTEM}"
}

build() {
  cd $srcdir/"build-${MSYSTEM}"
  # Upstream purposefully does not honor the user's LDFLAGS in order to keep
  # scdoc static. As we gain no benefit to statically linking and, in fact,
  # lose standard Arch practices (such as RELRO and PIE), we must override
  # LDFLAGS
  make PREFIX="${MINGW_PREFIX}" LDFLAGS="${LDFLAGS}"
}

check() {
  cd $srcdir/"build-${MSYSTEM}"
  make check
}

package() {
  cd $srcdir/"build-${MSYSTEM}"
  make PREFIX="${MINGW_PREFIX}" DESTDIR="${pkgdir}" install
  install -Dm644 COPYING -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
}
