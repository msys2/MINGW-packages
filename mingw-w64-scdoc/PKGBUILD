# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=scdoc
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.11.2
pkgrel=1
license=('MIT')
pkgdesc='Tool for generating roff manual pages (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='https://git.sr.ht/~sircmpwn/scdoc/'
source=("${_realname}-${pkgver}.tar.gz::https://git.sr.ht/~sircmpwn/${_realname}/archive/${pkgver}.tar.gz")
sha512sums=('3fbf1a74b222dd88410636124e4b6ca73f7e77f67d512cf430a878fefcaa5c6c13a1e9f6c0c9654de15353f94bb1bd528665acebc2293bebb325501d1eb6cda3')

prepare() {
  cd "${_realname}-${pkgver}"
  sed -i 's/-Werror//g' Makefile
}

build() {
  cd "${_realname}-${pkgver}"
  # Upstream purposefully does not honor the user's LDFLAGS in order to keep
  # scdoc static. As we gain no benefit to statically linking and, in fact,
  # lose standard Arch practices (such as RELRO and PIE), we must override
  # LDFLAGS
  make PREFIX="${MINGW_PREFIX}" LDFLAGS="${LDFLAGS}"
}

check() {
  cd "${_realname}-${pkgver}"
  make check
}

package() {
  cd "${_realname}-${pkgver}"
  make PREFIX="${MINGW_PREFIX}" DESTDIR="${pkgdir}" install
  install -Dm644 COPYING -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
}
