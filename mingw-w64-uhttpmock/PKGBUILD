# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=uhttpmock
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.5.1
pkgrel=1
arch=('any')
pkgdesc="uhttpmock is a HTTP web service mocking project for projects which use libsoup (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-libsoup")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
            "${MINGW_PACKAGE_PREFIX}-vala"
            "pkg-config"
            "make"
            "libtool"
            "automake-wrapper"
            "autoconf"
            "patch")
options=('strip' 'staticlibs' '!debug')
license=("LGPL 2.1")
url="https://tecnocode.co.uk/"
source=(https://tecnocode.co.uk/downloads/${_realname}/${_realname}-${pkgver}.tar.xz
        0001-wsock.patch
        0002-give-cc-to-gir-scanner.all.patch)
sha256sums=('a6a34a011db142a98473b492039c84468d575f416689484f1f82a84927f17798'
            '61a3563903d979a93514e09582400ed0647e3029a0632d5c6fbbd0f0589ba809'
            'd12318d529f768ddfb2107e249d9c181e009dc1e62fdcced4c3397ead36e4845')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-wsock.patch
  patch -p1 -i ${srcdir}/0002-give-cc-to-gir-scanner.all.patch

  autoreconf -fi
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib{

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
