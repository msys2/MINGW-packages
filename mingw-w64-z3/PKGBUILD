# Maintainer: Some One <some.one@some.email.com>

_realname=z3
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=4.7.1
pkgrel=2
pkgdesc="Z3 is a high-performance theorem prover being developed at Microsoft Research (mingw-w64)"
arch=('any')
url="https://github.com/Z3Prover/z3"
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-python2")
options=('strip' 'staticlibs')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/Z3Prover/z3/archive/z3-${pkgver}.tar.gz"
        001-mingw-fixes.patch
        002-fix-dll-destination.patch
        003-fix-dll-exports.patch
        004-fix-python-bindings-install.patch)
sha256sums=('a353e3da00cdaffd258052cc1406efc854606855222ab4bfd5679c58af5c11c7'
            '04155cee2e012e6a55ba4b3e6adb1fadce4f94ae9991b96db80c6c68efd39493'
            '9f36b8be5965aef006900f70366a87628e80c12899f7f8621f127c4bab0b5a0a'
            'a1d35c28dae7df017d457260bf75dbe87b3232819565aa1de84d8e9459c4fb0f'
            '7c73e0b6afac99948ca383a709668ae753b8034139332503c8f2d0e310382ac6')

apply_patch_with_msg() {
  for _fname in "$@"
  do
    msg2 "Applying ${_fname}"
    patch -Nbp1 -i "${srcdir}"/${_fname}
  done
}

prepare() {
  cd "${srcdir}"/${_realname}-${_realname}-${pkgver}
  apply_patch_with_msg \
    001-mingw-fixes.patch \
    002-fix-dll-destination.patch \
    003-fix-dll-exports.patch \
    004-fix-python-bindings-install.patch
}

build() {
  local -a extra_config

  if check_option "debug" "y"; then
    extra_config+=(-DCMAKE_BUILD_TYPE=Debug)
    VERBOSE="VERBOSE=1"
  else
    extra_config+=(-DCMAKE_BUILD_TYPE=Release)
  fi

  [[ -d "${srcdir}"/build-${CARCH}-static ]] && rm -rf "${srcdir}"/build-${CARCH}-static
  mkdir -p "${srcdir}"/build-${CARCH}-static && cd "${srcdir}"/build-${CARCH}-static

  msg "Build static z3"
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DBUILD_LIBZ3_SHARED=OFF \
    "${extra_config[@]}" \
    ../${_realname}-${_realname}-${pkgver}

  make

  [[ -d "${srcdir}"/build-${CARCH}-shared ]] && rm -rf "${srcdir}"/build-${CARCH}-shared
  mkdir -p "${srcdir}"/build-${CARCH}-shared && cd "${srcdir}"/build-${CARCH}-shared

  msg "Build shared z3"
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DBUILD_LIBZ3_SHARED=ON \
    -DBUILD_PYTHON_BINDINGS=ON \
    "${extra_config[@]}" \
    ../${_realname}-${_realname}-${pkgver}

  make
}

check() {
 cd "${srcdir}"/build-${CARCH}-shared
 make test
}

package() {
  cd "${srcdir}"/build-${CARCH}-static
  make install DESTDIR="${pkgdir}"

  cd "${srcdir}"/build-${CARCH}-shared
  make install DESTDIR="${pkgdir}"

  install -Dm644 ${srcdir}/${_realname}-${_realname}-${pkgver}/LICENSE.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.txt
}
