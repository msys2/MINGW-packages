# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=glm
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=0.9.5.3
pkgrel=1
pkgdesc="C++ mathematics library for 3D software based on the OpenGL Shading Language (GLSL) specification (mingw-w64)"
arch=('any')
url='http://glm.g-truc.net'
license=('MIT')
depends=("")
source=("http://sourceforge.net/projects/ogl-math/files/glm-$pkgver/glm-$pkgver.zip")
md5sums=('5aff891cf4b658829e3f0907e6544780')

build() {
	mkdir -p ${pkgdir}${MINGW_PREFIX}
	pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
	export PREFIX_WIN=`pwd -W`
	popd > /dev/null

	mkdir $srcdir/build-${MINGW_CHOST}
	cd "$srcdir/build-${MINGW_CHOST}"
	${MINGW_PREFIX}/bin/cmake.exe \
		-G"MSYS Makefiles" \
		-DCMAKE_INSTALL_PREFIX:PATH=${PREFIX_WIN} \
		-DGLM_TEST_ENABLE=ON \
		../${_realname}
	make
}

package() {
	cd "$srcdir/build-${MINGW_CHOST}"
	make install
	mkdir -p ${pkgdir}${MINGW_PREFIX}/share/doc
	cp -r $srcdir/${_realname}/doc ${pkgdir}${MINGW_PREFIX}/share/doc/glm
	rm -f ${pkgdir}${MINGW_PREFIX}/include/glm/CMakeLists.txt
	find ${pkgdir}${MINGW_PREFIX} -type f -exec chmod 644 {} \;
	find ${pkgdir}${MINGW_PREFIX} -type d -exec chmod 755 {} \;

	install -Dm644 $srcdir/${_realname}/util/FindGLM.cmake ${pkgdir}${MINGW_PREFIX}/share/cmake-2.8/Modules/FindGLM.cmake
	install -Dm644 $srcdir/${_realname}/copying.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}

