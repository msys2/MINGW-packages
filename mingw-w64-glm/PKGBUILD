# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=glm
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.9.6.3
pkgrel=2
pkgdesc="C++ mathematics library for 3D software based on the OpenGL Shading Language (GLSL) specification (mingw-w64)"
arch=('any')
url='http://glm.g-truc.net'
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
source=("https://sourceforge.net/projects/ogl-math/files/glm-${pkgver}/glm-${pkgver}.zip"
         001-remove-missing-define.patch)
sha256sums=('14651b56b10fa68082446acaf6a1116d56b757c8d375b34b5226a83140acd2b2'
            'c41bf00f35801428ba90610296cc235b7db59ec54d65fe976ba2e0e4f137f987')

prepare() {
  cd ${_realname}
  patch -p1 -i ${srcdir}/001-remove-missing-define.patch
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir ${srcdir}/build-${MINGW_CHOST}
  cd "${srcdir}/build-${MINGW_CHOST}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DGLM_TEST_ENABLE=ON \
    ../${_realname}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install

  mkdir -p ${pkgdir}${MINGW_PREFIX}/share/doc
  cp -r $srcdir/${_realname}/doc ${pkgdir}${MINGW_PREFIX}/share/doc/glm
  rm -f ${pkgdir}${MINGW_PREFIX}/include/glm/CMakeLists.txt
  find ${pkgdir}${MINGW_PREFIX} -type f -exec chmod 644 {} \;
  find ${pkgdir}${MINGW_PREFIX} -type d -exec chmod 755 {} \;

  #install -Dm644 ${srcdir}/${_realname}/util/FindGLM.cmake ${pkgdir}${MINGW_PREFIX}/share/cmake-3.2/Modules/FindGLM.cmake
  install -Dm644 ${srcdir}/${_realname}/copying.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}

