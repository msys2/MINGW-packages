# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=glm
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.9.8.5
pkgrel=1
pkgdesc="C++ mathematics library for 3D software based on the OpenGL Shading Language (GLSL) specification (mingw-w64)"
arch=('any')
url='https://glm.g-truc.net/'
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
source=("https://github.com/g-truc/${_realname}/archive/${pkgver}.tar.gz"
        "0001-glm-0.9.8.5-fix-platform-error.patch")
sha256sums=('80cf9958f06e5504f8df45ea14fde87411270102930be31c0a16c0da430fc920'
            '6d0ce80cf518a0d605f854cb97c941a6ec1dd97415ac9da45932a86cb2b902cb')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -Np1 -i "${srcdir}/0001-glm-0.9.8.5-fix-platform-error.patch"
}

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && \
     rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir ${srcdir}/build-${MINGW_CHOST}
  cd "${srcdir}/build-${MINGW_CHOST}"
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DGLM_TEST_ENABLE=ON \
    ../${_realname}-${pkgver}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install

  mkdir -p ${pkgdir}${MINGW_PREFIX}/share/doc
  cp -r $srcdir/${_realname}-${pkgver}/doc \
   ${pkgdir}${MINGW_PREFIX}/share/doc/glm
  rm -f ${pkgdir}${MINGW_PREFIX}/include/glm/CMakeLists.txt
  find ${pkgdir}${MINGW_PREFIX} -type f -exec chmod 644 {} \;
  find ${pkgdir}${MINGW_PREFIX} -type d -exec chmod 755 {} \;

  #install -Dm644 ${srcdir}/${_realname}-${pkgver}/util/FindGLM.cmake ${pkgdir}${MINGW_PREFIX}/share/cmake-3.2/Modules/FindGLM.cmake
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/copying.txt \
    ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}

