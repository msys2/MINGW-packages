# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=openvr
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.0.15
pkgrel=1
pkgdesc="API and SDK that allows access to VR hardware from multiple vendors (mingw-w64)"
arch=('any')
url="https://github.com/ValveSoftware/openvr/"
license=("BSD3")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-gcc")
source=(${_realname}-${pkgver}.tar.gz::https://github.com/ValveSoftware/openvr/archive/v${pkgver}.tar.gz
        001-_WIN32-macros.patch
        002-install-headers.patch
        003-mingw-library-name.patch
        004-install-location.patch)
sha256sums=('b60d2be04e9d689d86f665e311cd80b081c09e6102458c288d182409e68844bf'
            '51c697a176847aaead97f185422f844091b4d4257b84074ec1c6c8a466c74283'
            '10ece7ce59e78747c24ac98bb891d65ab6e57a888094a1fad5e87b56dff5ae10'
            '103ec4394f2fef820276906397011a498396694999e492c14ce0fda19f6d1014'
            'fe6b941fef6f831bab40c748dd031936c315be38bb8a272324e21732d8ab798d')
noextract=(${_realname}-${pkgver}.tar.gz)

prepare() {
  [[ -d ${srcdir}/openvr-${pkgver} ]] && rm -rf ${srcdir}/openvr-${pkgver}
  tar -xzf ${srcdir}/${_realname}-${pkgver}.tar.gz -C ${srcdir} || true
  
  cd openvr-${pkgver}
  patch -p1 -i ${srcdir}/001-_WIN32-macros.patch
  patch -p1 -i ${srcdir}/002-install-headers.patch
  patch -p1 -i ${srcdir}/003-mingw-library-name.patch
  patch -p1 -i ${srcdir}/004-install-location.patch
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -r build-${MINGW_CHOST}
  mkdir build-${MINGW_CHOST} && cd build-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DBUILD_SHARED=ON \
    ../openvr-${pkgver}

  make #VERBOSE=1
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}

  make DESTDIR=${pkgdir} install
  install -Dm644 "${srcdir}/openvr-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
