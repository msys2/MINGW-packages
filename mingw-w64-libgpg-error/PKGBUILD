# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libgpg-error
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.20
pkgrel=1
pkgdesc="Support library for libgcrypt (mingw-w64)"
arch=('any')
url="https://gnupg.org"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" "${MINGW_PACKAGE_PREFIX}-gettext")
options=('staticlibs' 'strip')
source=("https://gnupg.org/ftp/gcrypt/libgpg-error/${_realname}-${pkgver}.tar.bz2"
        01-mingw32-fix-potomo.mingw.patch
        02-fix-symbollist-on.mingw.patch
        04-use-winpthreads.mingw.patch
        05-w32-gen.all.patch
        07-windows-build.patch
        09-fix-generating-union.patch)
sha256sums=('3266895ce3419a7fb093e63e95e2ee3056c481a9bc0d6df694cfd26f74e72522'
            '252349e58d418adfec5621af1e09753db52b1bf39983aa3bc398d636afb9b495'
            '364da17febff3f6eeffee5a5f1e3ed1b644adeb5ca48a972c5c4675c10238a91'
            '2bf2572ac0bada49b8947a842d99e2190acbe34badd73a629173fda757525edc'
            '9ccdc567810d58526888fd11c5f7d01101627011840b7b75a91e96aa9e71f49d'
            'c162120f42a806f6d1f29efc510ea76a9e464ab629f566ff9607ac7565f440f9'
            '999a02c10a2f0c57520a009cebc2a4ea410aea53e2d457e074aa5dc4c877672b')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/01-mingw32-fix-potomo.mingw.patch
  patch -p1 -i ${srcdir}/02-fix-symbollist-on.mingw.patch
  #patch -p1 -i ${srcdir}/03-fix-config-on.mingw.patch
  #patch -p1 -i ${srcdir}/04-use-winpthreads.mingw.patch
  patch -p1 -i ${srcdir}/05-w32-gen.all.patch
  patch -p1 -i ${srcdir}/07-windows-build.patch
  #patch -p1 -i ${srcdir}/09-fix-generating-union.patch
  autoreconf -fi
}

build() {
  [ -d ${srcdir}/build-${MINGW_CHOST} ] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST} \
      --enable-shared \
      --enable-static

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
