# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libgpg-error
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.12
pkgrel=2
pkgdesc="Support library for libgcrypt (mingw-w64)"
arch=('any')
url="http://www.gnupg.org"
license=("LGPL")
makedepends=("${_mingw_suff}-gcc")
depends=("${_mingw_suff}-gcc-libs" "${_mingw_suff}-gettext")
groups=("${_mingw_suff}")
options=('staticlibs' 'strip')
source=("ftp://ftp.gnupg.org/gcrypt/libgpg-error/libgpg-error-${pkgver}.tar.bz2"
    "01-mingw32-fix-potomo.mingw.patch")
md5sums=('8f0eb41a344d19ac2aa9bd101dfb9ce6'
         '4865ce481ec9aa5efc0a4979f197a686')

prepare() {
  cd "$srcdir/libgpg-error-$pkgver"
  patch -p1 -i ${srcdir}/01-mingw32-fix-potomo.mingw.patch
}

build() {
  cd "$srcdir/libgpg-error-$pkgver"
  sed -i "s,EXPORTS,," "src/gpg-error.def.in"
  mkdir -p $srcdir/build-${CARCH} && cd $srcdir/build-${CARCH}
    ../libgpg-error-$pkgver/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST} \
      --enable-static
  make
}

package() {
    cd "${srcdir}/build-${CARCH}"
    make DESTDIR="$pkgdir" install
}
