# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libgpg-error
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.29
pkgrel=1
pkgdesc="Support library for libgcrypt (mingw-w64)"
arch=('any')
url="https://gnupg.org"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" "${MINGW_PACKAGE_PREFIX}-gettext")
options=('staticlibs' 'strip')
source=("https://gnupg.org/ftp/gcrypt/libgpg-error/${_realname}-${pkgver}.tar.bz2"{,.sig}
        01-mingw32-fix-potomo.mingw.patch
        02-fix-symbollist-on.mingw.patch
        04-use-winpthreads.mingw.patch
        05-w32-gen.all.patch
        07-windows-build.patch
        09-fix-generating-union.patch)
#These might be signed by any of these keys https://gnupg.org/signature_key.html
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6'
              '46CC730865BB5C78EBABADCF04376F3EE0856959'
              '031EC2536E580D8EA286A9F22071B08A33BD3F06'
              'D238EA65D64C67ED4C3073F28A861B1C7EFD60D9')
sha256sums=('ece926fa5719d17a7ad8da618712cfa2f8a796ab2f2af9d544c5bb093383b1ea'
            'SKIP'
            '252349e58d418adfec5621af1e09753db52b1bf39983aa3bc398d636afb9b495'
            '364da17febff3f6eeffee5a5f1e3ed1b644adeb5ca48a972c5c4675c10238a91'
            '2bf2572ac0bada49b8947a842d99e2190acbe34badd73a629173fda757525edc'
            '9ccdc567810d58526888fd11c5f7d01101627011840b7b75a91e96aa9e71f49d'
            'ff73e4bde792f5c84c99810b83467d20247856076a92e5940f9fd64a8815187d'
            '999a02c10a2f0c57520a009cebc2a4ea410aea53e2d457e074aa5dc4c877672b')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/01-mingw32-fix-potomo.mingw.patch
  patch -p1 -i ${srcdir}/02-fix-symbollist-on.mingw.patch
  #patch -p1 -i ${srcdir}/03-fix-config-on.mingw.patch
  #patch -p1 -i ${srcdir}/04-use-winpthreads.mingw.patch
  patch -p1 -i ${srcdir}/05-w32-gen.all.patch
  patch -p1 -i ${srcdir}/07-windows-build.patch
  #patch -p1 -i ${srcdir}/09-fix-generating-union.patch
  autoreconf -fi
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST} \
      --enable-shared \
      --enable-static

  make -j1
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING.LIB ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING.LIB
}
