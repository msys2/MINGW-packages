_realname=gnatcoll-sqlite
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.r2874.68c48d5
pkgrel=1
_branch=24.0

pkgdesc="GNAT Component Collection - SQLite database interface (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
msys2_references=(
  'aur: gnatcoll-sqlite'
)

url="https://github.com/AdaCore/gnatcoll-db"
license=('GPL-3.0-or-later WITH GCC-exception-3.1')
makedepends=("${MINGW_PACKAGE_PREFIX}-gprbuild")
depends=("${MINGW_PACKAGE_PREFIX}-gnatcoll-sql")
_repo_name=gnatcoll-db
source=("${_repo_name}::git+https://github.com/AdaCore/gnatcoll-db.git#branch=${_branch}")
sha256sums=('SKIP')

_makeopts=(BUILD=PROD PROCESSORS="$(nproc)" ENABLE_SHARED=yes)

pkgver() {
  cd ${_repo_name}
  printf "%s.r%s.%s" "$_branch" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
  cd "${srcdir}/${_repo_name}/sqlite"
  make "${_makeopts[@]}"
}

package() {
  cd "${srcdir}/${_repo_name}/sqlite"
  make install "${_makeopts[@]}" prefix="${pkgdir}${MINGW_PREFIX}"
  install -D -m644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" \
    "${srcdir}/${_repo_name}"/COPYING*
}
