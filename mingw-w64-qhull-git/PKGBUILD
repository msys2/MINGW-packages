# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: JÃ¼rgen Pfeifer <juergen@familiepfeifer.de>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=qhull
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=r113.60d5581
pkgrel=3
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgdesc="A general dimension code for computing convex hulls and related structures (mingw-w64)"
arch=('any')
url="http://www.qhull.org/"
license=('custom')
options=('strip' 'staticlibs')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("git"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cmake")
source=(#"${_realname}"::"git://gitorious.org/qhull/qhull.git"
        "${_realname}"::"git+https://github.com/manctl/qhull.git"
        "0001-MSYS2-Adopt-FHS-layout-in-cmake.patch"
        "0002-MinGW-w64-64-bit-ptr_intT-fix.patch")
sha256sums=('SKIP'
            '347f7f0f48bc24ffcb2649c188ab6f39dfbe8972ee002857dce4fca74c088148'
            '9aa307018c99e6270f2f7921a8a639ae26d7ce9900483fe70af7d7082f40e86f')

pkgver() {
  cd "${srcdir}"/${_realname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"/${_realname}
  git am "${srcdir}"/0001-MSYS2-Adopt-FHS-layout-in-cmake.patch
  git am "${srcdir}"/0002-MinGW-w64-64-bit-ptr_intT-fix.patch
}

build() {
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G "MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DBUILD_SHARED_LIBS=ON \
    ../${_realname}

  make
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make DESTDIR="${pkgdir}" install
  pushd ${pkgdir}${MINGW_PREFIX}/lib >/dev/null
  if [ -f lib${_realname}static.a ]; then
    mv -f lib${_realname}static.a lib${_realname}.a
  fi
  if [ -f lib${_realname}static_p.a ]; then
    mv -f lib${_realname}static_p.a lib${_realname}_p.a
  fi
  popd >/dev/null
  mkdir -p ${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname
  cp -pf "${srcdir}"/${_realname}/COPYING* \
    ${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname
}
