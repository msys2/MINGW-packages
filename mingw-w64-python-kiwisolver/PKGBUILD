# Maintainer: Peter Budai <peterbudai@hotmail.com>

_realname=kiwisolver
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}"  "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=1.1.0
pkgrel=1
pkgdesc="A fast implementation of the Cassowary constraint solver (mingw-w64)"
arch=('any')
url='https://github.com/nucleic/kiwi'
license=('PerlArtistic')

makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/nucleic/kiwi/archive/$pkgver.tar.gz")
sha256sums=('48168c0ace18319c649e31907f6d8586f648e503560f506f128798b99393bcdb')

prepare() {
  cd "$srcdir"/

  for pver in {2,3}; do
    rm -rf python${pver}-build-${CARCH} | true
    cp -r "kiwi-${pkgver}" "python${pver}-build-${CARCH}"
  done

}

build() {
  for pver in {2,3}; do
    msg "Python ${pver} build for ${CARCH}"
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py build
  done
}

#check() {
#  for pver in {2,3}; do
#    msg "Python ${pver} test for ${CARCH}"
#    cd "${srcdir}/python${pver}-build-${CARCH}"
#    ${MINGW_PREFIX}/bin/python${pver} setup.py pytest
#  done
#}

package_python3-kiwisolver() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3")

  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-kiwisolver() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2")

  cd "${srcdir}/python2-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
}

package_mingw-w64-i686-python2-kiwisolver() {
  package_python2-kiwisolver
}

package_mingw-w64-i686-python3-kiwisolver() {
  package_python3-kiwisolver
}

package_mingw-w64-x86_64-python2-kiwisolver() {
  package_python2-kiwisolver
}

package_mingw-w64-x86_64-python3-kiwisolver() {
  package_python3-kiwisolver
}
