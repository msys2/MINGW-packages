# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>
# Note!!!
# This is a temporary solution because Python 3.7 does not
# include the unicode 12 data that fonttools depends upon.
# We should drop this PKGBUILD once Python 3.8 is built.
_realname=unicodedata2
pkgbase=mingw-w64-python3-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-python3-${_realname}"
pkgver=12.0.0
pkgrel=1
pkgdesc='unicodedata backport/updates (mingw-w64)'
arch=('any')
url="https://github.com/mikekap/${_realname}"
license=('Apache')
depends=("${MINGW_PACKAGE_PREFIX}-python3<3.8")
makedepends=("${MINGW_PACKAGE_PREFIX}-cython" "${MINGW_PACKAGE_PREFIX}-python3-setuptools")
source=("${_realname}-$pkgver.tar.gz"::"https://github.com/mikekap/unicodedata2/archive/$pkgver.tar.gz")
sha256sums=('cf32b4fd0d25e31e4caa40c99cc048c41c56a49ede07f55afd90519dea4a837e')

prepare() {
  for builddir in python3-build-${CARCH}; do
    rm -rf $builddir | true
    cp -r "${_realname}-${pkgver}" "${builddir}"
  done
}

build() {
   cd "$srcdir/python3-build-${CARCH}"
   ${MINGW_PREFIX}/bin/python3 setup.py build
}

check() {
   cd "$srcdir/python3-build-${CARCH}"
   ${MINGW_PREFIX}/bin/python3 setup.py test
}

package() {
   cd "$srcdir/python3-build-${CARCH}"
   MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
     ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
       --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}
