# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=shapelib
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.6.2
pkgrel=1
pkgdesc="simple C API for reading and writing ESRI Shapefiles (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="http://shapelib.maptools.org/"
msys2_repository_url='https://github.com/OSGeo/shapelib'
msys2_references=(
  'archlinux: shapelib'
  "cpe: cpe:2.3:a:osgeo:shapelib"
  'gentoo: sci-libs/shapelib'
)
license=("LGPL" "MIT")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc" "${MINGW_PACKAGE_PREFIX}-autotools")
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs")
source=("https://download.osgeo.org/shapelib/${_realname}-${pkgver}.tar.gz"
        "https://github.com/OSGeo/shapelib/commit/bf455986557e88cce6a2a85448798e1dc05541f2.patch")
sha256sums=('4b74a36ced94e9a7bea401157e664addcc5be251e7df7f88d4674361da012c21'
            'bbb06459385e2df8834c3667b2c0541b6d08c52d31bc944453a6ff9988b13616')

prepare() {
  cd "${_realname}-${pkgver}"

  # Fix build with GCC 15, see <https://github.com/OSGeo/shapelib/pull/188>.
  patch -Np1 -i "${srcdir}/bf455986557e88cce6a2a85448798e1dc05541f2.patch"
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  #export lt_cv_deplibs_check_method='pass_all'

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --enable-static

  make
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
  find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
}
