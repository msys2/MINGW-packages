# Maintainer: kreijstal <kreijstal@hotmail.com>

_realname=mycli
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.27.0
pkgrel=1
pkgdesc="Command line interface for MySQL, MariaDB, and Percona with auto-completion and syntax highlighting (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='https://pypi.org/project/mycli/'
license=('BSD')
depends=(
  "${MINGW_PACKAGE_PREFIX}-python-click"
  "${MINGW_PACKAGE_PREFIX}-python-cryptography"
  "${MINGW_PACKAGE_PREFIX}-python-pygments"
  "${MINGW_PACKAGE_PREFIX}-python-prompt_toolkit"
  "${MINGW_PACKAGE_PREFIX}-python-sqlparse"
  "${MINGW_PACKAGE_PREFIX}-python-pyperclip"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python-build"
  "${MINGW_PACKAGE_PREFIX}-python-installer"
  "${MINGW_PACKAGE_PREFIX}-python-setuptools"
  "${MINGW_PACKAGE_PREFIX}-python-wheel"
)
source=("https://files.pythonhosted.org/packages/source/m/${_realname}/${_realname}-${pkgver}.tar.gz"
        0001-mycli-unpin-the-cryptography-version.patch)
sha256sums=('a71db5bd9c1a7d0006f4f2ff01548ce75637d3f50ca3a7e77b950b5b46aff7cd'
            '6e48527d57e0fa76565872a95b85d1ab6de1109f58d1505e6fcfdace57eedc6c')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-mycli-unpin-the-cryptography-version.patch"
  ${MINGW_PREFIX}/bin/python -m pip install sqlglot cli_helpers pymysql
}

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
