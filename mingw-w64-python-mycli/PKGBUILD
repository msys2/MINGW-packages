# Contributor: kreijstal <kreijstal@hotmail.com>

_realname=mycli
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.27.0
pkgrel=1
pkgdesc='A Terminal Client for MySQL with AutoCompletion and Syntax Highlighting (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/dbcli/mycli'
license=('BSD')
depends=(
    "${MINGW_PACKAGE_PREFIX}-python"
    "${MINGW_PACKAGE_PREFIX}-python-click"
    "${MINGW_PACKAGE_PREFIX}-python-cryptography"
    "${MINGW_PACKAGE_PREFIX}-python-pygments"
    "${MINGW_PACKAGE_PREFIX}-python-prompt_toolkit"
    "${MINGW_PACKAGE_PREFIX}-python-pymysql"
    "${MINGW_PACKAGE_PREFIX}-python-sqlparse"
    "${MINGW_PACKAGE_PREFIX}-python-sqlglot"
    "${MINGW_PACKAGE_PREFIX}-python-configobj"
    "${MINGW_PACKAGE_PREFIX}-python-cli_helpers"
    "${MINGW_PACKAGE_PREFIX}-python-pyperclip"
    "${MINGW_PACKAGE_PREFIX}-python-pyaes"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python-build"
  "${MINGW_PACKAGE_PREFIX}-python-installer"
  "${MINGW_PACKAGE_PREFIX}-python-setuptools"
  "${MINGW_PACKAGE_PREFIX}-python-wheel"
  "${MINGW_PACKAGE_PREFIX}-python-pip"
)
options=('!strip')
optdepends=("${MINGW_PACKAGE_PREFIX}-python-paramiko: SSH support")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-pytest")
options=('!emptydirs' '!strip')
source=(
    "https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz"
    "0001-mycli-unpin-the-cryptography-version.patch"
)
sha256sums=('a71db5bd9c1a7d0006f4f2ff01548ce75637d3f50ca3a7e77b950b5b46aff7cd'
            '6e48527d57e0fa76565872a95b85d1ab6de1109f58d1505e6fcfdace57eedc6c')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  
  patch -p1 -i "${srcdir}/0001-mycli-unpin-the-cryptography-version.patch"
}

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}


check() {
  cd "$srcdir/${_realname}-$pkgver"
  pytest -v -k "not test_list_dsn and not test_list_ssh_config and not test_ssh_config and not test_tee_command and not test_once_command"
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
