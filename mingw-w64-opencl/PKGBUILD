# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: wirx6 <wirx654@gmail.com>

pkgbase=mingw-w64-opencl
pkgname=("${MINGW_PACKAGE_PREFIX}-opencl")
conflicts=("${MINGW_PACKAGE_PREFIX}-opencl-headers")
replaces=("${MINGW_PACKAGE_PREFIX}-opencl-headers")
pkgver=2.1.20161129 # date of the commit, branch 2.1
pkgrel=3
epoch=2
pkgdesc='Khronos official ICD loader for OpenCL (mingw-w64)'
arch=('any')
url='https://www.khronos.org/registry/cl/'
license=('custom')
makedepends=(git python3
             "${MINGW_PACKAGE_PREFIX}-binutils")
source=('git+https://github.com/KhronosGroup/OpenCL-Headers.git#commit=abb2958'
        'git+https://github.com/KhronosGroup/OpenCL-CLHPP.git#commit=d54d52c'
        'LICENSE.txt'
        'OpenCL.def')
sha256sums=('SKIP'
            'SKIP'
            'd1059405dce021e0c0a7f7e01b48447e641477e0c14a9bc29983f4202b67c410'
            'ca710fe03f90437cab9bb49b74e2ce65e97b7b2179b4fc40e190658d73c1862a')

prepare() {
  cd "${srcdir}"/OpenCL-CLHPP

  # https://github.com/KhronosGroup/OpenCL-CLHPP/issues/26
  git cherry-pick -n befe77b66fd3dd87ce9fef940e475cdc71d8bd9f
}

build() {
  [[ -d ${srcdir}/build-${CARCH} ]] && rm -rf ${srcdir}/build-${CARCH}
  mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}
  
  dlltool -l libOpenCL.dll.a -d ../OpenCL.def
}

package() {
  cd "${srcdir}"/OpenCL-Headers

  install -dm755 "${pkgdir}${MINGW_PREFIX}/include/CL"

  for h in *.h; do
    install -m 644 ${h} "${pkgdir}${MINGW_PREFIX}/include/CL/"
  done

  cd "${srcdir}"/OpenCL-CLHPP

  python3 gen_cl_hpp.py -i input_cl.hpp -o cl.hpp
  install -m 644 cl.hpp "${pkgdir}${MINGW_PREFIX}/include/CL/"
  install -m 644 input_cl2.hpp "${pkgdir}${MINGW_PREFIX}/include/CL/cl2.hpp"

  install -Dm644 ${srcdir}/build-${CARCH}/libOpenCL.dll.a ${pkgdir}${MINGW_PREFIX}/lib/libOpenCL.dll.a
  install -Dm644 ../LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
