_realname=GENie
pkgbase=mingw-w64-genie
pkgname=("${MINGW_PACKAGE_PREFIX}-genie")
pkgver=r1170.22cc907
pkgrel=1
_commit='22cc907a4351db46c55f73e6aa901f1b2f0c52ad'
pkgdesc="GENie project generator tool (a fork of premake4 by Branimir Karadžić)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/bkaradzic/GENie'
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             $([[ $MINGW_PACKAGE_PREFIX == *-clang-* ]] && echo \
               "${MINGW_PACKAGE_PREFIX}-gcc-compat")
             "git")
source=("git+https://github.com/bkaradzic/GENie#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd "${_realname}"
  printf "r%s.%s" "$(awk '$1 == "version" {print $2}' README.md)" "$(git rev-parse --short HEAD)"
}

prepare() {
  rm -rf "build-${MSYSTEM}"
  cp -r "${_realname}" "build-${MSYSTEM}"
}

build() {
  cd "build-${MSYSTEM}"
  make
  ./bin/windows/genie release
  make -C build/gmake.windows clean
  make -C build/gmake.windows all
}

package() {
  cd "build-${MSYSTEM}"

  # Install the binary
  install -D -m755 "${srcdir}/build-${MSYSTEM}/bin/windows/genie.exe" \
    "${pkgdir}/usr/bin/genie.exe"

  # Install the readme
  install -D -m644 "${srcdir}/build-${MSYSTEM}/README.md" \
    "${pkgdir}/usr/share/doc/${_realname}/README.md"

  # Install the license
  install -D -m644 "${srcdir}/build-${MSYSTEM}/LICENSE" \
    "${pkgdir}/usr/share/licenses/${_realname}/LICENSE"
}
