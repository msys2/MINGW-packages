# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: JÃ¼rgen Pfeifer <juergen@familiepfeifer.de>

_realname=qrupdate
_mingw_suff=mingw-w64-${CARCH}
pkgname=${_mingw_suff}-${_realname}-svn
pkgver=r28
pkgrel=1
pkgdesc="qrupdate is a Fortran library for fast updates of QR and Cholesky decompositions."
arch=('any')
provides=("${_mingw_suff}-${_realname}")
url="http://qrupdate.sourceforge.net/"
options=('strip' 'staticlibs')
license=('GPL' 'LGPL')
groups=("${_mingw_suff}")
makedepends=("subversion"
    "${_mingw_suff}-gcc-fortran")
depends=("${_mingw_suff}-OpenBLAS")
source=("$_realname::svn+http://svn.code.sf.net/p/qrupdate/code")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_realname"
  local ver="$(svnversion)"
  printf "r%s" "${ver//[[:alpha:]]}"
}

build() {
  mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}
  cp -pR ../$_realname/* .
  make FPICFLAGS="" lib
  gfortran -shared -o lib${_realname}.dll -Wl,--out-implib,lib${_realname}.dll.a src/*.o -lopenblas -lgfortran
}

package() {
  cd ${srcdir}/build-${CARCH}
  mkdir -p ${pkgdir}${MINGW_PREFIX}/bin
  mkdir -p ${pkgdir}${MINGW_PREFIX}/lib
  cp -pf lib${_realname}.dll ${pkgdir}${MINGW_PREFIX}/bin
  cp -pf lib${_realname}*.a ${pkgdir}${MINGW_PREFIX}/lib
  mkdir -p ${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname  
  cp -pf ${srcdir}/${_realname}/COPYING* \
    ${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname
}
