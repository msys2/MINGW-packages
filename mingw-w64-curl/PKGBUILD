# Maintainer: Alexey Pavlov <alexpux@gmail.com>
_variant=-openssl
#_variant=-winssl
#_variant=-gnutls

_realname=curl
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=7.44.0
pkgrel=3
pkgdesc="An URL retrival utility and library. (mingw-w64)"
arch=('any')
url="http://curl.haxx.se"
license=("MIT")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-c-ares"
         "${MINGW_PACKAGE_PREFIX}-libidn"
         "${MINGW_PACKAGE_PREFIX}-libssh2"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-rtmpdump"
         $([[ "$_variant" == "-openssl" ]] && echo \
            "${MINGW_PACKAGE_PREFIX}-ca-certificates" \
            "${MINGW_PACKAGE_PREFIX}-openssl")
         $([[ "$_variant" == "-gnutls" ]] && echo \
            "${MINGW_PACKAGE_PREFIX}-ca-certificates" \
            "${MINGW_PACKAGE_PREFIX}-gnutls")
         )
options=('staticlibs')
source=("$url/download/${_realname}-${pkgver}.tar.bz2"{,.asc}
        "0001-curl-relocation.patch"
        "0002-curl-mingw-enable-static.patch"
        "0003-curl-detect-ipv6-on-windows.patch"
        "0004-NTLM-Reset-auth-done-when-using-a-fresh-connection.patch")
md5sums=('6b952ca00e5473b16a11f05f06aa8dae'
         'SKIP'
         '58520051c4ed77781d233c3fa40a5435'
         'eac9e212e619490966ae47004bec547b'
         'f1c5b8648af0dfab7bde4d68a6b65f25'
         '1e42a032267f66041a117df4dcd1cf1f')
validpgpkeys=('914C533DF9B2ADA2204F586D78E11C6B279D5C91')  # Daniel Stenberg

prepare() {
  cd "${_realname}-${pkgver}"
  rm -f lib/pathtools.h lib/pathtools.c > /dev/null 2>&1 || true
  patch -p1 -i "${srcdir}/0001-curl-relocation.patch"
  patch -p1 -i "${srcdir}/0002-curl-mingw-enable-static.patch"
  patch -p1 -i "${srcdir}/0003-curl-detect-ipv6-on-windows.patch"
  patch -p1 -i "${srcdir}/0004-NTLM-Reset-auth-done-when-using-a-fresh-connection.patch"
  autoreconf -vfi
}

build() {
  local -a extra_config
  if check_option "debug" "y"; then
    extra_config+=( --enable-debug )
  else
    extra_config+=( --disable-debug )
  fi
  mkdir -p "${srcdir}/build-${CARCH}"

  local -a _variant_config
  if [ "${_variant}" = "-winssl" ]; then
    _variant_config+=("--without-ssl")
    _variant_config+=("--without-gnutls")
    _variant_config+=("--with-winssl")
    _variant_config+=("--without-ca-bundle")
    _variant_config+=("--without-ca-path")
  elif [ "${_variant}" = "-gnutls" ]; then
    _variant_config+=("--without-ssl")
    _variant_config+=("--with-gnutls")
    _variant_config+=("--with-ca-bundle=${MINGW_PREFIX}/ssl/certs/ca-bundle.crt")
  elif [ "${_variant}" = "-openssl" ]; then
    _variant_config+=("--without-gnutls")
    _variant_config+=("--with-ssl")
    _variant_config+=("--with-ca-bundle=${MINGW_PREFIX}/ssl/certs/ca-bundle.crt")
  fi
  cd "${srcdir}/build-${CARCH}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --without-random \
    --enable-static \
    --enable-shared \
    --enable-sspi \
    "${_variant_config[@]}" \
    "${extra_config[@]}"
  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install
}
