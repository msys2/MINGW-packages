# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=just
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.31.0
pkgrel=1
pkgdesc="Just a command runner (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://just.systems'
msys2_repository_url='https://github.com/casey/just'
msys2_documentation_url='https://just.systems/man/en'
license=('spdx:CC0-1.0')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
options=('!lto')
source=("${msys2_repository_url}/archive/${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('6cef9159bb929c94b105015c28b0c94618556cfd2f1a1f17c012a1365ee8a8ac')
noextract=("${_realname}-${pkgver}.tar.gz")

prepare() {
  tar -xzf "${_realname}-${pkgver}.tar.gz" || true
  cd "${_realname}-${pkgver}"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${_realname}-${pkgver}"

  WINAPI_NO_BUNDLED_LIBRARIES=1 \
    cargo build --release --frozen
}

# run `just test` with pre-built binary. this will recompile everything with debug profile
check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  target/release/just test
}

package() {
  cd "${_realname}-${pkgver}"

  WINAPI_NO_BUNDLED_LIBRARIES=1 \
    cargo install \
      --offline \
      --no-track \
      --frozen \
      --path . \
      --root "${pkgdir}${MINGW_PREFIX}"

  target/release/just --man | install -Dm644 /dev/stdin "${pkgdir}${MINGW_PREFIX}/share/man/man1/just.1"
  local _complete='target/release/just --completions'
  $_complete bash | install -Dm644 /dev/stdin "${pkgdir}${MINGW_PREFIX}/share/bash-completion/completions/just"
  $_complete zsh | install -Dm644 /dev/stdin "${pkgdir}${MINGW_PREFIX}/share/zsh/site-functions/_just"
  $_complete fish | install -Dm644 /dev/stdin "${pkgdir}${MINGW_PREFIX}/share/fish/vendor_completions.d/just.fish"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
