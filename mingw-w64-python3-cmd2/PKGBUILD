# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

#Python 2 support was discontinued in this series

_realname=cmd2
pkgbase=mingw-w64-python-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-python3-${_realname}"
pkgver=0.9.4
pkgrel=1
pkgdesc="Extra features for standard library's cmd module (mingw-w64)"
arch=('any')
url='https://github.com/python-cmd2/cmd2'
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python3-attrs"
             "${MINGW_PACKAGE_PREFIX}-python3-pyparsing" 
             "${MINGW_PACKAGE_PREFIX}-python3-pyperclip" 
             "${MINGW_PACKAGE_PREFIX}-python3-pyreadline"
             "${MINGW_PACKAGE_PREFIX}-python3-colorama" 
             "${MINGW_PACKAGE_PREFIX}-python3-wcwidth"
             "${MINGW_PACKAGE_PREFIX}-python2-wcwidth")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools") 
checkdepends=("${MINGW_PACKAGE_PREFIX}-python3-pytest-runner"
              "${MINGW_PACKAGE_PREFIX}-python2-pytest-runner"
#              "${MINGW_PACKAGE_PREFIX}-python3-pytest-xdist"
#              "${MINGW_PACKAGE_PREFIX}-python2-pytest-xdist"
              "${MINGW_PACKAGE_PREFIX}-python3-mock" )
# 'vi')
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-$pkgver.tar.gz::https://github.com/python-cmd2/cmd2/archive/$pkgver.tar.gz")
sha512sums=('fd4ff48aeed4184797d9741e635dd3da4cc95f6efdddb24933c7619942c46cf5408f02b3ad6765088dd2d293628bc4674011817daeed4f109494c01e3653d58c')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"
  rm -rf python3-build-${CARCH} || true
  cp -r "${_realname}-${pkgver}" "python3-build-${CARCH}"
  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
  export PBR_VERSION=$pkgver
}

# Note that build() is sometimes skipped because it's done in 
# the packages setup.py install for simplicity if you can do so.
# but sometimes, you want to do a check before install which would
# also trigger the build.
build() {
    msg "Python 3 build for ${CARCH}"  
    cd "${srcdir}/python3-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python3 setup.py build
}

check() {
  msg "Python 3 test for ${CARCH}"
  cd "${srcdir}/python3-build-${CARCH}"
  ${MINGW_PREFIX}/bin/python3 setup.py pytest || true
}

package() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3"
           "${MINGW_PACKAGE_PREFIX}-python3-attrs"
           "${MINGW_PACKAGE_PREFIX}-python3-pyparsing"
           "${MINGW_PACKAGE_PREFIX}-python3-pyreadline"
           "${MINGW_PACKAGE_PREFIX}-python3-pyperclip"
           "${MINGW_PACKAGE_PREFIX}-python3-wcwidth")

  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}

