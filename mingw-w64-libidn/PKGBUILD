# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libidn
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.33
pkgrel=2
pkgdesc="Implementation of the Stringprep, Punycode and IDNA specifications (mingw-w64)"
arch=('any')
url="https://www.gnu.org/software/libidn"
license=("GPL3, LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config" "gtk-doc")
depends=("${MINGW_PACKAGE_PREFIX}-gettext")
options=('staticlibs' 'strip')
source=("https://ftp.gnu.org/gnu/libidn/${_realname}-${pkgver}.tar.gz"
        0001-fix-gtkdoc.all.patch
        0002-fix-gdoc.all.patch
        0003-nfkc.c-Fix-Win64-crash.patch
        0004-nfkc.c-Fixed-invalid-var-types.patch
        0005-gcc7-compatibility.patch)
sha256sums=('44a7aab635bb721ceef6beecc4d49dfd19478325e1b47f3196f7d2acc4930e19'
            '3c6498f1682aae763ea0e4264db0c16eda83837a77b59c2d458f836a16a94f69'
            '02d9b9e6e3f966cff2d4d763c0de9219da6c8cf444248011caa8eb2fb3067a24'
            '6293c730a98af32a337149a95d848f3c4619df8dc367e0bf0251a509b09f5963'
            '2a2c45dc6f595705a226687c8954390baf926e135168e1919baeadef5e5478cc'
            'f6ce87dcb44e558e88eeca4f6577a36bfe7f624ee3e95cd2549a9798d77cf8a8')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-fix-gtkdoc.all.patch
  patch -p1 -i ${srcdir}/0002-fix-gdoc.all.patch
  patch -p1 -i ${srcdir}/0003-nfkc.c-Fix-Win64-crash.patch
  patch -p1 -i ${srcdir}/0004-nfkc.c-Fixed-invalid-var-types.patch
  patch -p1 -i ${srcdir}/0005-gcc7-compatibility.patch
  
  autopoint --force
  autoreconf -i
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX}\
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --disable-csharp \
    --disable-java \
    --disable-rpath \
    --enable-threads=win32

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
