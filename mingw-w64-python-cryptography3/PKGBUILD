# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=cryptography
pkgbase=mingw-w64-python-${_realname}3
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}3")
pkgver=3.4.8
pkgrel=1
pkgdesc="A package designed to expose cryptographic recipes and primitives to Python developers (mingw-w64)"
url='https://pypi.org/project/cryptography/'
license=('Apache')
arch=('any')
mingw_arch=('clang32' 'clangarm64')
provides=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
replaces=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
depends=("${MINGW_PACKAGE_PREFIX}-python-cffi"
         "${MINGW_PACKAGE_PREFIX}-python-idna")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools" "${MINGW_PACKAGE_PREFIX}-cc")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-pytest-runner"
              #"${MINGW_PACKAGE_PREFIX}-python-iso8601"
              "${MINGW_PACKAGE_PREFIX}-python-pretend"
              #"${MINGW_PACKAGE_PREFIX}-python-cryptography-vectors"
              "${MINGW_PACKAGE_PREFIX}-python-hypothesis"
              "${MINGW_PACKAGE_PREFIX}-python-pytz")
source=(https://pypi.io/packages/source/c/cryptography/${_realname}-${pkgver}.tar.gz
        0002-move-setuptools-rust-import.patch)
sha256sums=('94cc5ed4ceaefcbe5bf38c8fba6a21fc1d365bb8fb826ea1688e3370b2e24a1c'
            '7374d57ae28eefe78394f2d6d983adf7d216c040c3b4bac8e83e4f5132c51fa2')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # don't fail for lack of setuptools-rust if not building rust
  patch -Np1 -i "${srcdir}"/0002-move-setuptools-rust-import.patch

  export CRYPTOGRAPHY_DONT_BUILD_RUST=1
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python setup.py build
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # ${MINGW_PREFIX}/bin/python setup.py pytest
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
