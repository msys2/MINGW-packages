# Maintainer: ImperatorS79 <fevrier.simon@gmail.com>

_realname=gn
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgdesc="Meta-build system that generates build files for Ninja."
pkgver=r1874.6966efb0
pkgrel=1
arch=('any')
url='https://gn.googlesource.com/gn'
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "git")
options=('strip' 'staticlibs')
source=("${_realname}"::git+${url})
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_realname}"
  printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  plain 'No step'
}

build() {  
  cd "${srcdir}/${_realname}"
  python build/gen.py --platform=mingw
  sed -i 's/command = rm -f $out && $ar rcsT $out $in/command = $ar rcsT $out $in/g' out/build.ninja
  ninja -C out
}

check() {
  cd "${srcdir}/${_realname}"
  ./out/gn_unittests
}

package() {
  cd "${srcdir}/${_realname}"
  install -D out/gn "${pkgdir}${MINGW_PREFIX}/bin/gn"
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}" docs/*
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" LICENSE
}
