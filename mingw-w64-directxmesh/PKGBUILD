# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=directxmesh
_rn=DirectXMesh
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
# Version from CMakeLists.txt
pkgver=1.6.7
_tag=oct2024
pkgrel=1
pkgdesc="DirectXMesh geometry processing library (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='http://go.microsoft.com/fwlink/?LinkID=324981'
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cc")
depends=("${MINGW_PACKAGE_PREFIX}-directxmath"
         "${MINGW_PACKAGE_PREFIX}-directx-headers"
         "${MINGW_PACKAGE_PREFIX}-gcc-libs")
source=("${_realname}-${_tag}.tar.gz"::"https://github.com/microsoft/DirectXMesh/archive/refs/tags/${_tag}.tar.gz")
sha256sums=('95fb742625b272b0fb382ea9807b4f336fed146d1b8f38f45de0da203f5d013e')

build() {
  cd "${srcdir}/${_realname}-${_tag}"
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  if [ "${CARCH}" == "x86_64" ]; then
    extra_config+=("-DDIRECTX_ARCH=x64")
  fi
  if [ "${CARCH}" == "i686" ]; then
    extra_config+=("-DDIRECTX_ARCH=x86")
  fi
  if [ "${CARCH}" == "aarch64" ]; then
    extra_config+=("-DDIRECTX_ARCH=arm64")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DBUILD_TOOLS=ON \
      -DBUILD_DX12=ON \
      ../${_realname}-${_tag}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .

  install -Dm644 "${srcdir}/${_realname}-${_tag}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
