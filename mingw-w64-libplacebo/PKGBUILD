# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libplacebo
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=5.229.2
pkgrel=1
pkgdesc="Reusable library for GPU-accelerated video/image rendering primitives (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://code.videolan.org/videolan/libplacebo"
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-glslang"
         "${MINGW_PACKAGE_PREFIX}-lcms2"
         "${MINGW_PACKAGE_PREFIX}-libepoxy"
         "${MINGW_PACKAGE_PREFIX}-shaderc"
         "${MINGW_PACKAGE_PREFIX}-spirv-cross"
         "${MINGW_PACKAGE_PREFIX}-spirv-tools"
         "${MINGW_PACKAGE_PREFIX}-vulkan"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread-git")
makedepends=("git"
             "${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-python-mako"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-vulkan-headers"
             "${MINGW_PACKAGE_PREFIX}-cc")
license=('LGPL2.1')
# "git+https://code.videolan.org/videolan/libplacebo.git"

source=("git+https://code.videolan.org/kasper93/libplacebo.git#branch=build"
        "git+https://github.com/Immediate-Mode-UI/Nuklear.git"
        "git+https://github.com/Dav1dde/glad.git"
        "git+https://github.com/pallets/jinja.git"
        "git+https://github.com/pallets/markupsafe.git"
        "git+https://github.com/KhronosGroup/Vulkan-Headers.git")
sha256sums=(SKIP
            SKIP
            SKIP
            SKIP
            SKIP
            SKIP)

prepare() {
  cd ${_realname}
  git submodule init
  git config submodule.demos/3rdparty/nuklear.url "$srcdir/Nuklear"
  git config submodule.3rdparty/glad.url "$srcdir/glad"
  git config submodule.3rdparty/jinja.url "$srcdir/jinja"
  git config submodule.3rdparty/markupsafe.url "$srcdir/markupsafe"
  git config submodule.3rdparty/Vulkan-Headers.url "$srcdir/Vulkan-Headers"
  git -c protocol.file.allow=always submodule update
}

build() {
  [[ -d "${srcdir}"/build-${MSYSTEM} ]] && rm -rf "${srcdir}"/build-${MSYSTEM}
  mkdir -p "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

  MSYS2_ARG_CONV_EXCL="--prefix=" \
  meson setup \
    --prefix="${MINGW_PREFIX}" \
    --buildtype plain \
    -Ddemos=false \
    -Dtests=true \
    -Dd3d11=enabled \
    -Dlcms=enabled \
    -Dglslang=enabled \
    -Dshaderc=enabled \
    -Dvulkan=enabled \
    "../${_realname}"

  meson compile
}

check() {
  cd "${srcdir}"/build-${MSYSTEM}

  meson test
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}
  DESTDIR="${pkgdir}" meson install

  install -Dm644 "${srcdir}/${_realname}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
