# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libplacebo
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=5.229.2
pkgrel=1
pkgdesc="Reusable library for GPU-accelerated video/image rendering primitives (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://code.videolan.org/videolan/libplacebo"
depends=("${MINGW_PACKAGE_PREFIX}-lcms2"
         "${MINGW_PACKAGE_PREFIX}-shaderc"
         "${MINGW_PACKAGE_PREFIX}-spirv-cross"
         "${MINGW_PACKAGE_PREFIX}-vulkan")
makedepends=("git"
             "patch"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-python-glad"
             "${MINGW_PACKAGE_PREFIX}-python-jinja"
             "${MINGW_PACKAGE_PREFIX}-vulkan-headers")
license=('LGPL2.1')
source=(${url}/-/archive/v${pkgver}/${_realname}-v${pkgver}.tar.gz
        https://code.videolan.org/videolan/libplacebo/-/commit/baec0e53c9591a005d2fd6ee1dadcd10b8ce8770.patch
        https://code.videolan.org/videolan/libplacebo/-/commit/8206a0bab12dcc7bf88d1cfe9b1f1a1ed9cc9d59.patch
        https://code.videolan.org/videolan/libplacebo/-/commit/d551a3c42bb3799e88e778ea7f3d0402a46efbff.patch)
sha256sums=(SKIP
            '68cc54f667e3c6eeec9ed0263dcbd7c1623607c7d8d98433d0a0333afa9929fe'
            'd70ee11f2d2081941ceab111dbe09ba856d9527e3772d898ad71afd98a00ff4d'
            'cb23bdf0ff215880f34cc819a7df0b07f7938042f374c15a8558192dbbd9fb18')

prepare() {
  cd "${srcdir}/${_realname}-v${pkgver}"

  patch -Nbp1 -i ${srcdir}/baec0e53c9591a005d2fd6ee1dadcd10b8ce8770.patch
  patch -Nbp1 -i ${srcdir}/8206a0bab12dcc7bf88d1cfe9b1f1a1ed9cc9d59.patch
  patch -Nbp1 -i ${srcdir}/d551a3c42bb3799e88e778ea7f3d0402a46efbff.patch
}

build() {
  [[ -d "${srcdir}"/build-${MSYSTEM} ]] && rm -rf "${srcdir}"/build-${MSYSTEM}
  mkdir -p "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    meson setup                   \
      --prefix="${MINGW_PREFIX}"  \
      --buildtype plain           \
      -Ddemos=false               \
      -Dtests=true                \
      -Dd3d11=enabled             \
      -Dlcms=enabled              \
      -Dshaderc=enabled           \
      -Dvulkan=enabled            \
      "../${_realname}-v${pkgver}"

  meson compile
}

check() {
  cd "${srcdir}"/build-${MSYSTEM}

  meson test
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}
  DESTDIR="${pkgdir}" meson install

  install -Dm644 "${srcdir}/${_realname}-v${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
