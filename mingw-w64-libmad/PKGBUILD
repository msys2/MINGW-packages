# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: AlexWMF <alexxwmf@gmail.com>

_realname=libmad
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.15.1b
pkgrel=2
pkgdesc="A high-quality MPEG audio decoder (mingw-w64)"
arch=('any')
url="http://www.underbit.com/products/mad/"
license=('GPL')
depends=()
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "autoconf"
             "m4")
source=(http://downloads.sourceforge.net/sourceforge/mad/${_realname}-${pkgver}.tar.gz
        0001-no-undefined.mingw.patch
        0003-update-ac-and-silent-rules.mingw.patch
        0004-amd64-64bit.diff
        0005-frame_length.diff
        0006-optimize.diff
        0007-libmad.patch)
sha1sums=('cac19cd00e1a907f3150cc040ccc077783496d76'
          '4f83e9b71e83686a12819e4e91a2d77d24aac77c'
          '51d7c0543f9bbb93281e7c5f71ab546ab931ebfa'
          '97eba307b3343d5ba8938d59ec8f2c4b7449ae17'
          'b9c61ecacc6a6d47425d66f33327e0634cd8a33c'
          '3d5b958244ef0395ccdcb00344f2cf301ca07e34'
          '448a6d597ce61bb5ea38be6e56f66196d3be6515')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i "${srcdir}/0001-no-undefined.mingw.patch"
  patch -p1 -i "${srcdir}/0003-update-ac-and-silent-rules.mingw.patch"
  patch -p1 -i "${srcdir}/0004-amd64-64bit.diff"
  patch -p1 -i "${srcdir}/0005-frame_length.diff"
  patch -p1 -i "${srcdir}/0006-optimize.diff"
  patch -p1 -i "${srcdir}/0007-libmad.patch"

  touch NEWS AUTHORS ChangeLog
  rm aclocal.m4
  rm Makefile.in

  WANT_AUTOMAKE=latest autoreconf -fi
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST}

  make
}

package() {
  cd build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
}
