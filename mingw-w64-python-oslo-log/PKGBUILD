# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=oslo-log
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}"  "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=3.40.1
pkgrel=1
pkgdesc="Oslo Logging Library (mingw-w64)"
arch=('any')
url='https://pypi.python.org/pypi/oslo.log'
license=('Apache')
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools" 
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-pbr"
             "${MINGW_PACKAGE_PREFIX}-python2-pbr"
             "${MINGW_PACKAGE_PREFIX}-python3-oslo-config"
             "${MINGW_PACKAGE_PREFIX}-python2-oslo-config"
             "${MINGW_PACKAGE_PREFIX}-python3-oslo-context"
             "${MINGW_PACKAGE_PREFIX}-python2-oslo-context"
             "${MINGW_PACKAGE_PREFIX}-python3-oslo-i18n"
             "${MINGW_PACKAGE_PREFIX}-python2-oslo-i18n" 
             "${MINGW_PACKAGE_PREFIX}-python3-oslo-utils"
             "${MINGW_PACKAGE_PREFIX}-python2-oslo-utils"
             "${MINGW_PACKAGE_PREFIX}-python3-oslo-serialization"
             "${MINGW_PACKAGE_PREFIX}-python2-oslo-serialization"
             "${MINGW_PACKAGE_PREFIX}-python3-debtcollector"
             "${MINGW_PACKAGE_PREFIX}-python2-debtcollector"
             "${MINGW_PACKAGE_PREFIX}-python3-dateutil"
             "${MINGW_PACKAGE_PREFIX}-python2-dateutil"
             "${MINGW_PACKAGE_PREFIX}-python3-monotonic"
             "${MINGW_PACKAGE_PREFIX}-python2-monotonic")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python3-mock"
              "${MINGW_PACKAGE_PREFIX}-python2-mock"
              "${MINGW_PACKAGE_PREFIX}-python3-oslotest"
              "${MINGW_PACKAGE_PREFIX}-python2-oslotest"
              "${MINGW_PACKAGE_PREFIX}-python3-testrepository"
              "${MINGW_PACKAGE_PREFIX}-python2-testrepository")
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-$pkgver.tar.gz"::"https://github.com/openstack/oslo.log/archive/$pkgver.tar.gz")
sha512sums=('ed6352830fe30bb5f49baef7e9a589990de185797466947b3625af6ce4431258873ebd09d917cf8a180ecac7bdd767923a058f196feda7c3085776bef5485ee4')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"
  sed -i 's/unittest2/unittest/g' oslo.log-$pkgver/oslo_log/tests/unit/*.py
  for builddir in python{2,3}-build-${CARCH}; do
    rm -rf ${builddir} | true
    cp -r "oslo.log-${pkgver}" "${builddir}"
  done
  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

  export PBR_VERSION=$pkgver
}

# Note that build() is sometimes skipped because it's done in 
# the packages setup.py install for simplicity if you can do so.
# but sometimes, you want to do a check before install which would
# also trigger the build.
build() {
  for pver in {2,3}; do  
    msg "Python ${pver} build for ${CARCH}"  
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py build
  done  
}

check() {
  for pver in {2,3}; do
    msg "Python ${pver} test for ${CARCH}"
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py testr || warning "Tests failed"
  done
}

package_python3-oslo-log() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3"
           "${MINGW_PACKAGE_PREFIX}-python3-six"
           "${MINGW_PACKAGE_PREFIX}-python3-pbr"
           "${MINGW_PACKAGE_PREFIX}-python3-oslo-config"
           "${MINGW_PACKAGE_PREFIX}-python3-oslo-context"
           "${MINGW_PACKAGE_PREFIX}-python3-oslo-i18n"
           "${MINGW_PACKAGE_PREFIX}-python3-oslo-utils"
           "${MINGW_PACKAGE_PREFIX}-python3-oslo-serialization"
           "${MINGW_PACKAGE_PREFIX}-python3-debtcollector"
           "${MINGW_PACKAGE_PREFIX}-python3-dateutil"
           "${MINGW_PACKAGE_PREFIX}-python3-monotonic")
  install=${_realname}2-${CARCH}.install

  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
# This entire section should be removed if the package does NOT install
# anything in the /mingw*/bin directory.
### begin section ###
  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  # fix python command in files
  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*.py; do
    sed -e "s|/usr/bin/env |${MINGW_PREFIX}/|g" \
        -e "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" -i ${_f}
  done
#### end section ####
}

package_python2-oslo-log() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2"
           "${MINGW_PACKAGE_PREFIX}-python2-six"
           "${MINGW_PACKAGE_PREFIX}-python2-pbr"
           "${MINGW_PACKAGE_PREFIX}-python2-oslo-config"
           "${MINGW_PACKAGE_PREFIX}-python2-oslo-context"
           "${MINGW_PACKAGE_PREFIX}-python2-oslo-i18n"
           "${MINGW_PACKAGE_PREFIX}-python2-oslo-utils"
           "${MINGW_PACKAGE_PREFIX}-python2-oslo-serialization"
           "${MINGW_PACKAGE_PREFIX}-python2-debtcollector"
           "${MINGW_PACKAGE_PREFIX}-python2-dateutil"
           "${MINGW_PACKAGE_PREFIX}-python2-monotonic")
  install=${_realname}2-${CARCH}.install

  cd "${srcdir}/python2-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
# This entire section should be removed if the package does NOT install
# anything in the /mingw*/bin directory. 
### begin section ###
  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  # fix python command in files
  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*.py; do
    sed -e "s|/usr/bin/env |${MINGW_PREFIX}|g" \
        -e "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" -i ${_f}
  done

# for Python2 packages, you want to rename some stuff from the bin directory 
# with the 2 suffix like in the mingw-w64-python-pygments package to avoid
# conflicts when installing both the Python2 and Python3 packages
  for f in convert-json; do
    mv "${pkgdir}${MINGW_PREFIX}"/bin/${f}{,2}.exe
    if [ -f "${pkgdir}${MINGW_PREFIX}"/bin/${f}.exe.manifest ]; then
      mv "${pkgdir}${MINGW_PREFIX}"/bin/${f}{,2}.exe.manifest
      sed -e "s|${f}|${f}2|g" -i "${pkgdir}${MINGW_PREFIX}"/bin/${f}2.exe.manifest
    fi
    mv "${pkgdir}${MINGW_PREFIX}"/bin/${f}{,2}-script.py
  done
#### end section ####
}

package_mingw-w64-i686-python2-oslo-log() {
  package_python2-oslo-log
}

package_mingw-w64-i686-python3-oslo-log() {
  package_python3-oslo-log
}

package_mingw-w64-x86_64-python2-oslo-log() {
  package_python2-oslo-log
}

package_mingw-w64-x86_64-python3-oslo-log() {
  package_python3-oslo-log
}
