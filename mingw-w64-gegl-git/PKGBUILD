# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gegl
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}-git"
provides=("${_mingw_suff}-${_realname}")
conflicts=("${_mingw_suff}-${_realname}")
pkgver=r5776.f50adad
pkgrel=1
pkgdesc="Generic Graphics Library (mingw-w64)"
arch=('any')
url="http://gegl.org/gegl/"
license=("GPL-3.0+" "LGPL-3.0+")
makedepends=("${_mingw_suff}-gcc"
		"${_mingw_suff}-pkg-config"
		"${_mingw_suff}-ruby"
		"asciidoc"
		"${_mingw_suff}-python2"
		"${_mingw_suff}-ruby"
		"${_mingw_suff}-lua"
		#"${_mingw_suff}-ffmpeg"
		)
depends=("${_mingw_suff}-crt"
		"${_mingw_suff}-cairo"
		"${_mingw_suff}-gettext"
		"${_mingw_suff}-glib2"
		"${_mingw_suff}-gtk2"
		"${_mingw_suff}-libjpeg"
		"${_mingw_suff}-libpng"
		"${_mingw_suff}-librsvg"
		"${_mingw_suff}-lcms"
		"${_mingw_suff}-lensfun"
		"${_mingw_suff}-pango"
		"${_mingw_suff}-openexr"
		"${_mingw_suff}-libwebp"
		"${_mingw_suff}-exiv2"
		"${_mingw_suff}-gdk-pixbuf2"
		"${_mingw_suff}-jasper"
		"${_mingw_suff}-SDL"
		"${_mingw_suff}-libspiro"
		#"${_mingw_suff}-gobject-introspection"
		)
optdepends=("${_mingw_suff}-openexr: for using the openexr plugin"
            "${_mingw_suff}-ffmpeg: for using the ffmpeg plugin"
            "${_mingw_suff}-librsvg: for using the svg plugin"
            "${_mingw_suff}-jasper: for using the jasper plugin")
options=('strip' 'staticlibs')
source=("${_realname}"::"git://git.gnome.org/gegl")
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/$_realname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd ${srcdir}/${_realname}
	autoreconf --force --install --verbose
}

build() {
	export lt_cv_deplibs_check_method='pass_all'
	mkdir -p "${srcdir}/build-${MINGW_CHOST}"
	cd "${srcdir}/build-${MINGW_CHOST}"
	"${srcdir}"/${_realname-}/configure \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--target=${MINGW_CHOST} \
		--enable-shared \
		--disable-static \
		--with-sdl \
		--with-openexr \
		--with-librsvg \
		--with-jasper \
		--without-libavformat \
		--disable-docs
	
	make
}

package() {
    cd "${srcdir}/build-${MINGW_CHOST}"
    make DESTDIR="$pkgdir" install
    find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
	rm "${pkgdir}${MINGW_PREFIX}"/lib/gegl-*/*.dll.a
}
