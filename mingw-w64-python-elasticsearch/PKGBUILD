# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=elasticsearch
pkgbase=mingw-w64-python-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-${_realname}
pkgver=8.14.0
pkgrel=1
pkgdesc='Python client for Elasticsearch (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'archlinux: python-elasticsearch'
  'pypi: elasticsearch'
)
msys2_repository_url='https://github.com/elastic/elasticsearch-py'
url='https://ela.st/es-python'
license=('spdx:Apache-2.0')
depends=(
    ${MINGW_PACKAGE_PREFIX}-python
    ${MINGW_PACKAGE_PREFIX}-python-elastic-transport)
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python-wheel")
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz"
        "0001-elasticsearch-setup-open-utf8-encoding.patch")
sha256sums=('aa2490029dd96f4015b333c1827aa21fd6c0a4d223b00dfb0fe933b8d09a511b'
            '4bbc12a143364eea1737df05b192ae5abafacf1e1a5f214f438aa081c9013525')

prepare() {
  cd "${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-elasticsearch-setup-open-utf8-encoding.patch"
}

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
