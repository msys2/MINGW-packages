# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gd
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.1.1
pkgrel=3
pkgdesc='Library for the dynamic creation of images by programmers (mingw-w64)'
url='https://www.libgd.org/'
arch=('any')
license=("CPL")
options=(!libtool strip staticlibs)
depends=("${MINGW_PACKAGE_PREFIX}-fontconfig"
         "${MINGW_PACKAGE_PREFIX}-libiconv"
         "${MINGW_PACKAGE_PREFIX}-libjpeg"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libtiff"
         "${MINGW_PACKAGE_PREFIX}-libvpx"
         "${MINGW_PACKAGE_PREFIX}-xpm-nox")
#optdepends=('perl: bdftogd script')
source=("https://bitbucket.org/libgd/gd-libgd/downloads/libgd-${pkgver}.tar.xz"
        "php-gd-newvpx.patch")
sha256sums=('9ada1ed45594abc998ebc942cef12b032fbad672e73efc22bc9ff54f5df2b285'
            '26409750a6d3deb7ced138daf5a37c514221f37281def372e542ffe3dde4c597')

prepare() {
  cd ${srcdir}/libgd-${pkgver}
  patch -p1 -i ${srcdir}/php-gd-newvpx.patch
}

build() {
  export lt_cv_deplibs_check_method='pass_all'
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  ../libgd-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-static \
    --enable-shared \
    LIBXPM_CFLAGS="-I${MINGW_PREFIX}/include/noX" \
    LIBXPM_LIBS="-L${MINGW_PREFIX}/lib -lXpm"

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="${pkgdir}" install

  install -Dm644 ${srcdir}/libgd-${pkgver}/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/libgd/LICENSE
}
