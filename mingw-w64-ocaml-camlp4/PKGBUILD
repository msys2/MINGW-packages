# Maintainer: Junjie Mao <eternal.n08@gmail.com>

_realname=ocaml-camlp4
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=4.02.1
pkgrel=1
pkgdesc='Pre-Processor-Pretty-Printer for OCaml'
groups=("${MINGW_PACKAGE_PREFIX}")
arch=('any')
url='https://github.com/ocaml/camlp4'
license=('LGPL2')
depends=("${MINGW_PACKAGE_PREFIX}-ocaml")
makedepends=("${MINGW_PACKAGE_PREFIX}-flexdll")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/ocaml/camlp4/archive/4.02.1.tar.gz"
	"0001-Properly-quote-preprocessor-command-to-ocamldep.patch"
	"0002-Add-DESTDIR-in-configure.patch")
sha1sums=('c16d9ae3693612c71e12d2880ec911772b8d23c3'
          'eb7a59d2e184f368d735670bea96571aa861f97b'
          '6aa01135f05a5e943412bf728f90ced871c6a064')

env=${MINGW_PREFIX#/}
_pkg="${_realname}-${pkgver}"
_pkgsrc="${env}-${_pkg}"
noextract=(${_pkg}.tar.gz)
prepare() {
  cd $startdir/
  [ -d ${srcdir}/${_pkgsrc} ] && rm -rf ${srcdir}/${_pkgsrc}
  tar -xzf ${startdir}/${_pkg}.tar.gz -C ${srcdir}
  cd ${srcdir}
  mv camlp4-${pkgver} ${_pkgsrc}
  cd ${_pkgsrc}

  patch -p1 -i ${srcdir}/0001-Properly-quote-preprocessor-command-to-ocamldep.patch
  patch -p1 -i ${srcdir}/0002-Add-DESTDIR-in-configure.patch
}

build() {
  cd ${srcdir}/${_pkgsrc}
  export OCAMLLIB=${MINGW_PREFIX}/lib/ocaml
  ./configure --bindir=${MINGW_PREFIX}/bin \
              --libdir=${MINGW_PREFIX}/lib/ocaml \
              --pkgdir=${MINGW_PREFIX}/lib/ocaml \
              --destdir=${pkgdir}
  make all -j1
}

check() {
  # The package does not include an automatic test process
  plain "skip"
}

package() {
  cd ${srcdir}/${_pkgsrc}
  make install -j1
  make install-META -j1
  [ -f LICENSE ] && install -Dm644 LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE

  mkdir -p ${pkgdir}/etc/profile.d
  echo '[ "$MSYSTEM" = "'$(echo ${env} | tr "[:lower:]" "[:upper:]")'" ] && export CAMLP4LIB='${MINGW_PREFIX}'/lib/ocaml' > ${pkgdir}/etc/profile.d/camlp4.${env}.sh
}
