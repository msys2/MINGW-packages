# Maintainer: Junjie Mao <eternal.n08@gmail.com>

_realname=ocaml-camlp4
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=4.02.1
pkgrel=5
pkgdesc="Pre-Processor-Pretty-Printer for OCaml (mingw-w64)"
arch=('any')
url='https://github.com/ocaml/camlp4'
license=('LGPL2')
depends=("${MINGW_PACKAGE_PREFIX}-ocaml")
makedepends=("${MINGW_PACKAGE_PREFIX}-flexdll")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/ocaml/camlp4/archive/${pkgver}.tar.gz"
        "0001-Properly-quote-preprocessor-command-to-ocamldep.patch"
        "0002-Add-DESTDIR-in-configure.patch"
        "0003-Guess-the-library-path-on-msys2.patch")
sha256sums=('70b8c80555a86b8d58e354421649900b9d80b727afdc8dbc94b7cb53b0dd9276'
            '5c384900589b505dd02c28b2dab8c7871ddc2cb3297dc85eaa0908043315e6b2'
            '511211c315f7ebd40578b5b90abb507aaa859951f8c66b5b4a47616148d8ce77'
            '08080dde32d4714bdf41349c597c1220468c609c15a81d4e75be828cded44fe2')

prepare() {
  cd ${srcdir}/camlp4-${pkgver}

  patch -p1 -i ${srcdir}/0001-Properly-quote-preprocessor-command-to-ocamldep.patch
  patch -p1 -i ${srcdir}/0002-Add-DESTDIR-in-configure.patch
  patch -p1 -i ${srcdir}/0003-Guess-the-library-path-on-msys2.patch
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  cp -rf camlp4-${pkgver} build-${MINGW_CHOST}

  cd ${srcdir}/build-${MINGW_CHOST}
  ./configure --bindir=${MINGW_PREFIX}/bin \
              --libdir=${MINGW_PREFIX}/lib/ocaml \
              --pkgdir=${MINGW_PREFIX}/lib/ocaml \
              --destdir=${pkgdir}
  make all -j1
}

check() {
  # The package does not include an automatic test process
  plain "skip"
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make install -j1
  make install-META -j1
  [ -f LICENSE ] && install -Dm644 LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
