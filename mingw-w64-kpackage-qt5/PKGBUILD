# Maintainer (MSYS2): Ray Donnelly <mingw.android@gmail.com>
# Contributor (MSYS2): Mateusz Miku≈Ça <mati865@gmail.com>

_variant=-${KF5_VARIANT:-shared}
source "$(dirname ${BASH_SOURCE[0]})"/../mingw-w64-PKGBUILD-common/kde-frameworks5
_kde_f5_init_package "${_variant}" "kpackage"
pkgver=5.26.0
pkgrel=1
arch=('any')
pkgdesc="Library to load and install packages of non binary files as they were a plugin (mingw-w64-qt5${_namesuff})"
license=('LGPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-extra-cmake-modules")
depends=()
_kde_f5_add_depends "${_variant}" "${MINGW_PACKAGE_PREFIX}-qt5${_namesuff}"
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${_basename}-${pkgver}.tar.xz")
sha256sums=('0fabacce5e7670ad2b80cc796d3c779e73805b0c85dbf4e533f61d139f4858d1')

prepare() {
  mkdir -p build-${CARCH}${_variant}

  # repalce old QStandardPaths functions with our new *Msys functions
  cd "${srcdir}"/${_basename}-${pkgver}
  find . -type f -name "*.cpp*" -print0 | xargs -0 sed -i -e "s/QStandardPaths::locateAll(/QStandardPaths::locateAllMsys(/g" \
                                                          -e "s/QStandardPaths::locate(/QStandardPaths::locateMsys(/g" \
                                                          -e "s/QStandardPaths::standardLocations(/QStandardPaths::standardLocationsMsys(/g" \
                                                          -e "s/QStandardPaths::writableLocation(/QStandardPaths::writableLocationMsys(/g"
}

build() {
  local -a extra_config
  cd build-${CARCH}${_variant}
  if [ "${_variant}" = "-static" ]; then
    extra_config+=( -DBUILD_SHARED_LIBS=NO )
    QT5_PREFIX=${MINGW_PREFIX}/qt5-static
    export PATH=${QT5_PREFIX}/bin:"$PATH"
  else
    QT5_PREFIX=${MINGW_PREFIX}
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake ../${_basename}-${pkgver} \
      -DCMAKE_BUILD_TYPE=$(_kde_f5_CMAKE_BUILD_TYPE) \
      -DCMAKE_INSTALL_PREFIX=${QT5_PREFIX} \
      -DKDE_INSTALL_LIBDIR=lib \
      -DBUILD_TESTING=OFF \
      -DECM_DIR=${MINGW_PREFIX}/share/ECM \
      "${extra_config[@]}" \
      -G'MSYS Makefiles'
  make
}

package() {
  cd build-${CARCH}${_variant}
  make DESTDIR="${pkgdir}" install
}
