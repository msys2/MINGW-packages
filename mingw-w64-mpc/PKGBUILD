# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=mpc
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.0.1
pkgrel=2
pkgdesc="Multiple precision complex arithmetic library (mingw-w64)"
groups=("${_mingw_suff}")
url='http://www.multiprecision.org'
license=('LGPL')
arch=('any')
depends=("${_mingw_suff}-mpfr")
makedepends=("${_mingw_suff}-gmp>=5.0" "${_mingw_suff}-mpfr")
options=('staticlibs' '!emptydirs')
source=("http://www.multiprecision.org/mpc/download/${_realname}-${pkgver}.tar.gz")
sha1sums=('8c7e19ad0dd9b3b5cc652273403423d6cf0c5edf')

build() {
	mkdir -p ${srcdir}/build-${MINGW_CHOST}
	cd "${srcdir}/build-${MINGW_CHOST}"
	../${_realname}-${pkgver}/configure --build=${MINGW_CHOST} \
		--prefix=${MINGW_PREFIX} \
		--enable-static \
		--disable-shared \
		--with-gmp=${MINGW_PREFIX} \
		--with-mpfr=${MINGW_PREFIX}
	make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
