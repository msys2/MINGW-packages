# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=ty
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.0.16
_ruffver=012ec6339058587319bec7d6505a085650138b8e
pkgrel=1
pkgdesc="An extremely fast Python type checker and language server, written in Rust (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://github.com/astral-sh/ty'
msys2_repository_url='https://github.com/astral-sh/ty'
msys2_documentation_url='https://docs.astral.sh/ty'
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-maturin"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             'git')
options=('!strip')
source=("git+${url}#tag=${pkgver}"
        "git+https://github.com/astral-sh/ruff#commit=${_ruffver}")
sha256sums=('e1dd43613976f5bad4a9f52f8812131d67b3e7708143d2d4d96cef888aee986a'
            'c77a032cc9c48b036156bbe69cd2483762cb9b3c96774dd7952ecdfd46a72f16')

prepare() {
  cd ${_realname}

  git submodule init
  git config submodule.ruff.url ../ruff
  git -c protocol.file.allow=always submodule update

  cargo fetch \
    --manifest-path ruff/crates/${_realname}/Cargo.toml \
    --locked \
    --target "${RUST_CHOST}" \
    --config='net.git-fetch-with-cli=true'
}

build() {
  local target_binary="ruff/target/${RUST_CHOST}/release/${_realname}"

  cd ${_realname}
  export _PYTHON_HOST_PLATFORM=$(python -c "import sysconfig, sys; sys.stdout.write(sysconfig.get_platform())")
  maturin build --locked --release --all-features --target "${RUST_CHOST}" --strip

  for completion in bash fish zsh; do
    $target_binary generate-shell-completion $completion > $completion-completions
  done
}

check() {
  cd ${_realname}

  cargo test \
    --manifest-path ruff/crates/${_realname}/Cargo.toml \
    --frozen \
    --release \
    --all-features
}

package() {
  cd ${_realname}

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" ruff/target/wheels/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSE
  install -Dm644 bash-completions "${pkgdir}${MINGW_PREFIX}"/share/bash-completion/completions/${_realname}
  install -Dm644 fish-completions "${pkgdir}${MINGW_PREFIX}"/share/fish/vendor_completions.d/${_realname}.fish
  install -Dm644 zsh-completions "${pkgdir}${MINGW_PREFIX}"/share/zsh/site-functions/_${_realname}
}
