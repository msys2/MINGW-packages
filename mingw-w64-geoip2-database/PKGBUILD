# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=geoip2-database
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=20180116
pkgrel=1
pkgdesc="GeoLite country geolocation database compiled by MaxMind (mingw-w64)"
arch=('any')
url="https://dev.maxmind.com/geoip/geoip2/geolite2/"
license=('custom:Creative Commons Attribution-ShareAlike 3.0 Unported')
source=(GeoLite2-City-${pkgver}.tar.gz::https://geolite.maxmind.com/download/geoip/database/GeoLite2-City.tar.gz
        GeoLite2-Country-${pkgver}.tar.gz::https://geolite.maxmind.com/download/geoip/database/GeoLite2-Country.tar.gz
        GeoLite2-ASN-${pkgver}.tar.gz::https://geolite.maxmind.com/download/geoip/database/GeoLite2-ASN.tar.gz)
noextract=(GeoLite2-City-${pkgver}.tar.gz
           GeoLite2-Country-${pkgver}.tar.gz
           GeoLite2-ASN-${pkgver}.tar.gz)
sha256sums=('025932f30ad6b5e77e1501cb953c87c77ca46eed5e22034e6cc3f206547db419'
            '788ec447ffbb408f484fbf43454141553882c214bd5b8d39accb82bcc3be9f27'
            'f717a9f562ff84bc129be75bb4fecd6fb1f66f384fd7202eca8872e83efa2afa')

prepare() {
  cd "${srcdir}"

  for _db in GeoLite2-{City,Country,ASN}; do
    tar --strip-components=1 -xf ${_db}-${pkgver}.tar.gz --wildcards "*/${_db}.mmdb"
  done

  tar --strip-components=1 -xf GeoLite2-ASN-${pkgver}.tar.gz --wildcards "*/COPYRIGHT.txt" "*/LICENSE.txt"
}

package() {
  cd "${srcdir}"

  install -d "${pkgdir}${MINGW_PREFIX}/share/GeoIP"
  install -m644 -t "${pkgdir}${MINGW_PREFIX}/share/GeoIP" GeoLite2-{City,Country,ASN}.mmdb

  install -Dm644 COPYRIGHT.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYRIGHT.txt"
  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.txt"
}
