# Maintainer: Aleksandr Palamar <void995@gmail.com>

_realname=gtk-engine-murrine
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.98.2
pkgrel=3
pkgdesc="GTK2 engine to make your desktop look like a 'murrina', an italian word meaning the art glass works done by Venicians glass blowers (mingw-w64)"
arch=('any')
url="http://cimitan.com/murrine/project/murrine"
license=('LGPL3')
depends=("${MINGW_PACKAGE_PREFIX}-gtk2")
makedepends=('pkg-config' 'intltool')
options=('staticlibs' 'strip')
source=(https://download.gnome.org/sources/murrine/0.98/murrine-${pkgver}.tar.xz)
sha256sums=('e9c68ae001b9130d0f9d1b311e8121a94e5c134b82553ba03971088e57d12c89')

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  ../murrine-${pkgver}/configure --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-animation \
    --enable-animationrtl
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
