# Maintainer: Andrew Sun <adsun701@gmail.com>

_realname=kvazaar
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.1.0
pkgrel=2
pkgdesc="An open-source HEVC encoder licensed under LGPLv2.1 (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="http://ultravideo.cs.tut.fi/#encoder"
license=("LGPL2.1")
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-yasm"
             "${MINGW_PACKAGE_PREFIX}-cc")
depends=("${MINGW_PACKAGE_PREFIX}-crypto++"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread-git")
source=("https://github.com/ultravideo/${_realname}/archive/v${pkgver}.tar.gz"
        "001-clang-pthread-fix.patch")
sha256sums=('bbdd3112182e5660a1c339e30677f871b6eac1e5b4ff1292ee1ae38ecbe11029'
            '953c0be5390b235c69e388e224c62b0d3f0e3a1b5ca3f97748c16fc3fbaa53b7')

prepare(){
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-clang-pthread-fix.patch

  autoreconf -fiv
}

build() {
  [[ -d build-${MSYSTEM} ]] && rm -rf build-${MSYSTEM}
  cp -r ${_realname}-${pkgver} build-${MSYSTEM} && cd build-${MSYSTEM}

  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-largefile \
    --with-cryptopp

  make
}

package() {
  cd build-${MSYSTEM}
  make DESTDIR=${pkgdir} install

  # fix .pc file
  sed -s "s|$(cygpath -m ${MINGW_PREFIX})|${MINGW_PREFIX}|g" -i "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/${_realname}.pc"

  install -Dm644 LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
