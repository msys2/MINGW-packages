# Maintainer: Andrew Sun <adsun701@gmail.com>

_realname=kvazaar
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.3.0
pkgrel=2
pkgdesc="An open-source HEVC encoder (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64' 'clang32')
msys2_repository_url="https://github.com/ultravideo/kvazaar"
url="https://ultravideo.fi/#encoder"
license=("spdx:BSD-3-Clause")
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc")
depends=("${MINGW_PACKAGE_PREFIX}-crypto++"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread-git")
source=("https://github.com/ultravideo/${_realname}/archive/v${pkgver}.tar.gz"
        "001-clang-pthread-fix.patch"
        "https://github.com/ultravideo/kvazaar/commit/f6a9d92a30083b5e228ca1126418e12c515bfa69.patch")
sha256sums=('6d88a9c92c06c275e33ff5df9ed6081f43277988b782298544caa76c20b2b601'
            '953c0be5390b235c69e388e224c62b0d3f0e3a1b5ca3f97748c16fc3fbaa53b7'
            'f227c39928fad5d34016babe4b017bbb943daa7fe214a673b823cdd9af72a1e3')

prepare(){
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-clang-pthread-fix.patch

  # https://github.com/ultravideo/kvazaar/issues/381
  patch -p1 -i ${srcdir}/f6a9d92a30083b5e228ca1126418e12c515bfa69.patch

  autoreconf -fiv
}

build() {
  [[ -d build-${MSYSTEM} ]] && rm -rf build-${MSYSTEM}
  cp -r ${_realname}-${pkgver} build-${MSYSTEM} && cd build-${MSYSTEM}

  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-largefile \
    --with-cryptopp

  make
}

package() {
  cd build-${MSYSTEM}
  make DESTDIR=${pkgdir} install

  # fix .pc file
  sed -s "s|$(cygpath -m ${MINGW_PREFIX})|${MINGW_PREFIX}|g" -i "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/${_realname}.pc"

  install -Dm644 LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
