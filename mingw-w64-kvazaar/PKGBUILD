# Maintainer: Andrew Sun <adsun701@gmail.com>

_realname=kvazaar
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.1.0
pkgrel=1
pkgdesc="An open-source HEVC encoder licensed under LGPLv2.1 (mingw-w64)"
arch=('any')
url="http://ultravideo.cs.tut.fi/#encoder"
license=("LGPL2.1")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-yasm")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-crypto++")
options=('!libtool')
source=("${_realname}-${pkgver}.tar.gz::https://github.com/ultravideo/${_realname}/archive/v${pkgver}.tar.gz"
        "0001-kvazaar-1.1.0-gcc7-fix.patch")
sha256sums=('8e382738a51004bfcfca4c832e8b41fe6a17f889f3c39151dc1c1a37261a3a6d'
            '9b77d529460bd2ed3be2c4afcd1c62f645c5dceeb2f55d316a4c78b2ee78a060')
prepare(){
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Np1 -i "${srcdir}/0001-kvazaar-1.1.0-gcc7-fix.patch"
  autoreconf -fiv
}

build() {
  [[ -d "build-${MINGW_CHOST}" ]] && rm -rf "build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static=yes \
    --enable-shared=yes \
    --enable-largefile \
    --with-cryptopp=${MINGW_PREFIX}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install
}
