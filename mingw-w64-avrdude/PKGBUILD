# Maintainer: David Grayson <davidegrayson@gmail.com>

_realname=avrdude
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=6.4
pkgrel=2
pkgdesc='Software for programming Atmel AVR Microcontrollers (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://www.nongnu.org/avrdude/"
license=('GPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools" "${MINGW_PACKAGE_PREFIX}-cc")
depends=("${MINGW_PACKAGE_PREFIX}-libftdi"
         "${MINGW_PACKAGE_PREFIX}-libusb"
         "${MINGW_PACKAGE_PREFIX}-libusb-compat"
         "${MINGW_PACKAGE_PREFIX}-libelf"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread-git")
options=('staticlibs' 'strip')
source=("https://download.savannah.gnu.org/releases/avrdude/avrdude-${pkgver}.tar.gz"{,.sig}
        '01-libtool.patch'
        '02-ddk-headers.patch'
        '03-handle-printing.patch'
        '04-no-giveio.patch')
sha256sums=('a9be7066f70a9dcf4bf0736fcf531db6a3250aed1a24cc643add27641b7110f9'
            'SKIP'
            '71ead9b3362a2f88e835c1fe42cacbcf485828bfe340c0c3ed92d3a2f37df9a1'
            '8b85af58ff9c7874646019c7938bb52eeaa1d3cc62379126cd54d2060cbdd9f2'
            '12881c2fa72195deb4d87e5d9ddc6ee5b1d2f1583c741f7a049c407c727c6120'
            'e9036afe7ee2a327615044b6b774f2df068b5f9f7b6dcf344abb262b33a5b37d')
validpgpkeys=('EF497ABE47ED91B3FC3D7EA54D902FF7723BDEE9'
              '5E84F980C3CAFD4BB5841070F48CA81B69A85873'
              '56628323218C669FF578705C7E9EADC3030D34EB')

prepare()
{
  cd "${srcdir}/${_realname}-${pkgver}"

  # Delete binaries.
  rm windows/giveio.sys

  # Let's use mingw-w64's hidsdi.h instead of AVRDUDE's.
  rm my_ddk_hidsdi.h

  # Fixes a warning from libtool.  However, a DLL still does not generated.
  patch -p1 -i ../01-libtool.patch

  # Look for headers like hidsdi.h and hidpi.h at the top level,
  # (do not assume they are inside the "ddk" directory).
  patch -p1 -i ../02-ddk-headers.patch

  # Proper printing of 64-bit HANDLE pointers.  The upstream code results
  # in a compiler warning on x86_64 and wouldn't print the whole pointer.
  patch -p1 -i ../03-handle-printing.patch

  # Don't install giveio.sys or the batch scripts associated with it since it is
  # a precompiled binary and unlikely to work on modern versions of Windows
  # anyway due to driver signing requirements.
  patch -p1 -i ../04-no-giveio.patch

  ./bootstrap
}

build() {
  [[ -d "${srcdir}"/build-${MSYSTEM} ]] && rm -rf "${srcdir}"/build-${MSYSTEM}
  mkdir -p "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

  CFLAGS+=" -I${MINGW_PREFIX}/include/libusb-compat"
  "../${_realname}-${pkgver}/configure" \
    --prefix="${MINGW_PREFIX}" \
    --build="${MINGW_CHOST}" \
    --host="${MINGW_CHOST}"

  make
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"
  make check
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install

  # Remove loaddrv.exe because we aren't distributing any drivers one could use
  # with it and driver signing requirements in modern versions of Windows mean
  # it is unlikely to work (it only claims to support Windows NT/2000/XP).
  # Also, with a generic name like loaddrv, it could cause a name collision.
  rm "${pkgdir}"${MINGW_PREFIX}/bin/loaddrv.exe

  # As we can see from confwin.c, AVRDUDE searches for avrdude.conf
  # using the PATH; it does not find it in /etc.
  mv "${pkgdir}"${MINGW_PREFIX}/etc/avrdude.conf "${pkgdir}"${MINGW_PREFIX}/bin

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
