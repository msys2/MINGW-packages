# Maintainer: Yusuke Sasaki <yusuke.sasaki.nuem@gmail.com>

_realname=msgpack-c
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.4.2
pkgrel=1
pkgdesc="MessagePack implementation for C and C++ (mingw-w64)"
arch=('any')
url="https://github.com/msgpack/msgpack-c"
license=('custom:Boost Software License 1.0')
makedepends=("${MINGW_PACKAGE_PREFIX}-gtest"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-zlib")
depends=()
source=("https://github.com/msgpack/msgpack-c/releases/download/cpp-$pkgver/msgpack-${pkgver}.tar.gz"
        "0010-detect-mingw.patch"
        "0020-runtime-output-directory.patch"
        "0030-fix-suffix.patch")
sha256sums=('c0f1da8462ea44b23f89573eff04a1f329bcff6fd80eb0d4b976d7f19caf1fa2'
            '14306459c014f18349bc5196ceea099cfd063329575c34efe5c2c5909b6d867e'
            '0ed894637e254c09966c8ccd6d1ec7ad79c2ca713c9f1cdc91b0e94efa21c72b'
            '70e43cce1e6bac50209634708a35f64bad46ddc271d89fd80626c2202e73f069')

prepare() {
  cd "msgpack-${pkgver}"
  patch -p1 -i "${srcdir}/0010-detect-mingw.patch"
  patch -p1 -i "${srcdir}/0020-runtime-output-directory.patch"
  patch -p1 -i "${srcdir}/0030-fix-suffix.patch"
}

build() {
  [ -d "${srcdir}/build-${MINGW_CHOST}" ] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  MSYS2_ARG_CONV_EXCL='-DCMAKE_INSTALL_PREFIX=' \
    ${MINGW_PREFIX}/bin/cmake \
      -G "MSYS Makefiles" \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DMSGPACK_CXX11=ON \
      -DMSGPACK_BUILD_EXAMPLES=OFF \
      ../msgpack-${pkgver}
  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make test
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}

# vim: set ft=sh ts=2 sw=2 et :
