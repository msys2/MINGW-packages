# Maintainer: Frederic Wang <fred.wang@free.fr>

_realname=ipaddress
pkgbase=mingw-w64-python2-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}")
pkgver=1.0.18
pkgrel=1
pkgdesc='Port of the 3.3+ ipaddress module to 2.6, 2.7, 3.2 (mingw-w64)'
url='https://github.com/phihag/ipaddress'
license=('Python Software Foundation License')
arch=('any')
makedepends=("${MINGW_PACKAGE_PREFIX}-python2")
_dtoken="4e/13/774faf38b445d0b3a844b65747175b2e0500164b7c28d78e34987a5bfe06"
source=("https://pypi.python.org/packages/${_dtoken}/${_realname}-${pkgver}.tar.gz")
sha256sums=('5d8534c8e185f2d8a1fda1ef73f2c8f4b23264e8e30063feeb9511d492a413e1')

prepare() {
  cd ${srcdir}
  cp -r ${_realname}-${pkgver} build-python2
}

build() {
  cd ${srcdir}/build-python2
  ${MINGW_PREFIX}/bin/python2 setup.py build
}

package() {
  cd ${srcdir}/build-python2
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}"
}
