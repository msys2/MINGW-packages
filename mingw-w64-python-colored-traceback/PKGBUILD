# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>

_realname=colored-traceback
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.3.0
pkgrel=4
pkgdesc='Automatically color Pythonâ€™s uncaught exception tracebacks (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/staticshock/colored-traceback.py"
msys2_references=(
  'pypi: colored-traceback'
)
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python-pygments")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools")
options=('!emptydirs')
source=("https://pypi.org/packages/source/${_realname::1}/$_realname/$_realname-$pkgver.tar.gz")
sha256sums=('6da7ce2b1da869f6bb54c927b415b95727c4bb6d9a84c4615ea77d9872911b05')

export PYTHONHASHSEED=0

prepare() {  
  cd "$srcdir"
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}"
}

build() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
}
