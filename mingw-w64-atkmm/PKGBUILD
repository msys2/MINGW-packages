# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=atkmm
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=2.22.7
pkgrel=2
pkgdesc="C++ bindings for atk (mingw-w64)"
arch=('any')
url="http://gtkmm.sourceforge.net"
license=("LGPL")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config")
depends=("${_mingw_suff}-gcc-libs"
		"${_mingw_suff}-libsigc++"
		"${_mingw_suff}-glibmm>=2.36.0"
		"${_mingw_suff}-atk"
		)
options=(strip staticlibs)
source=("http://ftp.gnome.org/pub/GNOME/sources/atkmm/${pkgver%.*}/atkmm-$pkgver.tar.xz")
md5sums=('fec7db3fc47ba2e0c95d130ec865a236')

build() {
	CPPFLAGS+=" -D_REENTRANT"
	mkdir -p "${srcdir}/build-${MINGW_CHOST}"
	cd "${srcdir}/build-${MINGW_CHOST}"
	../${_realname}-${pkgver}/configure \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--enable-shared \
		--enable-static \
		--disable-documentation
	make
}

package() {
	cd "${srcdir}/build-${MINGW_CHOST}"
	make DESTDIR="$pkgdir" install
	find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
}
