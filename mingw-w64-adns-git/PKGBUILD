# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=adns
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=46.92b7fed
pkgver=46.92b7fed
pkgrel=1
pkgdesc="adns is an asyncronous replacement resolver library (git) (mingw-w64)"
arch=('any')
url="http://www.chiark.greenend.org.uk/~ian/adns/"
license=('GPL LGPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "git")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
#Note that the original GIT http://www.chiark.greenend.org.uk/ucgi/~ianmdlvl/git/adns.git/
#does not have code for Win32.  GNUPG seems to be using their own version of this.
source=("${_realname}"::"git://git.gnupg.org/adns.git#branch=master"
        mingw-no-regress.patch
        mingw-mkdir.patch)
sha256sums=('SKIP'
            '56309cdcd39899124aa62f1231a3444886d4b460d520e390308c084749208249'
            'cd0384622dee28f9f034df0d2e67fc7e0e2b2aab636405b862df86baab1279e3')

pkgver() {
  cd "${srcdir}"/${_realname}
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${_realname}
  patch -p1 -i ${srcdir}/mingw-no-regress.patch
  patch -p1 -i ${srcdir}/mingw-mkdir.patch
  ./autogen.sh
}
build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-static \
    --enable-shared

  make
}

package() {
  cd "build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  install -Dm0755 ${srcdir}/${_realname}/LICENCE.WAIVERS "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENCE.WAIVERS
}
