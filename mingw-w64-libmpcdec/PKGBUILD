# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libmpcdec
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.2.6
pkgrel=2
pkgdesc="Musepack decoding library (mingw-w64)"
arch=("any")
url="http://musepack.net/"
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
source=("http://files.musepack.net/source/${_realname}-${pkgver}.tar.bz2")
md5sums=('7f7a060e83b4278acf4b77d7a7b9d2c0')

prepare() {
  cd ${_realname}-${pkgver}
  #patch -p1 -i ${srcdir}/1.1.6-byteorder-win32.patch
  #autoreconf -fvi
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --enable-static

  make
}

check() {
  cd build-${MINGW_CHOST}
  make -k check
}

package() {
  cd build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
