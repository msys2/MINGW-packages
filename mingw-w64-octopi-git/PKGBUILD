# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=octopi
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
#_qmake=${MINGW_PREFIX}/bin/qmake
_qmake=${MINGW_PREFIX}/qt5-static/bin/qmake
pkgver=r941.6df0f8a
pkgrel=2
pkgdesc="a powerful Pacman frontend using Qt libs"
arch=('any')
url="https://github.com/aarnt/octopi"
license=('GPL2')
makedepends=('git' "${MINGW_PACKAGE_PREFIX}-qt5-static" "${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
_gitroot="https://github.com/aarnt/octopi.git"
_gitname="octopi"
#options=('debug' '!strip')
source=("${_gitname}"::"git+${_gitroot}"
        "0001-Use-Q_PID-instead-of-int-reducing-QProcess-repititio.patch"
        "0002-Use-a-shared-QProcessEnvironment.patch"
        "0003-Win32-Link-to-Windows-Terminal-Services-wtsapi32.patch"
        "0004-Remove-redundancy-in-UnixCommand-s-exe-checking-func.patch"
        "0005-Use-instead-of-QDir-separator.patch"
        "0006-Append-to-instead-of-overwrite-m_readAllStandardOutp.patch"
        "0007-Define-QT_WA.patch"
        "0008-MSYS2-Add-ectn_MSYS2-as-a-LinuxDistro.patch"
        "0009-MSYS2-Add-getMSYS2Root.patch"
        "0010-MSYS2-hasPacmanDatabase-getMSYS2Root-changes.patch"
        "0011-MSYS2-Changes-to-various-system-calls.patch"
        "0012-MSYS2-Process-id-changes-WIP.patch"
        "0013-MSYS2-Prepend-getMSYS2Root-to-file-checks.patch"
        "0014-MSYS2-Support-discoverBinaryPath.patch")
sha256sums=('SKIP'
            'ffd65f2de0527278f8c872bd3d88e544cf0438a19952f9e8d0ddf88c4c35fbc5'
            '6e5a1729d2dacf312cc9503809a7ea6dd5d0d36f9576e1aef9aabcdacd98882d'
            'd3ba10164c7e721f437ec1ae74c6dc0793d4f307b6b56a53ee385f0d742cc435'
            'edec69051a513f312a75b1df96a017635d8b66fb1176c6c78e04d7ab3bbf42c8'
            'c86adfec659a3af90b175ad49d9505a35af58454d80e93785320dcfcbc83fa7f'
            '6c6db25d19d551f41e2a79ea19b88562a62867d6cf608d1438c2009de39cdbb6'
            '4e34d8d0380fdad6626a52396592fc2cf6cdc8f9d3e0e8608a0bc6e772195c65'
            '99a4246e38a96276a14f7da0bb79374c15f4ab1748028e992fdc64482112f1c3'
            '9e64d07f0f941c093b45f2c131ddae46893438d1645713bff96d315c725959e9'
            '7c38e05e5908faa09bb0840392e5ec0c98c02a8c1a0cb1199057f8ea3265030a'
            'b600c77761b6bda9a031b38de854e2603e199c0ee3e2189821390540f82d05b6'
            'b8c98d973733ed8b0497b1e68321c3b3b2e6af335b01ae10a7aae50653cee6f2'
            '01aae57819d08db29be9408594a99d8bf92768966fd7fb199b5220e5a9d19ebe'
            'c85c4f1710300f3793946ced1039667723411d5a8e97731399b040e1337d1ff3')

pkgver() {
    cd "${srcdir}/${_realname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_gitname}"

  git am "${srcdir}"/0001-Use-Q_PID-instead-of-int-reducing-QProcess-repititio.patch
  git am "${srcdir}"/0002-Use-a-shared-QProcessEnvironment.patch
  git am "${srcdir}"/0003-Win32-Link-to-Windows-Terminal-Services-wtsapi32.patch
  git am "${srcdir}"/0004-Remove-redundancy-in-UnixCommand-s-exe-checking-func.patch
  git am "${srcdir}"/0005-Use-instead-of-QDir-separator.patch
  git am "${srcdir}"/0006-Append-to-instead-of-overwrite-m_readAllStandardOutp.patch
  git am "${srcdir}"/0007-Define-QT_WA.patch
  git am "${srcdir}"/0008-MSYS2-Add-ectn_MSYS2-as-a-LinuxDistro.patch
  git am "${srcdir}"/0009-MSYS2-Add-getMSYS2Root.patch
  git am "${srcdir}"/0010-MSYS2-hasPacmanDatabase-getMSYS2Root-changes.patch
  git am "${srcdir}"/0011-MSYS2-Changes-to-various-system-calls.patch
  git am "${srcdir}"/0012-MSYS2-Process-id-changes-WIP.patch
  git am "${srcdir}"/0013-MSYS2-Prepend-getMSYS2Root-to-file-checks.patch
  git am "${srcdir}"/0014-MSYS2-Support-discoverBinaryPath.patch
}

build() {
  local _config
  cd ${srcdir}
  if check_option "debug" "n"; then
    _config="release"
  else
    _config="debug"
  fi

  rm -rf build-${MINGW_CHOST}
  mkdir build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  msg "Building Octopi..."
  ${_qmake} ../${_gitname}/${_gitname}.pro CONFIG+="c++11 ${_config}"
  make ${_config}

  mkdir -p notifier/pacmanhelper
  pushd notifier/pacmanhelper
  msg "Building pacmanhelper..."
  ${_qmake} ../../../${_gitname}/notifier/pacmanhelper/pacmanhelper.pro CONFIG+="${_config}"
  make ${_config}
  popd

  mkdir -p notifier/${_gitname}-notifier
  pushd notifier/${_gitname}-notifier
  msg "Building ${_gitname}-notifier..."
  ${_qmake} ../../../${_gitname}/notifier/${_gitname}-notifier/${_gitname}-notifier.pro CONFIG+="release"
  make ${_config}
  popd
}

package() {
  mkdir -p ${pkgdir}${MINGW_PREFIX}

  cd "${srcdir}/build-${MINGW_CHOST}"

  install -D -m755 bin/${_gitname}                                            ${pkgdir}${MINGW_PREFIX}/bin/${_gitname}
  install -D -m755 notifier/bin/${_gitname}-notifier                          ${pkgdir}${MINGW_PREFIX}/bin/${_gitname}-notifier
  install -D -m644 ${srcdir}/${_gitname}/resources/images/${_gitname}_red.png ${pkgdir}${MINGW_PREFIX}/share/icons/$}_gitname}.png

  #services
  install -D -m755 notifier/bin/pacmanhelper                                  ${pkgdir}${MINGW_PREFIX}/lib/octopi/pacmanhelper

  install -D -m644 ${srcdir}/${_gitname}/notifier/pacmanhelper/polkit/org.octopi.pacman.policy        ${pkgdir}${MINGW_PREFIX}/share/polkit-1/actions/org.octopi.pacman.policy
  install -D -m644 ${srcdir}/${_gitname}/notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.conf    ${pkgdir}${MINGW_PREFIX}/etc/dbus-1/system.d/org.octopi.pacmanhelper.conf
  install -D -m644 ${srcdir}/${_gitname}/notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.xml     ${pkgdir}${MINGW_PREFIX}/share/dbus-1/interfaces/org.octopi.pacmanhelper.xml
  install -D -m644 ${srcdir}/${_gitname}/notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.service ${pkgdir}${MINGW_PREFIX}/share/dbus-1/system-services/org.octopi.pacmanhelper.service
}
