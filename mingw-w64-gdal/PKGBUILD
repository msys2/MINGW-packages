# Maintainer: Alexey Kasatkin <alexeikasatkin@gmail.com>
# ArchLinux maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Petri Moisio <petri.k.moisio@gmail.com>

_realname=gdal
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.4.0
pkgrel=1
pkgdesc="A translator library for raster geospatial data formats (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://gdal.org/"
license=('custom')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-postgresql")
depends=("${MINGW_PACKAGE_PREFIX}-cfitsio"
         "${MINGW_PACKAGE_PREFIX}-crypto++"
         "${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-expat"
         "${MINGW_PACKAGE_PREFIX}-geos"
         "${MINGW_PACKAGE_PREFIX}-giflib"
         "${MINGW_PACKAGE_PREFIX}-hdf5"
         "${MINGW_PACKAGE_PREFIX}-jasper"
         "${MINGW_PACKAGE_PREFIX}-json-c"
         "${MINGW_PACKAGE_PREFIX}-libfreexl"
         "${MINGW_PACKAGE_PREFIX}-libgeotiff"
         "${MINGW_PACKAGE_PREFIX}-libheif"
         "${MINGW_PACKAGE_PREFIX}-libiconv"
         "${MINGW_PACKAGE_PREFIX}-libjpeg"
         "${MINGW_PACKAGE_PREFIX}-libkml"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libmariadbclient"
         "${MINGW_PACKAGE_PREFIX}-libspatialite"
         #"${MINGW_PACKAGE_PREFIX}-librasterlite2"
         "${MINGW_PACKAGE_PREFIX}-libtiff"
         "${MINGW_PACKAGE_PREFIX}-libwebp"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-netcdf"
         "${MINGW_PACKAGE_PREFIX}-openjpeg2"
         "${MINGW_PACKAGE_PREFIX}-pcre"
         "${MINGW_PACKAGE_PREFIX}-poppler"
         "${MINGW_PACKAGE_PREFIX}-postgresql"
         "${MINGW_PACKAGE_PREFIX}-proj"
         "${MINGW_PACKAGE_PREFIX}-sqlite3"
         "${MINGW_PACKAGE_PREFIX}-xerces-c"
         "${MINGW_PACKAGE_PREFIX}-xz")
options=('strip' 'staticlibs')
source=(https://download.osgeo.org/${_realname}/${pkgver}/${_realname}-${pkgver}.tar.xz
        001-dont-define-msc-ver.patch
        002-dont-define-msvcrt-version.patch
        003-fix-clang-visibility.patch
        004-fix-missing-declspec.patch
        005-fix-appslib-declspec.patch
        006-fix-compile-without-libtool.patch)
sha256sums=('ac7bd2bb9436f3fc38bc7309704672980f82d64b4d57627d27849259b8f71d5c'
            'd066e2b6cbf4fea2b81b94bb5ac43ca1aebd1ffb7dd4f8751104a9de86719562'
            '2eb8b8d7321f5a75cc7ff29bf63ae8bda1c900f463b6577bac109503346275f4'
            '792ded27c4acf6dcf96726e243403219c30ff0086ca2854a98fcad8a00823684'
            'dd45f5f23c80cdd453a8496c8a7b46cffc8193b5aee00bff534a61b2b592c20b'
            'b0d61629621aba838a16e7f7f851291ba8e1bd5df245aa326f7bb1dbb809076e'
            '4f427dc4f860d9c15ab38ba27971d917b4c477ee3e905e0a70eec88e54bd0423')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-dont-define-msc-ver.patch
  patch -p1 -i ${srcdir}/002-dont-define-msvcrt-version.patch
  patch -p1 -i ${srcdir}/003-fix-clang-visibility.patch
  patch -p1 -i ${srcdir}/004-fix-missing-declspec.patch
  patch -p1 -i ${srcdir}/005-fix-appslib-declspec.patch
  patch -p1 -i ${srcdir}/006-fix-compile-without-libtool.patch

  sed -i "s|/usr/local|${MINGW_PREFIX}|g" configure.ac
  sed -i "s|/usr|${MINGW_PREFIX}|g" configure.ac
  for p in m4/*.m4
  do
    sed -i "s|/usr|${MINGW_PREFIX}|g" $p
  done

  # for some reason autogen.sh's libtoolize call is commented out
  # but we need updated libtool for clang support
  libtoolize --force --copy
  ./autogen.sh
}

build() {
  cd ${srcdir}
  [[ -d build-${MSYSTEM} ]] && rm -rf build-${MSYSTEM}
  cp -r ${_realname}-${pkgver} build-${MSYSTEM}

  cd ${srcdir}/build-${MSYSTEM}
  CFLAGS+=" -fno-strict-aliasing"
  # somehow the configure test for redeclaring sprintf succeeds but
  # then errors during the build with clang.
  CPPFLAGS+=" -DDONT_DEPRECATE_SPRINTF"

  ./configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --with-poppler \
    --with-webp \
    --with-spatialite \
    --with-liblzma \
    --with-mysql=${MINGW_PREFIX}/bin/mariadb_config \
    --without-python \
    --without-perl \
    --without-libtool \
    --with-qhull=internal

  make
  make static-lib
}

package () {
  cd ${srcdir}/build-${MSYSTEM}
  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" install-man

  # install static lib
  install -Dm644 libgdal.a "${pkgdir}${MINGW_PREFIX}/lib/libgdal.a"

  # install license
  install -Dm644 LICENSE.TXT "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
