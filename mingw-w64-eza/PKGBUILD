# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=eza
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.19.2
pkgrel=1
pkgdesc="A modern, maintained replacement for ls (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64' 'mingw64')
url='https://eza.rocks'
msys2_repository_url='https://github.com/eza-community/eza'
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-openssl"
             'git')
optdepends=("${MINGW_PACKAGE_PREFIX}-ttf-font-nerd: for file icons")
options=('!strip' '!lto')
source=("${_realname}::git+${msys2_repository_url}.git#tag=v${pkgver}")
sha256sums=('c1a72e5efb5d9f4a79a0599f1086436831ebc7be448174c927f551bae44bead9')

_env() {
  #export LIBGIT2_NO_VENDOR=1
  export OPENSSL_NO_VENDOR=1
  export WINAPI_NO_BUNDLED_LIBRARIES=1
}

prepare() {
  cd "${_realname}"

  rm rust-toolchain.toml
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${_realname}"

  _env
  cargo build --release --frozen
}

check() {
  cd "${_realname}"

  _env
  cargo test --release --frozen
}

package() {
  cd "${_realname}"

  _env
  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENCE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENCE"

  install -Dm644 completions/bash/eza "${pkgdir}${MINGW_PREFIX}/share/bash-completion/completions/eza"
  install -Dm644 completions/zsh/_eza "${pkgdir}${MINGW_PREFIX}/share/zsh/site-functions/_eza"
  install -Dm644 completions/fish/eza.fish "${pkgdir}${MINGW_PREFIX}/share/fish/vendor_completions.d/eza.fish"
}
