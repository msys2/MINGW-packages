# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=eza
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.18.24
pkgrel=1
pkgdesc="A modern, maintained replacement for ls (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://eza.rocks'
msys2_repository_url='https://github.com/eza-community/eza'
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-openssl"
             'git')
optdepends=("${MINGW_PACKAGE_PREFIX}-ttf-font-nerd: for file icons")
options=('!strip' '!lto')
source=("${_realname}::git+${msys2_repository_url}.git#tag=v${pkgver}")
sha256sums=('3ff4215665d24c838573c9fe92c5908f4fd4d293fbdb2b1de34bae9e7944fb72')

_env() {
  #export LIBGIT2_NO_VENDOR=1
  export OPENSSL_NO_VENDOR=1
  export WINAPI_NO_BUNDLED_LIBRARIES=1
}

prepare() {
  cd "${_realname}"

  rm -f rust-toolchain.toml
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${_realname}"

  cargo build --release --frozen
}

check() {
  cd "${_realname}"

  cargo test --release --frozen
}

package() {
  cd "${_realname}"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENCE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENCE"

  cd completions
  install -Dm644 bash/eza "${pkgdir}${MINGW_PREFIX}/share/bash-completion/completions/eza"
  install -Dm644 zsh/_eza "${pkgdir}${MINGW_PREFIX}/share/zsh/site-functions/_eza"
  install -Dm644 fish/eza.fish "${pkgdir}${MINGW_PREFIX}/share/fish/vendor_completions.d/eza.fish"
}
