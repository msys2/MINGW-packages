# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=eza
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.20.16
pkgrel=1
pkgdesc="A modern, maintained replacement for ls (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64' 'mingw64')
url='https://eza.rocks'
msys2_repository_url='https://github.com/eza-community/eza'
msys2_references=(
  'purl: pkg:cargo/eza'
)
license=('spdx:EUPL-1.2')
depends=("${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-libgit2")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-openssl"
             'git')
optdepends=("${MINGW_PACKAGE_PREFIX}-ttf-font-nerd: for file icons")
options=('!strip')
source=("${_realname}::git+${msys2_repository_url}.git#tag=v${pkgver}"
        "https://github.com/eza-community/eza/commit/7241ea4d128ca47b8f5e756a65fa3d4f1d5b5ce7.patch")
sha256sums=('7f34210712b40a4e6815450a3518459cbf00dd0ea21f0abced7b6c870738ac9c'
            'ef387f0f9848a387bae1b412e2586bed164050a8bcdaac865b591a3b1691ffcd')

_env() {
  export LIBGIT2_NO_VENDOR=1
  export OPENSSL_NO_VENDOR=1
  export WINAPI_NO_BUNDLED_LIBRARIES=1
}

prepare() {
  cd "${_realname}"

  # revert git2 update to support libgit2 1.8.x
  patch -R -p1 -i ../7241ea4d128ca47b8f5e756a65fa3d4f1d5b5ce7.patch

  rm rust-toolchain.toml
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${_realname}"

  _env
  cargo build --release --frozen
}

check() {
  cd "${_realname}"

  _env
  cargo test --release --frozen
}

package() {
  cd "${_realname}"

  _env
  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.txt"

  install -Dm644 completions/bash/eza "${pkgdir}${MINGW_PREFIX}/share/bash-completion/completions/eza"
  install -Dm644 completions/zsh/_eza "${pkgdir}${MINGW_PREFIX}/share/zsh/site-functions/_eza"
  install -Dm644 completions/fish/eza.fish "${pkgdir}${MINGW_PREFIX}/share/fish/vendor_completions.d/eza.fish"
}
