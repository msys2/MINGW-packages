# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=mpg123
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.23.5
pkgrel=1
pkgdesc="A console based real time MPEG Audio Player for Layer 1, 2 and 3 (mingw-w64)"
arch=('any')
url="https://sourceforge.net/projects/mpg123"
license=("LGPL2.1")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-openal"
             "${MINGW_PACKAGE_PREFIX}-portaudio"
             "${MINGW_PACKAGE_PREFIX}-SDL")
depends=("${MINGW_PACKAGE_PREFIX}-libtool"
         "${MINGW_PACKAGE_PREFIX}-gcc-libs")
optdepends=("${MINGW_PACKAGE_PREFIX}-openal"
            "${MINGW_PACKAGE_PREFIX}-portaudio"
            "${MINGW_PACKAGE_PREFIX}-SDL")
options=('strip' 'staticlibs' 'libtool')
source=("https://downloads.sourceforge.net/${_realname}/${_realname}-${pkgver}.tar.bz2"{,.sig}
        0001-update-ac-and-silent-rules.mingw.patch
        0002-openal-library.patch)
sha256sums=('b658df672813511ff9b68a1553917220b675067204cdb58dbb59a2b8179350b2'
            'SKIP'
            '85544ff31689cc8c928c3a843b215984c9d67d798dbbe8aaceb2cd4c40e7499d'
            '1b7b0a7c6a85efac6e01748d437f0ac9e9a3971d453026df82dc1ca115257d1f')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}"/0001-update-ac-and-silent-rules.mingw.patch
  #patch -p1 -i "${srcdir}"/0002-openal-library.patch

  autoreconf -fi
}

build() {
  export lt_cv_deplibs_check_method='pass_all'
  [[ -d "build-${MINGW_CHOST}" ]] && rm -rf "build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --enable-static \
    --enable-silent-rules

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
