# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: AlexWMF <alexxwmf@gmail.com>

_realname=libcdio
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.93
pkgrel=1
pkgdesc="GNU Compact Disc Input and Control Library"
arch=('any')
license=('GPL3')
url="http://www.gnu.org/software/libcdio/"
options=('staticlibs' 'strip')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-libiconv"
         "${MINGW_PACKAGE_PREFIX}-libcddb")
source=(http://ftp.gnu.org/gnu/libcdio/${_realname}-${pkgver}.tar.gz{,.sig}
        libcdio-0.83-linking.patch)
md5sums=('d154476feaac5a7b5f180e83eaf3d689'
         'SKIP'
         '5a7f50209c03d5919d5b932f07871af7')

prepare() {
  cd ${_realname}-${pkgver}
  patch -Np1 -i "${srcdir}/libcdio-0.83-linking.patch"
  
  autoreconf -fi
}

build() {
  cd ${_realname}-${pkgver}
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-vcd-info \
    --enable-cpp-progs \
    --enable-shared \
    --enable-static
  make
}

package() {
  cd ${_realname}-${pkgver}
  make -j1 DESTDIR="${pkgdir}" install
  
  sed -i "/LIBCDIO_SOURCE_PATH/s|.*|/* #undef LIBCDIO_SOURCE_PATH */|" \
    ${pkgdir}${MINGW_PREFIX}/include/cdio/cdio_config.h
}
