# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=gitoxide
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.37.0
pkgrel=1
pkgdesc="An idiomatic, lean, fast & safe pure Rust implementation of Git (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64')
url="https://github.com/Byron/gitoxide"
license=('spdx:Apache-2.0 OR MIT')
msys2_references=(
  'archlinux: gitoxide'
)
depends=("${MINGW_PACKAGE_PREFIX}-sqlite3")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             'git')
source=("git+${url}.git#tag=v${pkgver}")
sha256sums=('bf840550e4ff5452707953087748c15b70034d8dda805e84584860f7846c5b84')

_env() {
  export WINAPI_NO_BUNDLED_LIBRARIES=1
  export LIBSQLITE3_SYS_USE_PKG_CONFIG=1
}

prepare() {
  cd "${_realname}"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${_realname}"

  _env
  cargo build \
    --release \
    --locked \
    --no-default-features \
    --features max-pure \
    --bin gix \
    --bin ein
}

check() {
  cd "${_realname}"

  _env
  cargo test \
    --release \
    --locked \
    --no-default-features \
    --features max-pure \
    --bin gix \
    --bin ein
}

package() {
  install -Dm755 "${_realname}"/target/release/{gix,ein} -t "${pkgdir}${MINGW_PREFIX}/bin/"

  install -Dm644 "${_realname}"/LICENSE-{APACHE,MIT} -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/"
}
