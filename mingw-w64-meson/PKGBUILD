# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=meson
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.41.1
pkgrel=2
pkgdesc="High-productivity build system (mingw-w64)"
arch=('any')
url="http://mesonbuild.com/"
license=("Apache 2")
options=('strip' 'staticlibs')
depends=("${MINGW_PACKAGE_PREFIX}-python3"
         "${MINGW_PACKAGE_PREFIX}-ninja")
source=(https://github.com/mesonbuild/${_realname}/releases/download/${pkgver}/${_realname}-${pkgver}.tar.gz
        'force-func-detect.patch'
        'no-script-suffix.patch'
        'gtkdoc-exec-perl.patch')
sha256sums=('3d160b0514ff3d25f0a47975c6f70fd82b76c589876d10413efc5e01df43e0c2'
            '91ff7975c3cae17536e983b35da17c5f7581ad744c4d846693a399052192faa4'
            'ffa0f8ed4df3ae73ab6cb42d49e4cb9148f095ad308276d4c18b65d6856467bb'
            '2bbea2b1e803473a56f5a0acd66a0051f76e5d836a7e7dbc33088bd352a9f450')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # https://github.com/mesonbuild/meson/issues/1083
  patch -Np1 -i "${srcdir}"/force-func-detect.patch
  patch -Np1 -i "${srcdir}"/no-script-suffix.patch
  patch -Np1 -i "${srcdir}"/gtkdoc-exec-perl.patch
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  PYTHON=${MINGW_PREFIX}/bin/python3 ${MINGW_PREFIX}/bin/python3 setup.py build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py \
    install --prefix=${MINGW_PREFIX} --root="${pkgdir}"

  PREFIX_WIN=$(cygpath -m ${MINGW_PREFIX})
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i "${pkgdir}${MINGW_PREFIX}/bin/meson"
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i "${pkgdir}${MINGW_PREFIX}/bin/mesonconf"
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i "${pkgdir}${MINGW_PREFIX}/bin/mesonintrospect"
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i "${pkgdir}${MINGW_PREFIX}/bin/mesontest"
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i "${pkgdir}${MINGW_PREFIX}/bin/wraptool"

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
