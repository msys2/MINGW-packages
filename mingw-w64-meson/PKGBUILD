# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=meson
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.43.0
pkgrel=1
pkgdesc="High-productivity build system (mingw-w64)"
arch=('any')
url="http://mesonbuild.com/"
license=("Apache 2")
options=('strip' 'staticlibs')
depends=("${MINGW_PACKAGE_PREFIX}-python3"
         "${MINGW_PACKAGE_PREFIX}-ninja")
source=(https://github.com/mesonbuild/${_realname}/releases/download/${pkgver}/${_realname}-${pkgver}.tar.gz
        'force-func-detect.patch'
        'no-script-suffix.patch'
        'color-term.patch'
        )
sha256sums=('c513eca90e0d70bf14cd1eaafea2fa91cf40a73326a7ff61f08a005048057340'
            '8c91268fad351862a6feb92f5d20c1be50d9948c14897edf2c60a771c4e52946'
            'ffa0f8ed4df3ae73ab6cb42d49e4cb9148f095ad308276d4c18b65d6856467bb'
            'eee384da0f3703ef64d088e9ad79be8a25566de1b0410b5e40f574e40cc41a65'
           )

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # https://github.com/mesonbuild/meson/issues/1083
  patch -Np1 -i "${srcdir}"/force-func-detect.patch
  patch -Np1 -i "${srcdir}"/no-script-suffix.patch
  patch -Np1 -i "${srcdir}"/color-term.patch
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  PYTHON=${MINGW_PREFIX}/bin/python3 ${MINGW_PREFIX}/bin/python3 setup.py build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py \
    install --prefix=${MINGW_PREFIX} --root="${pkgdir}"

  PREFIX_WIN=$(cygpath -m ${MINGW_PREFIX})
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i "${pkgdir}${MINGW_PREFIX}/bin/meson"
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i "${pkgdir}${MINGW_PREFIX}/bin/mesonconf"
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i "${pkgdir}${MINGW_PREFIX}/bin/mesonintrospect"
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i "${pkgdir}${MINGW_PREFIX}/bin/mesontest"
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i "${pkgdir}${MINGW_PREFIX}/bin/wraptool"

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
