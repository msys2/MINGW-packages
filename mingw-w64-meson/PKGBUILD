# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=meson
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.44.0
pkgrel=2
pkgdesc="High-productivity build system (mingw-w64)"
arch=('any')
url="http://mesonbuild.com/"
license=("Apache 2")
options=('strip' 'staticlibs')
depends=("${MINGW_PACKAGE_PREFIX}-python3"
         "${MINGW_PACKAGE_PREFIX}-ninja")
source=(https://github.com/mesonbuild/${_realname}/releases/download/${pkgver}/${_realname}-${pkgver}.tar.gz
        'force-func-detect.patch'
        'color-term.patch'
        '0001-Install-scripts-without-.py-extension.patch'
        '0002-Default-to-sys.prefix-as-the-default-prefix.patch'
        '0003-Strip-the-prefix-from-all-paths-when-installing-with.patch'
        )
sha256sums=('50f9b12b77272ef6ab064d26b7e06667f07fa9f931e6a20942bba2216ba4281b'
            '8c91268fad351862a6feb92f5d20c1be50d9948c14897edf2c60a771c4e52946'
            '1615db5a0f776be83c4b6c004320d0df1dcdb71a161da37f580a129a322009a6'
            '2c3fbb9d3f367d97e7262dc2991a2ef36eb06a7af08227b9e525049471d968c5'
            '4ef1e6fb0e9bd927b4646e416631b8a0a9aece763d259dbba58168bcba93f673'
            '963a5203f3428d41e4a43ac2a42b6c9d47dcc8f3ccccbd11414fdf5ac8608e40'
           )

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # https://github.com/mesonbuild/meson/issues/1083
  patch -Np1 -i "${srcdir}"/force-func-detect.patch
  patch -Np1 -i "${srcdir}"/color-term.patch

  patch -Np1 -i "${srcdir}"/0001-Install-scripts-without-.py-extension.patch
  patch -Np1 -i "${srcdir}"/0002-Default-to-sys.prefix-as-the-default-prefix.patch
  patch -Np1 -i "${srcdir}"/0003-Strip-the-prefix-from-all-paths-when-installing-with.patch
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python3 setup.py build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py \
    install --prefix=${MINGW_PREFIX} --root="${pkgdir}"

  PREFIX_WIN=$(cygpath -m ${MINGW_PREFIX})
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i "${pkgdir}${MINGW_PREFIX}/bin/meson"
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i "${pkgdir}${MINGW_PREFIX}/bin/mesonconf"
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i "${pkgdir}${MINGW_PREFIX}/bin/mesonintrospect"
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i "${pkgdir}${MINGW_PREFIX}/bin/mesontest"
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i "${pkgdir}${MINGW_PREFIX}/bin/wraptool"

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
