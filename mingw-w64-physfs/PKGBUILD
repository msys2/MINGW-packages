# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=physfs
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.0.3
pkgrel=2
pkgdesc="A library to provide abstract access to various archives (mingw-w64)"
arch=('any')
url="https://icculus.org/physfs/"
license=('ZLIB')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-cmake")
depends=("${MINGW_PACKAGE_PREFIX}-zlib")
options=('strip' 'staticlibs')
source=(https://icculus.org/physfs/downloads/${_realname}-${pkgver}.tar.bz2)
sha256sums=('ca862097c0fb451f2cacd286194d071289342c107b6fe69079c079883ff66b69')

build() {
  cd "${srcdir}/physfs-${pkgver}"
  # fix compiler error due to an unused variable
  sed -i "/lastDirectory/d" archivers/wad.c
  export CFLAGS+=" -fno-strict-aliasing"
  export CXXFLAGS+=" -fno-strict-aliasing"
  #Unstrict warnings are required as 64 bit compiler complains about an int->pointer cast (which should be safe)
  sed -i 's/-Werror//g' CMakeLists.txt

  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DPHYSFS_BUILD_TEST=OFF \
    -DPHYSFS_BUILD_WX_TEST=OFF \
    ../${_realname}-${pkgver}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install
  cd "${srcdir}/physfs-${pkgver}"
  install -D -m644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
