# Contributor: kreijstal <kreijstal@hotmail.com>

_realname=pyaes
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.6.1
pkgrel=1
pkgdesc='A pure-Python implementation of the AES block cipher algorithm and the common modes of operation (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/ricmoo/pyaes'
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=(
  git
  "${MINGW_PACKAGE_PREFIX}-python-setuptools"
  "${MINGW_PACKAGE_PREFIX}-python-wheel"
  "${MINGW_PACKAGE_PREFIX}-python-installer"
  "${MINGW_PACKAGE_PREFIX}-python-build"
)
options=('!strip')
checkdepends=(
  "${MINGW_PACKAGE_PREFIX}-python-pycryptodome" 
)
source=("https://github.com/ricmoo/pyaes/archive/v${pkgver}.tar.gz")
sha256sums=('d8be4e9e446861308ef028f9c9655686e13e0f4027d37f95443e4f32996394ac')

build() {
  cd "${srcdir}/pyaes-${pkgver}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --no-isolation --skip-dependency-check 
}

check() {
  cd "${srcdir}/pyaes-${pkgver}"

  PYTHONPATH=$(cygpath -am .):$PYTHONPATH ${MINGW_PREFIX}/bin/python tests/test-aes.py
  PYTHONPATH=$(cygpath -am .):$PYTHONPATH ${MINGW_PREFIX}/bin/python tests/test-blockfeeder.py
  PYTHONPATH=$(cygpath -am .):$PYTHONPATH ${MINGW_PREFIX}/bin/python tests/test-util.py
}

package() {
  cd "${srcdir}/pyaes-${pkgver}"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${pkgname}/LICENSE"
}
