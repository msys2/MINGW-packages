# Modified from https://gitlab.archlinux.org/archlinux/packaging/packages/lf/-/blob/6ffd2d8ad375a154f184b11c3d6ac8e46162c3b9/PKGBUILD
# Contributor: Suyun114
# Contributor: Dirk Stolle

_realname=lf
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=39
pkgrel=1
pkgdesc='A terminal file manager inspired by ranger'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/gokcehan/lf'
msys2_changelog_url='https://github.com/gokcehan/lf/blob/master/CHANGELOG.md'
msys2_references=(
  'anitya: 269016'
  'archlinux: lf'
  'purl: pkg:golang/github.com/gokcehan/lf'
)
license=('MIT')
makedepends=('git' "${MINGW_PACKAGE_PREFIX}-go" "${MINGW_PACKAGE_PREFIX}-cc")
source=("$pkgname::git+$url#tag=r$pkgver")
sha512sums=('d1a0daa80d24ce40b2f3b1002ebe58f05dd76c46bb8d49b2257c7d5090f95dd6262583bc11a2fc4f480d1a0bf07fd5a61672ccba111416d6b090a116f5c29da4')
b2sums=('116bd1fbabcd0ec406d320cb5bd1e198c6b8fc354759f7f4cb0e5e9a48b7add61aa923ed2b4bc8dc879db99b174918f62737df7b1c7a23a9d468051c063bffcc')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^r//'
}

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CC=cc
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X main.gVersion=$pkgver" \
    -o build \
    .
}

check() {
  cd "$pkgname"

  go test -v ./...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/${MINGW_PREFIX}/bin" build/lf

  # man page
  install -vDm644 -t "$pkgdir/${MINGW_PREFIX}/share/man/man1" lf.1

  # documentation
  install -vDm644 -t "$pkgdir/${MINGW_PREFIX}/share/doc/$pkgname" README.md etc/lfrc.example

  # desktop file
  install -vDm644 -t "$pkgdir/${MINGW_PREFIX}/share/applications" lf.desktop

  # vim integration
  install -vDm644 -t "$pkgdir/${MINGW_PREFIX}/share/vim/vimfiles/syntax" etc/lf.vim
  install -vDm644 -t "$pkgdir/${MINGW_PREFIX}/share/vim/vimfiles/ftdetect" etc/lf.vim

  # shell integrations
  install -vDm644 -t "$pkgdir/${MINGW_PREFIX}/etc/profile.d" etc/*.{sh,csh}
  install -vDm644 -t "$pkgdir/${MINGW_PREFIX}/share/fish/vendor_functions.d" etc/lfcd.fish

  # shell completions
  install -vDm644 etc/lf.bash "$pkgdir/${MINGW_PREFIX}/share/bash-completion/completions/lf"
  install -vDm644 etc/lf.fish "$pkgdir/${MINGW_PREFIX}/share/fish/vendor_completions.d/lf.fish"
  install -vDm644 etc/lf.zsh "$pkgdir/${MINGW_PREFIX}/share/zsh/site-functions/_lf"

  # license
  install -vDm644 -t "$pkgdir/${MINGW_PREFIX}/share/licenses/$pkgname" LICENSE
}
