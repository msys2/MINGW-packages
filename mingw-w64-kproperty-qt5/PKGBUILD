# Maintainer (MSYS2): Ilya Lesnov <gnoksel@gmail.com>
# Based on ArchLinux PKGBUILD

_variant=-${KF5_VARIANT:-shared}
pkgname=kproperty
pkgver=3.2.0
pkgrel=1
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'archlinux: kproperty'
)
pkgdesc='A property editing framework with editor widget similar to what is known from Qt Designer (mingw-w64)'
url='https://apps.kde.org/kexi-3.3/'
license=('GPL2')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-kconfig-qt5"
         "${MINGW_PACKAGE_PREFIX}-kcoreaddons-qt5"
         "${MINGW_PACKAGE_PREFIX}-kguiaddons-qt5"
         "${MINGW_PACKAGE_PREFIX}-kwidgetsaddons-qt5"
         "${MINGW_PACKAGE_PREFIX}-qt5-base")
makedepends=("${MINGW_PACKAGE_PREFIX}-extra-cmake-modules"
             "${MINGW_PACKAGE_PREFIX}-qt5-tools"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cc")
groups=("${MINGW_PACKAGE_PREFIX}-kde-applications")
source=("https://download.kde.org/stable/$pkgname/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('67af0c2d74715957bd5373a6a30589ff0a996cb1d267dfd0538dccaa9a768dfa'
            'SKIP')
validpgpkeys=(4866BAF713B465677A4059643C7C0E201B6524DB) # Jaros≈Çaw Staniek <staniek@kde.org>

prepare() {
  cd "$pkgname-$pkgver"
  sed -e '/SetKPropertyCMakePolicies/d' -i CMakeLists.txt
}

build() {
  local -a extra_config
  
  if [ "${_variant}" = "-static" ]; then
    extra_config+=( -DBUILD_SHARED_LIBS=NO )
  fi

  mkdir -p "build-${MSYSTEM}${_variant}" && cd "build-${MSYSTEM}${_variant}"

  ${MINGW_PREFIX}/bin/cmake.exe \
    -G 'Unix Makefiles' \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DBUILD_TESTING=OFF \
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
    "${extra_config[@]}" \
    ../${pkgname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake.exe --build .
}

package() {
  cd "build-${MSYSTEM}${_variant}"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install .
}