
_realname=av
pkgbase=mingw-w64-python-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-${_realname}
pkgver=14.3.0
pkgrel=1
pkgdesc='Pythonic bindings for FFmpeg (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://pyav.basswood-io.com'
license=('spdx:BSD-3-Clause')
msys2_repository_url="https://github.com/PyAV-Org/PyAV"
msys2_references=(
  'aur: python-av'
)
depends=(
    ${MINGW_PACKAGE_PREFIX}-ffmpeg
    ${MINGW_PACKAGE_PREFIX}-python
    ${MINGW_PACKAGE_PREFIX}-python-pillow
    ${MINGW_PACKAGE_PREFIX}-python-numpy)
makedepends=("${MINGW_PACKAGE_PREFIX}-cython"
             "${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-pip"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python-virtualenv")
options=('!strip')
source=("https://github.com/PyAV-Org/PyAV/archive/refs/tags/v${pkgver}.tar.gz"
        "001-makefile-vars.patch")
sha256sums=('9b281e52835a8c19e60a77ad94c6181b05db68d5eb5f66ee9f8222a24c01a4c3'
            'dd0d5871fa65d185c690e3e6bc2629d4f31a5e93e653fce4655d932b6a3f7f70')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

prepare() {
  cd "${srcdir}"/PyAV-${pkgver}

  apply_patch_with_msg \
    001-makefile-vars.patch

}

build() {
  cp -r "PyAV-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  PYAV_PYTHON=python \
  source scripts/activate.sh

  python.exe -m pip install --upgrade pip setuptools cython

  make

  #${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
