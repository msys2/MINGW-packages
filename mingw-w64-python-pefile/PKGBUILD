# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=pefile
pkgbase=mingw-w64-python-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-${_realname}
pkgver=2021.9.3
pkgrel=1
pkgdesc='A Python module to read and work with PE (Portable Executable) files (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=('MIT')
url='https://github.com/erocarrera/pefile'
depends=(
    ${MINGW_PACKAGE_PREFIX}-python
    ${MINGW_PACKAGE_PREFIX}-python-future
    ${MINGW_PACKAGE_PREFIX}-python-pygobject-devel)
makedepends=(
    ${MINGW_PACKAGE_PREFIX}-python-setuptools)
source=("https://github.com/erocarrera/${_realname}/archive/v${pkgver}.tar.gz")
sha256sums=('a92c1ca5dd7155b0d580cc3a832690eca99f3859e76ca16a14525f5362305ace')

prepare() {
  cd "${srcdir}"
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}"
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
