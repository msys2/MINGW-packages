# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gcc
_mingw_suff=mingw-w64-${CARCH}
pkgname=("${_mingw_suff}-${_realname}" "${_mingw_suff}-${_realname}-ada" "${_mingw_suff}-${_realname}-fortran" "${_mingw_suff}-${_realname}-objc" ) #"${_mingw_suff}-${_realname}-libs"
pkgver=4.8.2
pkgrel=1
pkgdesc="GCC for the MinGW-w64"
arch=('any')
url="http://gcc.gnu.org"
license=('GPL' 'LGPL' 'FDL' 'custom')
groups=("${_mingw_suff}-toolchain" "${_mingw_suff}")
#depends=("${_mingw_suff}-zlib" "${_mingw_suff}-mpc" "${_mingw_suff}-cloog" "${_mingw_suff}-crt" "${_mingw_suff}-binutils" "${_mingw_suff}-winpthreads" "${_mingw_suff}-headers")
makedepends=("${_mingw_suff}-${_realname}-ada=${pkgver}" "${_mingw_suff}-${_realname}" 'lndir')
#checkdepends=('dejagnu')
optdepends=()
provides=("${_mingw_suff}-${_realname}-base")
conflicts=("${_mingw_suff}-${_realname}-base")
replaces=()
backup=()
options=('strip' 'staticlibs' '!emptydirs') # '!buildflags'
source=("ftp://gcc.gnu.org/pub/gcc/releases/${_realname}-${pkgver}/${_realname}-${pkgver}.tar.bz2"
        'gcc-make-xmmintrin-header-cplusplus-compatible.patch'
		'gcc-4.8-filename-output.patch'
		'gcc-4.8-filename-output.patch'
		'gcc-4.8-lambda-ICE.patch'
		'gcc-4.8-iconv.patch'
		'4.8-libgcc-references.patch'
		'gcc-4.8-libstdc++export.patch'
		'gcc-4.7-stdthreads.patch')
md5sums=('a3d7d63b9cb6b6ea049469a0c4a43c9d'
         'da6c9ba6baebe1286f3219d4181cdbb8'
         '40cb437805e2f7a006aa0d0c3098ab0f'
         '40cb437805e2f7a006aa0d0c3098ab0f'
         '6eb6e080dbf7bc6825f53a0aaa6c4ef9'
         'd28f5653282c67bcfd1a8f81aebef72d'
         '928e151b33981e68d0e67a9bd387b37e'
         'f6b5fb08ff06a7d60ee3280e14240bb2'
         '71cdb550528efcf2a5c9112464bfbabc')

_threads="posix"

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  #do not install libiberty
  sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in
  # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
  #sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure
  
  # The file xmmintrin.h doesn't contain an extern "C" part
  # This conflicts with mingw-w64 intrin.h and results in build
  # failure like this one in mingw-w64-qt5-qtbase:
  # /usr/lib/gcc/i686-w64-mingw32/4.8.0/include/xmmintrin.h:997:1: error: previous declaration of 'int _m_pextrw(__m64, int)' with 'C++' linkage
  # /usr/i686-w64-mingw32/include/intrin.h:561:28: error: conflicts with new declaration with 'C' linkage
  #patch -p0 -i ${srcdir}/gcc-make-xmmintrin-header-cplusplus-compatible.patch
  
  patch -p0 -i ${srcdir}/gcc-4.8-filename-output.patch
  patch -p1 -i ${srcdir}/gcc-4.8-lambda-ICE.patch
  patch -p1 -i ${srcdir}/gcc-4.8-iconv.patch
  
  patch -p0 -i ${srcdir}/4.8-libgcc-references.patch
  patch -p1 -i ${srcdir}/gcc-4.8-libstdc++export.patch
  patch -p1 -i ${srcdir}/gcc-4.7-stdthreads.patch

}

hack_apply() {
  hack_cleanup
  mkdir -p ${destdir}${MINGW_PREFIX}/mingw
  lndir ${MINGW_PREFIX}/${MINGW_CHOST} ${destdir}${MINGW_PREFIX}/mingw
  #Also put into <SYSROOT>/mingw binutils, libiconv and zlib stuff
  cp ${MINGW_PREFIX}/include/{ansidecl,bfd,bfdlink,dis-asm,symcat,iconv,zconf,zlib}.h ${destdir}${MINGW_PREFIX}/mingw/include/
  cp ${MINGW_PREFIX}/lib/{libbfd,libopcodes,libz,libiconv}.a ${destdir}${MINGW_PREFIX}/mingw/lib/
}

hack_cleanup() {
 if [ -d ${destdir}${MINGW_PREFIX}/mingw ];
 then
	rm -rf ${destdir}${MINGW_PREFIX}/mingw
 fi
}

build() {
    mkdir -p ${srcdir}/gcc-build-${MINGW_CHOST} && cd ${srcdir}/gcc-build-${MINGW_CHOST}
    
	case "${CARCH}" in
		i686)
			local _conf="--disable-sjlj-exceptions --with-dwarf2"
			LDFLAGS+=" -Wl,--large-address-aware"
			local _arch=i686
		;;
		
		x86_64)
			local _arch=x86-64
			local _conf=""
		;;
	esac

	../${_realname}-${pkgver}/configure \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
        --target=${MINGW_CHOST} \
		--with-sysroot=${destdir}${MINGW_PREFIX} \
		--libexecdir=${MINGW_PREFIX}/lib \
		--enable-bootstrap \
		--with-arch=${_arch} \
		--with-tune=generic \
        --enable-languages=c,lto,c++,objc,obj-c++,fortran,ada \
        --enable-shared --enable-static \
        --enable-threads=${_threads} \
		--enable-graphite \
		--enable-fully-dynamic-string \
		--enable-libstdcxx-time=yes \
		--disable-libstdcxx-pch \
		--disable-libstdcxx-debug \
		--enable-cloog-backend=isl \
		--enable-version-specific-runtime-libs \
        --disable-cloog-version-check --disable-isl-version-check \
        --enable-lto \
		--enable-libgomp \
        --disable-multilib \
		--enable-checking=release \
		--disable-rpath \
		--disable-win32-registry \
		--disable-nls \
		--disable-werror \
		--disable-symvers \
		--with-libiconv \
		--with-system-zlib \
		--with-{gmp,mpfr,mpc,isl,cloog}="${MINGW_PREFIX}" \
		--with-pkgversion="Rev${pkgrel}, Built by MSYS2 project" \
		--with-bugurl="http://sourceforge.net/projects/msys2" \
		--with-gnu-as --with-gnu-ld \
		${_conf}

	hack_apply
    make all
	hack_cleanup
}

setup() {
	hack_apply
	cd ${srcdir}/gcc-build-${MINGW_CHOST}
    make -j1 DESTDIR=${destdir} install
	hack_cleanup
	
	mv ${destdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/adalib/*.dll ${destdir}${MINGW_PREFIX}/bin/
	mv ${destdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/lib/libgcc_s.a ${destdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/
}

package_mingw-w64-gcc() {
	pkgdesc="GNU Compiler Collection (C,C++,OpenMP) for MinGW-w64"
	depends=("${_mingw_suff}-crt" "${_mingw_suff}-binutils" "${_mingw_suff}-winpthreads" "${_mingw_suff}-headers") #"${_mingw_suff}-${_realname}-libs=${pkgver}" 
	
	mkdir -p ${pkgdir}${MINGW_PREFIX}/{bin,lib,share}
	
	cd ${destdir}${MINGW_PREFIX}
	cp bin/cpp.exe ${pkgdir}${MINGW_PREFIX}/bin/
	cp bin/gcc.exe ${pkgdir}${MINGW_PREFIX}/bin/
	cp bin/gcc-ar.exe ${pkgdir}${MINGW_PREFIX}/bin/
	cp bin/gcc-nm.exe ${pkgdir}${MINGW_PREFIX}/bin/
	cp bin/gcc-ranlib.exe ${pkgdir}${MINGW_PREFIX}/bin/
	cp bin/gcov.exe ${pkgdir}${MINGW_PREFIX}/bin/
	cp bin/c++.exe ${pkgdir}${MINGW_PREFIX}/bin/
    cp bin/g++.exe ${pkgdir}${MINGW_PREFIX}/bin/
    cp bin/${MINGW_CHOST}-c++.exe ${pkgdir}${MINGW_PREFIX}/bin/
    cp bin/${MINGW_CHOST}-g++.exe ${pkgdir}${MINGW_PREFIX}/bin/
    cp bin/${MINGW_CHOST}-gcc-${pkgver}.exe ${pkgdir}${MINGW_PREFIX}/bin/
    cp bin/${MINGW_CHOST}-gcc.exe ${pkgdir}${MINGW_PREFIX}/bin/
    cp bin/${MINGW_CHOST}-gcc-ar.exe ${pkgdir}${MINGW_PREFIX}/bin/
    cp bin/${MINGW_CHOST}-gcc-nm.exe ${pkgdir}${MINGW_PREFIX}/bin/
    cp bin/${MINGW_CHOST}-gcc-ranlib.exe ${pkgdir}${MINGW_PREFIX}/bin/
	
	cp bin/{libgcc*,libgomp*,libquadmath*,libssp*,libstdc*}.dll ${pkgdir}${MINGW_PREFIX}/bin/
	mkdir -p ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/include
    cp lib/gcc/${MINGW_CHOST}/${pkgver}/include/*.h ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/include/
    cp -r lib/gcc/${MINGW_CHOST}/${pkgver}/include/ssp ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/include/
    cp -r lib/gcc/${MINGW_CHOST}/${pkgver}/include-fixed ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/
    cp -r lib/gcc/${MINGW_CHOST}/${pkgver}/install-tools ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/
    cp lib/gcc/${MINGW_CHOST}/${pkgver}/cc1.exe ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/
    cp lib/gcc/${MINGW_CHOST}/${pkgver}/collect2.exe ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/
    cp lib/gcc/${MINGW_CHOST}/${pkgver}/crt*.o ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/
    cp lib/gcc/${MINGW_CHOST}/${pkgver}/liblto* ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/
    cp lib/gcc/${MINGW_CHOST}/${pkgver}/libgcc* ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/
    cp lib/gcc/${MINGW_CHOST}/${pkgver}/libgcov.* ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/
    cp lib/gcc/${MINGW_CHOST}/${pkgver}/libgomp.* ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/
    cp lib/gcc/${MINGW_CHOST}/${pkgver}/libquadmath.* ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/
    cp lib/gcc/${MINGW_CHOST}/${pkgver}/libssp* ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/
    cp lib/gcc/${MINGW_CHOST}/${pkgver}/lto*.exe ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/
    
	#mkdir -p ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/lib
	#cp lib/gcc/${MINGW_CHOST}/lib/libgcc*.a ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/lib/
	
	cp -r lib/gcc/${MINGW_CHOST}/${pkgver}/include/c++ ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/include/
    cp lib/gcc/${MINGW_CHOST}/${pkgver}/cc1plus.exe ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/
    cp lib/gcc/${MINGW_CHOST}/${pkgver}/libstdc++* ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/
    cp lib/gcc/${MINGW_CHOST}/${pkgver}/libsupc++* ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/
	
	mkdir -p ${pkgdir}${MINGW_PREFIX}/share/{doc,info,locale,man}
	#cp -r share/doc/gcc-${pkgver} ${pkgdir}${MINGW_PREFIX}/share/doc/
    cp share/info/cpp.info* ${pkgdir}${MINGW_PREFIX}/share/info/
    cp share/info/cppinternals.info* ${pkgdir}${MINGW_PREFIX}/share/info/
    cp share/info/gcc.info* ${pkgdir}${MINGW_PREFIX}/share/info/
    cp share/info/gccinstall.info* ${pkgdir}${MINGW_PREFIX}/share/info/
    cp share/info/gccint.info* ${pkgdir}${MINGW_PREFIX}/share/info/
    cp share/info/libgomp.info* ${pkgdir}${MINGW_PREFIX}/share/info/
    cp share/info/libquadmath.info* ${pkgdir}${MINGW_PREFIX}/share/info/
	
    #cp share/locale/* ${pkgdir}${MINGW_PREFIX}/share/locale/
	mkdir -p ${pkgdir}${MINGW_PREFIX}/share/gcc-${pkgver}/python
	cp -r share/gcc-${pkgver}/python/libstdcxx  ${pkgdir}${MINGW_PREFIX}/share/gcc-${pkgver}/python/
	mkdir -p ${pkgdir}${MINGW_PREFIX}/share/man/man1
    cp share/man/man1/cpp.1* ${pkgdir}${MINGW_PREFIX}/share/man/man1/
    cp share/man/man1/gcc.1* ${pkgdir}${MINGW_PREFIX}/share/man/man1/
    cp share/man/man1/gcov.1* ${pkgdir}${MINGW_PREFIX}/share/man/man1/
    cp share/man/man7/fsf-funding.7* ${pkgdir}${MINGW_PREFIX}/share/man/man1/
    cp share/man/man7/gfdl.7* ${pkgdir}${MINGW_PREFIX}/share/man/man1/
    cp share/man/man7/gpl.7* ${pkgdir}${MINGW_PREFIX}/share/man/man1/
	cp share/man/man1/g++.1* ${pkgdir}${MINGW_PREFIX}/share/man/man1/
}

package_mingw-w64-gcc-ada() {
	pkgdesc="GNU Compiler Collection (Ada) for MinGW-w64"
	depends=("${_mingw_suff}-${_realname}=${pkgver}")

	mkdir -p ${pkgdir}${MINGW_PREFIX}/{bin,lib,share}
	
	cd ${destdir}${MINGW_PREFIX}
    cp bin/gnat.exe ${pkgdir}${MINGW_PREFIX}/bin/
    cp bin/gnatbind.exe ${pkgdir}${MINGW_PREFIX}/bin/
    cp bin/gnatchop.exe ${pkgdir}${MINGW_PREFIX}/bin/
    cp bin/gnatclean.exe ${pkgdir}${MINGW_PREFIX}/bin/
    cp bin/gnatfind.exe ${pkgdir}${MINGW_PREFIX}/bin/
    cp bin/gnatkr.exe ${pkgdir}${MINGW_PREFIX}/bin/
    cp bin/gnatlink.exe ${pkgdir}${MINGW_PREFIX}/bin/
    cp bin/gnatls.exe ${pkgdir}${MINGW_PREFIX}/bin/
    cp bin/gnatmake.exe ${pkgdir}${MINGW_PREFIX}/bin/
    cp bin/gnatname.exe ${pkgdir}${MINGW_PREFIX}/bin/
    cp bin/gnatprep.exe ${pkgdir}${MINGW_PREFIX}/bin/
    cp bin/gnatxref.exe ${pkgdir}${MINGW_PREFIX}/bin/
	
	cp bin/{libgnarl*,libgnat*}.dll ${pkgdir}${MINGW_PREFIX}/bin/
	
	mkdir -p ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}
    cp -r lib/gcc/${MINGW_CHOST}/${pkgver}/adainclude ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/
    cp -r lib/gcc/${MINGW_CHOST}/${pkgver}/adalib ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/
    cp lib/gcc/${MINGW_CHOST}/${pkgver}/gnat1.exe ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/
	
	mkdir -p ${pkgdir}${MINGW_PREFIX}/share/info
    cp share/info/gnat-style.info* ${pkgdir}${MINGW_PREFIX}/share/info/
    cp share/info/gnat_rm.info* ${pkgdir}${MINGW_PREFIX}/share/info/
    cp share/info/gnat_ugn.info* ${pkgdir}${MINGW_PREFIX}/share/info/
}

package_mingw-w64-gcc-fortran() {
	pkgdesc="GNU Compiler Collection (Fortran) for MinGW-w64"
	depends=("${_mingw_suff}-${_realname}=${pkgver}")
	
	mkdir -p ${pkgdir}${MINGW_PREFIX}/{bin,lib,share}
	
	cd ${destdir}${MINGW_PREFIX}
	cp bin/gfortran.exe ${pkgdir}${MINGW_PREFIX}/bin/
    cp bin/${MINGW_CHOST}-gfortran.exe ${pkgdir}${MINGW_PREFIX}/bin/
	
	cp bin/libgfortran*.dll ${pkgdir}${MINGW_PREFIX}/bin/
	
	mkdir -p ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}
    cp -r lib/gcc/${MINGW_CHOST}/${pkgver}/finclude  ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/
    cp lib/gcc/${MINGW_CHOST}/${pkgver}/f951.exe  ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/
    cp lib/gcc/${MINGW_CHOST}/${pkgver}/libcaf_single.a  ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/
    cp lib/gcc/${MINGW_CHOST}/${pkgver}/libgfortran*  ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/
	mkdir -p ${pkgdir}${MINGW_PREFIX}/share/man/man1
    cp share/man/man1/gfortran.1* ${pkgdir}${MINGW_PREFIX}/share/man/man1/
	mkdir -p ${pkgdir}${MINGW_PREFIX}/share/info
    cp share/info/gfortran.info* ${pkgdir}${MINGW_PREFIX}/share/info/
}

package_mingw-w64-gcc-objc() {
	pkgdesc="GNU Compiler Collection (ObjC,Obj-C++) for MinGW-w64"
	depends=("${_mingw_suff}-${_realname}=${pkgver}")
	
	mkdir -p ${pkgdir}${MINGW_PREFIX}/{bin,lib}
	
	cd ${destdir}${MINGW_PREFIX}
	cp bin/libobjc*.dll ${pkgdir}${MINGW_PREFIX}/bin/
	
	mkdir -p ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/include
	cp -r lib/gcc/${MINGW_CHOST}/${pkgver}/include/objc ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/include/
    cp lib/gcc/${MINGW_CHOST}/${pkgver}/cc1obj.exe ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/
    cp lib/gcc/${MINGW_CHOST}/${pkgver}/libobjc.* ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/
	cp lib/gcc/${MINGW_CHOST}/${pkgver}/cc1objplus.exe ${pkgdir}${MINGW_PREFIX}/lib/gcc/${MINGW_CHOST}/${pkgver}/
}

#package_mingw-w64-gcc-libs() {
#	pkgdesc="GNU Compiler Collection (libraries) for MinGW-w64"
#	mkdir -p ${pkgdir}${MINGW_PREFIX}/bin
#	
#	cd ${destdir}${MINGW_PREFIX}
#	cp bin/*.dll ${pkgdir}${MINGW_PREFIX}/bin/
#}

package_mingw-w64-i686-gcc() {
	package_mingw-w64-gcc
}

package_mingw-w64-i686-gcc-ada() {
	package_mingw-w64-gcc-ada
}

package_mingw-w64-i686-gcc-fortran() {
	package_mingw-w64-gcc-fortran
}

package_mingw-w64-i686-gcc-objc() {
	package_mingw-w64-gcc-objc
}

package_mingw-w64-i686-gcc-libs() {
	package_mingw-w64-gcc-libs
}

package_mingw-w64-x86_64-gcc() {
	package_mingw-w64-gcc
}

package_mingw-w64-x86_64-gcc-ada() {
	package_mingw-w64-gcc-ada
}

package_mingw-w64-x86_64-gcc-fortran() {
	package_mingw-w64-gcc-fortran
}

package_mingw-w64-x86_64-gcc-objc() {
	package_mingw-w64-gcc-objc
}

#package_mingw-w64-x86_64-gcc-libs() {
#	package_mingw-w64-gcc-libs
#}
