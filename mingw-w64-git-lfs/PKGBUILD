# Contributor: Abd√≥ Roig-Maranges <abdo.roig@gmail.com>

_realname=git-lfs
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.0.2
pkgrel=1
arch=("any")
pkgdesc="An open source Git extension for versioning large files (mingw-w64)"
url="https://git-lfs.github.com/"
license=('MIT')
options=('!strip')
depends=("git")
makedepends=("${MINGW_PACKAGE_PREFIX}-go" "${MINGW_PACKAGE_PREFIX}-ruby")

source=(${_realname}-${pkgver}.zip::"https://github.com/github/git-lfs/archive/v${pkgver}.zip"
        "0001-Translate-git-paths-from-msys-to-windows.patch")

sha256sums=('3d1c43411727d17557c602d4e6f9b935564dc22c0f3b3fd0bd348debd5ad9c3c'
            '3ff65cb1b2dc13630a65ff694813854dc6dbb5ebfae3800aacd3fbb768c69bb9')

prepare() {
  # apply patches
  cd "${srcdir}/git-lfs-${pkgver}"
  patch -N -p1 -i "$srcdir/0001-Translate-git-paths-from-msys-to-windows.patch"

  # Clear go cache
  rm -Rf "$srcdir/src"

  # Setup local gopath
  mkdir -p "$srcdir/src/github.com/git-lfs"
  cp -R "$srcdir/git-lfs-$pkgver/" "$srcdir/src/github.com/git-lfs/git-lfs"

  # Fetch dependencies
  . ${MINGW_PREFIX}/etc/profile.d/go.sh
  GOPATH="$srcdir" go get -v -d
  gem install ronn
}

build() {
  cd "${srcdir}/git-lfs-${pkgver}"

  # build
  . ${MINGW_PREFIX}/etc/profile.d/go.sh
  GOPATH="$srcdir" go run script/*.go -cmd build

  # The man pages need ronn, which needs a bunch of missing ruby dependencies.
  ronn docs/man/*.ronn
}

package() {
  cd "${srcdir}/git-lfs-${pkgver}"
  install -Dm755 "bin/git-lfs" "${pkgdir}${MINGW_PREFIX}/bin/git-lfs"

  # man page
  install -d "${pkgdir}${MINGW_PREFIX}/share/man/man1"
  install -Dm644 docs/man/*.1 "${pkgdir}${MINGW_PREFIX}/share/man/man1"
}

