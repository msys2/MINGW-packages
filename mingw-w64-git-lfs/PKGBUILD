# Contributor: Abd√≥ Roig-Maranges <abdo.roig@gmail.com>

_realname=git-lfs
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.3.1
pkgrel=1
arch=("any")
pkgdesc="An open source Git extension for versioning large files (mingw-w64)"
url="https://git-lfs.github.com/"
license=('MIT')
options=('!strip')
depends=("git")
makedepends=("${MINGW_PACKAGE_PREFIX}-go")

source=(${_realname}-${pkgver}.zip::"https://github.com/github/git-lfs/archive/v${pkgver}.zip"
        "0001-Translate-git-paths-from-msys-to-windows.patch")

sha256sums=('94b13cb4593a2e7319bd24c96cac835df309adfb8ec68c4040779c2b7f1d7e5c'
            '3a66340cd262f550ba193336024d78dc3da123f59c2a9456bb5f7af87b56420a')

prepare() {
  # apply patches
  cd "${srcdir}/git-lfs-${pkgver}"
  patch -N -p1 -i "$srcdir/0001-Translate-git-paths-from-msys-to-windows.patch"

  # setup local gopath
  mkdir -p "${srcdir}/src/github.com/github"
  rm -Rf "${srcdir}/src/github.com/github/git-lfs"
  cp -R "${srcdir}/git-lfs-${pkgver}" "${srcdir}/src/github.com/github/git-lfs"
}

build() {
  cd "${srcdir}/git-lfs-${pkgver}"
  export GOPATH="$srcdir"
  go get -v -d
  go run script/*.go -cmd build

  # The man pages need ronn, which needs a bunch of missing ruby dependencies.
  # ronn docs/man/*.ronn
}

package() {
  cd "${srcdir}/git-lfs-${pkgver}"
  install -Dm755 "bin/git-lfs" "${pkgdir}${MINGW_PREFIX}/bin/git-lfs"

  # man page
  # install -d "${pkgdir}${MINGW_PREFIX}/share/man/man1"
  # install -Dm644 docs/man/*.1 "${pkgdir}${MINGW_PREFIX}/share/man/man1"
}

