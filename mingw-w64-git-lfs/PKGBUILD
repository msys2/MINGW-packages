# Contributor: Abd√≥ Roig-Maranges <abdo.roig@gmail.com>

_realname=git-lfs
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.5.4
pkgrel=1
arch=("any")
pkgdesc="An open source Git extension for versioning large files (mingw-w64)"
url="https://git-lfs.github.com/"
license=('MIT')
options=('!strip')
depends=("git")
makedepends=("${MINGW_PACKAGE_PREFIX}-go")

source=(${_realname}-${pkgver}.zip::"https://github.com/github/git-lfs/archive/v${pkgver}.zip"
        "0001-Translate-git-paths-from-msys-to-windows.patch")

sha256sums=('95e64dbc6ce6acfd014e789030f8f2c0b5fae0e7005a9760d0c917535fa29e65'
            '9f991fae2f0439b747b8cac577fe22924f963a6e15a27df6310b0d50c7f4a899')

prepare() {
  # apply patches
  cd "${srcdir}/git-lfs-${pkgver}"
  patch -N -p1 -i "$srcdir/0001-Translate-git-paths-from-msys-to-windows.patch"

  # Clear go cache
  rm -Rf "$srcdir/src"

  # Setup local gopath
  mkdir -p "$srcdir/src/github.com/git-lfs"
  cp -R "$srcdir/git-lfs-$pkgver/" "$srcdir/src/github.com/git-lfs/git-lfs"

  # Fetch dependencies
  GOPATH="$srcdir" go get -v -d
}

build() {
  cd "${srcdir}/git-lfs-${pkgver}"

  # build
  GOPATH="$srcdir" go run script/*.go -cmd build

  # The man pages need ronn, which needs a bunch of missing ruby dependencies.
  # ronn docs/man/*.ronn
}

package() {
  cd "${srcdir}/git-lfs-${pkgver}"
  install -Dm755 "bin/git-lfs" "${pkgdir}${MINGW_PREFIX}/bin/git-lfs"

  # man page
  # install -d "${pkgdir}${MINGW_PREFIX}/share/man/man1"
  # install -Dm644 docs/man/*.1 "${pkgdir}${MINGW_PREFIX}/share/man/man1"
}

