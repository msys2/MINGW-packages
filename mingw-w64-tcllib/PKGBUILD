# $Id$
# Maintainer: Oscar Fuentes <ofv@wanadoo.es>
# Based on Arch's PKGBUILD

_realname=tcllib
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.20
pkgrel=1
pkgdesc="Set of pure-Tcl extensions (mingw-w64)"
arch=('any')
url="https://core.tcl.tk/tcllib/"
license=('bsd')
depends=("${MINGW_PACKAGE_PREFIX}-tcl")
source=(https://github.com/tcltk/tcllib/archive/tcllib-${pkgver/./-}.tar.gz)
sha256sums=('e56892698903e0cc902a75a0eb9324845efcd3257c41e09edec25a6ab1654cf3')

package(){
  cd ${srcdir}/tcllib-tcllib-${pkgver/./-}
  tclsh installer.tcl \
    -pkg-path ${pkgdir}${MINGW_PREFIX}/lib/tcllib${pkgver} \
    -app-path ${pkgdir}${MINGW_PREFIX}/bin \
    -nroff-path ${pkgdir}${MINGW_PREFIX}/share/man/mann \
    -no-examples \
    -no-html \
    -no-wait \
    -no-gui
  mkdir -p ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/
  cp license.terms ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
  rm -f $pkgdir${MINGW_PREFIX}/share/man/mann/{try,string,variable,zlib,coroutine}.n
}
