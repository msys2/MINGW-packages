# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=pyinstaller-hooks-contrib
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2024.9
pkgrel=1
pkgdesc='Community maintained hooks for PyInstaller (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/pyinstaller/pyinstaller-hooks-contrib'
license=('APL' 'GPL')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-packaging")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools")
options=('!strip')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname//-/_}-${pkgver}.tar.gz")
sha256sums=('4793869f370d1dc4806c101efd2890e3c3e703467d8d27bb5a3db005ebfb008d')

build() {
  cp -r "${_realname//-/_}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE.APL.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.APL.txt"
  install -Dm644 LICENSE.GPL.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.GPL.txt"
}
