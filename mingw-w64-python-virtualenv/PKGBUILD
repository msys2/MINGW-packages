# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>
#This value is here because some python package names have capital letters
#while there is a strong preference for lowercase letters in ArchLinux and
#MSYS packages.

_pyname=virtualenv
_realname=${_pyname} #This should be lowercase
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}"  "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=16.0.0
pkgrel=1
pkgdesc="Virtual Python Environment builder (mingw-w64)"
arch=('any')
url='https://virtualenv.pypa.io/'
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python3-pytest" "${MINGW_PACKAGE_PREFIX}-python3-mock"
              "${MINGW_PACKAGE_PREFIX}-python2-pytest" "${MINGW_PACKAGE_PREFIX}-python2-mock")
options=('staticlibs' 'strip' '!debug')
source=(${_realname}-${pkgver}.tar.gz::https://github.com/pypa/virtualenv/archive/${pkgver}.tar.gz)
sha256sums=('5a597f845cf785d46c3ae39d6cb18ef0b6af4ca366da40682364f78f7fe00d22')

prepare() {
  for builddir in python{2,3}-build-${CARCH}; do
    rm -rf $builddir | true
    cp -r "${_pyname}-${pkgver}" "${builddir}"
  done
}  
  
# Note that build() is sometimes skipped because it's done in 
# the packages setup.py install for simplicity if you can do so.
# but sometimes, you want to do a check before install which would
# also trigger the build.  
build() {
  for pver in {2,3}; do  
    msg "Python ${pver} build for ${CARCH}"
    cd "${srcdir}/python${pver}-build-${CARCH}"
    MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
      ${MINGW_PREFIX}/bin/python${pver} setup.py build
  done  
}

#Uncomment when the prerequisits are finished.
#check() {  
#  for pver in {2,3}; do
#    msg "Python ${pver} test for ${CARCH}"  
#    cd "${srcdir}/python${pver}-build-${CARCH}"
#    if [ $pver -eq 2 ]; then
#      PYTHONPATH="$PWD"  ${MINGW_PREFIX}/bin/py.test2
#    else
#      PYTHONPATH="$PWD"  ${MINGW_PREFIX}/bin/py.test
#    fi
#  done  
#}

package_python3-virtualenv() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3")

  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/COPYING"

  local _mingw_prefix=$(cygpath -am ${MINGW_PREFIX})
  # fix python command in files
  for _ff in ${pkgdir}${MINGW_PREFIX}/bin/*.py; do
    sed -e "s|${_mingw_prefix}|${MINGW_PREFIX}|g" -i ${_ff}
  done
}

package_python2-virtualenv() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2")

  cd "${srcdir}/python2-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/COPYING"

  local _mingw_prefix=$(cygpath -am ${MINGW_PREFIX})
  # fix python command in files
  for _ff in ${pkgdir}${MINGW_PREFIX}/bin/*.py; do
    sed -e "s|${_mingw_prefix}|${MINGW_PREFIX}|g" -i ${_ff}
  done
# This is for packages that install scripts and .exe into the /mingw*/bin
  for f in virtualenv; do
    mv "${pkgdir}${MINGW_PREFIX}"/bin/${f}{,2}.exe
    if [ -f "${pkgdir}${MINGW_PREFIX}"/bin/${f}.exe.manifest ]; then
      mv "${pkgdir}${MINGW_PREFIX}"/bin/${f}{,2}.exe.manifest
      sed -e "s|${f}|${f}2|g" -i "${pkgdir}${MINGW_PREFIX}"/bin/${f}2.exe.manifest
    fi
    mv "${pkgdir}${MINGW_PREFIX}"/bin/${f}{,2}-script.py
  done
}

package_mingw-w64-i686-python2-virtualenv() {
  install=${_realname}2-${CARCH}.install
  package_python2-virtualenv
}

package_mingw-w64-i686-python3-virtualenv() {
  install=${_realname}3-${CARCH}.install
  package_python3-virtualenv
}

package_mingw-w64-x86_64-python2-virtualenv() {
  install=${_realname}2-${CARCH}.install
  package_python2-virtualenv
}

package_mingw-w64-x86_64-python3-virtualenv() {
  install=${_realname}3-${CARCH}.install
  package_python3-virtualenv
}
