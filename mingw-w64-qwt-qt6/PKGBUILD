# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ingo Maindorfer <ingo@liquidcooling.de>
# Based on work by AlexWMF <alexxwmf@gmail.com>

_realname=qwt
pkgbase=mingw-w64-${_realname}-qt6
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}-qt6")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}"
           "${MINGW_PACKAGE_PREFIX}-${_realname}-qt5")
pkgver=6.2.0
pkgrel=2
pkgdesc="Qt Widgets for Technical Applications (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=("custom:${_realname}")
url="https://qwt.sourceforge.io/"
depends=("${MINGW_PACKAGE_PREFIX}-qt6-base"
         "${MINGW_PACKAGE_PREFIX}-qt6-svg")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=(!strip staticlibs !buildflags)
source=("https://downloads.sourceforge.net/${_realname}/${_realname}-${pkgver}.tar.bz2"
        001-separate-debug-info-build.patch
        002-link-to-release.patch
        003-qwtconfig-msys2.patch
        004-pkgconfig-file.patch)
sha256sums=('9194f6513955d0fd7300f67158175064460197abab1a92fa127a67a4b0b71530'
            'ef39e5669cb6c0441cdb1fe20c3e51a706bfc3eb7f572a3c0a1dde362b0d40a4'
            '6e0101d2c2897c94da6284ba0b929a7a39d82120313e6cd6353fd5e3aaf4b059'
            '85072f5c854f5568ee109be1a13213b78890e5001e801cfcfdc00da59bb0e494'
            '442570bf827731c83027451f76b23f4b3ffbf0266d02d507c573ca047b06fc18')

prepare() {
  cd ${_realname}-${pkgver}

  patch -p1 -i ${srcdir}/001-separate-debug-info-build.patch
  patch -p1 -i ${srcdir}/002-link-to-release.patch
  patch -p1 -i ${srcdir}/003-qwtconfig-msys2.patch
  patch -p1 -i ${srcdir}/004-pkgconfig-file.patch
}

package() {
  [[ -d ${srcdir}/build-${MSYSTEM} ]] && rm -rf ${srcdir}/build-${MSYSTEM}
  cp -rf ${srcdir}/${_realname}-${pkgver} ${srcdir}/build-${MSYSTEM}

  cd ${srcdir}/build-${MSYSTEM}

  local PREFIX_WIN=$(cygpath -am ${pkgdir}${MINGW_PREFIX})
  sed -s "s|%VALUE_QWT_INSTALL_PREFIX%|${PREFIX_WIN}|g" -i qwtconfig.pri
  ${MINGW_PREFIX}/bin/qmake CONFIG-=debug_and_release CONFIG+=release qwt.pro

  make # VERBOSE=1
  make -j1 install

  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING \
    ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING

  mkdir -p ${pkgdir}${MINGW_PREFIX}/bin/
  mv "${pkgdir}${MINGW_PREFIX}"/lib/*.{debug,dll} "${pkgdir}${MINGW_PREFIX}"/bin/

  # install debug info
  #install -Dm644 lib/*.debug -t "${pkgdir}${MINGW_PREFIX}"/bin/

  local PREFIX_WIN=$(cygpath -am ${pkgdir}${MINGW_PREFIX})
  local PREFIX=$(cygpath -am ${MINGW_PREFIX})

  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i ${pkgdir}${MINGW_PREFIX}/share/qt6/mkspecs/features/qwtconfig.pri
  sed -s "s|${PREFIX_WIN}|\$\{prefix\}|g" -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/Qt6Qwt6.pc
  sed -s "s|${PREFIX}|${MINGW_PREFIX}|g" -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/Qt6Qwt6.pc
}
