# Maintainer: Diego Sogari <diego.sogari@gmail.com>

_realname=winsparkle
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
pkgver=0.7.0
pkgrel=1
pkgdesc='App update framework for Windows, inspired by Sparkle for OS X (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
url='https://winsparkle.org/'
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc")
depends=("${MINGW_PACKAGE_PREFIX}-expat"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-wxWidgets3.1") # gcc-libs not needed?
source=("${_realname}-${pkgver}.tar.gz::https://github.com/vslavik/winsparkle/archive/v${pkgver}.tar.gz"
        001-include-missing-time_h.patch
        Makefile)
sha256sums=('42114abe4cbc1e5ca12684b3355a92dff560eb68e744f70f1effe3a036a4e169'
            '7d8f94aa9dd1876285681875a322dd46541f66c12b3d20c652177552f8f8458f'
            '321ba5c7e37c7fdcc09081f558bb88510cef1725e45bf0e65e48557753a921da')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  cp "${srcdir}"/Makefile src
  patch -p1 -i "${srcdir}"/001-include-missing-time_h.patch
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}/src"
  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  install -D -m644 include/winsparkle.h         "${pkgdir}${MINGW_PREFIX}/include/winsparkle.h"
  install -D -m644 include/winsparkle-version.h "${pkgdir}${MINGW_PREFIX}/include/winsparkle-version.h"
  install -D -m644 src/libwinsparkle.a          "${pkgdir}${MINGW_PREFIX}/lib/libwinsparkle.a"
  install -D -m644 src/libwinsparkle.dll.a      "${pkgdir}${MINGW_PREFIX}/lib/libwinsparkle.dll.a"
  install -D -m755 src/libwinsparkle.dll        "${pkgdir}${MINGW_PREFIX}/bin/libwinsparkle.dll"
  install -D -m644 COPYING                      "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
