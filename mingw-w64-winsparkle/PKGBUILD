# Maintainer: Diego Sogari <diego.sogari@gmail.com>
# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=winsparkle
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
pkgver=0.7.0
pkgrel=1
pkgdesc='App update framework for Windows, inspired by Sparkle for OS X (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='https://winsparkle.org/'
license=('MIT')
options=('strip' 'staticlibs')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc")
depends=("${MINGW_PACKAGE_PREFIX}-expat"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-wxWidgets")
source=("https://github.com/vslavik/${_realname}/archive/v${pkgver}.tar.gz"
        "001-compile-fixes.patch")
sha256sums=('42114abe4cbc1e5ca12684b3355a92dff560eb68e744f70f1effe3a036a4e169'
            'fae024a6f7c32c21dddfd667b3530cb653ed49151e2aeeb13dac24007b2e5c88')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 < "${srcdir}/001-compile-fixes.patch"
}

build() {
  [[ -d "build-${MSYSTEM}" ]] && rm -rf "build-${MSYSTEM}"
  cp -r "${_realname}-${pkgver}" "build-${MSYSTEM}"

  cd "${srcdir}/build-${MSYSTEM}/src"
  make
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  install -Dm644 include/winsparkle.h         "${pkgdir}${MINGW_PREFIX}/include/winsparkle.h"
  install -Dm644 include/winsparkle-version.h "${pkgdir}${MINGW_PREFIX}/include/winsparkle-version.h"
  install -Dm644 src/libwinsparkle.a          "${pkgdir}${MINGW_PREFIX}/lib/libwinsparkle.a"
  install -Dm644 src/libwinsparkle.dll.a      "${pkgdir}${MINGW_PREFIX}/lib/libwinsparkle.dll.a"
  install -Dm755 src/libwinsparkle.dll        "${pkgdir}${MINGW_PREFIX}/bin/libwinsparkle.dll"
  install -Dm644 COPYING                      "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
