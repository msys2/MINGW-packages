# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_compiler=clang

_realname=flang
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
_version=17.0.4
_rc=""
_tag=llvmorg-${_version}${_rc}
pkgver=${_version}${_rc/-/}
pkgrel=1
pkgdesc="Fortran frontend for LLVM (mingw-w64)"
url="https://flang.llvm.org/"
license=("spdx:Apache-2.0 WITH LLVM-exception")
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
provides=($([[ ${MINGW_PACKAGE_PREFIX} != *-clang-* ]] || echo "${MINGW_PACKAGE_PREFIX}-fc"))
depends=("${MINGW_PACKAGE_PREFIX}-clang"
         "${MINGW_PACKAGE_PREFIX}-llvm"
         "${MINGW_PACKAGE_PREFIX}-mlir")
makedepends=("${MINGW_PACKAGE_PREFIX}-clang-tools-extra"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-lld"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "gawk"
             $([[ "$_compiler" == "clang" ]] && echo \
               "${MINGW_PACKAGE_PREFIX}-compiler-rt" \
               "${MINGW_PACKAGE_PREFIX}-libc++")
             $([[ "$_compiler" == "gcc" ]] && echo \
               "${MINGW_PACKAGE_PREFIX}-gcc")
             )
_pkgfn=${_realname}-${pkgver}.src
_url=https://github.com/llvm/llvm-project/releases/download/${_tag}
source=("${_url}/${_pkgfn}.tar.xz"{,.sig}
        "${_url}/cmake-${pkgver}.src.tar.xz"{,.sig}
        "0001-cast-cxx11-narrowing.patch"
        "0002-cmake-22162.patch"
        "0003-do-not-link-to-llvm-dylib.patch"
        "0004-Avoid-dependency-on-libcxx.patch"
        "0005-Fix-c_long_double-value-on-mingw-aarch64.patch"
        "0006-Rename-flang-new-flang-experimental-exec-to-flang.patch")
sha256sums=('4a8edb81a6e6071abcfc8c84f0f8028271ff084be058c87265e833e0e891be88'
            'SKIP'
            '1a5cbe4a1fcda56ecdd80f42c3437060a28c97ec31de1748f6ba6aa84948fb0f'
            'SKIP'
            'ba08064d737a2aa173125e88c3900dce804220fb0562596b03130177c2139312'
            '77fb0612217b6af7a122f586a9d0d334cd48bb201509bf72e8f8e6244616e895'
            '8d70b33d20d74801aeee82173944fed3b33ae185031537e1b50a640238f7a988'
            '0666e762b43e932f6f6cda061088a0426b98d06715b242b4c736172634a778db'
            'a1811227e2a26c2786b0c8577528580e679111f6c0889afea57d08333e2d5e4f'
            '645c3c5b55f03c7ce87a4927d4b6045283aef7926c029f1b2ca135a14389a9c3')
validpgpkeys=('B6C8F98282B944E3B0D5C2530FC3042E345AD05D'  # Hans Wennborg, Google.
              '474E22316ABF4785A88C6E8EA2C794A986419D8A'  # Tom Stellard
              'D574BD5D1D0E98895E3BF90044F2485E45D59042') # Tobias Hieta

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  [[ -d ./cmake ]] && rm -rf ./cmake
  mv cmake-$pkgver.src cmake
  cd "${srcdir}/flang-${pkgver}.src"

  apply_patch_with_msg \
    "0001-cast-cxx11-narrowing.patch" \
    "0002-cmake-22162.patch" \
    "0003-do-not-link-to-llvm-dylib.patch" \
    "0005-Fix-c_long_double-value-on-mingw-aarch64.patch"
  # https://reviews.llvm.org/D143592
  apply_patch_with_msg \
    "0006-Rename-flang-new-flang-experimental-exec-to-flang.patch"
  # https://reviews.llvm.org/D158957
  apply_patch_with_msg \
    "0004-Avoid-dependency-on-libcxx.patch"
}

build() {
  local -a extra_args
  if check_option "debug" "y"; then
    extra_args+=(-DCMAKE_BUILD_TYPE=Debug)
  else
    extra_args+=(-DCMAKE_BUILD_TYPE=Release)
  fi

  if [ "${_compiler}" == "clang" ]; then
    export CC='clang'
    export CXX='clang++'
    extra_args+=(-DLLVM_ENABLE_LLD=ON)
  fi

  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -GNinja \
    -Wno-dev \
    -DCLANG_DIR=${MINGW_PREFIX}/lib/cmake/clang \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_SYSTEM_IGNORE_PATH=/usr/lib \
    -DLLVM_ENABLE_ASSERTIONS=OFF \
    -DLLVM_ENABLE_THREADS=ON \
    -DLLVM_HOST_TRIPLE="${CARCH}-w64-windows-gnu" \
    -DLLVM_LINK_LLVM_DYLIB=ON \
    -DFLANG_INCLUDE_TESTS=OFF \
    "${extra_args[@]}" \
    ../${_pkgfn}

  ${MINGW_PREFIX}/bin/cmake.exe --build .
}

check() {
  cd "${srcdir}"/build-${MSYSTEM}
  ${MINGW_PREFIX}/bin/cmake.exe -DFLANG_INCLUDE_TESTS=ON ../${_pkgfn}
  ${MINGW_PREFIX}/bin/cmake.exe --build .
  ${MINGW_PREFIX}/bin/cmake.exe --build . -- check-flang || true
}

package() {
  DESTDIR="${pkgdir}" cmake --install "${srcdir}/build-${MSYSTEM}"
}
