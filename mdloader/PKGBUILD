# Maintainer: fauxpark <fauxpark@gmail.com>

_realname=mdloader
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=1.0.5
pkgrel=1
pkgdesc='Massdrop keyboard firmware loader (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=('GPL3')
url='https://github.com/Massdrop/mdloader'
source=(
    "https://github.com/Massdrop/mdloader/archive/${pkgver}.tar.gz"
    pathtools.c
    pathtools.h
    01-get-applet-path.patch
)
sha256sums=(
    '2b2e88e2d66165f2160a0673dcacd94dbcb4f96c855fc0b4923ae569cd8f9e34'
    '6f1016e6647b6340fdceefaf24ff391f4c0ea3c785ddf70c9794ca2356797888'
    '6ce4dcf4ef6c4bce48dbcb6f1b5226baf79f74ac76719fb0c06419a0aadb37a3'
    '3945fb00f207bb6203bed393f8d82784bcd88883d7c12855e9676e6e89ec1c96')

prepare() {
    test ! -d "${startdir}/../mingw-w64-pathtools" || {
        cmp "${startdir}/../mingw-w64-pathtools/pathtools.c" "${srcdir}/pathtools.c" &&
        cmp "${startdir}/../mingw-w64-pathtools/pathtools.h" "${srcdir}/pathtools.h"
    } || exit 1

    cd "${srcdir}/${_realname}-${pkgver}"
    cp -fHv "${srcdir}"/pathtools.[ch] ./

    patch -p1 -i ../01-get-applet-path.patch
}

build() {
    cd ${srcdir}/${_realname}-${pkgver}

    make
}

package() {
    cd ${srcdir}/${_realname}-${pkgver}/build

    install -Dm755 mdloader.exe ${pkgdir}${MINGW_PREFIX}/bin/mdloader.exe
    install -Dm644 applet-*.bin -t ${pkgdir}${MINGW_PREFIX}/share/mdloader
}
