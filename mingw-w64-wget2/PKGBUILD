# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=wget2
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.0.0
pkgrel=1
pkgdesc='Updated version of popular wget URL retrieval tool (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='https://www.gnu.org/software/wget/'
license=('GPL3')
depends=("${MINGW_PACKAGE_PREFIX}-brotli"
         "${MINGW_PACKAGE_PREFIX}-bzip2"
         "${MINGW_PACKAGE_PREFIX}-gpgme"
         "${MINGW_PACKAGE_PREFIX}-libidn2"
         "${MINGW_PACKAGE_PREFIX}-nghttp2"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-pcre2"
         "${MINGW_PACKAGE_PREFIX}-xz"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-zstd")
makedepends=("${MINGW_PACKAGE_PREFIX}-doxygen"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc")
checkdepends=("${MINGW_PACKAGE_PREFIX}-libmicrohttpd")
source=("https://ftp.gnu.org/gnu/wget/${_realname}-${pkgver}.tar.lz"{,.sig}
        "https://gitlab.com/gnuwget/wget2/-/raw/v2.0.0/contrib/libtool.patch"
        "configure-ac-use-srcdir.patch"
        "mingw-dllexport.patch")
sha256sums=('da95b1477fa4ede1ac712f179c1354d9624fbc91e778d89cf8d2ae46aa15e3d2'
            'SKIP'
            '88d385382cabddb5f12b624af4bd0efbe415bd6ce4d44111de58b7947edac1ed'
            '841e928a53e7f693aa44b075d00384da5c3930142f90943d2d6aee7f145b1715'
            'e6cb1e6088a98bc7da3cef9d6e97f11700bda564d0c8d21fa04f915518b0ed2d')
options=('strip' '!libtool' 'staticlibs')
validpgpkeys=('1CB27DBC98614B2D5841646D08302DB6A2670428')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  cp "${srcdir}/libtool.patch" contrib/
  patch -Nbp1 -i "${srcdir}/configure-ac-use-srcdir.patch"
  patch -Nbp1 -i "${srcdir}/mingw-dllexport.patch"
  autoreconf -fiv
}

build() {
  [[ -d ${srcdir}/build-${MSYSTEM} ]] && rm -rf ${srcdir}/build-${MSYSTEM}
  mkdir -p ${srcdir}/build-${MSYSTEM} && cd ${srcdir}/build-${MSYSTEM}

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --with-ssl=openssl

  make
}

check() {
  cd ${srcdir}/build-${MSYSTEM}
  make check
}

package() {
  cd ${srcdir}/build-${MSYSTEM}
  make DESTDIR="${pkgdir}" install

  # Licenses
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/README" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/README"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
