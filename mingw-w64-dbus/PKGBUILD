# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=dbus
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.16.2
pkgrel=1
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
pkgdesc="Freedesktop.org message bus system (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-expat")
makedepends=("git"
             "xmlto"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-doxygen"
             "${MINGW_PACKAGE_PREFIX}-docbook-xsl"
             "${MINGW_PACKAGE_PREFIX}-glib2"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-python"
             )
license=('spdx:AFL-2.1 OR GPL-2.0-or-later')
url="https://www.freedesktop.org/wiki/Software/dbus"
msys2_references=(
  "cpe: cpe:/a:d-bus_project:d-bus"
  "cpe: cpe:/a:freedesktop:dbus"
)
source=("https://dbus.freedesktop.org/releases/dbus/${_realname}-${pkgver}.tar.xz"{,.asc}
        "0001-add-aarch64-backtrace-support.patch")
sha256sums=('0ba2a1a4b16afe7bceb2c07e9ce99a8c2c3508e5dec290dbb643384bd6beb7e2'
            'SKIP'
            '8938f866ffa1699f24fe6c92334d17c3c65b595044156f35833ed731461c9dc3')
validpgpkeys=('DA98F25C0871C49A59EAFF2C4DE8FF2A63C7CC90')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -Np1 -i "${srcdir}/0001-add-aarch64-backtrace-support.patch"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

   MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -GNinja \
      -DBUILD_SHARED_LIBS=ON \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -B "build-${MSYSTEM}"

  cmake --build "build-${MSYSTEM}"
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}/build-${MSYSTEM}"
  ctest
}

package() {
  DESTDIR="${pkgdir}" cmake --install "${_realname}-${pkgver}/build-${MSYSTEM}"
  install -Dt ""${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}" -m644 "${srcdir}"/${_realname}-${pkgver}/COPYING
  install -Dt ""${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSES" -m644 \
                                                 "${srcdir}"/${_realname}-${pkgver}/LICENSES/AFL-2.1.txt \
                                                 "${srcdir}"/${_realname}-${pkgver}/LICENSES/GPL-2.0-or-later.txt
}
