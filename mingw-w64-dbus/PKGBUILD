# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=dbus
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.8.0
pkgrel=3
arch=('any')
pkgdesc="Freedesktop.org message bus system (mingw-w64)"
depends=("${_mingw_suff}-glib2" "${_mingw_suff}-expat")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config" "${_mingw_suff}-python2" "xmlto")
options=('strip' 'staticlibs')
license=("BSD")
url="http://www.freedesktop.org/wiki/Software/dbus"
source=("http://dbus.freedesktop.org/releases/dbus/${_realname}-${pkgver}.tar.gz"{,.asc})
md5sums=('059fbe84e39fc99c67a14f15b1f39dff'
         'SKIP')

prepare() {
  cd "${srcdir}"/dbus-${pkgver}
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}
  CFLAGS+=" -mms-bitfields"
  CXXFLAGS+=" -mms-bitfields"

  ../dbus-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --enable-xml-docs \
    --disable-systemd \
    --disable-tests \
    --disable-Werror \
    --enable-shared \
    --enable-static
  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install
  find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
  #rm -rf "${pkgdir}${MINGW_PREFIX}"/share/man
  #rm -rf "${pkgdir}${MINGW_PREFIX}"/{etc,var,libexec,share}
}
