# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Tim S. <stahta01@gmail.com>

_realname=kicad-doc
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}-ca"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-de"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-en"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-es"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-fr"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-id"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-it"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-ja"
         # "${MINGW_PACKAGE_PREFIX}-${_realname}-nl"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-pl"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-ru"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-zh")

# https://gitlab.com/kicad/services/kicad-doc/-/jobs/9178132758/artifacts/browse/archive/
_job_id='9178132758' # this is needed to change for new version
pkgver=9.0.0
pkgrel=1
pkgdesc="Documentation for KiCad (mingw-w64)"
arch=(any)
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'archlinux: kicad'
)
url="https://www.kicad.org/"
license=("spdx:GPL-3.0-or-later OR CC-BY-3.0")
makedepends=()
optdepends=("${MINGW_PACKAGE_PREFIX}-kicad-meta"
            "${MINGW_PACKAGE_PREFIX}-kicad")

# Latest available doc tarball from previously used source is for 8.0.0-rc3:
# https://kicad-downloads.s3.cern.ch/docs/kicad-doc-8.0.0-rc3.tar.gz
# Use the more current build artifact from the release tag instead:
# https://gitlab.com/kicad/services/kicad-doc/-/artifacts
source=("${_realname}-${pkgver}.tar.gz::https://gitlab.com/kicad/services/kicad-doc/-/jobs/${_job_id}/artifacts/raw/archive/kicad-doc-unknown.tar.gz")
sha256sums=('fec4e4ae87ad174666effb10b0c2b76ff684fef5f38ac6bc09aee62c79c9105b')

build_lang() {
  cd "${srcdir}/kicad-doc-unknown/share/doc/kicad/help"
  mkdir -p "${pkgdir}${MINGW_PREFIX}/share/doc/kicad/help/"
  cp -R ${langid} "${pkgdir}${MINGW_PREFIX}/share/doc/kicad/help/"
}

for _n in ${pkgname[@]}; do
  eval "package_${_n}() {
    langid="${_n#${MINGW_PACKAGE_PREFIX}-${_realname}-}" build_lang
  }"
done
