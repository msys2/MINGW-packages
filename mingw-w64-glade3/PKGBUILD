# Maintainer: Ricky Wu <rickleaf.wu@gmail.com>

_realname=glade
pkgbase="mingw-w64-${_realname}3"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}3"
pkgver=3.8.6
pkgrel=1
pkgdesc="User interface builder for GTK+ and GNOME (mingw-w64)"
arch=('any')
url="https://glade.gnome.org/"
license=("GPL" "LGPL")
options=('strip' '!debug' 'staticlibs')
depends=("${MINGW_PACKAGE_PREFIX}-gtk2"
         "${MINGW_PACKAGE_PREFIX}-libxml2")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "gtk-doc"
             "intltool"
             "itstool"
             "docbook-xsl")
optdepends=("${MINGW_PACKAGE_PREFIX}-python2: Python widgets support"
            #'devhelp: help browser'
            )
install=glade3-${CARCH}.install
source=(https://download.gnome.org/sources/${_realname}/${pkgver%.*}/${_realname}3-${pkgver}.tar.xz)
sha256sums=('aaeeebffaeb3068fb23757a2eede46adeb4c7cecc740feed7654e065491f5449')

prepare() {
  cd "${srcdir}/${_realname}3-${pkgver}"

  autoreconf -fi
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}

  mkdir -p data/icons
  cp -rf ../${_realname}3-${pkgver}/data/icons/* data/icons

  PYTHON=${MINGW_PREFIX}/bin/python2 \
  ../${_realname}3-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-static \
    --enable-shared \
    --disable-gtk-doc \
    --enable-compile-warnings=minimum

  make VERBOSE=1
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="$pkgdir" install
}
