# Maintainer: Wu, Zhenyu <whenyu@ustc.edu>

_name=wakatime-cli
_realname=wakatime
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver='1.98.3'
pkgrel=1
pkgdesc="Command line interface used by all WakaTime text editor plugins (mingw-w64)"
arch=(any)
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/wakatime/wakatime-cli"
msys2_references=('aur: wakatime')
license=('BSD-3-Clause')
makedepends=("$MINGW_PACKAGE_PREFIX-go")
# LTO renders GO BUILDID unreproducible: https://gcc.gnu.org/bugzilla/show_bug.cgi?id=108534
options=(!lto)
source=("$_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha512sums=('4e585fa7845fe23dede1564665e08a47b49e925e25b084e71ca1269457592bc2d4443d6ceedd058b0789d093579da48c643aa7324f0e6ef8117ece318d5fdeda')

build() {
  # provide date but make it reproducible
  local _date="$(date --utc --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}" '+%Y-%m-%dT%H:%M:%S %Z')"
  # we don't care about the commit as we are building from source tarball
  local _commit="0000000"
  local _repo="${url#https://}"

  cd "$_name-$pkgver"

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"

  go build -o build/$_realname.exe \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags '${LDFLAGS}' -X '${_repo}/pkg/version.OS=linux' -X '${_repo}/pkg/version.Arch=amd64' -X '${_repo}/pkg/version.BuildDate=${_date}' -X '${_repo}/pkg/version.Commit=${_commit}' -X '${_repo}/pkg/version.Version=${pkgver}'"
}

package() {
  cd "$_name-$pkgver"
  install -Dm755 build/$_realname.exe -t "${pkgdir}"/$MINGW_PREFIX/bin
  install -d "${pkgdir}"/$MINGW_PREFIX/share/licenses/"${pkgname}"
  install LICENSE "${pkgdir}"/$MINGW_PREFIX/share/licenses/"${pkgname}"/LICENSE
}
