# Contributor: Marcelo Duarte https://github.com/marcelotduarte

_realname=pytest-rerunfailures
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=16.1
pkgrel=3
pkgdesc="a pytest plugin that re-runs failed tests up to -n times to eliminate flakey failures (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'archlinux: python-pytest-rerunfailures'
  'gentoo: dev-python/pytest-rerunfailures'
  'purl: pkg:pypi/pytest-rerunfailures'
)
url='https://github.com/pytest-dev/pytest-rerunfailures'
msys2_repository_url='https://github.com/pytest-dev/pytest-rerunfailures'
msys2_changelog_url='https://pytest-rerunfailures.readthedocs.io/latest/changelog.html'
license=('spdx:MPL-2.0')
depends=(
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-python-pytest"
  "${MINGW_PACKAGE_PREFIX}-python-packaging"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python-build"
  "${MINGW_PACKAGE_PREFIX}-python-installer"
  "${MINGW_PACKAGE_PREFIX}-python-setuptools"
)
source=("https://pypi.org/packages/source/${_realname::1}/${_realname/-/_}/${_realname/-/_}-${pkgver}.tar.gz")
sha256sums=('c38b266db8a808953ebd71ac25c381cb1981a78ff9340a14bcb9f1b9bff1899e')

build() {
  cp -r "${_realname/-/_}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
