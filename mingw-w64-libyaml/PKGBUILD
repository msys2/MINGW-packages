# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libyaml
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=0.1.4
pkgrel=1
pkgdesc="YAML 1.1 library (mingw-w64)"
arch=('any')
url="http://pyyaml.org/wiki/LibYAML"
license=("MIT")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config")
depends=("${_mingw_suff}-crt")
options=('staticlibs' 'strip')
source=("http://pyyaml.org/download/libyaml/yaml-$pkgver.tar.gz"
"0001-Proper-mingw-YAML_DECLARE-definition.patch")
md5sums=('36c852831d02cf90508c29852361d01b'
         '62993ee836b420adc99bc42dff09436b')

prepare() {
	cd "$srcdir/yaml-$pkgver"
  patch -Np1 -i "$srcdir/0001-Proper-mingw-YAML_DECLARE-definition.patch"
}

build() {
    mkdir -p "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
    cd "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
    "${srcdir}"/yaml-$pkgver/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST}
    make
    gcc -shared src/.libs/*.o -o libyaml.dll -Xlinker --out-implib -Xlinker libyaml.dll.a
}

package() {
    cd "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
    make DESTDIR="$pkgdir" install
    install -Dm644 "libyaml.dll" "${pkgdir}${MINGW_PREFIX}/bin/libyaml.dll"
    install -m644 "libyaml.dll.a" "${pkgdir}${MINGW_PREFIX}/lib/libyaml.dll.a"
}