# Maintaner: Marty Plummer (The_NetZ) <ntzrmtthihu777 (at) gmail (dot) com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

_realname=SDL2_ttf
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=2.0.18
pkgrel=2
pkgdesc="A library that allows you to use TrueType fonts in your SDL applications (Version 2) (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://libsdl.org/projects/SDL_ttf"
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-SDL2"
         "${MINGW_PACKAGE_PREFIX}-freetype"
         "${MINGW_PACKAGE_PREFIX}-harfbuzz")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-autotools")
source=("${url}/release/${_realname}-${pkgver}.zip"
        https://github.com/libsdl-org/SDL_ttf/commit/ff94044.patch
        https://github.com/libsdl-org/SDL_ttf/commit/d7fe570.patch
        https://github.com/libsdl-org/SDL_ttf/commit/b8f6d5b.patch)
sha256sums=('64e6a93c7542aba1e32e1418413898dfde82be95fdd0c73ba265fbdada189b5f'
            '9169d463c2894642652bbe2dc22e3d2edc0da7d031737757284492669d73cf68'
            '453a9401b9191049edba01fe869b5786ae05975b89a6a6ddcde5f2961ee4e08e'
            '44afbc408ac257c83232e6fad114cb113c5eb3a002bf93fd774a0c97c5a12b07')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -p1 -i "${srcdir}/ff94044.patch"
  patch -p1 -i "${srcdir}/d7fe570.patch"
  patch -p1 -i "${srcdir}/b8f6d5b.patch"
  # Do not force generate to pacify libtool
  autoreconf
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --enable-static \
    --disable-freetype-builtin \
    --disable-harfbuzz-builtin

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}/"

  make DESTDIR="${pkgdir}/" install
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
