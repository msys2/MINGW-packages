# Maintaner: Marty Plummer (The_NetZ) <ntzrmtthihu777 (at) gmail (dot) com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

_realname=SDL2_ttf
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=2.0.12
pkgrel=2
pkgdesc="A library that allows you to use TrueType fonts in your SDL applications (Version 2)"
arch=('any')
url="http://www.libsdl.org/projects/SDL_ttf"
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-SDL2"
         "${MINGW_PACKAGE_PREFIX}-freetype")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake")
source=("$url/release/${_realname}-${pkgver}.tar.gz")
md5sums=('79787216b56cb4707f39d538f2225e00')

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}

  ${srcdir}/${_realname}-${pkgver}/configure \
    --disable-static \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --enable-static

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}/"

  make DESTDIR="${pkgdir}/" install
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
