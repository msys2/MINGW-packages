# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=pivy
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.6.9.a0
pkgrel=1
pkgdesc='Python bindings to Coin3D (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/coin3d/pivy'
msys2_references=(
  'archlinux: python-pivy'
)
license=('spdx:ISC')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-soqt-qt6")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python-wheel"
             "${MINGW_PACKAGE_PREFIX}-swig"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
source=("${url}/archive/${pkgver}/${_realname}-${pkgver}.tar.gz"
        "001-pivy-0.6.5-setup-py-msystem-env.patch"
        "003-find-qt6.patch"
        "004-fix-compile-flags.patch"
        "005-fix-install-destination.patch")
sha256sums=('2c2da80ae216fe06394562f4a8fc081179d678f20bf6f8ec412cda470d7eeb91'
            '0a37cf9f25ed3b0aca4547b484f448c840cf6a4d1f604ceed7094f8970a9c5b5'
            '0451f0daeebfadc5a69d437474c40180cbb61c9213ac15d1c91a2c2d1d474142'
            'e48a9c48999fa6fe01a913357407f13242727cfc350b963b14818c7ed0b348a6'
            'eb9fc0057ace20864aba5faaa68a2feaffef1a6abb948fc21958040cb9824e92')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

prepare() {
  cd "${srcdir}"/"${_realname}-${pkgver}"

  apply_patch_with_msg \
    001-pivy-0.6.5-setup-py-msystem-env.patch \
    003-find-qt6.patch \
    004-fix-compile-flags.patch \
    005-fix-install-destination.patch
}

build() {
  rm -rf "python-build-${MSYSTEM}" | true
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  GENERATOR="Ninja" \
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}

# vim: ts=2 sw=2 et:
