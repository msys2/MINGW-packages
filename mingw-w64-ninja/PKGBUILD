# Maintainer: Martell Malone <Martell Malone at g mail dot com>

_realname=ninja
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.9.0
pkgrel=2
pkgdesc="Ninja is a small build system with a focus on speed (mingw-w64)"
arch=('any')
url="https://ninja-build.org"
license=('Apache')
depends=()
options=('strip' 'staticlibs')
makedepends=("re2c" "${MINGW_PACKAGE_PREFIX}-python3" "${MINGW_PACKAGE_PREFIX}-emacs")
optdepends=("${MINGW_PACKAGE_PREFIX}-emacs: for ninja emacs mode")
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/ninja-build/ninja/archive/v${pkgver}.tar.gz")
sha256sums=('5d7ec75828f8d3fd1a0c2f31b5b0cea780cdfe1031359228c428c1a48bfcd5b9')

prepare () {
  cd ${srcdir}/ninja-${pkgver}
  #linking assumes windows cmd line but we are bash :)
  sed -i 's|cmd /c $ar cqs $out.tmp $in && move /Y $out.tmp $out|$ar crs $out $in|g' ${srcdir}/ninja-${pkgver}/configure.py
}

build() {
  mkdir -p "${pkgdir}${MINGW_PREFIX}"/bin
  cd ${srcdir}/ninja-${pkgver}
  ${MINGW_PREFIX}/bin/python3 configure.py --bootstrap --platform mingw
}

package() {
  mkdir -p "${pkgdir}${MINGW_PREFIX}"/bin
  mv "${srcdir}/ninja-${pkgver}/ninja.exe" "${pkgdir}${MINGW_PREFIX}"/bin/
  mkdir -p "${pkgdir}${MINGW_PREFIX}"/lib
  mv "${srcdir}/ninja-${pkgver}/build/libninja.a" "${pkgdir}${MINGW_PREFIX}"/lib/
  # Emacs mode
  install -d "${pkgdir}${MINGW_PREFIX}"/share/emacs/site-lisp/
  cp "${srcdir}/${_realname}-$pkgver/misc/ninja-mode.el" "${pkgdir}${MINGW_PREFIX}"/share/emacs/site-lisp/
  ${MINGW_PREFIX}/bin/emacs -batch -f batch-byte-compile \
    "${pkgdir}${MINGW_PREFIX}"/share/emacs/site-lisp/ninja-mode.el

  # vim syntax - leave this commented until mingw-w64-vim is actually made
#  vimpath="${pkgdir}${MINGW_PREFIX}/share/vim/vimfiles"
#  install -d "${vimpath}"/syntax
#  cp -r "${srcdir}/${_realname}-$pkgver/misc/ninja.vim" "${vimpath}/syntax/"
}
