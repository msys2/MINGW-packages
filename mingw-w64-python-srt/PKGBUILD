# Maintainer: Naveen M K <naveen521kk@gmail.com>

_pyname=srt
_realname=srt
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=3.5.3
pkgrel=3
pkgdesc='A tiny library for parsing, modifying, and composing SRT files. (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'purl: pkg:pypi/srt'
)
url="https://github.com/cdown/srt"
license=('MIT')
depends=(
  "${MINGW_PACKAGE_PREFIX}-python" 
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python-setuptools" 
)
source=("${_pyname}-${pkgver}.tar.gz::https://pypi.org/packages/source/${_pyname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha256sums=('4884315043a4f0740fd1f878ed6caa376ac06d70e135f306a6dc44632eed0cc0')

prepare() {  
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_pyname//_/-}-$pkgver" "python-build-${MSYSTEM}"
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  msg "Python build for ${MSYSTEM}"  
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"

}
