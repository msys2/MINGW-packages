# Maintainer: AlexWMF <alexxwmf@gmail.com>

_realname=libvmime
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=r1024.3848556
pkgrel=2
pkgdesc="An open source solution for working with MIME messages and Internet messaging services like IMAP, POP or SMTP (mingw-w64)"
url="http://www.vmime.org/"
arch=('any')
license=('GPL3')
options=('strip' 'staticlibs')
makedepends=("${MINGW_PACKAGE_PREFIX}-boost"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-doxygen"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "git")
depends=("${MINGW_PACKAGE_PREFIX}-icu"
        "${MINGW_PACKAGE_PREFIX}-gnutls"
        "${MINGW_PACKAGE_PREFIX}-gsasl"
        "${MINGW_PACKAGE_PREFIX}-libiconv"
        #"${MINGW_PACKAGE_PREFIX}-openssl"
        )
source=("${_realname}"::"git+https://github.com/kisli/vmime.git"
        cmake_mlang.patch
        libvmime-versioning.patch)
md5sums=('SKIP'
         '14b404eee1ea38be8d6d1673df54dc59'
         '510196ab043824bb0780ccf5e02cf323')

pkgver() {
  cd "$srcdir/$_realname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${_realname}"
  patch -p0 -i ${srcdir}/cmake_mlang.patch
  patch -p1 -i ${srcdir}/libvmime-versioning.patch
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    -DVMIME_HAVE_MESSAGING_PROTO_SENDMAIL:BOOL=OFF \
    -DVMIME_BUILD_SAMPLES:BOOL=OFF \
    -DVMIME_TLS_SUPPORT_LIB=gnutls \
    ../${_realname}

  make
}

package() {
  cd build-${MINGW_CHOST}
  make install

  mkdir -p ${pkgdir}${MINGW_PREFIX}
  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null

  pushd ${MINGW_PREFIX} > /dev/null
  export PREFIX_DEP=`pwd -W`
  popd > /dev/null
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/vmime.pc
  sed -s "s|${PREFIX_DEP}|${MINGW_PREFIX}|g" \
    -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/vmime.pc
}
