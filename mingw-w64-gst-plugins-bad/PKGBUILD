# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gst-plugins-bad
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.6.1
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Bad Plugins (mingw-w64)"
arch=('any')
url="http://gstreamer.freedesktop.org/"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-pkg-config"
            "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
            )
depends=(
        "${MINGW_PACKAGE_PREFIX}-celt"
        "${MINGW_PACKAGE_PREFIX}-chromaprint"
        "${MINGW_PACKAGE_PREFIX}-clutter"
        "${MINGW_PACKAGE_PREFIX}-curl"
        "${MINGW_PACKAGE_PREFIX}-daala"
        "${MINGW_PACKAGE_PREFIX}-faad2"
        "${MINGW_PACKAGE_PREFIX}-faac"
        "${MINGW_PACKAGE_PREFIX}-fluidsynth"
        "${MINGW_PACKAGE_PREFIX}-gnutls"
        "${MINGW_PACKAGE_PREFIX}-gsm"
        "${MINGW_PACKAGE_PREFIX}-gst-plugins-base"
        "${MINGW_PACKAGE_PREFIX}-gtk3"
        "${MINGW_PACKAGE_PREFIX}-libdca"
        "${MINGW_PACKAGE_PREFIX}-libdvdnav"
        "${MINGW_PACKAGE_PREFIX}-libexif"
        "${MINGW_PACKAGE_PREFIX}-libgme"
        "${MINGW_PACKAGE_PREFIX}-libjpeg"
        "${MINGW_PACKAGE_PREFIX}-libmimic"
        "${MINGW_PACKAGE_PREFIX}-libmodplug"
        "${MINGW_PACKAGE_PREFIX}-libmpeg2"
        "${MINGW_PACKAGE_PREFIX}-libpng"
        "${MINGW_PACKAGE_PREFIX}-librsvg"
        "${MINGW_PACKAGE_PREFIX}-libwebp"
        "${MINGW_PACKAGE_PREFIX}-libxml2"
        "${MINGW_PACKAGE_PREFIX}-mpg123"
        "${MINGW_PACKAGE_PREFIX}-nettle"
        "${MINGW_PACKAGE_PREFIX}-openal"
        "${MINGW_PACKAGE_PREFIX}-opencv"
        "${MINGW_PACKAGE_PREFIX}-openexr"
        "${MINGW_PACKAGE_PREFIX}-openjpeg2"
        "${MINGW_PACKAGE_PREFIX}-opus"
        "${MINGW_PACKAGE_PREFIX}-orc"
        "${MINGW_PACKAGE_PREFIX}-schroedinger"
        "${MINGW_PACKAGE_PREFIX}-soundtouch"
        )
options=(!libtool strip staticlibs)
source=(${url}/src/${_realname}/${_realname}-${pkgver}.tar.xz
        0002-better-openjpeg2-checking.all.patch
        0004-port-to-openjpeg-2.1.all.patch)
md5sums=('c92d7d32de68e7293712bf9b6c99dc77'
         '4545ec337afbd4d211d4b088b01ed77e'
         'c269630a0a08f750da9db84df1c82c49')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0002-better-openjpeg2-checking.all.patch
  patch -p1 -i ${srcdir}/0004-port-to-openjpeg-2.1.all.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  CPPFLAGS+=" --save-temps"
  CFLAGS+=" --save-temps"
  
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-static \
    --enable-shared \
    --disable-silent-rules \
    --enable-experimental \
    --disable-gtk-doc \
    --with-gtk=3.0

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="${pkgdir}" install
  
  rm -f ${pkgdir}${MINGW_PREFIX}/lib/gstreamer*/*.a
}
