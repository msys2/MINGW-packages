# Maintainer: Frode Solheim <frode@fs-uae.net>

_realname=lhafile
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-lhafile"
         "${MINGW_PACKAGE_PREFIX}-python3-lhafile")
pkgver=0.2.2
pkgrel=1
pkgdesc="LHA module for Python (mingw-w64)"
arch=('any')
license=('BSD')
url="https://fengestad.no/python-lhafile"
makedepends=("${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-python3")
options=('staticlibs' 'strip' '!debug')
source=(${_realname}-${pkgver}.tar.gz::https://github.com/FrodeSolheim/python-lhafile/archive/v${pkgver}.tar.gz)
sha256sums=('18396276b77d8c7e094f169d3b2471e60de3d412a2454a0f73c452a54817ee68')

prepare() {
  cp -a python-lhafile-${pkgver} python2-lhafile-${pkgver}-${CARCH}
  cp -a python-lhafile-${pkgver} python3-lhafile-${pkgver}-${CARCH}
}

build() {
  echo "Building for Python 2"
  cd python2-lhafile-${pkgver}-${CARCH}
  ${MINGW_PREFIX}/bin/python2 setup.py build
  cd ..

  echo "Building for Python 3"
  cd python3-lhafile-${pkgver}-${CARCH}
  ${MINGW_PREFIX}/bin/python3 setup.py build
}

package_python2-lhafile() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2"
           "${MINGW_PACKAGE_PREFIX}-python2-six")

  cd python2-lhafile-${pkgver}-${CARCH}
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1

  install -m755 -d "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-lhafile"
  install -m644 COPYING.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-lhafile/"
}

package_python3-lhafile() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3"
           "${MINGW_PACKAGE_PREFIX}-python3-six")

  cd python3-lhafile-${pkgver}-${CARCH}
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1

  install -m755 -d "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-lhafile"
  install -m644 COPYING.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-lhafile/"
}

package_mingw-w64-i686-python2-lhafile() {
  package_python2-lhafile
}

package_mingw-w64-i686-python3-lhafile() {
  package_python3-lhafile
}

package_mingw-w64-x86_64-python2-lhafile() {
  package_python2-lhafile
}

package_mingw-w64-x86_64-python3-lhafile() {
  package_python3-lhafile
}
