# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=hyperlink
pkgbase=mingw-w64-python-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-${_realname}
pkgver=21.0.0
pkgrel=5
pkgdesc='A featureful, immutable, and correct URL for Python (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/python-hyper/hyperlink'
msys2_references=(
  'purl: pkg:pypi/hyperlink'
)
license=('spdx:MIT')
depends=(${MINGW_PACKAGE_PREFIX}-python
         ${MINGW_PACKAGE_PREFIX}-python-idna)
makedepends=(${MINGW_PACKAGE_PREFIX}-python-build
             ${MINGW_PACKAGE_PREFIX}-python-installer
             ${MINGW_PACKAGE_PREFIX}-python-setuptools)
source=(https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz)
sha256sums=('427af957daa58bc909471c6c40f74c5450fa123dd093fc53efd2e91d2705a56b')

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}"/python-build-${MSYSTEM}

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
