# Maintainer: Andrew Chadwick <a.t.chadwick@gmail.com>

_realname=graphene
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.6.0
pkgrel=1
arch=('any')
pkgdesc="A thin layer of types for graphic libraries (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-gettext")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-python3")
#options=('!strip' 'debug' 'staticlibs')
options=('strip' '!debug')
license=("MIT")
url="https://ebassi.github.io/graphene/"
source=("${_realname}-${pkgver}.tar.gz::https://github.com/ebassi/graphene/archive/${pkgver}.tar.gz"
        "fix-mem-align.patch")
sha256sums=('98970f859e452ce421b72726ca727fdf3ac27cb4804b62bfe520157fa46aa2fd'
            '02b31dfafbb8458e4967773d126ff64725f2ada0379c24f533cc6d4e431cb76c')

prepare() {
    cd "${srcdir}"/${_realname}-${pkgver}

    patch -p1 -i ${srcdir}/fix-mem-align.patch

    NOCONFIGURE=true ./autogen.sh
}

build() {
    [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
    cp -a "${srcdir}/${_realname}-${pkgver}" build-${MINGW_CHOST}
    cd build-${MINGW_CHOST}
    ./configure \
        --enable-introspection=no \
        --prefix=${MINGW_PREFIX}
    make
}

package() {
    cd "${srcdir}"/build-${MINGW_CHOST}
    make -j1 DESTDIR="${pkgdir}" install
    for f in LICENSE README.md; do
        install -Dm644 ${srcdir}/${_realname}-${pkgver}/${f} \
            "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/${f}
    done
}
