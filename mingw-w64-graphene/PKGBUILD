# Maintainer: Andrew Chadwick <a.t.chadwick@gmail.com>

_realname=graphene
_realver=1.4.0
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=${_realver}
pkgrel=1
arch=('any')
pkgdesc="A thin layer of types for graphic libraries (mingw-w64)"
depends=(
    "${MINGW_PACKAGE_PREFIX}-glib2"
    "${MINGW_PACKAGE_PREFIX}-gettext"
)
makedepends=(
    # "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
)
#options=('!strip' 'debug' 'staticlibs')
options=('strip' '!debug')
license=("MIT")
url="http://ebassi.github.com/graphene"
source=("${_realname}-${_realver}.tar.gz::https://github.com/ebassi/graphene/archive/${_realver}.tar.gz")
sha256sums=('de80295fc62fe88be9617cc696c205947d354176f7fd60c217647b4843944219')

prepare() {
    cd "${srcdir}"/${_realname}-${_realver}
    NOCONFIGURE=true ./autogen.sh
}

build() {
    [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
    cp -a "${srcdir}/${_realname}-${_realver}" build-${MINGW_CHOST}
    cd build-${MINGW_CHOST}
    ./configure \
        --enable-introspection=no \
        --prefix=${MINGW_PREFIX}
    make
}

package() {
    cd "${srcdir}"/build-${MINGW_CHOST}
    make -j1 DESTDIR="${pkgdir}" install
    for f in LICENSE README.md; do
        install -Dm644 ${srcdir}/${_realname}-${_realver}/$f \
            "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/$f
    done
}
