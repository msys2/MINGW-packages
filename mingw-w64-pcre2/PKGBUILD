# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=pcre2
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=10.23
pkgrel=1
pkgdesc="A library that implements Perl 5-style regular expressions (mingw-w64)"
arch=('any')
url="http://pcre.org/"
license=("BSD")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-bzip2"
         #"${MINGW_PACKAGE_PREFIX}-readline"
         "${MINGW_PACKAGE_PREFIX}-wineditline"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' 'strip')
# Remove patches when upgrading to 10.30:
# https://vcs.pcre.org/pcre2/code/trunk/src/pcre2grep.c?r1=691&r2=690&pathrev=691&view=patch
# https://vcs.pcre.org/pcre2/code/trunk/src/pcre2grep.c?r1=737&r2=736&pathrev=737&view=patch
source=(https://ftp.pcre.org/pub/pcre/${_realname}-${pkgver}.tar.bz2{,.sig}
        rev691.patch
        rev737.patch)
sha256sums=('dfc79b918771f02d33968bd34a749ad7487fa1014aeb787fad29dd392b78c56e'
            'SKIP'
            '5231aa1f95c313348dd89a8e900225bb236bcbf9d28a21633c926ccfffccf51e'
            '7e4d5991e52202c66b1f1c372eb4be5999d954ead99f0978cff022c8cb49ed32')
validpgpkeys=('45F68D54BBE23FB3039B46E59766E084FB0F43D8')

prepare() {
  [[ -d ${srcdir}/${_realname}-${pkgver} ]] && rm -rf ${srcdir}/${_realname}-${pkgver}
  tar -xvf ${srcdir}/${_realname}-${pkgver}.tar.bz2 -C ${srcdir} || true

  cd "${srcdir}/${_realname}-${pkgver}"

  patch -Np2 -i ${srcdir}/rev691.patch
  patch -Np2 -i ${srcdir}/rev737.patch
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  "${srcdir}"/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-pcre2-8 \
    --enable-pcre2-16 \
    --enable-pcre2-32 \
    --enable-pcre2grep-libz \
    --enable-pcre2grep-libbz2 \
    --enable-pcre2test-libedit

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  # Two pcre2grep failures. Retry when upgrading to 10.30
  make check || true
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  # License files
  cd "${pkgdir}${MINGW_PREFIX}/share"
  mkdir -p licenses/${_realname}
  mv doc/${_realname}/COPYING licenses/${_realname}
  mv doc/${_realname}/LICENCE licenses/${_realname}
}
