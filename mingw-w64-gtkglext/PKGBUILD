# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gtkglext
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.2.0
pkgrel=1
pkgdesc="opengl extensions for gtk2 (mingw-w64)"
arch=('any')
url="http://gtkglext.sourceforge.net"
license=("LGPL")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config")
depends=("${_mingw_suff}-gcc-libs" "${_mingw_suff}-gtk2" "${_mingw_suff}-gdk-pixbuf2")
options=('staticlibs' 'strip')
source=("http://downloads.sourceforge.net/sourceforge/gtkglext/gtkglext-${pkgver}.tar.bz2"
    "gtk2.20.patch"
    "gtkglext-1.0.6-pangox.patch"
    )
md5sums=('ed7ba24ce06a8630c07f2d0ee5f04ab4'
         'e5a87ec3f2d0e616c6f32f90c3f7237f'
         '7e35925b3c722717f969558cc627f39f')

prepare() {
  cd "${srcdir}/gtkglext-${pkgver}"
  patch -Np1 -i "${srcdir}/gtk2.20.patch"
  patch -p1 -R -i "${srcdir}/gtkglext-1.0.6-pangox.patch"
  autoconf --force
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  cp -r "${srcdir}/gtkglext-${pkgver}" "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --with-gdktarget=win32 \
    --without-x \
    --disable-glibtest \
    --disable-gtktest \
    --disable-debug
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install
  find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
  #rm -r "${pkgdir}${MINGW_PREFIX}/share"
}