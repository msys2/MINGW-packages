# Maintainer: @naveen521kk on Github, Naveen M K <naveen521kk@gmail.com>

_perlname=Business-ISMN
_realname="${_perlname,,}"
pkgbase=mingw-w64-perl-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-perl-${_realname}")
pkgver=1.202
pkgrel=1
pkgdesc="work with International Standard Music Numbers (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://metacpan.org/release/${_perlname}"
groups=("${MINGW_PACKAGE_PREFIX}-perl-modules")
provides=(
  "${MINGW_PACKAGE_PREFIX}-perl-business-ismn-data"
)
depends=(
  "${MINGW_PACKAGE_PREFIX}-perl"
  "${MINGW_PACKAGE_PREFIX}-perl-list-util"
  "${MINGW_PACKAGE_PREFIX}-perl-tie-cycle"
)
options=('!emptydirs')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-cc"
  $([[ ${MINGW_PACKAGE_PREFIX} != *-clang-* ]] || \
    echo "${MINGW_PACKAGE_PREFIX}-gcc-compat")
)
license=('artistic_2')
source=(
  'https://cpan.metacpan.org/authors/id/B/BD/BDFOY/Business-ISMN-1.202.tar.gz'
  'patchmakefile.py'
)
sha256sums=('ca8db9253ddda906b47d5068e745b9f2a03754589455ffcf26b0706c8194db26'
            '00fe5ced42aa635714909a8eba1347a6c132011a9bce68e9c75b27ebd64628f2')

build() {
  cd "$srcdir/${_perlname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL
  export pkgdir=$pkgdir
  python $srcdir/patchmakefile.py
  mingw32-make
}

package() {
  cd "$srcdir/${_perlname}-${pkgver}"
  mingw32-make DESTDIR="${pkgdir}/" install
}
