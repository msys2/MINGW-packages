# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=xz
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=5.0.5
pkgrel=2
pkgdesc="Library and command line tools for XZ and LZMA compressed files (mingw-w64)"
arch=('any')
url="http://tukaani.org/xz"
license=("custom" "GPL" "LGPL")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config")
depends=("${_mingw_suff}-gcc-libs" "${_mingw_suff}-gettext")
options=('staticlibs' 'strip')
source=("$url/xz-${pkgver}.tar.gz")
md5sums=('19d924e066b6fff0bc9d1981b4e53196')

build() {
  mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}
  ../xz-$pkgver/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --disable-rpath \
    --disable-lzma-links
  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="$pkgdir" install
  #rm "${pkgdir}${MINGW_PREFIX}/bin/"{lz{cmp,diff,egrep,fgrep,grep,less,more},xz{cmp,diff,egrep,fgrep,grep,less,more}}
  #rm -r "${pkgdir}${MINGW_PREFIX}/share"
}

