# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=ispc
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.18.0
pkgrel=1
pkgdesc="Intel SPMD Program Compiler (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url='https://ispc.github.io'
license=('BSD-3-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-libssp"
         "${MINGW_PACKAGE_PREFIX}-llvm")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-clang"
             "${MINGW_PACKAGE_PREFIX}-python")
source=("https://github.com/${_realname}/${_realname}/archive/v${pkgver}.tar.gz"
        001-fix-target-triple.patch
        002-no-_xgetbv-on-mingw.patch
        003-fix-compile-and-link-options.patch)
sha256sums=('81f2cc23b555c815faf53429e9eee37d1f2f16873ae7074e382ede94721ee042'
            'cd7e038db1c49d270b34a4ee39d5986d9f398c22eb91ce9523065892a38de60f'
            'aa4fc101ea4727feb502c78a19f5b2cbb63494786066fb06649fac5fc655de38'
            '4c60a5717dbd54b12c4ee7f230e6fc9bb79c8cfc126340f97018706b2c4d2c35')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  apply_patch_with_msg \
    001-fix-target-triple.patch \
    002-no-_xgetbv-on-mingw.patch \
    003-fix-compile-and-link-options.patch

  sed -e "s|__declspec(safe, |__declspec(safe) __declspec(|g" -i stdlib.ispc
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DPython3_EXECUTABLE=${MINGW_PREFIX}/bin/python \
      -DISPC_NO_DUMPS=ON \
      -DISPC_INCLUDE_RT=OFF \
      -DISPC_INCLUDE_EXAMPLES=OFF \
      -DISPC_INCLUDE_TESTS=OFF \
      -DISPC_INCLUDE_UTILS=OFF \
      ../${_realname}-${pkgver}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE.txt" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
