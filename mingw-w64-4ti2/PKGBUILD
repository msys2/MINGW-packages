# Maintainer: Dirk Stolle

_realname=4ti2
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.6.13
pkgrel=1
pkgdesc="Software package for algebraic, geometric and combinatorial problems on linear spaces (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://4ti2.github.io/'
msys2_references=(
  'archlinux: 4ti2'
  'cygwin: lib4ti2'
  'gentoo: sci-mathematics/4ti2'
)
license=('spdx:GPL-2.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-glpk"
         "${MINGW_PACKAGE_PREFIX}-gmp"
         'bash')
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc"
             'git')
source=(git+https://github.com/4ti2/4ti2#tag=Release_${pkgver//./_}
        0001-fix-timer-code.patch)
sha256sums=('2345dcc8d17c3632716950af16d416a971a3d81f36ca858e63e8d3ee42f166cb'
            'c8865e4becca2a2ce74565869e90d55ee133c879626ad04c5cd187bc50616cb1')

prepare() {
  cd "${_realname}"

  patch -p1 -i "${srcdir}/0001-fix-timer-code.patch"

  autoreconf -fiv
}

build() {
  mkdir -p "build-${MSYSTEM}" && cd "build-${MSYSTEM}"

  ../"${_realname}"/configure \
    --prefix="${MINGW_PREFIX}" \
    --build="${MINGW_CHOST}" \
    --host="${MINGW_CHOST}" \
    --target="${MINGW_CHOST}" \
    --disable-static \
    --enable-shared

  # Fix overlinking (copied from Arch's PKGBUILD)
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd "build-${MSYSTEM}"

  make check
}

package() {
  cd "build-${MSYSTEM}"

  make install DESTDIR="${pkgdir}"

  install -Dm644 "${srcdir}/${_realname}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
