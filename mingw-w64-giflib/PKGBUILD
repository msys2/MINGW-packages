# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=giflib
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=5.1.8
pkgrel=2
pkgdesc="A library for reading and writing gif images (mingw-w64)"
arch=('any')
url="https://sourceforge.net/projects/giflib"
license=("MIT")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "xmlto")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
options=('staticlibs' 'strip')
source=("https://downloads.sourceforge.net/sourceforge/giflib/${_realname}-${pkgver}.tar.gz"
        "001-mingw-build.patch")
sha256sums=('d105a905df34a7822172d13657cdae3d4b0c8e8c7067ccf05e39a40044f8ca53'
            'a2f7a25a75041def0b775367a7497658a89e5c7c55fec716fdd965aa97fb97b5')
noextract=(${_realname}-${pkgver}.tar.gz)

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

prepare() {
  [[ -d ${srcdir}/${_realname}-${pkgver} ]] && rm -rf ${srcdir}/${_realname}-${pkgver}
  tar -xzf ${srcdir}/${_realname}-${pkgver}.tar.gz -C ${srcdir} || true

  cd "${srcdir}/${_realname}-${pkgver}"
  apply_patch_with_msg "001-mingw-build.patch"
}

build() {
  rm -rf "${srcdir}/build-${CARCH}"
  cp -r "${srcdir}/${_realname}-${pkgver}" "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"
  CC=${MINGW_PREFIX}/bin/gcc make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" PREFIX="${MINGW_PREFIX}" install
}
