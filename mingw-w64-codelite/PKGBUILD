# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Tim Stahlhut <stahta01@gmail.com>

_realname=codelite
_wx_basever=3.2
pkgbase=mingw-w64-${_realname}
pkgname=(
  "${MINGW_PACKAGE_PREFIX}-${_realname}"
  "${MINGW_PACKAGE_PREFIX}-${_realname}-wxcrafter"
)
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
pkgver=16.2.0
pkgrel=1
pkgdesc="Open-source, cross platform IDE for the C/C++ programming languages (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
license=('GPL')
url="https://www.codelite.org/"
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-hunspell"
         "${MINGW_PACKAGE_PREFIX}-libssh"
         "${MINGW_PACKAGE_PREFIX}-drmingw"
         "${MINGW_PACKAGE_PREFIX}-clang"      # libclang
         "${MINGW_PACKAGE_PREFIX}-uchardet"
         "${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-msw-libs"
         "${MINGW_PACKAGE_PREFIX}-sqlite3"
         )
makedepends=("${MINGW_PACKAGE_PREFIX}-boost"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-msw"
             #"${MINGW_PACKAGE_PREFIX}-clang"
            )
options=(!strip staticlibs !debug)
#options=(!strip staticlibs debug)
source=("${_realname}-${pkgver}.tar.gz::https://github.com/eranif/codelite/archive/${pkgver}.tar.gz"
        '001-disable-adding-DebugAdapterClient-folder.patch'
        '002-Comment-out-wxCrafter-plugin.patch'
        '003-wxCrafter-standalone-build-fixes-for-MSys2.patch')
sha256sums=('786ca889f49c86a42b1d7b631e20b23516e258392b159d14d4d9157fa909aa31'
            '97452e97a42d5742b231b56c6e8b84089cc0186aad541bea6c590406f8b9b9f2'
            '8a88a522f3f63e6c4841a309981bcc060bb83af584811af62770eab7a9874b6f'
            'ad3e68b82f4cf281603409b3ec551dadfeb508964975172b6f4c067ce5edeaf6')
prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # The tar.gz is missing the wxdap/dap folder so disable the debug
  # client that wants it. Next, full release try without the patch.
  patch -p1 -i "${srcdir}"/001-disable-adding-DebugAdapterClient-folder.patch

  # wxcrafter plugin is seg faulting
  patch -p1 -i "${srcdir}"/002-Comment-out-wxCrafter-plugin.patch

  patch -p1 -i "${srcdir}"/003-wxCrafter-standalone-build-fixes-for-MSys2.patch
}

build() {
  local -a extra_config

  if check_option "debug" "y"; then
    extra_config+=( -DCMAKE_BUILD_TYPE=Debug )
  else
    extra_config+=( -DCMAKE_BUILD_TYPE=Release )
  fi

  extra_config+=( -Wno-dev ) # Hides developer warnings

  # disables the part of urlmon.h that defines IsLoggingEnabled
  # This replaces 010-strange-conflicts.patch
  CXXFLAGS+=" -D_HITLOGGING_DEFINED=0"

  # build wxCrafter as stand alone app
  mkdir -p build-${MINGW_CHOST}-wxcrafter
  cd build-${MINGW_CHOST}-wxcrafter
  MSYS2_ARG_CONV_EXCL="-DCL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCL_PREFIX=${MINGW_PREFIX} \
    -DWXC_APP=1 \
    -DWITH_SFTP=0 \
    -DWITH_WX_CONFIG=${MINGW_PREFIX}/bin/wx-config-${_wx_basever} \
    -DwxWidgets_wxrc_EXECUTABLE=${MINGW_PREFIX}/bin/wxrc-${_wx_basever} \
    "${extra_config[@]}" \
    ../${_realname}-${pkgver}

  make # -j1 VERBOSE=1

  cd "${srcdir}"

  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}
  MSYS2_ARG_CONV_EXCL="-DCL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCL_PREFIX=${MINGW_PREFIX} \
    -DWITH_WX_CONFIG=${MINGW_PREFIX}/bin/wx-config-${_wx_basever} \
    -DwxWidgets_wxrc_EXECUTABLE=${MINGW_PREFIX}/bin/wxrc-${_wx_basever} \
    "${extra_config[@]}" \
    ../${_realname}-${pkgver}

  make # -j1 VERBOSE=1
}

package_codelite() {
  optdepends=("${MINGW_PACKAGE_PREFIX}-clang")

  cd build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
  # why?
  rm -f ${pkgdir}${MINGW_PREFIX}/lib/codelite/*.a
  rm -f ${pkgdir}${MINGW_PREFIX}/lib/*.a
}

package_codelite-wxcrafter() {
  pkgdesc="CodeLite's wxCrafter (mingw-w64)"
  optdepends=()
  depends=("${MINGW_PACKAGE_PREFIX}-${_realname}")

  cd build-${MINGW_CHOST}-wxcrafter
  make DESTDIR=${pkgdir} install
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}-wxcrafter/LICENSE

  # Remove DLLs provided by CodeLite IDE
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/codelite.dll
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/libplugin.dll
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/libwxshapeframework.dll
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/libwxsqlite3.dll
  # Remove lib that makes sense only for IDE package
  rm -rf ${pkgdir}${MINGW_PREFIX}/lib/
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
