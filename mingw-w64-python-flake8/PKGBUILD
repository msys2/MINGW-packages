# $Id$
# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=flake8
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}" "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=3.6.0
pkgrel=1
pkgdesc="The modular source code checker: pep8, pyflakes and co"
arch=('any')
url="http://flake8.pycqa.org"
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools" 
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-pyflakes" 
             "${MINGW_PACKAGE_PREFIX}-python2-pyflakes"
             "${MINGW_PACKAGE_PREFIX}-python3-pycodestyle" 
             "${MINGW_PACKAGE_PREFIX}-python2-pycodestyle" 
             "${MINGW_PACKAGE_PREFIX}-python3-mccabe" 
             "${MINGW_PACKAGE_PREFIX}-python2-mccabe"
             "${MINGW_PACKAGE_PREFIX}-python2-enum34"
             "${MINGW_PACKAGE_PREFIX}-python2-configparser"
             "${MINGW_PACKAGE_PREFIX}-python3-pytest-runner"
             "${MINGW_PACKAGE_PREFIX}-python2-pytest-runner")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python3-mock" "${MINGW_PACKAGE_PREFIX}-python2-mock")
source=("${_realname}-$pkgver.tar.gz"::"https://github.com/PyCQA/flake8/archive/${pkgver}.tar.gz")
sha512sums=('112f02fbeb93c7d6088f057936013b76ccf38258e3674cae32b0abfdbbe0ad553c57065e8128906c4fbbe49278a15c3f32d2326e2f8b8b889003de53c8ebf0f1')

prepare() {
  # disable overly restrictive dependency pinning
  sed -i -e 's/, *< *[0-9=.]*//' flake8-$pkgver/setup.py
  for builddir in python{2,3}-build-${CARCH}; do
    rm -rf ${builddir} | true
    cp -r "${_realname}-${pkgver}" "${builddir}"
  done
  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  for pver in {2,3}; do  
    msg "Python ${pver} build for ${CARCH}"  
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py build
  done  
}

check() {
  for pver in {2,3}; do
    msg "Python ${pver} test for ${CARCH}"
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py pytest
  done
}

package_python3-flake8() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3-pyflakes"
   "${MINGW_PACKAGE_PREFIX}-python3-mccabe" 
   "${MINGW_PACKAGE_PREFIX}-python3-pycodestyle")
  install=${_realname}3-${CARCH}.install

  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py install --root="$pkgdir" --prefix=${MINGW_PREFIX} --optimize=1
  install -D -m644 LICENSE "$pkgdir${MINGW_PREFIX}"/share/licenses/$pkgname/LICENSE

# This entire section should be removed if the package does NOT install
# anything in the /mingw*/bin directory.
### begin section ###
  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  # fix python command in files
  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*.py; do
    sed -e "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" -i ${_f}
  done
#### end section ####
}

package_python2-flake8() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2-pyflakes"
           "${MINGW_PACKAGE_PREFIX}-python2-mccabe"
           "${MINGW_PACKAGE_PREFIX}-python2-pycodestyle"
           "${MINGW_PACKAGE_PREFIX}-python2-enum34"
           "${MINGW_PACKAGE_PREFIX}-python2-configparser")
  install=${_realname}2-${CARCH}.install

  cd "${srcdir}/python2-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -D -m644 LICENSE "$pkgdir${MINGW_PREFIX}"/share/licenses/$pkgname/LICENSE

# This entire section should be removed if the package does NOT install
# anything in the /mingw*/bin directory.
### begin section ###
  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  # fix python command in files
  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*.py; do
    sed -e "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" -i ${_f}
  done

# for Python2 packages, you want to rename some stuff from the bin directory 
# with the 2 suffix like in the mingw-w64-python-pygments package to avoid
# conflicts when installing both the Python2 and Python3 packages
  for f in flake8; do
    mv "${pkgdir}${MINGW_PREFIX}"/bin/${f}{,2}.exe
    if [ -f "${pkgdir}${MINGW_PREFIX}"/bin/${f}.exe.manifest ]; then
      mv "${pkgdir}${MINGW_PREFIX}"/bin/${f}{,2}.exe.manifest
      sed -e "s|${f}|${f}2|g" -i "${pkgdir}${MINGW_PREFIX}"/bin/${f}2.exe.manifest
    fi
    mv "${pkgdir}${MINGW_PREFIX}"/bin/${f}{,2}-script.py
  done
#### end section ####
}

package_mingw-w64-i686-python2-flake8() {
  package_python2-flake8
}

package_mingw-w64-i686-python3-flake8() {
  package_python3-flake8
}

package_mingw-w64-x86_64-python2-flake8() {
  package_python2-flake8
}

package_mingw-w64-x86_64-python3-flake8() {
  package_python3-flake8
}

