# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=uv
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}"
         "${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.1.34
pkgrel=1
pkgdesc='An extremely fast Python package installer and resolver, written in Rust (mingw-w64)'
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url="https://github.com/astral-sh/uv"
license=('spdx:Apache-2.0 OR MIT')
msys2_references=(
  'archlinux: uv'
)
depends=("${MINGW_PACKAGE_PREFIX}-libgit2"
         "${MINGW_PACKAGE_PREFIX}-libssh2"
         "${MINGW_PACKAGE_PREFIX}-zstd")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-python-maturin"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-zlib"
             "${MINGW_PACKAGE_PREFIX}-openssl"
             "${MINGW_PACKAGE_PREFIX}-pkgconf")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python"
              "${MINGW_PACKAGE_PREFIX}-python-zstandard"
              #"${MINGW_PACKAGE_PREFIX}-libxcrypt-compat"
              "${MINGW_PACKAGE_PREFIX}-clang")
options=('!strip' '!lto')
source=("https://github.com/astral-sh/uv/archive/${pkgver}/${_realname}-${pkgver}.tar.gz"
        "link-sys-libs-dynamically.patch")
sha256sums=('542b7127398774cf00938b3bc612aa76d360f79b7600b7a0dac1ad9744495d2d'
            'aab389cd81bdb5161733e14947b158df551316977af3f6be36de596e74e581ba')

_env() {
  export LIBGIT2_NO_VENDOR=1
  export OPENSSL_NO_VENDOR=1
  export PKG_CONFIG_ALL_DYNAMIC=1
  export ZSTD_SYS_USE_PKG_CONFIG=1
  export LIBSSH2_SYS_USE_PKG_CONFIG=1
}

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  rm -f rust-toolchain.toml
  cargo vendor --locked --versioned-dirs >> .cargo/config.toml

  patch -Np1 -i "${srcdir}/link-sys-libs-dynamically.patch"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  _env
  maturin build --frozen --strip --release --all-features
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  _env
  python3 ./scripts/bootstrap/install.py
  cargo test -p uv --frozen --all-features
}

package_uv() {
  cd "${srcdir}/${_realname}-${pkgver}"

  install -Dm755 target/release/uv "${pkgdir}${MINGW_PREFIX}/bin/uv"

  local _complete="./target/release/uv --generate-shell-completion"
  $_complete bash | install -Dm644 /dev/stdin "${pkgdir}${MINGW_PREFIX}/share/bash-completion/completions/uv"
  $_complete fish | install -Dm644 /dev/stdin "${pkgdir}${MINGW_PREFIX}/share/fish/vendor_completions.d/uv.fish"
  $_complete zsh | install -Dm644 /dev/stdin "${pkgdir}${MINGW_PREFIX}/share/zsh/site-functions/_uv"

  install -Dm644 LICENSE-{MIT,APACHE} -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/"
}

package_python-uv() {
  cd "${srcdir}/${_realname}-${pkgver}"

  depends=("${MINGW_PACKAGE_PREFIX}-python" "${MINGW_PACKAGE_PREFIX}-uv")

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" target/wheels/*.whl

  rm -rf "${pkgdir}${MINGW_PREFIX}/bin"
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
