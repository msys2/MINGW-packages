# Maintainer: Alberto Fanjul Alonso <albfan@gnome.org>

_realname=linthesia
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.7.0
pkgrel=1
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
pkgdesc="A game of playing music (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-SDL2"
         "${MINGW_PACKAGE_PREFIX}-SDL2_image"
         "${MINGW_PACKAGE_PREFIX}-SDL2_ttf")
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-itstool")
options=('strip')
license=("GPL2+")
url="http://linthesia.sourceforge.net"
source=(https://github.com/linthesia/${_realname}/archive/refs/tags/${pkgver}.tar.gz)
sha256sums=('9c46b8b4a6abffa283a85e2096d6ab5c845404d2b471d8c91db36f01c7b5861c')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  autoreconf -ivf
}

build() {
  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"

  "${srcdir}"/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --{build,host}=${MINGW_CHOST}

  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}"/${_longname}-${pkgver}/LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
