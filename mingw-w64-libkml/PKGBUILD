# Maintainer: Diego Sogari <diego.sogari@falker.com.br>

_realname=libkml
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3.0
pkgrel=2
pkgdesc="Reference implementation of OGC KML 2.2 (mingw-w64)"
arch=('any')
url="https://github.com/google/libkml/"
license=('New BSD License')
makedepends=("automake" "autoconf"
             "${MINGW_PACKAGE_PREFIX}-curl"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-python2")
depends=("${MINGW_PACKAGE_PREFIX}-boost"
         "${MINGW_PACKAGE_PREFIX}-uriparser"
         "${MINGW_PACKAGE_PREFIX}-zlib")
checkdepends=("${MINGW_PACKAGE_PREFIX}-gtest")
optdepends=("${MINGW_PACKAGE_PREFIX}-python2")
commit=8609edf7c8d13ae2ddb6eac2bca7c8e49c67a5f8
source=("${_realname}-${pkgver}.zip::https://github.com/google/libkml/archive/${commit}.zip"
        "${_realname}-${pkgver}.patch"
        "${_realname}-${pkgver}-complement.zip"
        "minizipext-1.0.zip")
sha256sums=('7f92545c1af1070fcce95da335e88dfbab5bb098784b1eded247cc31d90b5f6b'
            '331f5cb8d638bbd2c5c68422314abd70b6fae6cf1e81ba565cd92669333a80b8'
            'f6c685042f11d049efc0ef02234523c670e51b24cd36d5ea6dfc99f4bb801ef8'
            '306d04f06a435229917016366baae629030154ed93f41dfc2f8f7468f2691334')

prepare() {
  cd "${srcdir}/${_realname}-${commit}"
  patch -Np1 -i "../${_realname}-${pkgver}.patch"
  cp -rf ../minizipext-1.0 third_party
  autoreconf -fvi
}

build() {
  rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  
  ../${_realname}-${commit}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --disable-java

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
