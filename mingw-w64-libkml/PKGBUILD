# Maintainer: Diego Sogari <diego.sogari@gmail.com>

_realname=libkml
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3.0
pkgrel=4
pkgdesc="Reference implementation of OGC KML 2.2 (mingw-w64)"
arch=('any')
url="https://github.com/libkml/libkml/"
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-swig")
depends=("${MINGW_PACKAGE_PREFIX}-boost"
         "${MINGW_PACKAGE_PREFIX}-uriparser"
         "${MINGW_PACKAGE_PREFIX}-zlib")
optdepends=("${MINGW_PACKAGE_PREFIX}-python2"
            "${MINGW_PACKAGE_PREFIX}-swig")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/libkml/libkml/archive/${pkgver}.tar.gz"
        "${_realname}-${pkgver}.patch"
        "strptime.c")
sha256sums=('8892439e5570091965aaffe30b08631fdf7ca7f81f6495b4648f0950d7ea7963'
            'a7b3c492c471bff3ccc745b56389c6714f6bbef09cf3fa011faef353d90f63b0'
            '49433be91643aaccef032ded7d413782a6ed62f545883165814e13c0e1f4182c')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Np1 -i "../${_realname}-${pkgver}.patch"
  cp -f ../strptime.c src/kml/base/contrib
}

build() {
  rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DWITH_SWIG=ON \
    -DWITH_PYTHON=ON \
    "../${_realname}-${pkgver}"

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
