# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=wasm-bindgen
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.2.93
pkgrel=3
pkgdesc="Facilitating high-level interactions between Wasm modules and JavaScript (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/rustwasm/wasm-bindgen'
license=('spdx:Apache-2.0 OR MIT')
depends=("${MINGW_PACKAGE_PREFIX}-rust-wasm"
         "${MINGW_PACKAGE_PREFIX}-nodejs")
makedepends=('git')
source=("git+${url}.git#tag=${pkgver}"
        "git+https://github.com/alexcrichton/raytracer.git#commit=42faa13859f7d8d47fd18be785c108003a207786"
        "Cargo.lock")
sha256sums=('8442f1bacb4447dfcfa8afb24a1dba37e3b6b997c7f151df757b4931f14d04b4'
            '0a96c55cb3ed65a958aeaadca093b25c6725b166e9c5a85b635a0e3d76d5f533'
            '8d6765474a20fab986a7f78ea65333199f5bf2077563128ee1b3e188ab1dfdef')

prepare() {
  cd "${_realname}"

  # https://github.com/rustwasm/wasm-bindgen/issues/1819
  cp ../Cargo.lock .
  # update image crate to remove security alert
  sed -i 's|^image = "0\.12\.3"$|image = "0.25"|' ../raytracer/Cargo.toml
  cat >> Cargo.toml <<END

[patch.'https://github.com/alexcrichton/raytracer']
raytracer = { path = "../raytracer" }
END

  cargo update -p raytracer
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${_realname}"

  cargo build --release --frozen -p wasm-bindgen-cli
}

check() {
  cd "${_realname}"

  cargo test --release --frozen -p wasm-bindgen-cli
}

package() {
  cd "${_realname}"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path crates/cli \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE-{APACHE,MIT} -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/"
}
