# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=wasm-bindgen
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-docs")
pkgver=0.2.97
pkgrel=1
pkgdesc="Facilitating high-level interactions between Wasm modules and JavaScript (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/rustwasm/wasm-bindgen'
license=('spdx:Apache-2.0 OR MIT')
depends=("${MINGW_PACKAGE_PREFIX}-rust-wasm"
         "${MINGW_PACKAGE_PREFIX}-nodejs")
makedepends=("${MINGW_PACKAGE_PREFIX}-mdbook"
             'git')
source=("git+${url}.git#tag=${pkgver}"
        "git+https://github.com/alexcrichton/raytracer.git#commit=42faa13859f7d8d47fd18be785c108003a207786"
        "Cargo.lock")
sha256sums=('fd927ab055bb5156ea0c8c60462240541f484b166a493849a48c3e83c995b91d'
            '0a96c55cb3ed65a958aeaadca093b25c6725b166e9c5a85b635a0e3d76d5f533'
            '70f06894a6f08a4633dac660120d0f105bdd6b9ff95f4e9990d682af6b96f658')

prepare() {
  cd "${_realname}"

  # https://github.com/rustwasm/wasm-bindgen/issues/1819
  cp ../Cargo.lock .
  # update image crate to fix security alert
  sed -i 's|^image = "0\.12\.3"$|image = "0.25"|' ../raytracer/Cargo.toml
  cat >> Cargo.toml <<END

[patch.'https://github.com/alexcrichton/raytracer']
raytracer = { path = "../raytracer" }
END

  cargo update -p raytracer
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${_realname}"

  cargo build --release --frozen -p wasm-bindgen-cli

  cd guide
  mdbook build
}

check() {
  cd "${_realname}"

  cargo test --release --frozen -p wasm-bindgen-cli
}

package_wasm-bindgen() {
  cd "${_realname}"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path crates/cli \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE-{APACHE,MIT} -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/"
}

package_wasm-bindgen-docs() {
  pkgdesc+=" (Documentation)"
  depends=()
  cd "${_realname}"
  install -d "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/html/"
  cp -r -t "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/html/" guide/book/html/*
}

# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
