_realname=llama.cpp
_realver=b3091
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=b3091.r0.g2b33896
pkgrel=1
pkgdesc="Port of Facebook's LLaMA model in C/C++ (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64' 'mingw64')
url="https://github.com/ggerganov/llama.cpp"
license=('spdx:MIT')
depends=(
  "${MINGW_PACKAGE_PREFIX}-openblas"
  "${MINGW_PACKAGE_PREFIX}-gcc-libs"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-ninja"
  "${MINGW_PACKAGE_PREFIX}-cc"
  'git'
)
source=("${_realname}"::"git+https://github.com/ggerganov/llama.cpp.git#tag=${_realver}")
sha256sums=('c72c6b7ca9c57e887e0bce94b113cc237cb59c09fb83f4eb490192bb6a7e67a0')

pkgver() {
  cd "${srcdir}/${_realname}"

  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${srcdir}/${_realname}"
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DBUILD_SHARED_LIBS=ON \
      -DLLAMA_BLAS=ON \
      -DLLAMA_BLAS_VENDOR=OpenBLAS \
      -DLLAMA_WIN_VER="0x603" \
      -DLLAMA_NATIVE=OFF \
      -DLLAMA_CCACHE=OFF \
      ../${_realname}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .

  # Install the examples by prefixing them with llama.cpp
  # except main.exe, which gets installed as plain llama.cpp.exe
  cd "${pkgdir}${MINGW_PREFIX}"
  for i in bin/*.{exe,py}; do
    mv "${i}" "${pkgdir}${MINGW_PREFIX}/bin/${_realname}-${i//bin\//}"
  done
  cd -
  mv "${pkgdir}${MINGW_PREFIX}/bin/${_realname}-main.exe" \
    "${pkgdir}${MINGW_PREFIX}/bin/${_realname}.exe"

  install -Dm644 "${srcdir}/${_realname}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
