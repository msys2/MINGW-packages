# Maintainer: Andrew Sun <adsun701@gmail.com>

_gemname=pkg-config
_realname=ruby-${_gemname}
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.2.8
pkgrel=1
pkgdesc="Implementation of pkg-config in ruby (mingw-w64)"
arch=('any')
url="http://github.com/ruby-gnome2/pkg-config"
license=('LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-ruby")
provides=("${MINGW_PACKAGE_PREFIX}-ruby-pkgconfig")
replaces=("${MINGW_PACKAGE_PREFIX}-ruby-pkgconfig")
conflicts=("${MINGW_PACKAGE_PREFIX}-ruby-pkgconfig")
source=("http://gems.rubyforge.org/gems/${_gemname}-${pkgver}.gem")
noextract=("${_gemname}-${pkgver}.gem")
sha256sums=('9aff7ab9d6aea2218dba94791a1b3ba6f149fa57dc9c81634e54f0c59959d814')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  _gemdir="$(cygpath -u ${_gemdir})"
  
  ${MINGW_PREFIX}/bin/gem install --ignore-dependencies --no-user-install -i "$pkgdir/$_gemdir" -n "${pkgdir}${MINGW_PREFIX}/bin" ${_gemname}-${pkgver}.gem
  
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"  
}
