# Maintainer: Andrew Sun <adsun701@gmail.com>

_realname=libnova
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.16
pkgrel=1
pkgdesc="A general purpose, double precision, celestial mechanics, astrometry and astrodynamics library. (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://libnova.sourceforge.io/"
license=('spdx:GPL-2.0-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc"
             'git')
source=("git+https://git.code.sf.net/p/libnova/libnova#tag=v${pkgver}"
        "libnova-remove-conflicting-definition.patch")
sha256sums=('d2b85412a45603c15d44a05a501aab4109116c762ea8ca759b16dd28efe89148'
            '29516dae20654f6a8b70c37d7581fb2680b40db32847356a107656af4134eb25')

prepare() {
  cd "${_realname}"
  patch -Np1 -i ../libnova-remove-conflicting-definition.patch
  autoreconf -fiv
}

build() {
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  ../${_realname}/configure \
    --prefix=${MINGW_PREFIX} \
    --enable-static \
    --enable-shared

  make
}

check() {
  cd build-${MSYSTEM}
  make check
}

package() {
  cd build-${MSYSTEM}
  make install DESTDIR="${pkgdir}"
}
