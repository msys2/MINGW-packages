# Maintainer: @naveen521kk on Github, Naveen M K <naveen521kk@gmail.com>
 
_realname=gi-docgen
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2022.1
pkgrel=1
pkgdesc='Documentation tool for GObject-based libraries (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://gitlab.gnome.org/GNOME/gi-docgen"
license=('GPL3' 'Apache2.0' 'CC0-1.0')
depends=(
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-python-jinja"
  "${MINGW_PACKAGE_PREFIX}-python-markdown" 
  "${MINGW_PACKAGE_PREFIX}-python-markupsafe" 
  "${MINGW_PACKAGE_PREFIX}-python-pygments"
  "${MINGW_PACKAGE_PREFIX}-python-toml" 
  "${MINGW_PACKAGE_PREFIX}-python-typogrify"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python-setuptools"
  "${MINGW_PACKAGE_PREFIX}-python-sphinx"
  "${MINGW_PACKAGE_PREFIX}-python-sphinx_rtd_theme"
  "${MINGW_PACKAGE_PREFIX}-cc"
)
checkdepends=(
  "${MINGW_PACKAGE_PREFIX}-python-flake8"
)
source=("${_realname}-${pkgver}.tar.gz::https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('f91d879ff28d7d5265cde84275ee510e32386bfeb7ec6203a647342aead55cec')

prepare() {
  cd "$srcdir"
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname//_/-}-$pkgver" "python-build-${MSYSTEM}"
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python setup.py build
  make -C docs
}

check() {
  msg "Python test for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/flake8 --show-source
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSES/Apache-2.0.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/Apache-2.0.txt"
  install -Dm644 LICENSES/GPL-3.0-or-later.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/GPL-3.0-or-later.txt"

  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*-script.py; do
    sed -e '1 { s/^#!.*$// }' -i ${_f}
  done
  
  mkdir -p "$pkgdir${MINGW_PREFIX}/share/doc/${_realname}"
  cp -r docs/_build/* "$pkgdir${MINGW_PREFIX}/share/doc/${_realname}"  
}
