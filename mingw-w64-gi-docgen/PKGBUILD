# Maintainer: @naveen521kk on Github, Naveen M K <naveen521kk@gmail.com>

_realname=gi-docgen
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2026.1
pkgrel=1
pkgdesc='Documentation tool for GObject-based libraries (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://gnome.pages.gitlab.gnome.org/gi-docgen/'
msys2_repository_url='https://gitlab.gnome.org/GNOME/gi-docgen/'
msys2_changelog_url='https://gitlab.gnome.org/GNOME/gi-docgen/-/blob/main/CHANGES.md'
msys2_references=(
  'archlinux: gi-docgen'
  'gentoo: dev-util/gi-docgen'
  'purl: pkg:pypi/gi-docgen'
)
license=('spdx:Apache-2.0 OR GPL-3.0-or-later')
depends=(
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-python-jinja"
  "${MINGW_PACKAGE_PREFIX}-python-markdown"
  "${MINGW_PACKAGE_PREFIX}-python-markupsafe"
  "${MINGW_PACKAGE_PREFIX}-python-pygments"
  "${MINGW_PACKAGE_PREFIX}-python-typogrify"
  "${MINGW_PACKAGE_PREFIX}-python-packaging"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python-build"
  "${MINGW_PACKAGE_PREFIX}-python-installer"
  "${MINGW_PACKAGE_PREFIX}-python-setuptools"
  "${MINGW_PACKAGE_PREFIX}-python-sphinx"
  "${MINGW_PACKAGE_PREFIX}-python-sphinx_rtd_theme"
  "${MINGW_PACKAGE_PREFIX}-cc"
)
checkdepends=(
  "${MINGW_PACKAGE_PREFIX}-python-flake8"
)
options=('!strip')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname/-/_}/${_realname/-/_}-${pkgver}.tar.gz")
sha256sums=('65ac3c4f2b4255d1c616fb8eae55139b6ca10071545ca2272759ffd2ccf2c7b5')

build() {
  cp -r "${_realname/-/_}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  python -m build --wheel --skip-dependency-check --no-isolation
  make -C docs
}

check() {
  cd "python-build-${MSYSTEM}"
  flake8 --show-source
}

package() {
  cd "python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
    python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSES/Apache-2.0.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/Apache-2.0.txt"
  install -Dm644 LICENSES/GPL-3.0-or-later.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/GPL-3.0-or-later.txt"

  mkdir -p "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}"
  cp -r docs/_build/* "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}"
}
