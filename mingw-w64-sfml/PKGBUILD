# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=sfml
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.4.2
pkgrel=1
cmake_ver=3.9
pkgdesc="A simple, fast, cross-platform, and object-oriented multimedia API (mingw-w64)"
arch=('any')
url="http://www.sfml-dev.org/"
license=("zlib")
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
depends=("${MINGW_PACKAGE_PREFIX}-flac"
         "${MINGW_PACKAGE_PREFIX}-freetype"
         "${MINGW_PACKAGE_PREFIX}-libjpeg"
         "${MINGW_PACKAGE_PREFIX}-libvorbis"
         "${MINGW_PACKAGE_PREFIX}-openal")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-doxygen"
             "unzip")
source=("http://www.sfml-dev.org/files/SFML-${pkgver}-sources.zip"
        mingw-w64-msys2.patch)
sha256sums=('8ba04f6fde6a7b42527d69742c49da2ac529354f71f553409f9f821d618de4b6'
            'd7cf7f13f76f7e77bc759616d73fc81b97552132da99005b45ef538bcfdcc338')
noextract=(SFML-${pkgver}-sources.zip)

prepare() {
  rm -fr ${srcdir}/SFML-${pkgver}
  unzip SFML-${pkgver}-sources.zip
  cd ${srcdir}/SFML-${pkgver}
  patch -p1 -i ${srcdir}/mingw-w64-msys2.patch
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -r build-${MINGW_CHOST}
  mkdir build-${MINGW_CHOST} && cd build-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DSFML_BUILD_EXAMPLES=ON \
    -DSFML_BUILD_DOC=ON \
    -DSFML_INSTALL_PKGCONFIG_FILES=ON \
    ../SFML-${pkgver}

  make
  make doc
}

package() {
  cd build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install

  install -Dm644 "${srcdir}/SFML-${pkgver}/cmake/Modules/FindSFML.cmake" "${pkgdir}${MINGW_PREFIX}/share/cmake-${cmake_ver}/Modules/FindSFML.cmake"
  install -Dm644 "${srcdir}/SFML-${pkgver}/license.txt" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
