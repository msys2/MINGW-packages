# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=hdr10plus_tool
pkgbase=mingw-w64-${_realname/_/-}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname/_/-}
pkgver=1.7.2
pkgrel=1
pkgdesc='CLI utility to work with HDR10+ in HEVC files (mingw-w64)'
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://github.com/quietvoid/hdr10plus_tool'
license=('spdx:MIT')
msys2_references=(
  'archlinux: hdr10plus-tool'
)
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
source=("https://github.com/quietvoid/hdr10plus_tool/archive/${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('cc917e769bad85323c7f596179798cc96ac878a01ddfd53b210fecae4c891849')

prepare() {
  cd "${_realname}-${pkgver}"

  cargo fetch --locked --target "${RUST_CHOST}"
}

build() {
  cd "${_realname}-${pkgver}"

  cargo build --frozen --profile release-deploy
}

check() {
  cd "${_realname}-${pkgver}"

  cargo test --frozen --profile release-deploy
}

package() {
  cd "${_realname}-${pkgver}"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --profile release-deploy \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname/_/-}/LICENSE"
}
