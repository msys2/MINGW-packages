# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libntlm
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.4
pkgrel=3
pkgdesc="Library that implement Microsoft's NTLM authentication (mingw-w64)"
arch=('any')
url="http://www.nongnu.org/libntlm/"
license=("LGPL2.1")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('strip' 'staticlibs')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
source=("http://www.nongnu.org/libntlm/releases/${_realname}-${pkgver}.tar.gz"{,.sig})
sha256sums=('8415d75e31d3135dc7062787eaf4119b984d50f86f0d004b964cdc18a3182589'
            'SKIP')

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --enable-static
  make
}

package() {
  cd build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
}
