# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libntlm
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.8
pkgrel=1
pkgdesc="Library that implement Microsoft's NTLM authentication (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://gitlab.com/gsasl/libntlm/"
msys2_repository_url='https://gitlab.com/gsasl/libntlm'
msys2_references=(
  "cpe: cpe:/a:nongnu:libntlm"
)
license=("spdx:LGPL-2.1-only")
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc" "${MINGW_PACKAGE_PREFIX}-autotools")
source=("https://download.savannah.nongnu.org/releases/libntlm/${_realname}-${pkgver}.tar.gz"{,.sig})
sha256sums=('ce6569a47a21173ba69c990965f73eb82d9a093eb871f935ab64ee13df47fda1'
            'SKIP')
validpgpkeys=('B1D2BD1375BECB784CF4F8C4D73CF638C53C06BE') # Simon Josefsson <simon@josefsson.org>

build() {
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --enable-static

  make
}

package() {
  cd build-${MSYSTEM}
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
