_realname=fswatch
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=1.17.1.r36.gba411e0
pkgrel=1
_branch=master
pkgdesc="A cross-platform file change monitor with multiple backends: Apple OS X File System Events API, *BSD kqueue, Linux inotify, Microsoft Windows and a stat-based backend (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64')
msys2_references=(
  'aur: fswatch'
  'gentoo: sys-fs/fswatch'
)

url="https://github.com/emcrisostomo/fswatch"
license=('spdx:GPL-3.0-or-later WITH GCC-exception-3.1')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "git")
source=("${_realname}-git::git+https://github.com/emcrisostomo/fswatch.git#branch=${_branch}"
        '001-Fix-for-MinGW-w64.patch')
sha256sums=('SKIP'
            '1e8056258d14cb0bfe0a21000849cf685c0ab113bd71d02c288b84a9fd0bab4e')

pkgver() {
  cd "${_realname}-git"
  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${srcdir}/${_realname}-git"

  # https://github.com/emcrisostomo/fswatch/issues/214
  patch -p1 -i ${srcdir}/001-Fix-for-MinGW-w64.patch
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  ${MINGW_PREFIX}/bin/cmake -B "${srcdir}/build-${MINGW_CHOST}" -S "${srcdir}/${_realname}-git" -DCMAKE_BUILD_TYPE=Release -DBUILD_SHARED_LIBS=ON
  ${MINGW_PREFIX}/bin/cmake --build "${srcdir}/build-${MINGW_CHOST}" --config Release
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  ${MINGW_PREFIX}/bin/cmake --install "${srcdir}/build-${MINGW_CHOST}" --prefix="${pkgdir}/${MINGW_PREFIX}" --config Release
  install -Dm644 ${srcdir}/${_realname}-git/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
