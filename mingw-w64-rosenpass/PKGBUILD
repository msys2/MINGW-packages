# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=rosenpass
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.2.1.r213.gb45b7bc
pkgrel=1
pkgdesc="Rosenpass is a post-quantum-secure VPN that uses WireGuard to transport the actual data (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64')
url='https://rosenpass.eu'
msys2_repository_url='https://github.com/rosenpass/rosenpass'
license=('Apache-2.0 OR MIT')
depends=("${MINGW_PACKAGE_PREFIX}-liboqs")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-clang"
             'git')
source=("git+${msys2_repository_url}.git#commit=b45b7bc7f58facec93b645cbdff4bd9a5574a1af"
        "288.patch")
sha256sums=('02bd0b57525681ec6fc8ee04cf9c148021809b9fb4a4e8b12f761926c66e1704'
            'ad8abda7236cfdae6605c452297f78488a8ce384eab432446f5b31473dba4533')

pkgver() {
  git -C rosenpass describe --long HEAD | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//g'
}

prepare() {
  cd "${_realname}"

  # https://github.com/rosenpass/rosenpass/pull/288 with `wireguard-nt` and `oqs-sys` updated
  patch -Np1 -i ../288.patch

  cargo update -p cc
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${_realname}"

  export LIBOQS_NO_VENDOR=1
  cargo build --release --frozen -p rosenpass -p rp
}

check() {
  cd "${_realname}"

  export LIBOQS_NO_VENDOR=1
  cargo test --release --frozen -p rosenpass -p rp
}

package() {
  cd "${_realname}"

  install -Dm755 target/release/rosenpass "${pkgdir}${MINGW_PREFIX}/bin/rosenpass"
  install -Dm755 target/release/rp "${pkgdir}${MINGW_PREFIX}/bin/rp"
  install -Dm644 LICENSE-{APACHE,MIT} -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/"
}
