# Maintainer (MSYS2): Mateusz Miku≈Ça <mati865@gmail.com>

_basename=kdewin
_realname=${_basename}-qt5
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.5.11
pkgrel=1
arch=('any')
url="https://github.com/KDE/kdewin"
pkgdesc="KDEWin is a supplementary library and provides supplementary tools which are required for compiling KDE applications on Windows (mingw-w64-qt5${_namesuff})"
license=('LGPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-extra-cmake-modules")
source=(${_basename}-${pkgver}.tar.gz::"https://github.com/KDE/${_basename}/archive/v${pkgver}.tar.gz"
        "MinGW-w64-backport-fix.patch")
sha256sums=('62d8d862891fd74fafd0276e589f6a3a17e0d2082b06ed42bb209300cee6683f'
            '58f96818108b9c0aa910f426548601c94c66191593f7f7f3ea2d03a274d1e7db')

prepare(){
  cd ${srcdir}/${_basename}-${pkgver}
  patch -p1 -i ${srcdir}/MinGW-w64-backport-fix.patch
}

build() {
  [[ -d "${srcdir}/build-${CARCH}" ]] && rm -rf "${srcdir}/build-${CARCH}"
  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -G'MSYS Makefiles' \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      ../${_basename}-${pkgver}
  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install
}
