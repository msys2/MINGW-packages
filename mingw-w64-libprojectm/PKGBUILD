# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=libprojectm
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=4.1.6
pkgrel=1
pkgdesc="Music visualizer library which uses 3D accelerated iterative image based rendering (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/projectM-visualizer/projectm'
msys2_repository_url='https://github.com/projectM-visualizer/projectm'
license=('spdx:LGPL-2.1-or-later')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-ninja"
  "${MINGW_PACKAGE_PREFIX}-glm"
)
depends=(
  "${MINGW_PACKAGE_PREFIX}-glew"
  "${MINGW_PACKAGE_PREFIX}-cc-libs"
)
source=("https://github.com/projectM-visualizer/projectm/releases/download/v${pkgver}/libprojectM-${pkgver}.tar.gz"
        "0001-playlist-missing-exports.patch")
sha256sums=('1b9e6d56c59fe24e5416da4d42e941a34c982811003e43ac88b5aca8afa52c87'
            'a5a3494ac72f953278d9fb7e4375b9741e97326e98500656bf7a8cbc54b34677')

prepare() {
  cd "${_realname}-${pkgver}"

  patch -p1 -i "${srcdir}/0001-playlist-missing-exports.patch"
}

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  cmake \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -DCMAKE_DLL_NAME_WITH_SOVERSION=ON \
    "${extra_config[@]}" \
    -DENABLE_SYSTEM_GLM=ON \
    -S "${_realname}-${pkgver}" \
    -B "build-${MSYSTEM}"

  cmake --build "build-${MSYSTEM}"
}

package() {
  DESTDIR="${pkgdir}" cmake --install "build-${MSYSTEM}"

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE.txt" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.txt"
}
