# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libsoup
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=2.45.90
pkgrel=1
pkgdesc="HTTP client/server library (mingw-w64)"
arch=(any)
url="http://ftp.gnome.org/pub/GNOME/sources/libsoup"
license=("LGPL")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config" "${_mingw_suff}-gobject-introspection" "intltool")
depends=("${_mingw_suff}-gcc-libs"
		"${_mingw_suff}-glib2"
		"${_mingw_suff}-libxml2"
		"${_mingw_suff}-sqlite3"
		"${_mingw_suff}-glib-networking")
options=('staticlibs' 'strip')
source=("http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver%.*}/${_realname}-$pkgver.tar.xz"
		0001-give-cc-to-gir-scanner.mingw.patch)
md5sums=('4d82db51fa495dd7af7cbb2c40e99a1b'
         'a1ddc19ab3f73bbddda1d41a6a7de399')

prepare() {
	cd "$srcdir/${_realname}-$pkgver"
	patch -p1 -i ${srcdir}/0001-give-cc-to-gir-scanner.mingw.patch
	
	autoreconf -fi
}

build() {
	mkdir -p "${srcdir}/build-${MINGW_CHOST}"
	cd "${srcdir}/build-${MINGW_CHOST}"
	"${srcdir}"/${_realname}-$pkgver/configure \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--without-apache-httpd \
		--without-apache-module-dir \
		--with-gnome \
		--enable-introspection
	make -j1
}

package() {
	cd "${srcdir}/build-${MINGW_CHOST}"
	make DESTDIR="$pkgdir" install
}
