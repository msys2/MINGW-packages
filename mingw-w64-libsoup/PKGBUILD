# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libsoup
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.52.0
pkgrel=1
pkgdesc="HTTP client/server library (mingw-w64)"
arch=(any)
url="http://ftp.gnome.org/pub/GNOME/sources/libsoup"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-vala"
             "intltool")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-glib-networking"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-sqlite3")
options=('staticlibs' 'strip')
source=("http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver%.*}/${_realname}-${pkgver}.tar.xz"
        0001-give-cc-to-gir-scanner.mingw.patch
        0004-localedir-fix.patch
        0005-missing-declaration.patch
        0006-change-pkg-config-invocations.mingw.patch
        0007-dont-hardcode-paths.patch)
md5sums=('74ef72cd984dc6daf3ba601288974c26'
         'a1ddc19ab3f73bbddda1d41a6a7de399'
         'ee2cc56c1448c32020288c817b81ff7f'
         '8d30c2d030281a6e363167f5c9dd8599'
         '412394f65ad3153aa9587b888acf2f96'
         '74ffcee27943dfded1d0da15c3975eff')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/0001-give-cc-to-gir-scanner.mingw.patch
  patch -p1 -i ${srcdir}/0004-localedir-fix.patch
  #patch -p1 -i ${srcdir}/0005-missing-declaration.patch
  patch -p1 -i ${srcdir}/0006-change-pkg-config-invocations.mingw.patch
  
  patch -p1 -R -i ${srcdir}/0007-dont-hardcode-paths.patch

  autoreconf -fi
}

build() {
  #CPPFLAGS+=" --save-temps"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --without-apache-httpd \
    --without-apache-module-dir \
    --with-gnome \
    --enable-vala \
    --enable-introspection \
    --disable-silent-rules

  make #-j1 V=1
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
