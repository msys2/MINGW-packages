# Maintainer: Wu, Zhenyu <wuzhenyu@ustc.edu>

_realname=mesonlsp
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=4.3.3
pkgrel=1
pkgdesc='Meson language server (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url=https://github.com/JCWasmx86/mesonlsp
license=(GPL-3.0-or-later)
depends=(
  "${MINGW_PACKAGE_PREFIX}-curl"
  git
  "${MINGW_PACKAGE_PREFIX}-libarchive"
  mercurial
  patch
  "${MINGW_PACKAGE_PREFIX}-pkgconf"
  subversion
  "${MINGW_PACKAGE_PREFIX}-tree-sitter"
  "${MINGW_PACKAGE_PREFIX}-dlfcn"
  "${MINGW_PACKAGE_PREFIX}-tomlplusplus"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-gtest"
  "${MINGW_PACKAGE_PREFIX}-meson"
  "${MINGW_PACKAGE_PREFIX}-ninja"
  "${MINGW_PACKAGE_PREFIX}-nlohmann-json"
  "${MINGW_PACKAGE_PREFIX}-python-lsprotocol"
  "${MINGW_PACKAGE_PREFIX}-python-pygls"
)
source=(
  "git+$url.git#tag=v$pkgver"
  "git+https://github.com/ada-url/ada#tag=v2.7.4"
  "sha256::git+https://github.com/amosnier/sha-2#commit=49265c656f9b370da660531db8cc6bf0a2e110a6"
  git+https://github.com/JCWasmx86/muon
  git+https://github.com/JCWasmx86/tree-sitter-ini
  git+https://github.com/JCWasmx86/tree-sitter-meson
)
b2sums=('eb85569f5d18256247d057e7e269258ae71d09fde64c39812cca841b848fda5250f7213804c10c9f0ea65aad834e1c1f84bee50cb98047cce2915922a71b266c'
        'a1bb73cac9898267aec9b019857581f2895351f3014727a297654a3c55a32d69e480e93c53a0089f342268049e0f23e9ba6c0ae1cefa703795e2655eacc74f8a'
        'e4e59dcd348ea3ec9fc6ce7f62f944b8f5b40370c5738e6453e45aa201eadb9eb0b58d9d08e7068b8b1a4a6c90e94d58fb7df81337124c8e96e6e418fce488ae'
        'SKIP'
        'SKIP'
        'SKIP')

build() {
  export MESON_PACKAGE_CACHE_DIR="${srcdir}"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
    meson setup \
      --prefix="${MINGW_PREFIX}" \
      --wrap-mode=nodownload \
      --auto-features=enabled \
      --buildtype=plain \
      -Dbenchmarks=false -Duse_own_tree_sitter=false \
      "build-${MSYSTEM}" \
      "${_realname}"

  meson compile -C "build-${MSYSTEM}"
}

package() {
  meson install -C "build-${MSYSTEM}" --destdir "${pkgdir}"
  rm "${pkgdir}${MINGW_PREFIX}/bin/Swift-MesonLSP"

  install -Dm644 "${srcdir}/${_realname}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
