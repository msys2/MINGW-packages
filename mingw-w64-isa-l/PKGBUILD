# Contributor: Teemu NÃ¤tkinniemi ("teepean") <tnatkinn@gmail.com>

_realname=isa-l
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.31.0
pkgrel=1
pkgdesc="Intelligent Storage Acceleration Library (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url="https://github.com/intel/isa-l"
msys2_references=(
  'aur: isa-l'
)
license=('spdx:BSD-3-Clear')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-nasm")
source=("https://github.com/intel/${_realname}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        001-fix-building-shared-library.patch
        002-do-not-require-yasm.patch)
sha256sums=('e218b7b2e241cfb8e8b68f54a6e5eed80968cc387c4b1af03708b54e9fb236f1'
            '92a43ca667f1f40b2b78ae38434a6dad4c359e5ee7fdd73bd8d09d40bff03aed'
            'c6ee2f69d92bc94a554c4bbbb790eca30c9c7696e2af8cba72baabe7238eb6a3')

prepare(){
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}"/001-fix-building-shared-library.patch
  patch -p1 -i "${srcdir}"/002-do-not-require-yasm.patch

  ./autogen.sh
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared

  make
}

check() {
    cd "${srcdir}/build-${MSYSTEM}"
    make check
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
