# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=ilmbase
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=2.1.0
pkgrel=4
pkgdesc="Base libraries from ILM for OpenEXR (mingw-w64)"
arch=('any')
url="http://http://www.openexr.com/"
license=("custom")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config" "libtool")
depends=("${_mingw_suff}-crt")
options=('staticlibs' 'strip')
source=("http://download.savannah.nongnu.org/releases/openexr/${_realname}-${pkgver}.tar.gz"
		ilmthread-mingw-win32.patch
		ilmthread-mingw-pthreads.patch
		ilmbase-2.1.0_obsolete-macros.patch)
md5sums=('8ba2f608191ad020e50277d8a3ba0850'
         '3a34cf48b52c2af0584ef3e8cf50499a'
         '8b9d9c6e4ed88dc9dfb80ce4f637460a'
         '1c9a320c8ab09c98abe9ecb80eb24e2e')
prepare(){
  cd "$srcdir/${_realname}-$pkgver"
  # Use either one or the other of the ilmthread patches
  # depending on the sort of threads that you want.
  # patch -Np1 -i "$srcdir/ilmthread-mingw-win32.patch"
  patch -Np1 -i "$srcdir/ilmthread-mingw-pthreads.patch"
  patch -Np1 -i "$srcdir/ilmbase-2.1.0_obsolete-macros.patch"
  ./bootstrap
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  mkdir build-${CARCH}
  cd build-${CARCH}
  ../configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX}

  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}/build-${CARCH}"
  make DESTDIR="$pkgdir" install
}
