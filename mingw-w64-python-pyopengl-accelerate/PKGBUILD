# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=pyopengl-accelerate
_pyname=PyOpenGL-accelerate
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=3.1.8
pkgrel=1
pkgdesc="Acceleration code for PyOpenGL (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'pypi: PyOpenGL-accelerate'
)
url='https://pyopengl.sourceforge.io/'
license=('spdx:BSD-3-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-pyopengl")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-cython"
             "${MINGW_PACKAGE_PREFIX}-cc")
options=(!strip)
source=("https://github.com/mcfletch/pyopengl/archive/refs/tags/release-${pkgver}.tar.gz"
	"pyopengl-version.patch"
	"pyopengl-array.patch"
)
sha256sums=('78f4016f13705d66dc89d5d046eee660c2f5f0915e5ecfeeed79dffac741bc97'
            'f88697750e5cfe6d685094895f62e64ac4a768a36d0aa9eec5c6fd6199e93651'
            '8a41eeb11a59f69bd4097ae2daab37d68d845bfd496ca0929ff437d061b7e52f'
)

prepare() {
  cd "${srcdir}"
  rm -rf python-build-${MSYSTEM} | true
  cp -r "pyopengl-release-${pkgver}" "python-build-${MSYSTEM}"
  cd "python-build-${MSYSTEM}"
  patch -Np1 -i ../pyopengl-version.patch
  patch -Np1 -i ../pyopengl-array.patch
}

build() {
  cd "${srcdir}/python-build-${MSYSTEM}/accelerate"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  # skipped as the checks fail for no good reason
  true
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}/accelerate"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
    "${MINGW_PREFIX}"/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  cd "${srcdir}/python-build-${MSYSTEM}"
  install -Dm644 license.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
