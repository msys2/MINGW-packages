# Maintainer: Quinton Miller <nicetas.c@gmail.com>

_bootstrap=1
_realname=crystal
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.15.1
pkgrel=3
pkgdesc="Fast and statically typed, compiled language with Ruby-like syntax (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'mingw64' 'clang64')
url="https://crystal-lang.org/"
msys2_repository_url="https://github.com/crystal-lang/crystal"
msys2_documentation_url="https://crystal-lang.org/reference/${pkgver%.*}/"
msys2_changelog_url="https://github.com/crystal-lang/crystal/releases/tag/${pkgver}"
msys2_references=(
  "archlinux: crystal"
)
license=('spdx:Apache-2.0 WITH Swift-exception')
depends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-gc"
  "${MINGW_PACKAGE_PREFIX}-pcre2"
  "${MINGW_PACKAGE_PREFIX}-gmp"
  "${MINGW_PACKAGE_PREFIX}-libffi"
  "${MINGW_PACKAGE_PREFIX}-libiconv"
  "${MINGW_PACKAGE_PREFIX}-libxml2"
  "${MINGW_PACKAGE_PREFIX}-libyaml"
  "${MINGW_PACKAGE_PREFIX}-llvm-libs"
  "${MINGW_PACKAGE_PREFIX}-openssl"
  "${MINGW_PACKAGE_PREFIX}-zlib"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-llvm"
  $( (( _bootstrap )) || echo "${MINGW_PACKAGE_PREFIX}-crystal")
  "git"
)
checkdepends=(
  "${MINGW_PACKAGE_PREFIX}-lld" # needed for linking std_spec
)
source=("${_realname}::git+${msys2_repository_url}.git#tag=${pkgver}"
        "001-support-llvm-20.patch::https://github.com/crystal-lang/crystal/commit/1c03dfa3.patch"
        "002-fix-llvm-version-detection.patch::https://github.com/crystal-lang/crystal/commit/1fafbb2b.patch")
sha256sums=('626fed60399d7f1dc83e2e5a2ecde20d235e19a3dcf6d70d6f52ec4de063b32b'
            '23957732539eb4dc1b2cce03ebdaa169804fade3c0ba81a53ad430556989e340'
            'f02c6393f6a4bf8fd92b0fb42131b0e45c61b30d86441458b370d7c7ebe6a816')

if (( _bootstrap ))
then
  # stage 0 compiler
  source+=("https://github.com/crystal-lang/crystal/releases/download/1.15.0/crystal-1.15.0-windows-x86_64-gnu-unsupported.zip")
  sha256sums+=('7d7df1b4a99cb3f938106ba45e0b55ce1d9913491999f1acaa57da168a18becb')
fi

prepare() {
  cd "${_realname}"

  if test true != "$(git config core.symlinks)"; then
    git config core.symlinks true &&
    MSYS='winsymlinks:nativestrict' git restore --source=HEAD :/
  fi

  patch -p1 -i "${srcdir}"/001-support-llvm-20.patch
  patch -p1 -i "${srcdir}"/002-fix-llvm-version-detection.patch
}

build() {
  cd "${_realname}"
  if (( _bootstrap )); then
    CRYSTAL="$srcdir/bin/crystal.exe" make interpreter=1 release=1
  else
    make interpreter=1 release=1
  fi
}

check() {
  cd "${_realname}"
  # the full suite is run on the crystal-lang/crystal repo's own CI
  CRYSTAL_SPEC_COMPILER_BIN="$(pwd)/.build/crystal.exe" make std_spec
}

package() {
  cd "${_realname}"
  make install DESTDIR="$pkgdir" PREFIX="${MINGW_PREFIX}" deref_symlinks=1
}
