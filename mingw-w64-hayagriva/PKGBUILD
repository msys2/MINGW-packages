# Maintainer: Wu, Zhenyu <wuzhenyu@ustc.edu>

_realname=hayagriva
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.5.3
pkgrel=1
pkgdesc="Format references and citations for your YAML-encoded or BibLaTeX bibliography files and query bibliographies using selectors. (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url="https://github.com/typst/hayagriva"
license=('spdx:Apache-2.0 OR MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
source=("$_realname-$pkgver.tar.gz::https://github.com/typst/hayagriva/archive/refs/tags/v$pkgver.tar.gz")
b2sums=("32bd05c2e98e25462662b9a8e390f1d621ba1d8c0348f632299b85cf6f38ffea67f1a15e86701a24a8d01c50a839576ef815da58b3ee6ec7e928170352da4708")

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  cargo update
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  cargo build --release --frozen --bin hayagriva --features cli
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE-* -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
  install -Dm755 target/release/$_realname -t "${pkgdir}${MINGW_PREFIX}/bin"
}
