# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>
# Contributor: Wu, Zhenyu <wuzhenyu@ustc.edu>

_realname=hayagriva
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.9.1
pkgrel=3
pkgdesc="Rusty bibliography management (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url="https://github.com/typst/hayagriva"
msys2_repository_url='https://github.com/typst/hayagriva'
msys2_references=(
  'purl: pkg:cargo/hayagriva'
)
license=('spdx:Apache-2.0 OR MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
checkdepends=("${MINGW_PACKAGE_PREFIX}-git")
source=("https://github.com/typst/hayagriva/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "Cargo.lock")
sha256sums=('91d451608e96b3e06fe98d6bc20915b62eda69ff4fdfdbb2ae9b098eeb11a690'
            'ebd094bec77e0827328e19af3e57e352222a8031583f5169a6551de52a009a42')

prepare() {
  cd "${_realname}-${pkgver}"

  # lockfile is not generated upstream, regenerate it manually after update
  cp ../Cargo.lock .
  cargo fetch --locked --target "${RUST_CHOST}"
}

build() {
  cd "${_realname}-${pkgver}"

  cargo build --release --frozen --all-features
}

check() {
  cd "${_realname}-${pkgver}"

  cargo test --release --frozen --all-features
}

package() {
  cd "${_realname}-${pkgver}"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}" \
    --all-features

  install -Dm644 LICENSE-{APACHE,MIT} -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
  install -Dm755 target/release/${_realname} -t "${pkgdir}${MINGW_PREFIX}/bin"
}
