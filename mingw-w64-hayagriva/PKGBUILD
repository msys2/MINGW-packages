# Maintainer: Wu, Zhenyu <wuzhenyu@ustc.edu>

_realname=hayagriva
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.8.0
pkgrel=2
pkgdesc="Format references and citations for your YAML-encoded or BibLaTeX bibliography files and query bibliographies using selectors (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url="https://github.com/typst/hayagriva"
license=('spdx:Apache-2.0 OR MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
checkdepends=('git')
source=("https://github.com/typst/hayagriva/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "Cargo.lock")
sha256sums=('c127ac42191ec949d6a8e5a3b6185342c09e435c0e19742af12828d75e8e8127'
            'c22bad7d186fed6f61c8e6f625210e65d10b3d23675ddf8d8a464c191e0234e6')

prepare() {
  cd "${_realname}-${pkgver}"

  # lockfile is not generated upstream, regenerate it manually after update
  cp ../Cargo.lock .
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${_realname}-${pkgver}"

  cargo build --release --frozen --all-features
}

check() {
  cd "${_realname}-${pkgver}"

  cargo test --release --frozen --all-features
}

package() {
  cd "${_realname}-${pkgver}"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}" \
    --all-features

  install -Dm644 LICENSE-{APACHE,MIT} -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
  install -Dm755 target/release/${_realname} -t "${pkgdir}${MINGW_PREFIX}/bin"
}
