# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>
# Contributor: Wu, Zhenyu <wuzhenyu@ustc.edu>

_realname=hayagriva
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.9.0
pkgrel=1
pkgdesc="Format references and citations for your YAML-encoded or BibLaTeX bibliography files and query bibliographies using selectors (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url="https://github.com/typst/hayagriva"
msys2_references=(
  'purl: pkg:cargo/hayagriva'
)
license=('spdx:Apache-2.0 OR MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
checkdepends=('git')
source=("https://github.com/typst/hayagriva/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "Cargo.lock")
sha256sums=('1304176197ff684130f4996182534516d067df33f4de21f79949a350bfefdd11'
            '1bc195f327d9eaa381f4a25ea9e6e7e5a38bc026b72c3c4bb6437b42f1cab04c')

prepare() {
  cd "${_realname}-${pkgver}"

  # lockfile is not generated upstream, regenerate it manually after update
  cp ../Cargo.lock .
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${_realname}-${pkgver}"

  cargo build --release --frozen --all-features
}

check() {
  cd "${_realname}-${pkgver}"

  cargo test --release --frozen --all-features
}

package() {
  cd "${_realname}-${pkgver}"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}" \
    --all-features

  install -Dm644 LICENSE-{APACHE,MIT} -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
  install -Dm755 target/release/${_realname} -t "${pkgdir}${MINGW_PREFIX}/bin"
}
