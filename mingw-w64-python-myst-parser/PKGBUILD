# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=MyST-Parser
pkgbase=mingw-w64-python-${_realname,,}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname,,}")
pkgver=2.0.0
pkgrel=2
pkgdesc="An extended commonmark compliant parser, with bridges to docutils/sphinx (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/executablebooks/MyST-Parser'
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python-docutils"
         "${MINGW_PACKAGE_PREFIX}-python-jinja"
         "${MINGW_PACKAGE_PREFIX}-python-markdown-it-py"
         "${MINGW_PACKAGE_PREFIX}-python-mdit_py_plugins"
         "${MINGW_PACKAGE_PREFIX}-python-typing_extensions"
         "${MINGW_PACKAGE_PREFIX}-python-sphinx"
         "${MINGW_PACKAGE_PREFIX}-python-yaml")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-flit-core")
options=('!strip')
source=("$url/archive/v$pkgver/${_realname}-${pkgver}.tar.gz")
sha256sums=('6d03d257af6e7a4f336aaccd400e13537a85a0c260a58f95de51618c46b51579')

build() {
  cd "${srcdir}"
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname,,}/LICENSE"
}
