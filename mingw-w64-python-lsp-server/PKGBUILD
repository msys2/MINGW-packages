# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=python-lsp-server
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=1.9.0
pkgrel=1
pkgdesc='Python Language Server for the Language Server Protocol (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64' 'clang32')
msys2_references=(
  'pypi: python-lsp-server'
)
license=('spdx:MIT')
url='https://github.com/python-lsp/python-lsp-server'
depends=(
    "${MINGW_PACKAGE_PREFIX}-python"
    "${MINGW_PACKAGE_PREFIX}-python-jedi"
    "${MINGW_PACKAGE_PREFIX}-python-lsp-jsonrpc"
    "${MINGW_PACKAGE_PREFIX}-python-pluggy"
    "${MINGW_PACKAGE_PREFIX}-python-docstring-to-markdown"
    "${MINGW_PACKAGE_PREFIX}-python-ujson"
    "${MINGW_PACKAGE_PREFIX}-python-setuptools"
)
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools-scm"
             "${MINGW_PACKAGE_PREFIX}-python-wheel")
options=(!strip)
source=(https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz
        0001-python-lsp-server-1.5.0-allow-newlest-depends.patch)
sha256sums=('dc0c8298f0222fd66a52aa3170f3a5c8fe3021007a02098bb72f7fd8df353d13'
            'b344d82a4dd5ba6eb2f885b4f9dc0e6bc1878dc8f22e6af79f66ca491d1d765c')

prepare() {
  cd "${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/0001-python-lsp-server-1.5.0-allow-newlest-depends.patch

  cd "${srcdir}"
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
