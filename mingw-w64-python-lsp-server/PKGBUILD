# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=python-lsp-server
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=1.4.1
pkgrel=2
pkgdesc='Python Language Server for the Language Server Protocol (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=('MIT')
url='https://github.com/python-lsp/python-lsp-server'
depends=(
    ${MINGW_PACKAGE_PREFIX}-python
    ${MINGW_PACKAGE_PREFIX}-python-autopep8
    ${MINGW_PACKAGE_PREFIX}-python-flake8
    ${MINGW_PACKAGE_PREFIX}-python-jedi
    ${MINGW_PACKAGE_PREFIX}-python-jsonrpc-server
    ${MINGW_PACKAGE_PREFIX}-python-lsp-jsonrpc
    ${MINGW_PACKAGE_PREFIX}-python-mccabe
    ${MINGW_PACKAGE_PREFIX}-python-pluggy
    ${MINGW_PACKAGE_PREFIX}-python-pycodestyle
    ${MINGW_PACKAGE_PREFIX}-python-pydocstyle
    ${MINGW_PACKAGE_PREFIX}-python-pyflakes
    ${MINGW_PACKAGE_PREFIX}-python-pylint
    ${MINGW_PACKAGE_PREFIX}-python-pytest
    ${MINGW_PACKAGE_PREFIX}-python-rope
    ${MINGW_PACKAGE_PREFIX}-python-ujson
    ${MINGW_PACKAGE_PREFIX}-python-yapf
)
makedepends=(${MINGW_PACKAGE_PREFIX}-python-setuptools
             ${MINGW_PACKAGE_PREFIX}-python-setuptools-scm
             ${MINGW_PACKAGE_PREFIX}-python-wheel)
source=(https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz)
sha256sums=('be7f83298af9f0951a93972cafc9db04fd7cf5c05f20812515275f0ba70e342f')

prepare() {
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}"
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python setup.py build

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*-script.py; do
    # Remove shebang line
    sed -e '1 { s/^#!.*$// }' -i "${_f}"
  done

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
