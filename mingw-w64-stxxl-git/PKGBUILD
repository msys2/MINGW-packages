# Maintainer: Alexey Kasatkin <alexeikasatkin@gmail.com>

_realname=stxxl
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=1.4.0.349.g20bfe2b
pkgrel=1
pkgdesc="Standard Template Library for Extra Large Data Sets (mingw-w64)"
arch=('any')
url="http://stxxl.sourceforge.net/"
license=('custom:Boost')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-cmake" 'git')
options=('strip' 'staticlibs')
source=("${_realname}"::"git+https://github.com/stxxl/stxxl.git"
        001-cmake-files-location.patch
        002-install-shared-lib.patch)
md5sums=('SKIP'
         'fa7f1ce5e89b63a01e5c82593a01dadd'
         '401d99e0a4852603054606a815e9a949')

pkgver() {
  cd "$_realname"
  git describe --tags | sed 's|-|.|g'
}

prepare() {
  cd ${_realname}
  patch -p1 -i ${srcdir}/001-cmake-files-location.patch
  patch -p1 -i ${srcdir}/002-install-shared-lib.patch
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DUSE_GNU_PARALLEL=ON \
    -DBUILD_SHARED_LIBS=ON -DBUILD_STATIC_LIBS=ON \
    ../${_realname}
  make
}


package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make install
 
  mkdir -p "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
  install -Dm644 ../${_realname}/LICENSE_1_0.txt \
    "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE_1_0.txt"

  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  local PREFIX_WIN=`pwd -W`
  popd > /dev/null
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/stxxl.pc
}
