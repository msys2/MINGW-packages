# Maintainer: Oscar Fuentes <ofv@wanadoo.es>

_realname=civetweb
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.15
pkgrel=1
pkgdesc="Embedded C/C++ web server (mingw-w64)"
arch=(any)
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://github.com/civetweb/civetweb"
license=("MIT")
depends=("${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread-git")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
options=('strip' 'staticlibs')
source=("${_realname}-${pkgver}.tar.gz::https://github.com/civetweb/civetweb/archive/v$pkgver.tar.gz")
sha256sums=('90a533422944ab327a4fbb9969f0845d0dba05354f9cacce3a5005fa59f593b9')

build() {
    [[ -d ${srcdir}/build-${MSYSTEM} ]] && rm -rf ${srcdir}/build-${MSYSTEM}
    mkdir -p ${srcdir}/build-${MSYSTEM} && cd ${srcdir}/build-${MSYSTEM}

    declare -a extra_config
    if check_option "debug" "n"; then
      extra_config+=("-DCMAKE_BUILD_TYPE=Release")
    else
      extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
    fi

    MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
        CFLAGS="-Wno-incompatible-pointer-types" \
        ${MINGW_PREFIX}/bin/cmake \
        -G "Ninja" \
        -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
        "${extra_config[@]}" \
        -DBUILD_SHARED_LIBS=YES \
        -DCIVETWEB_BUILD_TESTING=NO \
        -DCIVETWEB_ENABLE_WEBSOCKETS=YES \
        -DCIVETWEB_ENABLE_IPV6=YES \
        -DCIVETWEB_ENABLE_CXX=NO \
        -DCIVETWEB_ENABLE_SERVER_STATS=YES \
        -DCIVETWEB_SSL_OPENSSL_API_1_1=YES \
        -DCIVETWEB_ENABLE_SSL_DYNAMIC_LOADING=NO \
        -DLIBRT_LIBRARIES= \
        "${srcdir}/${_realname}-${pkgver}"

    ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
    cd "${srcdir}/build-${MSYSTEM}"
    DESTDIR=${pkgdir} ${MINGW_PREFIX}/bin/cmake --install .

    local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
    for _f in "${pkgdir}${MINGW_PREFIX}"/lib/cmake/${_realname}/*.cmake; do
        sed -e "s|${PREFIX_WIN}|\$\{_IMPORT_PREFIX\}|g" -i ${_f}
        sed -e "s|${MINGW_PREFIX}|\$\{_IMPORT_PREFIX\}|g" -i ${_f}
    done

    install -Dm644 ${srcdir}/${_realname}-${pkgver}/LICENSE.md \
        "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.md"
}
