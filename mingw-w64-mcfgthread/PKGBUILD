# Maintainer: LH_Mouse <lh_mouse@126.com>

_realname=mcfgthread
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}"
         "${MINGW_PACKAGE_PREFIX}-${_realname}"-libs)
pkgver=1.9.1
pkgrel=2
pkgdesc="An efficient gthread implementation, providing C++11 threading support (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/lhmouse/mcfgthread"
license=('spdx:LGPL-3.0-or-later WITH GCC-exception-3.1')
options=('!buildflags')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-meson"
  "${MINGW_PACKAGE_PREFIX}-crt"
  "${MINGW_PACKAGE_PREFIX}-headers"
  "git"
)
source=("git+https://github.com/lhmouse/mcfgthread.git#tag=v${pkgver%.*}-ga.${pkgver##*.}")
sha256sums=('1e42831d4c0da24928cdddd695d5b42c4f2699952fd248867328ca6e81d73a2b')

build() {
  [[ -d "${srcdir}/build-${MSYSTEM}" ]] && rm -rf "${srcdir}/build-${MSYSTEM}"
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" meson setup -Dbuildtype=plain  \
    -Dc_args='-Os -g' -Dc_link_args='-Os'  \
    --prefix="${MINGW_PREFIX}" . ../${_realname}

  # Build the DLL first, as Meson creates the DLL in the working directory,
  # which might get picked up by cc1.exe when it is only half-baked.
  ninja libmcfgthread-1.dll
  meson compile

  rm -rf "${srcdir}${MINGW_PREFIX}"
  DESTDIR="${srcdir}" meson install
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"
  meson test
}

package_mcfgthread() {
  pkgdesc+=" (devel)"
  depends=("${MINGW_PACKAGE_PREFIX}-${_realname}-libs=${pkgver}")

  mkdir -p "${pkgdir}${MINGW_PREFIX}"
  cd "${srcdir}${MINGW_PREFIX}"
  cp --parents -r include -t "${pkgdir}${MINGW_PREFIX}"
  cp --parents -r lib -t "${pkgdir}${MINGW_PREFIX}"
}

package_mcfgthread-libs() {
  pkgdesc+=" (libs)"

  mkdir -p "${pkgdir}${MINGW_PREFIX}"
  cd "${srcdir}${MINGW_PREFIX}"
  cp --parents -r bin/libmcfgthread-1.dll -t "${pkgdir}${MINGW_PREFIX}"
  cp --parents -r bin/libmcfgthread-minimal-1.dll -t "${pkgdir}${MINGW_PREFIX}"
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
