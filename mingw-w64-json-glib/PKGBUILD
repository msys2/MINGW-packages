# Maintainer: Ignacio Casal Quinteiro <icq@gnome.org>

_realname=json-glib
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.4.2
pkgrel=3
arch=('any')
pkgdesc="JSON-GLib implements a full suite of JSON-related tools using GLib and GObject (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-glib2")
makedepends=("${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             "${MINGW_PACKAGE_PREFIX}-gtk-doc")
options=('strip' 'staticlibs')
license=('LGPL2.1+')
url="https://wiki.gnome.org/Projects/JsonGlib"
source=("https://download.gnome.org/sources/${_realname}/${pkgver%.*}/${_realname}-${pkgver}.tar.xz")
sha256sums=('2d7709a44749c7318599a6829322e081915bdc73f5be5045882ed120bb686dc8')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  meson \
    --buildtype plain \
    -Ddocs=true \
    ../${_realname}-${pkgver}

  ninja

  local PREFIX_SRC="$(cygpath -wm $srcdir/)build-${MINGW_CHOST}/../"
  for _f in ${srcdir}/build-${MINGW_CHOST}/json-glib/*.h; do
    echo "Replacing ${_f}"
    sed -e "s|${PREFIX_SRC}||g" -i ${_f}
  done
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  DESTDIR=${pkgdir}${MINGW_PREFIX} ninja install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"

  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  # fix python command in files
  for _f in ${pkgdir}/${MINGW_PREFIX}/share/gtk-doc/html/json-glib/*.html; do
    echo "Replacing ${_f}"
    sed -e "s|${PREFIX_WIN}|${MINGW_PREFIX}/|g" -i ${_f}
  done

}
