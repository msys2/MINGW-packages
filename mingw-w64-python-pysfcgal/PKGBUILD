# Maintainer: Lo√Øc Bartoletti <loic.bartoletti@oslandia.com>

_realname=pysfcgal
pkgbase=mingw-w64-python-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-${_realname}
pkgver=1.5.1
pkgrel=1
pkgdesc='Python SFCGAL binding (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64' 'clang32')
msys2_references=(
  'pypi: PySFCGAL'
)
license=('spdx:MIT')
url='https://gitlab.com/SFCGAL/pysfcgal'
depends=(
    ${MINGW_PACKAGE_PREFIX}-python
    ${MINGW_PACKAGE_PREFIX}-sfcgal
    ${MINGW_PACKAGE_PREFIX}-python-cffi
)
makedepends=(${MINGW_PACKAGE_PREFIX}-python-setuptools
    ${MINGW_PACKAGE_PREFIX}-python-wheel
    ${MINGW_PACKAGE_PREFIX}-python-cffi
    ${MINGW_PACKAGE_PREFIX}-cc)
source=(https://gitlab.com/SFCGAL/pysfcgal/-/archive/v${pkgver}/pysfcgal-v${pkgver}.tar.gz)
sha256sums=('1207d4dad15eb7d18e5fcd1ffa33d8f7968505c26ec1ddbb520ecd6f0a487315')

prepare() {
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname}-v${pkgver}" "python-build-${MSYSTEM}"
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
