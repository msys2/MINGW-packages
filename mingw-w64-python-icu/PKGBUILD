# Contributor: Josip <bpisoj@gmail.com>

_realname=PyICU
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-icu" "${MINGW_PACKAGE_PREFIX}-python3-icu")
pkgver=1.9.2
pkgrel=3
pkgdesc="Python extension wrapping the ICU C++ API (mingw-w64)"
arch=('any')
license=('BSD')
url="https://pypi.python.org/pypi/PyICU/"
makedepends=("${MINGW_PACKAGE_PREFIX}-python2"
                        "${MINGW_PACKAGE_PREFIX}-python3"
                        "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
                        "${MINGW_PACKAGE_PREFIX}-python2-setuptools"
                        "${MINGW_PACKAGE_PREFIX}-icu")
options=('staticlibs' 'strip' '!debug')
source=("https://pypi.python.org/packages/source/P/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('9b580801eb17b09a5e0e8ef7d3b29170361ed505c5f954b723c1b48a1e502bb6')

prepare() {
  cd "${srcdir}"
  cp -r ${_realname}-${pkgver} build-${CARCH}
}

build() {
  CFLAGS+=" -fpermissive"
  export PYICU_CFLAGS="-DPYICU_VER=\"${pkgver}\""
  export PYICU_LFLAGS="-L${MINGW_PREFIX}/lib"
  export PYICU_INCLUDES="${MINGW_PREFIX}/include"
  export PYICU_LIBRARIES="icuin;icuuc;icudt;icule"

  # Build python 2 module
  cd "${srcdir}/build-${CARCH}"
  ${MINGW_PREFIX}/bin/python2 setup.py build

  # Build python 3 module
  cd "${srcdir}/build-${CARCH}"
  ${MINGW_PREFIX}/bin/python3 setup.py build
}

package_python3-icu() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3"
           "${MINGW_PACKAGE_PREFIX}-icu")

  cd "${srcdir}/build-${CARCH}"
  ${MINGW_PREFIX}/bin/python3 setup.py install \
      --prefix=${MINGW_PREFIX#\/} \
      --root="${pkgdir}" \
      --optimize=1 \
      --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-icu/COPYING"
}

package_python2-icu() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2"
           "${MINGW_PACKAGE_PREFIX}-icu")

  cd "${srcdir}/build-${CARCH}"
  ${MINGW_PREFIX}/bin/python2 setup.py install \
      --prefix=${MINGW_PREFIX#\/} \
      --root="${pkgdir}" \
      --optimize=1 \
      --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-icu/COPYING"
}

package_mingw-w64-i686-python2-icu() {
  package_python2-icu
}

package_mingw-w64-i686-python3-icu() {
  package_python3-icu
}

package_mingw-w64-x86_64-python2-icu() {
  package_python2-icu
}

package_mingw-w64-x86_64-python3-icu() {
  package_python3-icu
}
