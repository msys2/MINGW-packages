# Contributor: Josip <bpisoj@gmail.com>

_realname=PyICU
pkgbase=mingw-w64-python-icu
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-icu" "${MINGW_PACKAGE_PREFIX}-python3-icu")
pkgver=2.3
pkgrel=1
pkgdesc="Python extension wrapping the ICU C++ API (mingw-w64)"
arch=('any')
license=('BSD')
url="https://github.com/ovalhub/pyicu"
makedepends=("${MINGW_PACKAGE_PREFIX}-python2"
                        "${MINGW_PACKAGE_PREFIX}-python3"
                        "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
                        "${MINGW_PACKAGE_PREFIX}-python2-setuptools"
                        "${MINGW_PACKAGE_PREFIX}-icu")
options=('staticlibs' 'strip' '!debug')
source=("https://pypi.io/packages/source/P/${_realname}/${_realname}-${pkgver}.tar.gz"
        001-icu-config.patch
        002-fix-mingw-build.patch)
sha256sums=('419d389b014ee48f31014920f300c842df0770a283ab1fb4de82a6af334cac4d'
            'b12166984b7e7118c2d22934ff4370dea57635a161b7db4184e790472ec5b337'
            '95c6738d18639a2551ea42283d4a7a0d4ab675e6e9c9baa17718909c3c54aa2e')

prepare() {
  cd "${srcdir}"
  pushd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-icu-config.patch
  patch -p1 -i ${srcdir}/002-fix-mingw-build.patch
  popd

  cp -r ${_realname}-${pkgver} build-${CARCH}
}

build() {
  CFLAGS+=" -fpermissive"
  export PYICU_CFLAGS="-DPYICU_VER=\"${pkgver}\""
  export PYICU_LFLAGS="-L${MINGW_PREFIX}/lib"
  export PYICU_INCLUDES="${MINGW_PREFIX}/include"
  export PYICU_LIBRARIES="icuin;icuuc;icudt"

  # Build python 2 module
  cd "${srcdir}/build-${CARCH}"
  ${MINGW_PREFIX}/bin/python2 setup.py build

  # Build python 3 module
  cd "${srcdir}/build-${CARCH}"
  ${MINGW_PREFIX}/bin/python3 setup.py build
}

package_python3-icu() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3"
           "${MINGW_PACKAGE_PREFIX}-icu")

  cd "${srcdir}/build-${CARCH}"
  ${MINGW_PREFIX}/bin/python3 setup.py install \
      --prefix=${MINGW_PREFIX#\/} \
      --root="${pkgdir}" \
      --optimize=1 \
      --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-icu/COPYING"
}

package_python2-icu() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2"
           "${MINGW_PACKAGE_PREFIX}-icu")

  cd "${srcdir}/build-${CARCH}"
  ${MINGW_PREFIX}/bin/python2 setup.py install \
      --prefix=${MINGW_PREFIX#\/} \
      --root="${pkgdir}" \
      --optimize=1 \
      --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-icu/COPYING"
}

package_mingw-w64-i686-python2-icu() {
  package_python2-icu
}

package_mingw-w64-i686-python3-icu() {
  package_python3-icu
}

package_mingw-w64-x86_64-python2-icu() {
  package_python2-icu
}

package_mingw-w64-x86_64-python3-icu() {
  package_python3-icu
}
