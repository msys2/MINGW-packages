# Maintainer: Dirk Stolle

_realname=maxima
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=5.49.0
_sbclver=2.6.0
pkgrel=1
pkgdesc="A computer algebra system (mingw-w64)"
arch=('any')
# clangarm64 is not supported, because we do not have sbcl for that environment.
# This may change after we succeed to package ecl (Embeddable Common Lisp) and
# use that as an alternative backend.
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url='https://maxima.sourceforge.io/'
license=('spdx:GPL-2.0-only')
depends=("${MINGW_PACKAGE_PREFIX}-sbcl=${_sbclver}"
         "${MINGW_PACKAGE_PREFIX}-texinfo")
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc"
             'git'
             'perl')
optdepends=("${MINGW_PACKAGE_PREFIX}-gnuplot: plotting capabilities")
options=(!zipman)
source=("maxima::git://git.code.sf.net/p/maxima/code#tag=${pkgver}")
sha256sums=('0bbc6258d1982b60be2e6c6ed7868f687dee5af00c2d0468e19b36ad90188117')

prepare() {
  cd "${_realname}"

  autoreconf -fiv
}

build() {
  mkdir -p "build-${MSYSTEM}" && cd "build-${MSYSTEM}"

  ../"${_realname}"/configure \
    --prefix="${MINGW_PREFIX}" \
    --build="${MINGW_CHOST}" \
    --host="${MINGW_CHOST}" \
    --target="${MINGW_CHOST}" \
    --enable-sbcl \
    --with-default-lisp=sbcl

  make
}

check() {
  cd "build-${MSYSTEM}"

  make check
}

package() {
  cd "build-${MSYSTEM}"

  make install DESTDIR="${pkgdir}"

  install -Dm644 "${srcdir}/${_realname}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
