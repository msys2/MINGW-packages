# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.su>

_realname=winfile
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=10.4.0.0
pkgrel=1
pkgdesc="Original Windows File Manager (winfile) with enhancements (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/microsoft/winfile"
msys2_repository_url='https://github.com/microsoft/winfile'
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc")
options=(!strip !buildflags)
source=("${url}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "001-cpp-17.patch")
sha256sums=('90f457ed6487a1462d97525f7a2f841706b35eafeddc0b455f4b7dd213969f65'
            'd69af0453a86996bd1b1211cf8d2c7728a1c53c837162c1d239794fb42569676')

_apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -p1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  _apply_patch_with_msg \
    001-cpp-17.patch
}

build() {
  [[ -d "${srcdir}/build-${MSYSTEM}" ]] && rm -rf "${srcdir}/build-${MSYSTEM}"
  cp -r "${srcdir}/${_realname}-${pkgver}" "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  CC=cc CXX=c++ make
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  mkdir -p "${pkgdir}${MINGW_PREFIX}"/bin
  cp winfile.exe "${pkgdir}${MINGW_PREFIX}"/bin/

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSE
}
