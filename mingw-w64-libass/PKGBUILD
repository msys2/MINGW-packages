# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libass
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=0.11.1
pkgrel=1
pkgdesc="A portable library for SSA/ASS subtitles rendering (mingw-w64)"
arch=('any')
url="http://code.google.com/p/libass/"
license=("BSD")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config")
depends=("${_mingw_suff}-fribidi"
    "${_mingw_suff}-fontconfig"
    "${_mingw_suff}-freetype"
    "${_mingw_suff}-harfbuzz"
    "${_mingw_suff}-enca")
options=('strip' 'staticlibs')
source=(https://github.com/libass/${_realname}/archive/${pkgver}.tar.gz)
md5sums=('223bf8f92c0ac5f6153ab44ee4fd1855')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  ./autogen.sh
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --enable-harfbuzz \
    --enable-fontconfig \
    --enable-enca
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
