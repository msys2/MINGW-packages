# Maintainer: Taozuhong <taozuhong@gmail.com>

_realname=arrow-adbc
pkgbase=mingw-w64-${_realname}
pkgname=(
  "${MINGW_PACKAGE_PREFIX}-${_realname}"
  "${MINGW_PACKAGE_PREFIX}-${_realname}-driver-bigquery"
  "${MINGW_PACKAGE_PREFIX}-${_realname}-driver-flightsql"
  "${MINGW_PACKAGE_PREFIX}-${_realname}-driver-postgresql"
  "${MINGW_PACKAGE_PREFIX}-${_realname}-driver-sqlite"
  "${MINGW_PACKAGE_PREFIX}-${_realname}-driver-snowflake"
)
pkgver=22
pkgrel=1
pkgdesc="Database connectivity API for Apache Arrow"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://arrow.apache.org/adbc/current/index.html"
license=('Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-arrow"
         "${MINGW_PACKAGE_PREFIX}-nanoarrow")
makedepends=("git"
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-go"
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-ninja"
  "${MINGW_PACKAGE_PREFIX}-postgresql"
  "${MINGW_PACKAGE_PREFIX}-sqlite3"
  "${MINGW_PACKAGE_PREFIX}-pkgconf")
options=('staticlibs' 'strip' 'emptydirs')
source=("https://github.com/apache/arrow-adbc/archive/refs/tags/apache-arrow-adbc-${pkgver}.tar.gz")
sha256sums=('55e4129a76ead89a868fa9267e8be2ec7fcb7c41ac44a55e70c2d3591fa0b227')

build() {   
  # Update Go compiler path 
  export GOROOT=${MINGW_PREFIX}/lib/go
  
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}  
  
  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DADBC_BUILD_PYTHON=ON \
      -DADBC_DRIVER_MANAGER=ON \
      -DADBC_DRIVER_BIGQUERY=ON \
      -DADBC_DRIVER_FLIGHTSQL=ON \
      -DADBC_DRIVER_POSTGRESQL=ON \
      -DADBC_DRIVER_SQLITE=ON \
      -DADBC_DRIVER_SNOWFLAKE=ON \
      -W no-dev \
      "${_extra_config[@]}" \
      ../arrow-adbc-apache-arrow-adbc-${pkgver}/c

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

package_arrow-adbc() {
  pkgdesc+=" (mingw-w64)"
  depends=(
    ${MINGW_PACKAGE_PREFIX}-gcc-libs
  )
  optdepends=(
    "${MINGW_PACKAGE_PREFIX}-${_realname}-driver-bigquery: BigQuery driver"
    "${MINGW_PACKAGE_PREFIX}-${_realname}-driver-flightsql: Flight SQL driver"
    "${MINGW_PACKAGE_PREFIX}-${_realname}-driver-postgresql: PostgreSQL driver"
    "${MINGW_PACKAGE_PREFIX}-${_realname}-driver-sqlite: SQLite driver"
    "${MINGW_PACKAGE_PREFIX}-${_realname}-driver-snowflake: Snowflake driver"
  )

  cd "${srcdir}/build-${MINGW_CHOST}/driver_manager"
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .
}

package_arrow-adbc-driver-bigquery() {
  pkgdesc+=" - BigQuery driver (mingw-w64)"
  depends=(
    ${MINGW_PACKAGE_PREFIX}-gcc-libs
  )

  cd "${srcdir}/build-${MINGW_CHOST}/driver/bigquery"
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .
}

package_arrow-adbc-driver-flightsql() {
  pkgdesc+=" - Flight SQL driver (mingw-w64)"
  depends=(
    ${MINGW_PACKAGE_PREFIX}-gcc-libs
  )

  cd "${srcdir}/build-${MINGW_CHOST}/driver/flightsql"
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .
}

package_arrow-adbc-driver-postgresql() {
  pkgdesc+=" - PostgreSQL driver (mingw-w64)"
  depends=(
    ${MINGW_PACKAGE_PREFIX}-gcc-libs
    ${MINGW_PACKAGE_PREFIX}-postgresql
  )

  cd "${srcdir}/build-${MINGW_CHOST}/driver/postgresql"
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .
}

package_arrow-adbc-driver-sqlite() {
  pkgdesc+=" - SQLite driver (mingw-w64)"
  depends=(
    ${MINGW_PACKAGE_PREFIX}-gcc-libs
    ${MINGW_PACKAGE_PREFIX}-sqlite3
  )

  cd "${srcdir}/build-${MINGW_CHOST}/driver/sqlite"
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .
}

package_arrow-adbc-driver-snowflake() {
  pkgdesc+=" - Snowflake driver (mingw-w64)"
  depends=(
    ${MINGW_PACKAGE_PREFIX}-gcc-libs
  )

  cd "${srcdir}/build-${MINGW_CHOST}/driver/snowflake"
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
