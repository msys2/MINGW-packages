# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=mathematical
pkgbase="mingw-w64-ruby-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-ruby-${_realname}"
pkgver=1.6.14
pkgrel=1
pkgdesc="An Asciidoctor extension to converts latexmath equations to SVG or PNGs (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64')
url='https://github.com/gjtorikian/mathematical'
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-cairo"
         "${MINGW_PACKAGE_PREFIX}-glib2"  
         "${MINGW_PACKAGE_PREFIX}-lasem"         
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-pango"
         "${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2"
         "${MINGW_PACKAGE_PREFIX}-mtex2MML"
         "${MINGW_PACKAGE_PREFIX}-ruby"
         "${MINGW_PACKAGE_PREFIX}-ruby-enum" )
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake" "${MINGW_PACKAGE_PREFIX}-gcc")
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/gjtorikian/mathematical/archive/refs/tags/v${pkgver}.tar.gz"
         locate-libxml.patch)
sha256sums=('f57beac87116ac04b9ec4acfdc7c4e147938e6a11ee924f7b65e621731323abc'
            '9a9ef1193dade7da88b55c8fa133548a641f5872248b04fdb445d98cf5b3808d')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
   patch -Nbp1 -i "${srcdir}/locate-libxml.patch"
   _libxml="$(cygpath -m ${MINGW_PREFIX}/include/libxml2)"
   _pkgconf="$(cygpath -m ${MINGW_PREFIX}/bin/pkg-config)"
   sed -e "s|include/libxml2|${_libxml}|g" \
         -e "s|pkg-config|${_pkgconf}|g" \
    -i ext/mathematical/extconf.rb
}

build() {
  cd ${srcdir}/${_realname}-${pkgver}

   ${MINGW_PREFIX}/bin/gem build --verbose \
    "${_realname}.gemspec"
}

check() {
  local _gemdir="$(ruby -e 'puts Gem.default_dir')"
  _gemdir="$(cygpath -u ${_gemdir})"

  MATHEMATICAL_USE_SYSTEM_LASEM=1 \
  MATHEMATICAL_USE_SYSTEM_MTEX2MML=1 \
  ${MINGW_PREFIX}/bin/gem check --verbose \
    "${srcdir}/${_realname}-${pkgver}/${_realname}-${pkgver}.gem"
}

package() {
  local _gemdir="$(${MINGW_PREFIX}/bin/ruby -e 'puts Gem.default_dir')"
  _gemdir="$(cygpath -u ${_gemdir})"

  MATHEMATICAL_USE_SYSTEM_LASEM=1 \
  MATHEMATICAL_USE_SYSTEM_MTEX2MML=1 \
  ${MINGW_PREFIX}/bin/gem install --ignore-dependencies --no-user-install --verbose \
    -i "${pkgdir}${_gemdir}" -n "${pkgdir}${MINGW_PREFIX}/bin" \
    "${srcdir}/${_realname}-${pkgver}/${_realname}-${pkgver}.gem"

  rm "${pkgdir}${_gemdir}/cache/${_realname}-${pkgver}.gem"
  rm -rf "${pkgdir}${_gemdir}/gems/${_realname}-${pkgver}/man"
  
  # Remove local path reference from any .out file.
  local o_inst_dir=$(cygpath -m /)
  find ${pkgdir} -name "*.out" -exec  sed -e "s|${pkgdir}||g"  \
       -e "s|${o_inst_dir}||g" -i {} \;
  find ${pkgdir} -name "*.rb" -exec  sed -e "s|${pkgdir}||g"  \
       -e "s|${o_inst_dir}||g" -i {} \;       
  find ${pkgdir} -name "*.log" -exec  sed -e "s|${pkgdir}||g"  \
       -e "s|${o_inst_dir}||g" -i {} \;       
  find ${pkgdir} -name "Makefile" -exec  sed -e "s|${pkgdir}||g"  \
       -e "s|${o_inst_dir}||g" -i {} \;
  
}
