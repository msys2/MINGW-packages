_realname=highlight
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=4.3
pkgrel=1
url="http://www.andre-simon.de/doku/highlight/highlight.html"
pkgdesc="Fast and flexible source code highlighter (CLI version)"
license=('GPL')
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-lua"
             "${MINGW_PACKAGE_PREFIX}-boost")
source=("https://gitlab.com/saalen/highlight/-/archive/v${pkgver}/highlight-v${pkgver}.tar.bz2"
        "001-fix-installation-prefix-and-destdir.patch")
sha256sums=('3377562676ce1aadd97648aa0c4e4e85d26db190a090df49587ae8d2471ca851'
            '13bfb4f326bd890c14c70c0fb30fc1d9ca7a0a8f0712f1c5e946bb59b942f3bb')

prepare() {
  cd "$srcdir/${_realname}-v${pkgver}/"
  patch -p1 -i "${srcdir}"/001-fix-installation-prefix-and-destdir.patch
}

build() {
  cp -r "$srcdir/${_realname}-v${pkgver}/" "${srcdir}"/build-${MSYSTEM}
  cd "${srcdir}"/build-${MSYSTEM}

  make PREFIX=${MINGW_PREFIX}
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}
  DESTDIR="${pkgdir}" PREFIX=${MINGW_PREFIX} make install
}
