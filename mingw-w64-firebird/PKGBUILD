# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=firebird
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=5.0.1.1469
pkgrel=1
pkgdesc="Cross-platform relational database offering many ANSI SQL standard features (mingw-w64)"
url="https://www.firebirdsql.org/"
license=('spdx:Interbase-1.0 AND MPL-1.0')
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
msys2_references=(
  'archlinux: firebird'
  "cpe: cpe:/a:firebirdsql:firebird"
)
msys2_repository_url='https://github.com/FirebirdSQL/firebird/'
msys2_documentation_url='https://www.firebirdsql.org/en/documentation/'
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-icu"
         "${MINGW_PACKAGE_PREFIX}-libiconv"
         "${MINGW_PACKAGE_PREFIX}-libtomcrypt"
         "${MINGW_PACKAGE_PREFIX}-libtommath")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-curl"
             "unzip"
             "zip")
source=("https://github.com/FirebirdSQL/firebird/releases/download/v${pkgver%.*}/Firebird-${pkgver}-0-source.tar.xz"
        001-cmake-configure-fixes.patch
        002-cmake-buildconfig-fixes.patch
        003-cmake-main-sources-fixes.patch
        004-external-sources-fixes.patch
        005-firebird-sources-fixes.patch
        006-resource-file-fixes.patch
        007-pathtools-fixes.patch
        fb_config.in
        pathtools.cpp
        pathtools.h)
sha256sums=('e62d719d1d1898d777a7888b75ddbf67df8872c7a755c35d3736cbde4d57809a'
            '128725a0d2174616bc2ab58c6c644e595d25c108aaaa62c614459aa0413af665'
            'c60b4c179060720545b5a31e63f146d7551afb16b3d3deaf825ffae9404012b9'
            '608b02278ee5066df36c60543ef409b7f3ea2634db51e479da76b769b0c7839b'
            '1ea3b493d740bb7a75f1073acf8ae7820e02851d3732199f336ca6d3a3f73a63'
            '3282c8a0d4242d39784db905b2f82b2507ca2c5762802a16a39e7d5020a985cb'
            'e52e031a771391c0807272248a1b797c2b4da542beeaa27d322e4ebcc61554ed'
            'f9babbf59dfb99a5d0048647bb64fce39f821c110e768049aa955614396382e2'
            '61b8d29111b7932a4e658494b69f4eaa144095eb50b4b20b431a7cf7444b7589'
            '2f357fe2c6cdec3bfd90979616cfd075aee2de51beeaa08cd7a4192878efa64e'
            '0e6014650703817bc106825bb6a92bea72d9d790e89975c88d058fba2a26eddc')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -p1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  mv -f Firebird-${pkgver}-0-source fbsrc

  cd fbsrc
  cp -f ../fb_config.in builds/install/arch-specific/mingw
  cp -f ../pathtools.h src/common
  cp -f ../pathtools.cpp src/common/os/win32

  apply_patch_with_msg \
    001-cmake-configure-fixes.patch \
    002-cmake-buildconfig-fixes.patch \
    003-cmake-main-sources-fixes.patch \
    004-external-sources-fixes.patch \
    005-firebird-sources-fixes.patch \
    006-resource-file-fixes.patch \
    007-pathtools-fixes.patch

  cd extern/icu/tzdata
  sed -e 's|g++|c++|g' -e 's|licui18n|licuin|g' -i update.sh

  echo " "
  echo "Updating tzdata files..."
  ./update.sh
  unzip -q le.zip -d .
}

build() {
  MSYS2_ARG_CONV_EXCL="-DFB_INSTALL_PREFIX=" \
  cmake \
    -GNinja \
    -DCMAKE_BUILD_TYPE=Release \
    -DFB_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DFB_VERSION=${pkgver} \
    -DFB_PACKAGE_VERSION=${pkgrel} \
    -S fbsrc \
    -B build-${MSYSTEM}

  cmake --build build-${MSYSTEM}
}

package() {
  cd build-${MSYSTEM}
  mkdir -p ${pkgdir}${MINGW_PREFIX}/{bin,etc/${_realname},include,lib,share/${_realname}/{doc,examples/{empbuild,prebuilt},intl,misc,plugins/udr,tzdata}}

  cp -f bin/*.{exe,dll}                        ${pkgdir}${MINGW_PREFIX}/bin/
  cp -f fb_config                              ${pkgdir}${MINGW_PREFIX}/bin/fb_config
  cp -f install_service.bat                    ${pkgdir}${MINGW_PREFIX}/bin/fb_install_service.bat
  cp -f uninstall_service.bat                  ${pkgdir}${MINGW_PREFIX}/bin/fb_uninstall_service.bat
  cp -f lib/*.dll.a                            ${pkgdir}${MINGW_PREFIX}/lib/
  cp -rf include/*                             ${pkgdir}${MINGW_PREFIX}/include/
  cp -f ../fbsrc/src/misc/pascal/ib_util.pas   ${pkgdir}${MINGW_PREFIX}/include/
  cp -f ../fbsrc/src/include/gen/Firebird.pas  ${pkgdir}${MINGW_PREFIX}/include/${_realname}/
  cp -f {databases,fbtrace,firebird,plugins,replication}.conf  ${pkgdir}${MINGW_PREFIX}/etc/${_realname}/
  cp -f etc/firebird/firebird.msg              ${pkgdir}${MINGW_PREFIX}/etc/${_realname}/firebird.msg
  cp -f etc/firebird/security.fdb              ${pkgdir}${MINGW_PREFIX}/etc/${_realname}/security5.fdb
  cp -rf ../fbsrc/doc/*                        ${pkgdir}${MINGW_PREFIX}/share/${_realname}/doc/
  cp -rf ../fbsrc/examples/*                   ${pkgdir}${MINGW_PREFIX}/share/${_realname}/examples/
  cp -f examples/empbuild/employee.fdb         ${pkgdir}${MINGW_PREFIX}/share/${_realname}/examples/empbuild
  cp -rf examples/prebuilt/*                   ${pkgdir}${MINGW_PREFIX}/share/${_realname}/examples/prebuilt/
  cp -f intl/*                                 ${pkgdir}${MINGW_PREFIX}/share/${_realname}/intl/
  cp -f ../fbsrc/src/misc/intl.sql             ${pkgdir}${MINGW_PREFIX}/share/${_realname}/misc/
  cp -rf ../fbsrc/src/misc/upgrade             ${pkgdir}${MINGW_PREFIX}/share/${_realname}/misc/
  cp -rf plugins/*                             ${pkgdir}${MINGW_PREFIX}/share/${_realname}/plugins/
  cp -f share/firebird/plugins/engine13.dll    ${pkgdir}${MINGW_PREFIX}/share/${_realname}/plugins/
  cp -f ../fbsrc/extern/icu/tzdata/*.{dat,res} ${pkgdir}${MINGW_PREFIX}/share/${_realname}/tzdata/

  # Cleanups
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/*_{test,benchmark}.exe
  rm -rf ${pkgdir}${MINGW_PREFIX}/share/${_realname}/examples/build_{unix,win32}
  rm -rf ${pkgdir}${MINGW_PREFIX}/share/${_realname}/examples/CMakeFiles
  rm -rf ${pkgdir}${MINGW_PREFIX}/share/${_realname}/examples/dbcrypt/msvc
  rm -rf ${pkgdir}${MINGW_PREFIX}/share/${_realname}/examples/dyn

  install -Dm644 IDPLicense.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/IDPLicense.txt
  install -Dm644 IPLicense.txt  ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/IPLicense.txt
}
