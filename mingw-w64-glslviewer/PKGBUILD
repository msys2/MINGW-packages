# Maintainer: Naveen M K <naveen521kk@gmail.com> @naveen521kk on Github.

_realname=glslviewer
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.6.1
pkgrel=2
pkgdesc=" Console-based GLSL Sandbox for 2D/3D shaders shaders (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/patriciogonzalezvivo/glslViewer"
license=('spdx:BSD-3-Clause')
depends=(
  "${MINGW_PACKAGE_PREFIX}-cc-libs"
  "${MINGW_PACKAGE_PREFIX}-glfw"
  "${MINGW_PACKAGE_PREFIX}-glew"
)
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-pkgconf")
source=(
  "https://github.com/patriciogonzalezvivo/glslViewer/archive/${pkgver}/${_realname}-${pkgver}.tar.gz"
  '0001-add-mingw.patch'
  '0002-use-platform-windows-def.patch'
  '0003-add-missing-header.patch'
)
sha256sums=('2408e1662f2d4dd1922f00a747090c13ee8aff561123bdc86aff9da77b3ccf74'
            '608663342fa2efc54bb406e598b6fb1f090c07c137a5d121cf13fa276f72ae64'
            'a05d51af5be141e2217b5ae8c2b621d474bb1862d229fb4bc1d3f9ec94ae62aa'
            '806751b9cf1d0d4acccce0690dc2582f99f66d2d643bb56ddb98a1276b714bc6')

prepare () {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -Np1 -i "${srcdir}"/0001-add-mingw.patch
  patch -Np1 -i "${srcdir}"/0002-use-platform-windows-def.patch
  patch -Np1 -i "${srcdir}"/0003-add-missing-header.patch
  rm -r include/glew-2.1.0/*
}

build() {
  cd "${srcdir}"/${_realname}-${pkgver}
  make CXX=${CXX}
}

package() {
  install -Dm0755 "${srcdir}/${_realname}-${pkgver}/bin/glslViewer.exe" "${pkgdir}${MINGW_PREFIX}/bin/glslViewer.exe"
  install -Dm0755 "${srcdir}/${_realname}-${pkgver}/bin/glslLoader" "${pkgdir}${MINGW_PREFIX}/bin/glslLoader"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.md"
}
