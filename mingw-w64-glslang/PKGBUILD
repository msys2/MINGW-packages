# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>
_realname=glslang
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
_gitver=eee9d53
pkgver=3.0.git2.${_gitver}
pkgrel=1
pkgdesc='An OpenGL and OpenGL ES shader front end and validator (mingw-w64)'
arch=('any')
url='https://github.com/KhronosGroup/glslang'
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake" "git")
source=("git://github.com/KhronosGroup/glslang.git#commit=${_gitver}")
sha256sums=('SKIP')

build() {
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi
  ${MINGW_PREFIX}/bin/cmake \
    -G'MSYS Makefiles' \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/${MINGW_PREFIX} \
    "${extra_config[@]}" \
    -DBUILD_SHARED_LIBS=ON \
    "${srcdir}/${_realname}"
  make
}

package() {
    cd "${srcdir}"/build-${CARCH}
    make DESTDIR="${pkgdir}" install

    cd "${srcdir}/${_realname}"
    mkdir -p "${pkgdir}/${MINGW_PREFIX}/include/glslang/SPIRV"
    cp SPIRV/*.h "${pkgdir}/${MINGW_PREFIX}/include/glslang/SPIRV"

    mkdir -p "${pkgdir}/${MINGW_PREFIX}/include/glslang/glslang/"{Include,MachineIndependent,OSDependent,Public}
    cp glslang/Include/*.h "${pkgdir}/${MINGW_PREFIX}/include/glslang/glslang/Include/"
    cp glslang/MachineIndependent/*.h "${pkgdir}/${MINGW_PREFIX}/include/glslang/glslang/MachineIndependent/"
    cp glslang/OSDependent/*.h "${pkgdir}/${MINGW_PREFIX}/include/glslang/glslang/OSDependent/"
    cp glslang/Public/*.h "${pkgdir}/${MINGW_PREFIX}/include/glslang/glslang/Public/"
}
