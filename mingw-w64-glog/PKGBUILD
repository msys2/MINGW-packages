# Maintainer: Diego Sogari <diego.sogari@gmail.com>

_realname=glog
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.3.4.r232.ab65454
pkgrel=1
pkgdesc="C++ implementation of the Google logging module (mingw-w64)"
arch=('any')
url="https://github.com/google/glog"
license=('New BSD License')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-cmake")
depends=("${MINGW_PACKAGE_PREFIX}-gflags")
commit=ab6545470bdb6fb14442923b2aa3289983bea968
source=("${_realname}-${commit}.zip::https://codeload.github.com/google/glog/zip/${commit}"
        "${_realname}-${pkgver}.patch")
sha256sums=('a23bb2e9bbf7fe57d3d357511a96a47e4e236277776fd846ebb1376727fbbd6d'
            'd10e074556517a64d558a91b79fa6f5d3511d4ade0e104cce6ff4354c2ad5e10')

prepare() {
  cd "${srcdir}/${_realname}-${commit}"
  patch -Np1 -i "../${_realname}-${pkgver}.patch"
}

build() {
  rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DBUILD_SHARED_LIBS=YES \
    "../${_realname}-${commit}"

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make test
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
