# Maintainer: Nazar Mishturak <nazar m x at gmail dot com>

_realname=libbotan
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.11.0
pkgrel=1
arch=('any')
url='https://botan.randombit.net/'
pkgdesc='Botan is a C++ cryptography library released under the permissive Simplified BSD license (mingw-w64)'
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-boost"
         "${MINGW_PACKAGE_PREFIX}-bzip2"
         "${MINGW_PACKAGE_PREFIX}-sqlite3"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-xz")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-python3-sphinx"
             "${MINGW_PACKAGE_PREFIX}-python3-imagesize")
             #"${MINGW_PACKAGE_PREFIX}-doxygen"
             #"${MINGW_PACKAGE_PREFIX}-graphviz")
license=('BSD')
options=('strip' 'staticlibs' 'docs')
source=(https://botan.randombit.net/releases/Botan-${pkgver}.tar.xz{,.asc}
        '001-boost-link.patch'
        '002-winsock-link.patch'
        '003-python3-install.patch')
noextract=("Botan-${pkgver}.tar.xz")
sha256sums=('f7874da2aeb8c018fd77df40b2137879bf90b66f5589490c991e83fb3e8094be'
            'SKIP'
            'c4dfd71c7e6a9592e2a95634e3fabfc071c1110a96ba218c4a3565fa14cb34da'
            '9e0ec560ca754ea68b6763a1a4a748f2b69b0ecf79a2a7f33547b59992ef1a92'
            '4c3160fcb47791c34984d2ada5cdb6f0dfc029dfe39822ba969d4672dc2fa170')
validpgpkeys=('621DAF6411E1851C4CF9A2E16211EBF1EFBADFBC')

prepare() {
  cd "${srcdir}"
  # Tarball contains Botan-${pkgver}.tgz/.travis.yml symlink
  bsdtar -xJf "Botan-${pkgver}.tar.xz" || true
  cd "Botan-${pkgver}"
  patch -p1 -i "${srcdir}/001-boost-link.patch"
  patch -p1 -i "${srcdir}/002-winsock-link.patch"
  patch -p1 -i "${srcdir}/003-python3-install.patch"
}

build() {
  rm -rf build-${MINGW_CHOST}
  cp -rf Botan-${pkgver} build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  sed -i "s|/mingw|${MINGW_PREFIX}|g" src/build-data/os/mingw.txt

  local BUILD_TYPE_FLAGS=''
  if check_option 'debug' 'y'; then
    BUILD_TYPE_FLAGS='--with-debug-info'
  fi

  MSYS2_ARG_CONV_EXCL="--prefix=" \
  ${MINGW_PREFIX}/bin/python3 configure.py \
    --disable-shared \
    --os=mingw \
    --cc=gcc \
    --prefix=${MINGW_PREFIX} \
    --cpu=${CARCH} \
    ${BUILD_TYPE_FLAGS} \
    --link-method=copy \
    --without-doxygen \
    --with-sphinx \
    --without-rst2man \
    --with-boost \
    --with-openmp \
    --with-bzip2 \
    --with-lzma \
    --with-sqlite3 \
    --with-zlib

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  ./botan-test
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
  DESTDIR="${pkgdir}" \
  make install
}
