# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_pyname=PyMuPDF
_realname=pymupdf
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.26.7
pkgrel=3
pkgdesc="Python bindings for MuPDF's rendering library (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://pymupdf.io/'
msys2_repository_url='https://github.com/pymupdf/PyMuPDF/'
msys2_documentation_url='https://pymupdf.readthedocs.io/'
msys2_references=(
  'archlinux: python-pymupdf'
  'purl: pkg:pypi/pymupdf'
)
license=('spdx:AGPL-3.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-mupdf"
         "${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-libmupdf")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-distro"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-swig")
optdepends=("${MINGW_PACKAGE_PREFIX}-python-fonttools: for building font subsets using fontTools"
            "${MINGW_PACKAGE_PREFIX}-python-pillow: for image file saving using pillow")
options=('!strip')
source=("https://pypi.org/packages/source/${_pyname::1}/${_pyname}/${_realname}-${pkgver}.tar.gz"
        0001-pymupdf-add-mingw-setup.patch)
sha256sums=('71add8bdc8eb1aaa207c69a13400693f06ad9b927bea976f5d5ab9df0bb489c3'
            '0cd55c465bb0e10d42c57cd8d95ea840e30adcf368db2007475fcdaedafa7eae')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-pymupdf-add-mingw-setup.patch"
}

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  # Use system provided mupdf libraries
  export PYMUPDF_SETUP_IMPLEMENTATIONS=b
  export PYMUPDF_SETUP_MUPDF_BUILD=''
  export PYMUPDF_MUPDF_LIB=${MINGW_PREFIX}/bin
  export PIPCL_PYTHON_CONFIG=${MINGW_PREFIX}/bin/python-config

  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/COPYING"
}
