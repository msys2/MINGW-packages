# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_pyname=PyMuPDF
_realname=pymupdf
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.24.4
pkgrel=3
pkgdesc="Python bindings for MuPDF's rendering library (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://pymupdf.io/'
msys2_repository_url='https://github.com/pymupdf/PyMuPDF/'
msys2_documentation_url='https://pymupdf.readthedocs.io/'
msys2_references=(
  'archlinux: python-pymupdf'
  'purl: pkg:pypi/pymupdf'
)
license=('spdx:AGPL-3.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-freetype"
         "${MINGW_PACKAGE_PREFIX}-gumbo-parser"
         "${MINGW_PACKAGE_PREFIX}-harfbuzz"
         "${MINGW_PACKAGE_PREFIX}-jbig2dec"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-openjpeg2"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-distro"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-libmupdf"
             "${MINGW_PACKAGE_PREFIX}-swig")
optdepends=("${MINGW_PACKAGE_PREFIX}-python-fonttools: for building font subsets using fontTools"
            "${MINGW_PACKAGE_PREFIX}-python-pillow: for image file saving using pillow")
options=('!strip')
source=("https://pypi.org/packages/source/${_pyname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz"
        0001-pymupdf-add-mingw-setup.patch)
sha256sums=('03f9e8656e2799a2d7a1d69719da4cd45ce9a030e06a5887e04c5d531035bfa2'
            '92b2ee25555c6d1f2728c2e87d868a6921c262e03c1e6dc722d9857b0e666d33')

prepare() {
  cd "${srcdir}/${_pyname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-pymupdf-add-mingw-setup.patch"
}

build() {
  cp -r "${_pyname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  # Use system provided mupdf libraries
  PYMUPDF_SETUP_IMPLEMENTATIONS=a \
  PYMUPDF_SETUP_MUPDF_BUILD="" \
  PIPCL_PYTHON_CONFIG=${MINGW_PREFIX}/bin/python-config \
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/COPYING"
}
