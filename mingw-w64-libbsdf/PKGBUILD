# Maintainer: Marko KÃ¤ning <mk-lists@mailbox.org>
# Contributor: Andrew Sun <adsun701@gmail.com>

_realname=libbsdf
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.9.7
pkgrel=1
epoch=
pkgdesc="Library for handling BSDF files (mingw-w64)"
arch=('any')
url='https://github.com/KimuraRyo/libbsdf'
license=('MPL2')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-eigen3")
source=(${_realname}-${pkgver}.tar.gz::"https://github.com/KimuraRyo/${_realname}/archive/v${pkgver}.tar.gz"
        'optimisations-CMakeLists.txt.patch')
sha256sums=('1c7c4f4c0a3f9e1cce345f43f3568f96529260cfdb4701669cabfce42348d273'
            '9eee27da0e486e186cf75ad05dcfa0b0266dafec3881e45aa295a85474d66c14')


prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -Np0 -i "${srcdir}/optimisations-CMakeLists.txt.patch"
}


build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir build-${MINGW_CHOST} && cd build-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DEIGEN3_INCLUDE_DIR=${MINGW_PREFIX}/include/eigen3 \
    -DCMAKE_CXX_FLAGS=-std=c++11 \
    ..

  make
}

package() {
  mkdir -p ${pkgdir}${MINGW_PREFIX}/share/licenses/libbsdf
  cp -f "${srcdir}"/${_realname}-${pkgver}/LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/libbsdf

  mkdir -p ${pkgdir}${MINGW_PREFIX}/include
  cp -rf "${srcdir}"/${_realname}-${pkgver}/include/libbsdf ${pkgdir}${MINGW_PREFIX}/include/

  mkdir -p ${pkgdir}${MINGW_PREFIX}/lib
  cp -f "${srcdir}"/${_realname}-${pkgver}/build-${MINGW_CHOST}/liblibbsdf.a ${pkgdir}${MINGW_PREFIX}/lib/
}
