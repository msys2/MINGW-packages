# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=pytest-cov
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}"  "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=2.8.1
pkgrel=1
pkgdesc="py.test plugin for coverage reporting with support for both centralised and distributed testing, including subprocesses and multiprocessing (mingw-w64)"
arch=('any')
url='https://github.com/schlamar/pytest-cov'
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-python2-coverage"
             "${MINGW_PACKAGE_PREFIX}-python3-coverage"
             "${MINGW_PACKAGE_PREFIX}-python2-pytest"
             "${MINGW_PACKAGE_PREFIX}-python3-pytest"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python2-virtualenv"
              "${MINGW_PACKAGE_PREFIX}-python3-virtualenv"
#              "${MINGW_PACKAGE_PREFIX}-python2-process-tests"
#              "${MINGW_PACKAGE_PREFIX}-python3-process-tests"
#              "${MINGW_PACKAGE_PREFIX}-python2-pytest-cache"
#              "${MINGW_PACKAGE_PREFIX}-python3-pytest-cache"
              "${MINGW_PACKAGE_PREFIX}-python2-pytest-xdist"
              "${MINGW_PACKAGE_PREFIX}-python3-pytest-xdist"
#              "${MINGW_PACKAGE_PREFIX}-python2-fields"
#              "${MINGW_PACKAGE_PREFIX}-python3-fields"
)
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-$pkgver.tar.gz::https://github.com/pytest-dev/pytest-cov/archive/v$pkgver.tar.gz")
sha512sums=('63e4180a5e64579c87aada2f5f3afb5bfe4c4d3f4d39d694abebb05c56761b758cb9a5ccc96569acea595b9333412f34be6b1a0e1cea8a9b580694bfa11df303')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"
  for builddir in python{2,3}-build-${CARCH}; do
    rm -rf ${builddir} | true
    cp -r "${_realname}-${pkgver}" "${builddir}"
  done
  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  for pver in {2,3}; do  
    msg "Python ${pver} build for ${CARCH}"  
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py build
  done  
}

check() {
  for pver in {2,3}; do
    msg "Python ${pver} test for ${CARCH}"
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py test
  done
}

package_python3-pytest-cov() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3"
           "${MINGW_PACKAGE_PREFIX}-python3-coverage"
           "${MINGW_PACKAGE_PREFIX}-python3-pytest")

  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}

package_python2-pytest-cov() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2"
           "${MINGW_PACKAGE_PREFIX}-python2-coverage"
           "${MINGW_PACKAGE_PREFIX}-python2-pytest")

  cd "${srcdir}/python2-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE"
}

package_mingw-w64-i686-python2-pytest-cov() {
  package_python2-pytest-cov
}

package_mingw-w64-i686-python3-pytest-cov() {
  package_python3-pytest-cov
}

package_mingw-w64-x86_64-python2-pytest-cov() {
  package_python2-pytest-cov
}

package_mingw-w64-x86_64-python3-pytest-cov() {
  package_python3-pytest-cov
}
