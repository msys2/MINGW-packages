# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.su>

_realname=webview2-loader
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.0.3650.58
pkgrel=1
pkgdesc="Microsoft WebView2 Loader (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://learn.microsoft.com/en-us/microsoft-edge/webview2'
license=('spdx:BSD-3-Clause')
source=("webview2-loader-${pkgver}.zip::https://www.nuget.org/api/v2/package/Microsoft.Web.WebView2/${pkgver}")
sha256sums=('911a472128c82ac8baa0c486c23342cc9dd6e7dc50d754e676726642ca065c60')

package() {
  if [[ ${CARCH} == x86_64 ]]; then
    _arch=x64
  elif [[ ${CARCH} == aarch64 ]]; then
    _arch=arm64
  fi

  install -Dm644 build/native/${_arch}/WebView2Loader.dll "${pkgdir}"${MINGW_PREFIX}/bin/WebView2Loader.dll

  install -d "${pkgdir}"${MINGW_PREFIX}/include
  install -Dm644 build/native/include/*.h "${pkgdir}"${MINGW_PREFIX}/include/

  install -Dm644 LICENSE.txt "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
