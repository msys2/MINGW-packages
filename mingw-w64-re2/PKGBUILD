# Maintainer: gym603 <gui_yuan_miao@163.com>

_realname=re2
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
_pkgver=2019-03-01
pkgver=${_pkgver//-/_}
pkgrel=1
pkgdesc="A fast, safe, thread-friendly alternative to backtracking regular expression engines like those used in PCRE, Perl, and Python (mingw-w64)"
arch=('any')
url="https://github.com/google/re2"
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "make"
             "${MINGW_PACKAGE_PREFIX}-gcc")
_archive=${_realname}-${_pkgver}
source=(${_archive}.tar.gz::https://github.com/google/re2/archive/${_pkgver}.tar.gz
        0001-re2-mingw-w64.patch)
sha256sums=('05DC21AAFC1FA21161214C6145AEED8A6D108BDE390584C762AA3E64DF557D6D'
            'EF8D8B99949AB3B0F1EB67CE1694EA9F852ED4E2AD2586232A39575F7719E2B7')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  rm -rf "${srcdir}/${_archive}.orig"
  cp -rp "${srcdir}/${_archive}" "${srcdir}/${_archive}.orig"
  cd "${srcdir}/${_archive}"
  apply_patch_with_msg \
    0001-re2-mingw-w64.patch
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  ${MINGW_PREFIX}/bin/cmake -G "MSYS Makefiles" \
    -DCMAKE_BUILD_TYPE:STRING=Release \
    -DRE2_BUILD_TESTING=OFF \
    -DBUILD_SHARED_LIBS=ON \
    ../${_archive}

  make -j1
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"

  #make test
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"

  ${MINGW_PREFIX}/bin/cmake -DCMAKE_INSTALL_PREFIX:PATH="${pkgdir}${MINGW_PREFIX}" -P cmake_install.cmake

  cd "${srcdir}/${_archive}"
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
