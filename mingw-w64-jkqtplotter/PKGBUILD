# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=jkqtplotter
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
_pkgver=4.0.3
pkgver=4.0.3.r680
pkgrel=1
pkgdesc="An extensive Qt Plotter framework. (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url="https://github.com/jkriege2/JKQtPlotter"
msys2_repository_url='https://github.com/jkriege2/JKQtPlotter'
msys2_references=(
  'archlinux: jkqtplotter'
)
license=('spdx:LGPL-2.1-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-qt6-base"
         "${MINGW_PACKAGE_PREFIX}-qt6-svg"
         "${MINGW_PACKAGE_PREFIX}-qt6-translations")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             'git')
_commit='d243218119b1632987df26baea0d4bc6ccdee533'
source=("${_realname}"::"git+https://github.com/jkriege2/JKQtPlotter.git#commit=${_commit}"
        "001-some-fixes-for-mingw.patch")
sha256sums=('f5fdc43c64ad5ddb9be9c7b9a33098848d8103d15b3633c0242090579f82d5bb'
            '9ccfe3940b777c44cc0ac5e1919889161b4530dec61864339dd00c575100d96b')

pkgver() {
  cd "${_realname}"
  echo "${_pkgver}.r"`git rev-list --count v${_pkgver}..$_commit`
}

prepare() {
  cd "${_realname}"

  patch -Np1 -i "${srcdir}"/001-some-fixes-for-mingw.patch
}

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DBUILD_SHARED_LIBS=ON \
      -DJKQtPlotter_BUILD_EXAMPLES=OFF \
      -DJKQtPlotter_BUILD_TESTS=OFF \
      -DJKQtPlotter_BUILD_DECORATE_LIBNAMES_WITH_BUILDTYPE=OFF \
      -S "${_realname}" \
      -B "build-${MSYSTEM}"

  cmake --build "build-${MSYSTEM}"
}

check() {
  cmake -DJKQtPlotter_BUILD_TESTS=ON -S "${_realname}" -B "build-${MSYSTEM}"
  cmake --build "build-${MSYSTEM}"
  ctest --test-dir "build-${MSYSTEM}" --output-on-failure || true
}

package() {
  DESTDIR="${pkgdir}" cmake --install "build-${MSYSTEM}"

  install -Dm644 "${srcdir}"/${_realname}/LICENSE \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
