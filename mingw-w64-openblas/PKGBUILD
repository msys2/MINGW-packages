# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: JÃ¼rgen Pfeifer <juergen@familiepfeifer.de>

_realname=OpenBLAS
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-openblas"
pkgver=0.2.14
pkgrel=2
pkgdesc="An optimized BLAS library based on GotoBLAS2 1.13 BSD, providing optimized blas, lapack, and cblas (mingw-w64)"
arch=('any')
url="http://www.openblas.net/"
license=('BSD')
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-openblas-git")
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}"
          "${MINGW_PACKAGE_PREFIX}-${_realname}-git")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gcc-libgfortran"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread")
makedepends=("perl"
             "${MINGW_PACKAGE_PREFIX}-gcc-fortran")
options=('!buildflags' '!makeflags' 'strip' 'staticlibs')
source=(${_realname}-${pkgver}.tar.gz::https://github.com/xianyi/OpenBLAS/archive/v${pkgver}.tar.gz)
install=${_realname}.install
sha256sums=('2411c4f56f477b42dff54db2b7ffc0b7cf53bb9778d54982595c64cc69c40fc1')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  unset CFLAGS
  unset CXXFLAGS

  make LIBNAME=libopenblasp.a NO_LAPACK=0 DYNAMIC_ARCH=1 USE_THREAD=1 \
       MAJOR_VERSION=3 NO_AFFINITY=1 CC=gcc FC=gfortran \
       OPENBLAS_INCLUDE_DIR=${MINGW_PREFIX}/include/${_realname} \
       2>make.err
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  if [ -f libopenblas.dll ]; then
    if [ -f exports/libopenblas.def ]; then
      dlltool -D libopenblas.dll -d exports/libopenblas.def -l libopenblas.dll.a
    else
      echo "Cannot build export library" >&2
      exit 1
    fi
  else
    echo "DLL not found" >&2
    exit 1
  fi
  mkdir -p "${pkgdir}${MINGW_PREFIX}/bin"
  mkdir -p "${pkgdir}${MINGW_PREFIX}/lib"

  make $MAKEFLAGS -f Makefile LIBNAME=libopenblasp.a \
       DESTDIR="${pkgdir}" PREFIX="${MINGW_PREFIX}"  \
       OPENBLAS_INCLUDE_DIR=${MINGW_PREFIX}/include/${_realname} \
       install

  if [ -f libopenblas.dll ]; then
    cp -f libopenblas.dll "${pkgdir}${MINGW_PREFIX}/bin/"
  else
    echo "DLL not found" >&2
    exit 1
  fi

  if [ -f libopenblas.dll.a ]; then
    cp -f libopenblas.dll.a "${pkgdir}${MINGW_PREFIX}/lib/"
  else
    echo "Export library not found" >&2
    exit 1
  fi

  if [ -f libopenblasp.a ]; then
    rm -f "${pkgdir}$MINGW_PREFIX/lib/libopenblasp.a"
    rm -f "${pkgdir}$MINGW_PREFIX/lib/libopenblas.a"
    cp -f libopenblasp.a "${pkgdir}${MINGW_PREFIX}/lib/libopenblas.a"
  else
    echo "Static library not found" >&2
    exit 1
  fi

  # Copy Licencse
  mkdir -p ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}
  cp -f ${srcdir}/${_realname}-${pkgver}/LICENSE \
    ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}
  cp -f ${srcdir}/${_realname}-${pkgver}/lapack-netlib/LICENSE \
    ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE-lapack
}
