# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=adwaita-icon-theme
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=3.22.0
pkgrel=1
pkgdesc="GNOME icon theme (mingw-w64)"

provides=(
    "${MINGW_PACKAGE_PREFIX}-${_realname}"
)
conflicts=(
    "${MINGW_PACKAGE_PREFIX}-${_realname}"
)

arch=('any')
url="https://git.gnome.org/browse/adwaita-icon-theme/"
license=("GPL")
makedepends=("autoconf" "automake" "intltool" "icon-naming-utils" "${MINGW_PACKAGE_PREFIX}-gtk3" "${MINGW_PACKAGE_PREFIX}-python2" "${MINGW_PACKAGE_PREFIX}-python2-pillow")
depends=("${MINGW_PACKAGE_PREFIX}-hicolor-icon-theme"
         "${MINGW_PACKAGE_PREFIX}-librsvg")
options=(!libtool strip staticlibs)
install=theme-${CARCH}.install
source=(${_realname}-${pkgver}::git+https://git.gnome.org/browse/adwaita-icon-theme#tag=3.22.0
        send-to-symbolic_fix.patch)
sha256sums=('SKIP'
            0352ad24dc1c587711e578fb556dc3ab11e7eab4bb87dbe3f37e3c438e2fd5b9)

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p0 -i ${srcdir}/send-to-symbolic_fix.patch
  autoreconf -vfi
}

build() {  
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST}

  make
  
  echo "Generating cursors (ani/cur) ..."
  cd ${srcdir}/${_realname}-${pkgver}/src/cursors/pngs
  sed -i 's_#!/usr/bin/python2_#!/usr/bin/env python2_' -i ../anicursorgen.py
  sed -i 's_for theme in Adwaita Adwaita-Large Adwaita-ExtraLarge_for theme in Adwaita_' -i make-w32.sh
  ./make-w32.sh
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install
  mkdir -p ${pkgdir}${MINGW_PREFIX}/lib
  mv ${pkgdir}${MINGW_PREFIX}/share/pkgconfig ${pkgdir}${MINGW_PREFIX}/lib/
  
  rm -f ${pkgdir}${MINGW_PREFIX}/share/icons/Adwaita/cursors/*

  echo "Installing cursors (ani)"
  for f in "${srcdir}/${_realname}-${pkgver}"/Adwaita/cursors/*.ani
  do
    install -Dm644 $f ${pkgdir}${MINGW_PREFIX}/share/icons/Adwaita/cursors/
  done

  echo "Installing cursors (cur)"  
  for f in "${srcdir}/${_realname}-${pkgver}"/Adwaita/cursors/*.cur
  do
    install -Dm644 $f ${pkgdir}${MINGW_PREFIX}/share/icons/Adwaita/cursors/
  done
  
  install -Dm644 "${srcdir}/${_realname}-${pkgver}"/Adwaita/cursors/hand1.cur ${pkgdir}${MINGW_PREFIX}/share/icons/Adwaita/cursors/grab.cur
  install -Dm644 "${srcdir}/${_realname}-${pkgver}"/Adwaita/cursors/dnd-no-drop.cur ${pkgdir}${MINGW_PREFIX}/share/icons/Adwaita/cursors/no-drop.cur
  install -Dm644 "${srcdir}/${_realname}-${pkgver}"/Adwaita/cursors/dnd-link.cur ${pkgdir}${MINGW_PREFIX}/share/icons/Adwaita/cursors/alias.cur  
  
  install -Dm644 "${srcdir}/${_realname}-${pkgver}"/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
  install -Dm644 "${srcdir}/${_realname}-${pkgver}"/COPYING_LGPL ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING_LGPL
  install -Dm644 "${srcdir}/${_realname}-${pkgver}"/COPYING_CCBYSA3 ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING_CCBYSA3
}
