# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=dia
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.97.0.r2562.gbd551bb25
pkgrel=1
pkgdesc="A gtk+ based diagram creation program"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://gitlab.gnome.org/GNOME/dia'
license=('spdx:GPL-2.0-or-later')
depends=(
  "${MINGW_PACKAGE_PREFIX}-glib2"
  "${MINGW_PACKAGE_PREFIX}-gtk3"
  "${MINGW_PACKAGE_PREFIX}-libxml2"
  "${MINGW_PACKAGE_PREFIX}-graphene"
  "${MINGW_PACKAGE_PREFIX}-cairo"
  "${MINGW_PACKAGE_PREFIX}-zlib"
  "${MINGW_PACKAGE_PREFIX}-libxslt"
  "${MINGW_PACKAGE_PREFIX}-poppler"
)
makedepends=("${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-desktop-file-utils"
             "${MINGW_PACKAGE_PREFIX}-gtk-update-icon-cache"
             "git")
_commit="bd551bb2558dcc89bc0bf7b4dd85b38cd85ad322"
source=("${_realname}"::"git+https://gitlab.gnome.org/GNOME/dia.git#commit=${_commit}"
        "0001-fix-lib-path.patch")
sha256sums=('SKIP'
            '8d113f6ebbe609497c6d2eaa6c3c6d620002440d86dda676e0f6d9e1566643e5')

pkgver() {
  cd "${_realname}"

  git describe --long "${_commit}" | sed 's/DIA_//;s/_/./g' | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//g'
}

prepare() {
  cd "${_realname}"

  patch -Np1 -i "${srcdir}/0001-fix-lib-path.patch"
}

build() {
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    meson setup \
      --prefix="${MINGW_PREFIX}" \
      --wrap-mode=nodownload \
      --auto-features=enabled \
      --buildtype=plain \
      -Ddoc=false \
      ../${_realname}

  meson compile
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" meson install
  rm "${pkgdir}${MINGW_PREFIX}"/lib/dia/*.dll.a

  install -Dm644 "${srcdir}/${_realname}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
