# $Id$
# Maintainer (ArchLinux): Andrea Scarpino <andrea@archlinux.org>
# Maintainer (MSYS2): Ray Donnelly <mingw.android@gmail.com>

_variant=-static
#_variant=-shared

if [ "${_variant}" = "-static" ]; then
  _namesuff="-static"
else
  _namesuff=
fi

_realname=attica-qt5${_namesuff}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=5.3.0
pkgrel=1
arch=('any')
url='https://projects.kde.org/projects/kdesupport/attica'
pkgdesc="A Qt5 library that implements the Open Collaboration Services API (mingw-w64-qt5${_namesuff})"
license=('LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-qt5${_namesuff}")
makedepends=("${MINGW_PACKAGE_PREFIX}-extra-cmake-modules")
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver}/attica-${pkgver}.tar.xz")
md5sums=('194b9a2141a2a4e23f2bc6dde92178f5')

prepare() {
  mkdir -p build-${CARCH}${_variant}
}

build() {
  local -a extra_config
  cd build-${CARCH}${_variant}
  if [ "${_variant}" = "-static" ]; then
    extra_config+=("-DATTICA_STATIC_BUILD=NO")
    QT5_PREFIX=${MINGW_PREFIX}/qt5-static
    export PATH=${QT5_PREFIX}/bin:"$PATH"
  else
    QT5_PREFIX=${MINGW_PREFIX}
  fi

  cmake ../attica-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=${QT5_PREFIX} \
    -DLIB_INSTALL_DIR=lib \
    -DECM_MKSPECS_INSTALL_DIR=${QT5_PREFIX} \
    -DQt5_DIR=${QT5_PREFIX}/lib/cmake/Qt5 \
    -DBUILD_TESTING=OFF \
    -DECM_DIR=${MINGW_PREFIX}/share/ECM \
    "${extra_config[@]}" \
    -G'MSYS Makefiles' --debug-output
  make
}

package() {
  cd build-${CARCH}${_variant}
  make DESTDIR="${pkgdir}" install
}

