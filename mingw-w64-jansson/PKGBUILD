# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Maintainer: Chilledheart <rwindz0@gmail.com>

_realname=jansson
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.7
pkgrel=2
pkgdesc="A C library for encoding, decoding and manipulating JSON data (mingw-w64)"
arch=('any')
url="http://www.digip.org/jansson/"
license=('MIT')
source=("http://www.digip.org/${_realname}/releases/${_realname}-${pkgver}.tar.gz")
sha256sums=('7905e6590fb316c0ff943df3dc6a21cd81a59cff7a6d12514054c359d04d78d7')

prepare() {
  cd "${srcdir}/${_realname}-$pkgver"
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
