# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>
# Contributor: wirx6 <wirx654@gmail.com>

_realname=Vulkan-ValidationLayers
pkgbase=mingw-w64-vulkan-validation-layers
pkgname=("${MINGW_PACKAGE_PREFIX}-vulkan-validation-layers")
pkgver=1.3.240
pkgrel=2
pkgdesc='Vulkan Validation Layers (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://www.khronos.org/vulkan/"
groups=("${MINGW_PACKAGE_PREFIX}-vulkan-devel")
license=('spdx:Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-vulkan-loader"
         "${MINGW_PACKAGE_PREFIX}-spirv-tools")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-glslang"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-vulkan-headers"
             "${MINGW_PACKAGE_PREFIX}-spirv-headers")
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/KhronosGroup/${_realname}/archive/v${pkgver}.tar.gz"
        "001-fix-linking.patch::https://github.com/KhronosGroup/Vulkan-ValidationLayers/commit/d6245860b5a52efb89fe5d6de7aeab6e6b380f12.patch"
        "003-install-layout.patch::https://github.com/KhronosGroup/Vulkan-ValidationLayers/commit/40a2836962aaccd12dee38665b827cebf39b38c8.patch")
sha256sums=('4ea885a071bcac8af8e9a0fd319af3e4114e214ebe2564f67729a78b37eee1f0'
            'a6f2dfbd044ce86e978aac7842b99d5f0456879f4c2e6d9a3297f67b21f4d4ce'
            '1f38010033763eb3f5cacd62f6b9f61bb9b331734ec3acc8cd680c95fc19a70c')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Np1 -i "${srcdir}/$_patch"
  done
}

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  apply_patch_with_msg \
    001-fix-linking.patch \
    003-install-layout.patch
}

build() {
  [[ -d ${srcdir}/build-${MSYSTEM} ]] && rm -rf ${srcdir}/build-${MSYSTEM}
  mkdir -p ${srcdir}/build-${MSYSTEM} && cd ${srcdir}/build-${MSYSTEM}

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  if [[ "${MSYSTEM}" == "MINGW32" ]]; then
    # https://www.mail-archive.com/devel@lists.fedoraproject.org/msg167502.html
    extra_config+=("-DCMAKE_CXX_FLAGS_RELEASE=-O1 -DNDEBUG")
  fi

  CXXFLAGS+=" -Wno-unused-function" \
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G "Ninja" \
    "${extra_config[@]}" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DGLSLANG_INSTALL_DIR=${MINGW_PREFIX} \
    -DSPIRV_HEADERS_INSTALL_DIR=${MINGW_PREFIX} \
    -DBUILD_LAYER_SUPPORT_FILES=ON \
    -DBUILD_TESTS=OFF \
    -DBUILD_WERROR=OFF \
    -DUSE_ROBIN_HOOD_HASHING=OFF \
    ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake.exe --build ./
}

package() {
  cd ${srcdir}/build-${MSYSTEM}
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install .

  install -dm755 ${pkgdir}${MINGW_PREFIX}/share/doc/vulkan-validation-layers/
  cp -r ${srcdir}/${_realname}-${pkgver}/docs/* ${pkgdir}${MINGW_PREFIX}/share/doc/vulkan-validation-layers/
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/LICENSE.TXT ${pkgdir}${MINGW_PREFIX}/share/licenses/vulkan-validation-layers/LICENSE
}
