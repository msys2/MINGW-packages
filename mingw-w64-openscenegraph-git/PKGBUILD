# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=OpenSceneGraph
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}-git"
pkgver=r12610.ae9d19b
pkgrel=1
pkgdesc="Open source high performance 3D graphics toolkit (mingw-w64)"
arch=('any')
url="http://www.openscenegraph.org/"
license=("LGPL")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config" "${_mingw_suff}-cmake" "subversion")
depends=("${_mingw_suff}-crt"
    "${_mingw_suff}-collada-dom-svn"
    "${_mingw_suff}-zlib"
    "${_mingw_suff}-boost"
    "${_mingw_suff}-curl"
    "${_mingw_suff}-lua"
    "${_mingw_suff}-freeglut"
    "${_mingw_suff}-libjpeg"
    "${_mingw_suff}-libpng"
    "${_mingw_suff}-libtiff"
    "${_mingw_suff}-libxml2"
    "${_mingw_suff}-SDL"
    "${_mingw_suff}-jasper"
    "${_mingw_suff}-qt5"
    "${_mingw_suff}-gtkglext"
    "${_mingw_suff}-giflib")
provides=("${_mingw_suff}-${_realname}")
conflicts=("${_mingw_suff}-${_realname}")
options=('staticlibs' '!strip')
source=("${_realname}"::"git+https://github.com/openscenegraph/osg.git"
    osg-collada-dae-fixes.patch
    find-collada.patch
    find-freetype.patch)
md5sums=('SKIP'
         '45d0668f56c5e4ca58002f38fd3eb4a0'
         '668def9c99f044db9a8215fa927d6ac2'
         '9ab69e1cd26826196fac5b3f01e6cb95')

pkgver() {
  cd "$srcdir/$_realname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_realname}"
  patch -p1 -i ${srcdir}/osg-collada-dae-fixes.patch
  patch -p1 -i ${srcdir}/find-collada.patch
  patch -p1 -i ${srcdir}/find-freetype.patch
}

build() {
  mkdir -p ${pkgdir}${MINGW_PREFIX}
  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null

  pushd ${MINGW_PREFIX} > /dev/null
  export PREFIX_DEPS=`pwd -W`
  popd > /dev/null
  
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  export FREETYPE_DIR=${PREFIX_DEPS}
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX:PATH=${PREFIX_WIN} \
    -DBUILD_OSG_EXAMPLES:BOOL=ON \
    -DACTUAL_3DPARTY_DIR=${PREFIX_DEPS} \
    ../${_realname}
    #-DFREETYPE_DIR=${PREFIX_DEPS}
    #-DCOLLADA_INCLUDE_DIR=$PREFIX/include/collada-dom2.4 \
    #-DCOLLADA_DYNAMIC_LIBRARY=$PREFIX/lib/libcollada-dom2.4-dp.dll.a \
    #-DCOLLADA_BOOST_FILESYSTEM_LIBRARY=$PREFIX/lib/libboost_filesystem-mt.a \
    #-DCOLLADA_BOOST_SYSTEM_LIBRARY=$PREFIX/lib/libboost_system-mt.a
    #-DGIFLIB_DIR=$PREFIX \
    #
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 install
  mkdir -p ${pkgdir}${MINGW_PREFIX}/bin
  mv ${pkgdir}${MINGW_PREFIX}/lib/*.dll ${pkgdir}${MINGW_PREFIX}/bin/
}
