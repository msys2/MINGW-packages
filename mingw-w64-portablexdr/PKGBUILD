# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=portablexdr
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
_base_ver=4.9.2
pkgver=4.9.2.r27.94fb83c
pkgrel=6
pkgdesc="PortableXDR / RPC Library (mingw-w64)"
arch=(any)
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/Alexpux/portablexdr"
msys2_repository_url='https://github.com/Alexpux/portablexdr'
license=('spdx:LGPL-2.0-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "git")
_commit=94fb83c2bd67e5f9ff08e9c492d391b297f7c2f8
source=(${_realname}::git+https://github.com/Alexpux/portablexdr.git#commit=${_commit})
sha256sums=('78a9fe0066af6a222d07c93bfa7c75ed578447ad3be75c271c48e7609ea26626')

pkgver() {
  cd "${srcdir}/${_realname}"
  printf "%s.r%s.%s" ${_base_ver} "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_realname}"
  sed -i "s|-Wall -Werror||g" Makefile.am
  autoreconf -fiv
}

build() {
  mkdir -p "$srcdir/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  CFLAGS+=" -std=gnu17 -Wno-implicit-function-declaration -Wno-incompatible-pointer-types" \
  ../${_realname}/configure \
      --prefix=${MINGW_PREFIX} \
      --enable-shared \
      --enable-static

  make
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/${_realname}/COPYING \
    "$pkgdir"${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
  install -Dm644 "${srcdir}"/${_realname}/COPYING>LIB \
    "$pkgdir"${MINGW_PREFIX}/share/licenses/${_realname}/COPYING.LIB
}
