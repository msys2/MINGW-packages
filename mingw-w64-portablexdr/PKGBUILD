# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=portablexdr
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.9.1
pkgrel=1
pkgdesc="PortableXDR / RPC Library (mingw-w64)"
arch=(any)
url="http://et.redhat.com/~rjones/portablexdr/"
license=('LGPL2')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
groups=("${MINGW_PACKAGE_PREFIX}")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-libtool")
options=('staticlibs' 'strip')
source=("http://people.redhat.com/~rjones/portablexdr/files/${_realname}-${pkgver}.tar.gz"
        portablexdr-4.9.1-no-config-h.patch
        portablexdr-build-use-intptr_t-and-uintptr_t-to-cast-ptr-to-int.patch)
md5sums=('949e6dc9815590e5688f18bfdd2a98f9'
         'e5e44853226f8e756aa499299404d505'
         '0b9fd1983d9bf3253f547c8e813311d8')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/portablexdr-4.9.1-no-config-h.patch
  patch -p1 -i ${srcdir}/portablexdr-build-use-intptr_t-and-uintptr_t-to-cast-ptr-to-int.patch
}

build() {
  [[ -d "$srcdir/build-${MINGW_CHOST}" ]] && rm -rf "$srcdir/build-${MINGW_CHOST}"
  mkdir "$srcdir/build-${MINGW_CHOST}"
  cd "$srcdir/build-${MINGW_CHOST}"

  ../${_realname}-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST} \
      --enable-shared \
      --enable-static
  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install
  find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
}
