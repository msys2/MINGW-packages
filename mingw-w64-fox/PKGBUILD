# Maintainer: David Vachulka <david@konstrukce-cad.com>

_realname=fox
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.6.54
pkgrel=1
pkgdesc="C++ user interface toolkit (mingw-w64)"
arch=('any')
url="http://www.fox-toolkit.org"
license=('LGPLv2+ with exceptions')
groups=("${MINGW_PACKAGE_PREFIX}")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
        "${MINGW_PACKAGE_PREFIX}-libtiff"
        "${MINGW_PACKAGE_PREFIX}-zlib"
        "${MINGW_PACKAGE_PREFIX}-libpng"
        "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('strip')
source=("http://ftp.fox-toolkit.org/pub/${_realname}-${pkgver}.tar.gz"
        fox-1-rdynamic.patch
        fox-2-FXTrayIcon.patch)
sha256sums=('960f16a8a69d41468f841039e83c2f58f3cb32622fc283a69f20381abb355219'
            '98394112211e360046745040de7f6203fd4c8141bf99f9e10d34d848585a8368'
            '200e604581b20bfaa35a8747c293d43e3024ce554b23c47c466b3ca11341b209')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p0 -i ${srcdir}/fox-1-rdynamic.patch
  patch -p0 -i ${srcdir}/fox-2-FXTrayIcon.patch
}

build() {
  [[ -d ${srcdir}/build-${CARCH} ]] && rm -rf ${srcdir}/build-${CARCH}
  cp -rf ${_realname}-${pkgver} build-${CARCH}
  cd build-${CARCH}
  autoreconf -fvi
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --enable-release \
    --disable-debug \
    --with-xft=no \
    --with-x=no
    make VERBOSE=1
}

package() {
  cd ${srcdir}/build-${CARCH}
  make DESTDIR="${pkgdir}" install
}
