# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>

_realname=imageio
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=2.9.0
pkgrel=3
pkgdesc='a Python library that provides an easy interface to read and write a wide range of image data (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64')
url="https://github.com/imageio/imageio"
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-python-numpy"
         "${MINGW_PACKAGE_PREFIX}-python-pillow")
optdepends=("${MINGW_PACKAGE_PREFIX}-freeimage"
            "${MINGW_PACKAGE_PREFIX}-python-tifffile")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools")
source=("python-$_realname-$pkgver.tar.gz"::"${url}/archive/v${pkgver}.tar.gz"
        "0001-mingw-freeimage-search-path.patch"::"${url}/commit/ac2ac5d6ab48dc2dea9bb8b056f295612cd2d9c1.diff")
sha256sums=('6a4bb17127d1af103bf146f8b0bd68ccb647a926002e8a6800c6d7859048661a'
            'ea098f2ca89a4bd8f0cea27b7ad4e3b4012e69aa7ff72da6cc6b89496df0bc15')

prepare() {
  cd "$srcdir/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/0001-mingw-freeimage-search-path.patch
}

build() {
  [[ -d "${srcdir}/python-build-${CARCH}" ]] && rm -rf "${srcdir}/python-build-${CARCH}"
  cp -r "${srcdir}/${_realname}-${pkgver}" "${srcdir}/python-build-${CARCH}" && cd "${srcdir}/python-build-${CARCH}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"

  # remove utilities for downloading binary dependencies
  rm -rf $pkgdir/${MINGW_PREFIX}/bin
}
