# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>
# Contributor: Miloš Komarčević <miloskomarcevic@aim.com>

_realname=imageio
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=2.35.0
pkgrel=1
pkgdesc="Python library for reading and writing a wide range of image, video, scientific, and volumetric data formats (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'aur: python-imageio'
  'gentoo: dev-python/imageio'
  'pypi: imageio'
)
url='https://github.com/imageio/imageio'
license=('spdx:BSD-2-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-numpy"
         "${MINGW_PACKAGE_PREFIX}-python-pillow")
optdepends=("${MINGW_PACKAGE_PREFIX}-freeimage: for FreeImage backend"
            "${MINGW_PACKAGE_PREFIX}-opencv: for OpenCV backend"
            "${MINGW_PACKAGE_PREFIX}-python-tifffile: for working with TIFF files")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools")
source=("${url}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        https://github.com/imageio/imageio/commit/81eb3f60b95921315f69268fd74b3b55e544b6b3.patch)
sha256sums=('c296c994b9e2b1c746be8410c3138c9614a623a17dcbce7029bceb81f37b60b0'
            'ac42ad476ff4b46c3592871b0ed81d9ef31957a72ba6ebfdcb5118aec1ca5eca')

prepare() {
  cd "${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/81eb3f60b95921315f69268fd74b3b55e544b6b3.patch"
}

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  "${MINGW_PREFIX}"/bin/python setup.py build
}

check() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  "${MINGW_PREFIX}"/bin/python setup.py check --strict
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    "${MINGW_PREFIX}"/bin/python setup.py install --prefix="${MINGW_PREFIX}" \
      --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"

  # Remove utilities for downloading binary dependencies
  rm -rf "$pkgdir${MINGW_PREFIX}"/bin
}
