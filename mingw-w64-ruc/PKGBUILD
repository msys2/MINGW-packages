# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.su>

_realName=RuC
_realname=${_realName,,}
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.6.0
pkgrel=1
pkgdesc="Yet another compiler from Russian version of C into virtual machine (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/andrey-terekhov/RuC'
license=('spdx:Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
optdepends=("${MINGW_PACKAGE_PREFIX}-ruc-vm: For default output interpretation")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cc")
source=("${_realName}-${pkgver}.tar.gz::${url}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        '0001-fix-build-with-latest-mingw-w64.patch'
        '0002-ruc-2.4.3-gnu-install-dirs.patch'
        '0003-ruc-2.4.3-libruc-prefix.patch'
        '0004-ruc-2.4.3-codegen-env.patch')
sha256sums=('fd448b5e5c3bd03da77daaa9d989018e86491cf55f8bcff29b3a8e343890fcaf'
            'cbd65e0e7b9e3838c2761f8e8f7c2f2cd50a94f3c08c4f553f7a6c9e7e7e00c0'
            'bb6f6f8824b008e38a388ca74d5698dedc7864282960830982df6514e34cec7f'
            'd27579d289c975dfa48375c215013a12977097ef9dad6c2547e3a9107894b486'
            '6a11f0bf10c4237d1cc09447a59070b905530fa6b3d6fb2df0b5d649808881b3')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

prepare() {
  cd "${srcdir}"/${_realName}-${pkgver}

  apply_patch_with_msg \
    0001-fix-build-with-latest-mingw-w64.patch \
    0002-ruc-2.4.3-gnu-install-dirs.patch \
    0003-ruc-2.4.3-libruc-prefix.patch \
    0004-ruc-2.4.3-codegen-env.patch
}

build() {
  cd "${srcdir}/${_realName}-${pkgver}"
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DBUILD_SHARED_LIBS=ON \
      ../${_realName}-${pkgver}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
