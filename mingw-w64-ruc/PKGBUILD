# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realName=RuC
_realname=${_realName,,}
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.4.3
pkgrel=1
pkgdesc="Yet another compiler from Russian version of C into virtual machine (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='https://github.com/andrey-terekhov/RuC'
license=('spdx:Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
optdepends=("${MINGW_PACKAGE_PREFIX}-ruc-vm: For default output interpretation")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cc")
source=("${_realName}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        "0001-ruc-2.4.3-win32.patch::${url}/commit/5a7e333d12c723e8f7277ee52ddbeec4fa19ab29.patch"
	'0002-ruc-2.4.3-gnu-install-dirs.patch'
	'0003-ruc-2.4.3-libruc-prefix.patch'
	'0004-ruc-2.4.3-codegen-env.patch')
sha256sums=('7846c476fd63b9d0ca1e8d15aa297328bcb80fc3de85ec0ec3c9f149e4e67e4c'
            '9fec9cd7aeb8dcdd7bb64e80e5d4760e2d25cf95709ecbf98a4b6b6f00230ea4'
            '51faa02b2f19d635036b402591541a22cb7d611bc387f4899c28454d15ab02da'
            'd27579d289c975dfa48375c215013a12977097ef9dad6c2547e3a9107894b486'
            '6a11f0bf10c4237d1cc09447a59070b905530fa6b3d6fb2df0b5d649808881b3')

prepare() {
  cd "${srcdir}"/${_realName}-${pkgver}

  patch -Np1 -i "${srcdir}"/0001-ruc-2.4.3-win32.patch
  patch -Np1 -i "${srcdir}"/0002-ruc-2.4.3-gnu-install-dirs.patch
  patch -Np1 -i "${srcdir}"/0003-ruc-2.4.3-libruc-prefix.patch
  patch -Np1 -i "${srcdir}"/0004-ruc-2.4.3-codegen-env.patch
}

build() {
  cd "${srcdir}/${_realName}-${pkgver}"
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DBUILD_{SHARED,STATIC}_LIBS=ON \
      ../${_realName}-${pkgver}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
