# Maintainer: kez cleal <clealk@cardiff.ac.uk>

_realname=gw
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.2.1
pkgrel=1
pkgdesc="A genome browser for bam/cram and vcf files (mingw-w64)"
arch=(any)
mingw_arch=('ucrt64' 'clang64')
url="https://github.com/kcleal/gw"
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-htslib"
         "${MINGW_PACKAGE_PREFIX}-glfw"
         "${MINGW_PACKAGE_PREFIX}-libwebp"
         "${MINGW_PACKAGE_PREFIX}-freeglut"
         "${MINGW_PACKAGE_PREFIX}-glew"
         "${MINGW_PACKAGE_PREFIX}-ncurses"
         "${MINGW_PACKAGE_PREFIX}-fontconfig"
         "${MINGW_PACKAGE_PREFIX}-dlfcn"
         "${MINGW_PACKAGE_PREFIX}-libpng")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-skia"
             "${MINGW_PACKAGE_PREFIX}-pkgconf")
source=(https://github.com/kcleal/${_realname}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz)
sha256sums=('9afcdcca2cf3815639a73dbb8d0e929b7899db7c129eed7805a155fa8787e805')

build() {
    cd ${_realname}-${pkgver}
    OLD_SKIA=1 make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  install -D -m755 gw.exe "${pkgdir}${MINGW_PREFIX}/bin/gw.exe"
  install -D -m644 .gw.ini "${pkgdir}${MINGW_PREFIX}/bin/.gw.ini"
}
