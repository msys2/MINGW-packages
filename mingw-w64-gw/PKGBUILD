# Maintainer: kez cleal <clealk@cardiff.ac.uk>
_realname=gw
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.2.2
pkgrel=1
pkgdesc="A genome browser for bam/cram and vcf files (mingw-w64)"
arch=(any)
mingw_arch=('ucrt64' 'clang64')
url="https://github.com/kcleal/gw"
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-htslib"
         "${MINGW_PACKAGE_PREFIX}-glfw"
         "${MINGW_PACKAGE_PREFIX}-libwebp"
         "${MINGW_PACKAGE_PREFIX}-freeglut"
         "${MINGW_PACKAGE_PREFIX}-glew"
         "${MINGW_PACKAGE_PREFIX}-ncurses"
         "${MINGW_PACKAGE_PREFIX}-fontconfig"
         "${MINGW_PACKAGE_PREFIX}-dlfcn"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-expat"
         "${MINGW_PACKAGE_PREFIX}-harfbuzz")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-skia"
             "${MINGW_PACKAGE_PREFIX}-pkgconf")
source=(https://github.com/kcleal/${_realname}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz)
sha256sums=('d428fdf85f14588e3ba48151700e91bffa0b136a6cad5a5d1f89a5f3bfdb82c0')

build() {
    cd ${_realname}-${pkgver}
    # Patch the Makefile
    sed -i 's#shell pkg-config --cflags skia 2>/dev/null || echo "-I/ucrt64/include/skia"#shell pkgconf --cflags skia#g' Makefile
    sed -i 's#shell pkg-config --libs skia 2>/dev/null || echo "-lskia"#shell pkgconf --libs skia#g' Makefile
    sed -i 's#shell pkg-config --cflags ncursesw 2>/dev/null || echo ""#shell ncursesw6-config --cflags#g' Makefile
    
    OLD_SKIA=1 LDLIBS+="-lharfbuzz -lz -lexpat -lwebp -lwebpmux -lopengl32 -lgdi32 -lole32" make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  install -D -m755 gw.exe "${pkgdir}${MINGW_PREFIX}/bin/gw.exe"
  install -D -m644 .gw.ini "${pkgdir}${MINGW_PREFIX}/bin/.gw.ini"
}
