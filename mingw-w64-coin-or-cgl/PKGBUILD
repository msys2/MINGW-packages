# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=coin-or-cgl
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.60.8
pkgrel=1
pkgdesc="COIN-OR Cut Generator Library. (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64')
url='https://github.com/coin-or/Cgl'
msys2_references=(
  'archlinux: coin-or-cgl'
)
license=('spdx:EPL-2.0')
groups=("${MINGW_PACKAGE_PREFIX}-coin-or")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-coin-or-coinutils"
         "${MINGW_PACKAGE_PREFIX}-coin-or-osi"
         "${MINGW_PACKAGE_PREFIX}-coin-or-clp"
         "${MINGW_PACKAGE_PREFIX}-suitesparse")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools")
source=("${url}/archive/releases/${pkgver}/${_realname}-${pkgver}.tar.gz"
        001-fix-build-on-mingw.patch
        002-fix-build-with-clang.patch)
sha256sums=('1482ba38afb783d124df8d5392337f79fdd507716e9f1fb6b98fc090acd1ad96'
            '0e78c010ece6f73ac99068d1783bf6ea83fcf9992b85f71be78081a92cab0251'
            'bc7a0b0847cba2c771354625d77fc8d5ed711260b37fb02e7bafbd0c212a2974')

prepare() {
  cd Cgl-releases-${pkgver}
  patch -p1 -i "${srcdir}"/001-fix-build-on-mingw.patch

  if [[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]]; then
    patch -p1 -i "${srcdir}"/002-fix-build-with-clang.patch
  fi
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  export lt_cv_deplibs_check_method='pass_all'

  CFLAGS+=" -Wno-implicit-function-declaration" \
  ../"Cgl-releases-${pkgver}"/configure \
    --prefix="${MINGW_PREFIX}" \
    --enable-shared \
    --disable-static \
    --enable-dependency-linking

  make
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  make check
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  make install DESTDIR="${pkgdir}"

  install -Dm644 "${srcdir}/Cgl-releases-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
