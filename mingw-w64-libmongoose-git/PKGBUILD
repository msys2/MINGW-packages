# Maintainer: Renato Silva <br.renatosilva@gmail.com>

_realname='libmongoose'
url='https://github.com/cesanta/mongoose'
pkgdesc=' Embedded web server for C/C++ (mingw-w64)'
license=('GPL2')
arch=('any')
pkgver=r1764.eef70aa
pkgrel=1

pkgname=(${MINGW_PACKAGE_PREFIX}-${_realname}-git)
provides=(${MINGW_PACKAGE_PREFIX}-${_realname})
conflicts=(${MINGW_PACKAGE_PREFIX}-${_realname})
makedepends=(${MINGW_PACKAGE_PREFIX}-gcc)
source=(${_realname}::'git+https://github.com/cesanta/mongoose')
sha1sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_realname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_realname}"
    # See https://github.com/cesanta/mongoose/issues/326
    # See https://github.com/cesanta/mongoose/issues/506
    gcc -c -DFD_SETSIZE=1024 mongoose.c -o mongoose.o
    ar rcs libmongoose.a mongoose.o
    gcc -shared -o libmongoose.dll mongoose.o -Wl,--out-implib,libmongoose.dll.a -lws2_32
}

package() {
    cd "${srcdir}/${_realname}"
    install -D -m644 mongoose.h        "${pkgdir}${MINGW_PREFIX}/include/mongoose.h"
    install -D -m644 libmongoose.a     "${pkgdir}${MINGW_PREFIX}/lib/libmongoose.a"
    install -D -m644 libmongoose.dll.a "${pkgdir}${MINGW_PREFIX}/lib/libmongoose.dll.a"
    install -D -m755 libmongoose.dll   "${pkgdir}${MINGW_PREFIX}/bin/libmongoose.dll"
    install -D -m644 LICENSE           "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
