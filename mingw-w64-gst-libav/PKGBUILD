# Maintainer: AlexWMF <alexxwmf@gmail.com>

_realname=gst-libav
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.4.5
pkgrel=1
pkgdesc="GStreamer libav (mingw-w64)"
arch=('any')
url="http://gstreamer.freedesktop.org/"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-pkg-config"
            "${MINGW_PACKAGE_PREFIX}-python2"
            "yasm"
            )
depends=("${MINGW_PACKAGE_PREFIX}-bzip2"
        "${MINGW_PACKAGE_PREFIX}-gst-plugins-base"
        "${MINGW_PACKAGE_PREFIX}-gstreamer")
options=(!libtool strip staticlibs)
source=(${url}/src/${_realname}/${_realname}-${pkgver}.tar.xz)
md5sums=('f4922a46adbcbe7bd01331ff5dc7979d')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-static \
    --enable-shared \
    --enable-silent-rules \
    --disable-gtk-doc
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="$pkgdir" install
  rm -f ${pkgdir}${MINGW_PREFIX}/lib/gstreamer*/*.a
}
