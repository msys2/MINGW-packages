# Maintainer: umarcor <unai.martinezcorral@ehu.eus>

_realname=serial-studio
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.2.1
pkgrel=1
pkgdesc="Multi-purpose serial data visualization & processing program (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url="https://serial-studio.com"
msys2_repository_url="https://github.com/Serial-Studio/Serial-Studio"
license=('spdx:GPL-3.0-only')
groups=("${MINGW_PACKAGE_PREFIX}-eda")
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-expat"
         "${MINGW_PACKAGE_PREFIX}-qt6-5compat"
         "${MINGW_PACKAGE_PREFIX}-qt6-base"
         "${MINGW_PACKAGE_PREFIX}-qt6-connectivity"
         "${MINGW_PACKAGE_PREFIX}-qt6-declarative"
         "${MINGW_PACKAGE_PREFIX}-qt6-graphs"
         "${MINGW_PACKAGE_PREFIX}-qt6-serialport"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-qt6-location"
  "${MINGW_PACKAGE_PREFIX}-qt6-positioning"
  "${MINGW_PACKAGE_PREFIX}-qt6-svg"
  "${MINGW_PACKAGE_PREFIX}-qt6-tools"
)
source=("https://github.com/${_realname}/${_realname}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        001-fix-build-and-package.patch
        002-no-qt-deployment.patch
        003-armv8.2-a.patch)
sha256sums=('495f3e16ad5ba617599f7d766ceee0b1d73a51d9d0bc69fce6d0ec6b213bf46c'
            '869646eeb5558206520ba107021cb772da0fba97744d257eae7b6b0e28e21e11'
            '637f7a60dfa69983bd41d5047e507c4020b01d1b761805b66f6e1f12e3cb39cb'
            '5b92a375b628e3aa5628e8b9a3f88b2b0cfdc37d3dc39c7a62c53922fff67a2a')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  apply_patch_with_msg \
    001-fix-build-and-package.patch \
    002-no-qt-deployment.patch

  if [[ ${MSYSTEM} == CLANGARM64 ]]; then
    apply_patch_with_msg \
      003-armv8.2-a.patch
  fi
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake -Wno-dev \
      -G"Ninja" \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DPRODUCTION_OPTIMIZATION=ON \
      -DUSE_SYSTEM_ZLIB=ON \
      -DUSE_SYSTEM_EXPAT=ON \
      ${_extra_config[@]} \
      ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake --build .
}

check() {
  "${srcdir}"/build-${MSYSTEM}/app/Serial-Studio-GPL3.exe --help
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/LICENSE.md \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.md
}
