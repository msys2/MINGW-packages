# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=kdstatemachineeditor
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.3.0
pkgrel=2
pkgdesc="Library for visualizing and editing state charts (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://kdab.github.io/KDStateMachineEditor/"
msys2_repository_url="https://github.com/KDAB/KDStateMachineEditor/"
license=('spdx:LGPL-2.1-or-later')
depends=(${MINGW_PACKAGE_PREFIX}-cc-libs
         ${MINGW_PACKAGE_PREFIX}-graphviz
         ${MINGW_PACKAGE_PREFIX}-qt6-base
         ${MINGW_PACKAGE_PREFIX}-qt6-declarative
         ${MINGW_PACKAGE_PREFIX}-qt6-remoteobjects
         ${MINGW_PACKAGE_PREFIX}-qt6-scxml)
makedepends=(${MINGW_PACKAGE_PREFIX}-cc
             ${MINGW_PACKAGE_PREFIX}-cmake
             ${MINGW_PACKAGE_PREFIX}-extra-cmake-modules
             ${MINGW_PACKAGE_PREFIX}-ninja
             ${MINGW_PACKAGE_PREFIX}-doxygen)
source=(https://github.com/KDAB/KDStateMachineEditor/releases/download/v${pkgver}/${_realname}-${pkgver}.tar.gz)
sha256sums=('05f4bc24ea0e499aa8390745abfb1d5b1db42ab19a44643b22f140daaaad89c8')

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=;" \
  ${MINGW_PREFIX}/bin/cmake.exe \
      -G "Ninja" \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DCMAKE_INSTALL_OLDINCLUDEDIR=${MINGW_PREFIX}/include \
      -DKDSME_EXAMPLES=OFF \
      -DKDSME_INTERNAL_GRAPHVIZ=OFF \
      -DBUILD_TESTING=OFF \
      -DGRAPHVIZ_ROOT=${MINGW_PREFIX} \
      "${extra_config[@]}" \
      -S ${_realname}-${pkgver} \
      -B build-${MSYSTEM}

  ${MINGW_PREFIX}/bin/cmake.exe --build build-${MSYSTEM}
}

package() {
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install build-${MSYSTEM}

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}
  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/LICENSES/*.* \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/
}
