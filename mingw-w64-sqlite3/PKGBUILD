# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=sqlite3
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
_amalgamationver=3080403
pkgver=3.8.4.3
pkgrel=1
pkgdesc="A C library that implements an SQL database engine (mingw-w64)"
arch=('any')
license=("custom:Public Domain")
groups=("${_mingw_suff}")
url="http://www.sqlite.org"
depends=("${_mingw_suff}-gcc-libs" "${_mingw_suff}-ncurses" "${_mingw_suff}-readline")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-tcl")
source=("http://www.sqlite.org/2014/sqlite-autoconf-$_amalgamationver.tar.gz")
sha1sums=('70f3b100fa22e5bfebfe1b0a2102612e3c6c53fb')
options=('!strip' 'staticlibs')

build() {
  mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}
  CFLAGS+=" -fexceptions -DSQLITE_ENABLE_COLUMN_METADATA=1 -DSQLITE_DISABLE_DIRSYNC=1 -DSQLITE_ENABLE_FTS3=3 -DSQLITE_ENABLE_RTREE=1 -fno-strict-aliasing"
  ../sqlite-autoconf-${_amalgamationver}/configure \
    --prefix=${MINGW_PREFIX} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --enable-threadsafe
  make
}

package() {
  cd ${srcdir}/build-${CARCH}
  make DESTDIR="${pkgdir}" install
}
