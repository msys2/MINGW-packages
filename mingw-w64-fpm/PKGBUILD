# Maintainer: Sebastian Ehlert <awvwgk@disroot.org>

_bootstrap=0

_realname=fpm
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.8.1
pkgrel=1
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
pkgdesc="Fortran package manager (mingw-w64)"
url="https://github.com/fortran-lang/fpm"
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-omp"
         $([[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]] || echo "${MINGW_PACKAGE_PREFIX}-gcc-libgfortran"))
makedepends=("${MINGW_PACKAGE_PREFIX}-fc"
             $( (( _bootstrap )) || echo "${MINGW_PACKAGE_PREFIX}-fpm" )
             "git")
optdepends=("git: Support for fetching projects with git")
license=('spdx:MIT')
source=(${_realname}-${pkgver}.zip::"${url}/releases/download/v${pkgver}/fpm-${pkgver}.zip"
        ${_realname}-${pkgver}.F90::"${url}/releases/download/v${pkgver}/fpm-${pkgver}.F90")
sha256sums=('0bd978bb1d3f2a3297d82a0d6ac009746a466cfa9a59ba3b6513b74e5ce4b7bf'
            'fa865132527d7bf2e1240f406d2e6d8a5921a92c8c8632ba97b3756d09335417')
noextract=(${_realname}-${pkgver}.F90)

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  local _build="build-${MSYSTEM}"
  if [[ ${MINGW_PACKAGE_PREFIX} != *-clang-* ]]; then
    _fc="${MINGW_PREFIX}/bin/gfortran"
    _fflags="-g -fbacktrace -fopenmp"
  else
    _fc="${MINGW_PREFIX}/bin/flang"
    _fflags="-g -fopenmp"
  fi

  if (( _bootstrap )); then
    mkdir -p "${_build}/bootstrap"
    "${_fc}" -J "${_build}/bootstrap" -o "${_build}/bootstrap/fpm" ${srcdir}/${_realname}-${pkgver}.F90
    _fpm="${_build}/bootstrap/fpm"
  else
    _fpm="${MINGW_PREFIX}/bin/fpm"
  fi

  "${_fpm}" install \
    --compiler "${_fc}" \
    --profile release \
    --flag "${_fflags}" \
    --prefix "${_build}"
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  install -Dm755 build-${MSYSTEM}/bin/fpm "${pkgdir}"${MINGW_PREFIX}/bin/fpm
  install -Dm644 LICENSE "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
