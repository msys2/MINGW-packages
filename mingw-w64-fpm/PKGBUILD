# Maintainer: Sebastian Ehlert <awvwgk@disroot.org>

_realname=fpm
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.10.0
pkgrel=1
arch=('any')
mingw_arch=('mingw64' 'ucrt64')
pkgdesc="Fortran package manager (mingw-w64)"
url="https://github.com/fortran-lang/fpm"
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-omp"
         $([[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]] || echo "${MINGW_PACKAGE_PREFIX}-gcc-libgfortran"))
makedepends=("${MINGW_PACKAGE_PREFIX}-fc"
             "git")
optdepends=("git: Support for fetching projects with git")
source=(${_realname}-${pkgver}.zip::"${url}/releases/download/v${pkgver}/fpm-${pkgver}.zip"
        ${_realname}-${pkgver}.F90::"${url}/releases/download/v${pkgver}/fpm-${pkgver}.F90")
sha256sums=('00d687e17bdada4dcae0ff1ea2e01bad287dcc77a74c3bbde0c9ff9633b655bb'
            '48e563db74af6b9396ebe4a67bd371210e2ea8c6e2b3cc230e68183ce7509422')
noextract=(${_realname}-${pkgver}.F90)

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  local _build="build-${MSYSTEM}"
  if [[ ${MINGW_PACKAGE_PREFIX} != *-clang-* ]]; then
    _fc="${MINGW_PREFIX}/bin/gfortran"
    _fflags="-g -O3 -fbacktrace -fopenmp"
  else
    _fc="${MINGW_PREFIX}/bin/flang"
    _fflags="-g -O3 -fopenmp"
  fi

  mkdir -p "${_build}/bootstrap"
  "${_fc}" -J "${_build}/bootstrap" -o "${_build}/bootstrap/fpm" "${srcdir}"/${_realname}-${pkgver}.F90

  "${_build}/bootstrap/fpm" install \
    --compiler "${_fc}" \
    --flag "${_fflags}" \
    --prefix "${_build}"
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  install -Dm755 build-${MSYSTEM}/bin/fpm "${pkgdir}"${MINGW_PREFIX}/bin/fpm
  install -Dm644 LICENSE "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
