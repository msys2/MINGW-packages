# Maintainer: Andrew Sun <adsun701@gmail.com>

_realname=libsigsegv
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.14
pkgrel=1
pkgdesc="Page fault detection library (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64')
url="https://www.gnu.org/software/libsigsegv/"
license=('GPL2')
source=("${_realname}-${pkgver}.tar.gz"::"https://ftp.gnu.org/gnu/${_realname}/${_realname}-${pkgver}.tar.gz"
        "0001-libsigsegv-2.11-fix-assembly.patch"
        "0002-aarch64.patch")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc" "${MINGW_PACKAGE_PREFIX}-autotools")
sha256sums=('cdac3941803364cf81a908499beb79c200ead60b6b5b40cad124fd1e06caa295'
            '73d963f144531fad281ca3f4f374c1e328cc505080ef135e8728f114e5aec371'
            '61c550967b9a543cfacb9d36f7e070588bd30d3bd742b1f7124b470af033dddf')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  apply_patch_with_msg \
    0001-libsigsegv-2.11-fix-assembly.patch \
    0002-aarch64.patch

  autoreconf -fiv
}

build() {
  mkdir -p "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared

  make
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}
  make install DESTDIR="${pkgdir}"
}
