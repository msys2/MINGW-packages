_realname=notepad++
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=8.7.9
pkgrel=3
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
pkgdesc="Notepad++ is a free (as in “free speech” and also as in “free beer”) source code editor and Notepad replacement that supports several languages."
license=("spdx:GPL-3.0-or-later")
url="https://notepad-plus-plus.org/"
msys2_repository_url="https://github.com/notepad-plus-plus/notepad-plus-plus"
options=('!emptydirs')

makedepends=(
  "${MINGW_PACKAGE_PREFIX}-gcc"
  "${MINGW_PACKAGE_PREFIX}-make"
)

source=(
  https://github.com/notepad-plus-plus/notepad-plus-plus/archive/refs/tags/v${pkgver}.tar.gz
  notepad++
  msys2.readme.txt
  msys2.create.portable.sh
  001-aarch64.patch
  002-windres-codepage.patch
)
validpgpkeys=('14BCE4362749B2B51F8C71226C429F1D8D84F46E')
sha256sums=('fb2ca0ed1a9c2ff1bb7a9d5a4b45eb59ec053008268c92d33563b3fecaebf8a1'
            'bbb81ac3893a3701cdf6a193666d063b223bffcae4f78bf6e09ad81099ece508'
            'f4468e0fa0e476fc77282cb0b3cff845ed8bce91a816a7c04e8d272abf5c5b1c'
            '2643cce3dbd5fcb65481be4afe7bf6fdea084cde134e04b38dd07239e1f09d59'
            'b8b0ba1750837d771ee3b7fa7e766f220b6e291a0f4f52c07b11e6c5c9fd2750'
            '552d9300422184c945aad3daba639367f9dd831a9855f83be927e31c8942bc64')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

prepare() {
  cd "${srcdir}/notepad-plus-plus-${pkgver}"

  apply_patch_with_msg \
    001-aarch64.patch \
    002-windres-codepage.patch
}

build() {
  # Build in source dir is mandatory
  cd "${srcdir}"/notepad-plus-plus-${pkgver}
  # Absolute path for makefile is mandatory
  VERBOSE=1 mingw32-make -f "${srcdir}"/notepad-plus-plus-${pkgver}/PowerEditor/gcc/makefile
}

package() {
  SRC_MSYS="${srcdir}"
  SRC_NPP="${srcdir}"/notepad-plus-plus-${pkgver}
  BIN="${srcdir}"/notepad-plus-plus-${pkgver}/bin.${CARCH}
  [ -d "$BIN" ] || BIN=${BIN}-debug

  PKG_BIN="${pkgdir}"/${MINGW_PREFIX}/bin
  mkdir -p "$PKG_BIN" && cd "$PKG_BIN"
  cp -p "$SRC_MSYS"/notepad++ ./

  PKG_LIB="${pkgdir}"/${MINGW_PREFIX}/lib/notepad++
  mkdir -p "$PKG_LIB" && cd "$PKG_LIB"
  cp -rp "$BIN"/* ./
  rm doLocalConf.xml readme.txt change.log

  PKG_DOC="${pkgdir}"/${MINGW_PREFIX}/share/doc/notepad++
  mkdir -p "$PKG_DOC" && cd "$PKG_DOC"
  cp -p "$BIN"/readme.txt "$BIN"/change.log ./
  cp -p "$SRC_MSYS"/msys2.readme.txt "$SRC_MSYS"/msys2.create.portable.sh ./

  PKG_LICENSES="${pkgdir}"/${MINGW_PREFIX}/share/licenses/notepad++
  mkdir -p "$PKG_LICENSES" && cd "$PKG_LICENSES"
  cp -p "$SRC_NPP"/LICENSE license.txt
  cp -p "$SRC_NPP"/lexilla/License.txt license.lexilla.txt
  cp -p "$SRC_NPP"/scintilla/License.txt license.scintilla.txt
}
