_realname=notepad++
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=8.7.7
pkgrel=1
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
pkgdesc="Notepad++ is a free (as in “free speech” and also as in “free beer”) source code editor and Notepad replacement that supports several languages."
license=("spdx:GPL-3.0-or-later")
url="https://notepad-plus-plus.org/"
msys2_repository_url="https://github.com/notepad-plus-plus/notepad-plus-plus"
options=('!emptydirs')

makedepends=(
  "${MINGW_PACKAGE_PREFIX}-gcc"
  "${MINGW_PACKAGE_PREFIX}-make"
)

source=(
  https://github.com/notepad-plus-plus/notepad-plus-plus/archive/refs/tags/v${pkgver}.tar.gz
  notepad++
  msys2.readme.txt
  msys2.create.portable.sh
)
validpgpkeys=('14BCE4362749B2B51F8C71226C429F1D8D84F46E')
sha256sums=(
  '4e2b27672adaf2dcaf1d463b75343e009eb708d2625c2282b418aba0de8f2485'
  'bbb81ac3893a3701cdf6a193666d063b223bffcae4f78bf6e09ad81099ece508'
  'f4468e0fa0e476fc77282cb0b3cff845ed8bce91a816a7c04e8d272abf5c5b1c'
  '2643cce3dbd5fcb65481be4afe7bf6fdea084cde134e04b38dd07239e1f09d59'
)

prepare() {
  cd "${srcdir}/notepad-plus-plus-${pkgver}"
}

build() {
  mkdir -p "${srcdir}"/build-${MSYSTEM}
  cd "${srcdir}"/build-${MSYSTEM}
  VERBOSE=1 mingw32-make -f "${srcdir}"/notepad-plus-plus-${pkgver}/PowerEditor/gcc/makefile
}

package() {
  SRC_MSYS="${srcdir}"
  SRC_NPP="${srcdir}"/notepad-plus-plus-${pkgver}
  BIN="${srcdir}"/build-${MSYSTEM}/bin.${CARCH}
  [ -d "$BIN" ] || BIN=${BIN}-debug

  PKG_BIN="${pkgdir}"/${MINGW_PREFIX}/bin
  mkdir -p "$PKG_BIN" && cd "$PKG_BIN"
  cp -p "$SRC_MSYS"/notepad++ ./

  PKG_LIB="${pkgdir}"/${MINGW_PREFIX}/lib/notepad++
  mkdir -p "$PKG_LIB" && cd "$PKG_LIB"
  cp -rp "$BIN"/* ./
  rm doLocalConf.xml readme.txt change.log

  PKG_DOC="${pkgdir}"/${MINGW_PREFIX}/share/doc/notepad++
  mkdir -p "$PKG_DOC" && cd "$PKG_DOC"
  cp -p "$BIN"/readme.txt "$BIN"/change.log ./
  cp -p "$SRC_MSYS"/msys2.readme.txt "$SRC_MSYS"/msys2.create.portable.sh ./

  PKG_LICENSES="${pkgdir}"/${MINGW_PREFIX}/share/licenses/notepad++
  mkdir -p "$PKG_LICENSES" && cd "$PKG_LICENSES"
  cp -p "$SRC_NPP"/LICENSE license.txt
  cp -p "$SRC_NPP"/lexilla/License.txt license.lexilla.txt
  cp -p "$SRC_NPP"/scintilla/License.txt license.scintilla.txt
}
