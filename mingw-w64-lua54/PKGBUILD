# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: David Macek <david.macek.0@gmail.com>
# Contributor: Felix Huettner <huettner94@gmx.de>
# Contributor: Ray Donnelly <mingw.android@gmail.com>
# Contributor: dragon-archer <dragon-archer@outlook.com>

_realname=lua54
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=5.4.8
pkgrel=1
pkgdesc="A powerful light-weight programming language designed for extending applications. Version 5.4.x (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://www.lua.org/"
msys2_repository_url='https://github.com/lua/lua'
msys2_references=(
  "cpe: cpe:/a:lua:lua"
)
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc")
source=("${url}/ftp/lua-${pkgver}.tar.gz"
        'lua.pc'
        '0001-When-running-lua-on-mintty-or-other.patch'
        '0002-link-implib.patch'
        '0003-Fix-LUA_-DIR-for-MSYS2-FHS-layout.patch'
        '0004-fix-fseek-on-mingw.patch'
        'LICENSE')
sha256sums=('4f18ddae154e793e46eeab727c59ef1c0c0c2b744e7b94219710d76f530629ae'
            'ca9252633e782b8f85d6a94ea4f6babd4fe30bd759085b373160b1878e36ff78'
            '9c0474177e8d19712f9081544a38a6854756f13ac246a97e92722ca85f9a47ea'
            'c34ea31604e7aea1f4758c1042664002b3b40bbf82c84bb80815a2d880d3b404'
            '2294a245794fac0ec6a6bae990ca2cb7c77b8426026192b5446480abd2a2fd21'
            '166e1c0b108e75502f1cc38e67d34943271ab7c7c7826eedcc2a605cc5ad536d'
            '142fb08b41a807b192b4b2c166696a1830a1c97967e5099ad0e579bf500e1da4')

prepare() {
  cd "${srcdir}/lua-${pkgver}"
  cp "${srcdir}/lua.pc" .
  rm -f src/iscygpty.{c,h} || true
  sed -r -e '/^LUA_(SO|A|T)=/ s/lua/lua5.4/' \
    -e '/^LUAC_T=/ s/luac/luac5.4/' \
    -e 's/lua.exe/lua5.4.exe/' \
    -e 's/luac.exe/luac5.4.exe/g' \
    -i src/Makefile
  patch -p1 -i "${srcdir}/0001-When-running-lua-on-mintty-or-other.patch"
  patch -p1 -i "${srcdir}/0002-link-implib.patch"
  patch -p1 -i "${srcdir}/0003-Fix-LUA_-DIR-for-MSYS2-FHS-layout.patch"
  patch -p1 -i "${srcdir}/0004-fix-fseek-on-mingw.patch"
}

build() {
  cp -rf "${srcdir}/lua-${pkgver}" "${srcdir}/build-${MSYSTEM}"
  cd "${srcdir}/build-${MSYSTEM}"
  sed -e "s|%VER%|${pkgver%.*}|g;s|%REL%|${pkgver}|g" \
    -e 's:llua:llua5.4:' \
    -e 's:/include:/include/lua5.4:' \
    -e "s|/usr|${MINGW_PREFIX}|g" \
    -i lua.pc

  make \
    AR="ar rcu" \
    RANLIB="ranlib" \
    STRIP="strip" \
    CC="${CC}" \
    mingw
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"
  make test
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make \
    TO_BIN="lua5.4.exe luac5.4.exe lua54.dll" \
    TO_LIB="liblua5.4.a liblua5.4.dll.a" \
    INSTALL_TOP="${pkgdir}${MINGW_PREFIX}" \
    INSTALL_INC="${pkgdir}${MINGW_PREFIX}"/include/lua5.4 \
    INSTALL_MAN="${pkgdir}${MINGW_PREFIX}"/share/man/man1 \
    install

  install -Dm644 lua.pc "${pkgdir}${MINGW_PREFIX}"/lib/pkgconfig/lua5.4.pc

  # Install the documentation
  install -d "${pkgdir}${MINGW_PREFIX}"/share/doc/${_realname}
  install -m644 doc/*.{gif,png,css,html} "${pkgdir}${MINGW_PREFIX}"/share/doc/${_realname}
  install -Dm644 ../LICENSE "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE

  cd "${pkgdir}${MINGW_PREFIX}"/share/man/man1
  mv lua.1 lua5.4.1
  mv luac.1 luac5.4.1
}
