# Maintainer: Oscar Fuentes <ofv@wanadoo.es>

_realname=ctags
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=5.8
pkgrel=1
pkgdesc="A multilanguage implementation of Ctags (mingw-w64)"
arch=('any')
url="http://ctags.sourceforge.net/"
license=("GPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
options=('staticlibs' 'strip')
source=("http://prdownloads.sourceforge.net/ctags/ctags-${pkgver}.tar.gz"
	'001-fix-unused-attribute.patch')
md5sums=('c00f82ecdcc357434731913e5b48630d'
	 'a85a1bd06a4f26546d2687d44195ff3e')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-fix-unused-attribute.patch
}

build() {
  mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}
  ../ctags-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --disable-etags \
    --disable-external-sort
  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make prefix="${pkgdir}/${MINGW_PREFIX}" install
}
