# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=qtwebkit
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
_pkgver=5.212.0-alpha4
pkgver=${_pkgver//-/}
pkgrel=21
pkgdesc="Webkit module for Qt5 (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url="https://github.com/qtwebkit/qtwebkit/wiki"
msys2_repository_url="https://github.com/qtwebkit/qtwebkit"
msys2_references=(
  'archlinux: qt5-webkit'
  "cpe: cpe:/a:qt:qtwebkit"
)
license=('spdx:Qt-GPL-exception-1.0 AND GPL-3.0-or-later AND LGPL-2.1-or-later AND LGPL-3.0-only AND GFDL-1.3-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-gperf"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-ruby"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-qt5-tools")
depends=("${MINGW_PACKAGE_PREFIX}-icu"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-libxslt"
         "${MINGW_PACKAGE_PREFIX}-libwebp"
         "${MINGW_PACKAGE_PREFIX}-fontconfig"
         "${MINGW_PACKAGE_PREFIX}-sqlite3"
         "${MINGW_PACKAGE_PREFIX}-qt5-location"
         "${MINGW_PACKAGE_PREFIX}-qt5-multimedia"
         "${MINGW_PACKAGE_PREFIX}-qt5-sensors"
         "${MINGW_PACKAGE_PREFIX}-qt5-webchannel"
         "${MINGW_PACKAGE_PREFIX}-woff2")
source=(https://github.com/annulen/webkit/releases/download/qtwebkit-${_pkgver}/${_realname}-${_pkgver}.tar.xz
        0001-fix-build-with-gcc-14.patch
        0003-qtwebkit-mfence-mingw.patch
        0005-fix-icu-find.patch
        0006-python-output-unix-line-endings.patch
        0008-fix-using-msys-perl.patch
        0011-mingw-posix-layout-files.patch
        0014-disable-asm-win64.patch
        0015-use-proper-import-suffix-mingw.patch
        0016-separate-debug-into-bin.patch
        0202-install-private-headers-and-modules.patch
        0300-fix-generating-module-files.patch
        0501-build-gcc9.patch
        0502-clang-compat.patch
        0503-python3.patch
        0504-qwebinspector-crash-workaround.patch
        0601-missing-include.patch
        0701-ruby-parser-asm-fixes.patch
        0801-suppress-some-warnings.patch
        0802-properly-link-with-system-libs.patch
        0803-c++17-updates.patch
        0804-printing-size_t-correctly.patch
        0805-libxml2-updates.patch
        0806-inspector-codegen-python3.patch
        0807-deprecated-pure-parser.patch
        0808-remove-bogus-defines.patch
        0809-non-literal-format-string.patch
        0810-static-libs-no-export.patch
        0811-remove-user-defined-copy.patch
        0812-final-class-inheritence.patch
        0813-logical-expression-operands.patch
        0814-cachevalidation-string-concatenation.patch
        0815-missing-override.patch
        0816-use-explicit-copy.patch
        d92b11fea65364fefa700249bd3340e0cd4c5b31.patch
        6e18844035d9c5e25d770ed817b858c2423d894b.patch
        4e1f209f21c3958e5a8d3a0646dccf9e429ca7e4.patch)
sha256sums=('9ca126da9273664dd23a3ccd0c9bebceb7bb534bddd743db31caf6a5a6d4a9e6'
            'e77d528e0bd203d4c7a657ff13485d2dc33dc0f262005fc1732f084b586bd05a'
            'b7100bc8c27ef6abbdb704848224c6a4f8ffea665fe80c2292b64950b1c5bdaa'
            'b9e39597d140f3fc40d07ae0f0eea0cbabd7b16d8e430d26445ae3063b8ad055'
            '02d0eb76207c4c2d36a50291976f8cc141fcff030c4b77c399402cd10a789b99'
            '9ef33dc05e5652362e10d34d75fd2cf9e7952c1c99306eb2402367401f86770b'
            'a1ef305a81b4e1b70290d4c3f70bee545f06c17736fe77abde474dba0df8c5c7'
            'c39b65235a61217bf4197285d21765a47a91d2e8c267f4b777c982d56d32b408'
            'ff9d153634afc8e2a43f1d6abec88e5a81bbc2f2ee211a86239ea6d2e53c80bc'
            '38850d42f3f35c96a66df404c1efbbe82b00c848988a5e6649deaee00a8b727c'
            'bc65b6e2bca1d8b304b063a4783dbcb845f0554202c7e2035f2cdca9b165d8ea'
            '60d7fd05a7c09649fff61f73d4573f20a95b7cd3ce822598f430232678542517'
            'e9704db6eb404ef8c441880920c20c2176aedacd621de4983d6ebc4682c5ced6'
            '17bfe3bf5dde380b16198d9f63c7f74032c1fa7e8530047625f0f413d365a722'
            '08031fdddd5e0ce66d5ca8343d0eac80345795a9c2a30610a67ca10acbb3f5a1'
            '491826ff49965c007137e95164fde7d28bd1eb24cf929406c5c6748259d54ea6'
            '83e9ffd3e74f6b533d5a3f5929e7918d31da690c66407cd1119f50d178fa94a0'
            '8d0b282c0799000b463f9b7d72383d9f64e860bf0f7ecae015439cd4110b30e6'
            '164e6d39d326bad3da839e2155c002e7f23658c1a4428b9dcbe8b5d5437f1fe2'
            '4259daf214645d7577b0eb523fc28c73c523577d22f011b7f459706e4ff2d7f9'
            '9cd72e6d517709ad7f482d3b095219a1bb2e5ef0ae1cfb5bc2c1812e94aabea6'
            '66b2823d8dc39e176277f495ab6142d159ff46a7420d76948a4960efa86bdab4'
            '42a6f4a6febdcbb29d6cd3112db80ecafd513e6b41ae41f71cd7e48b107bb686'
            'ede3d4ef7626ed741b099b5cdcddb38a27a63c8c7726c4124e4a5c474ef815dc'
            '672841d1f8ba5d1abb2d5f99a80730b375eac80644900a455426ae4ba8fbecf0'
            '7127cd650cf028b6ba4b10fa01606ee9e5cd785b80370626125ca8cb5b191684'
            'bad8f6bfc13f4363a46f7a5b4debe72c4090c13d63c625dab34c05652ca1bde1'
            'dde1d1c3871143a16e4be1d99164a915474c5c87ef4ae672d7f8b45525b5f3db'
            'f5189aaf301164f58ce4dc3f8395832d6c2a1f2be22d27ebf4850e0a0369ef2f'
            '2ec9ea16a59e5823c751d744d945b2d83e91eb7e8874e3c35bda2c8e118b7258'
            '9ef619887908dcb76d1a62f28b49c8bf62c55ec24ffb835c715765e8d1052a18'
            'f1aa96626ad949380fa519dea2e761e366cb0bd73db24ce33945ca4186d162db'
            'a7ca0336bcc6e8c190f3812679ae4cd52b8d7b1ea0c59462ad1490981aff540d'
            'c1f196d923e6681b2572cb8dd47bd1dc3db7ab1b7a1c1fcf2d0695e3eb31f485'
            'cc5a2b762426e9cda5a3ae056bba266b5f775ee75c0590913839c255d5f10db0'
            '49d4b19885806f82bff65b09e575488de176e63d91fad4545fda520aec96af82'
            '8e5f2d183c05d740d5eb1c3a191796bfb43743a462c5a0341f8171202f048af7')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd ${_realname}-${_pkgver}

  apply_patch_with_msg \
    0001-fix-build-with-gcc-14.patch \
    0003-qtwebkit-mfence-mingw.patch \
    0005-fix-icu-find.patch \
    0006-python-output-unix-line-endings.patch \
    0008-fix-using-msys-perl.patch \
    0011-mingw-posix-layout-files.patch \
    0014-disable-asm-win64.patch \
    0015-use-proper-import-suffix-mingw.patch \
    0016-separate-debug-into-bin.patch

  # Upstream patches
  apply_patch_with_msg \
    0202-install-private-headers-and-modules.patch

  apply_patch_with_msg \
    0300-fix-generating-module-files.patch \
    0501-build-gcc9.patch \
    0502-clang-compat.patch \
    0503-python3.patch \
    0504-qwebinspector-crash-workaround.patch \
    0601-missing-include.patch

  # Fix for updated ruby
  apply_patch_with_msg \
    0701-ruby-parser-asm-fixes.patch

  # Fix build with bison 3.7+
  apply_patch_with_msg \
    d92b11fea65364fefa700249bd3340e0cd4c5b31.patch \
    6e18844035d9c5e25d770ed817b858c2423d894b.patch \
    4e1f209f21c3958e5a8d3a0646dccf9e429ca7e4.patch

  apply_patch_with_msg \
    0801-suppress-some-warnings.patch \
    0802-properly-link-with-system-libs.patch \
    0803-c++17-updates.patch \
    0804-printing-size_t-correctly.patch \
    0805-libxml2-updates.patch \
    0806-inspector-codegen-python3.patch \
    0807-deprecated-pure-parser.patch \
    0808-remove-bogus-defines.patch \
    0809-non-literal-format-string.patch \
    0810-static-libs-no-export.patch \
    0811-remove-user-defined-copy.patch \
    0812-final-class-inheritence.patch \
    0813-logical-expression-operands.patch \
    0814-cachevalidation-string-concatenation.patch \
    0815-missing-override.patch \
    0816-use-explicit-copy.patch
}

build() {
  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug" "-DSEPARATE_DEBUG_INFO=ON")
  fi

  if [[ "${CARCH}" == "x86_64" ]]; then
    _extra_config+=("-DENABLE_JIT=OFF")
  else
    _extra_config+=("-DENABLE_JIT=ON")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  cmake -Wno-dev \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    "${_extra_config[@]}" \
    -DPORT=Qt \
    -DUSE_QT_MULTIMEDIA=ON \
    -DENABLE_API_TESTS=OFF \
    -S ${_realname}-${_pkgver} \
    -B build-${MSYSTEM}

  cmake --build build-${MSYSTEM}
}

package() {
  DESTDIR="${pkgdir}" cmake --install build-${MSYSTEM}

  install -Dm755 build-${MSYSTEM}/bin/QtTestBrowser.exe ${pkgdir}${MINGW_PREFIX}/bin/QtTestBrowser.exe
}
