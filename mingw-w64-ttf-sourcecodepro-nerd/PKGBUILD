# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_font=sourcecodepro
_realname=ttf-${_font}-nerd
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgdesc="Patched font Source Code Pro from nerd fonts library (mingw-w64)"
pkgver=3.0.2
pkgrel=1
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://github.com/ryanoasis/nerd-fonts'
_urlbase="${url}/releases/download/v${pkgver}"
license=('spdx:OFL-1.1')
options=('!debug' '!strip')
groups=("${MINGW_PACKAGE_PREFIX}-nerd-fonts")
source=("${_font}-${pkgver}.tar.xz::${_urlbase}/SourceCodePro.tar.xz")
sha256sums=('e2ce1b45d071b2e549ce72d19a2f4e120b6b79f939fa954a40288917a3e2f863')
noextract=("${_font}-${pkgver}.tar.xz")

prepare() {
  rm -rf ${_font}
  mkdir ${_font}
  bsdtar xf ${_font}-${pkgver}.tar.xz -C ${_font}
}

package() {
  install -Dm644 ${_font}/*.ttf -t "${pkgdir}${MINGW_PREFIX}/share/fonts/TTF"
  install -Dm644 ${_font}/LICENSE.txt -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/"
}
