# Contributor: Runar Tenfjord < runar dot tenfjord at gmail dot com >

_realname=scipy
pkgbase=mingw-w64-python3-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=1.3.2
pkgrel=1
pkgdesc="SciPy is open-source software for mathematics, science, and engineering (mingw-w64)"
arch=('any')
url="https://www.scipy.org/"
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc-fortran"
             "${MINGW_PACKAGE_PREFIX}-openblas"
             "${MINGW_PACKAGE_PREFIX}-python3-numpy"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools")
source=(${_realname}-${pkgver}.tar.gz::https://github.com/scipy/scipy/releases/download/v${pkgver}/${_realname}-${pkgver}.tar.gz)
sha256sums=('a03939b431994289f39373c57bbe452974a7da724ae7f9620a1beee575434da4')

prepare() {
  cp -a ${_realname}-${pkgver} ${MINGW_CHOST}-py3
}

build() {
  LDFLAGS="${LDFLAGS} -Wall -shared"

  # build for python3
  cd ${srcdir}/${MINGW_CHOST}-py3
  result="fail"
  while [ "$result" = "fail" ]; do
    MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py config_fc --fcompiler=gnu95 build && result="success"
  done
}

package() {
  depends=("${MINGW_PACKAGE_PREFIX}-gcc-libgfortran"
           "${MINGW_PACKAGE_PREFIX}-openblas"
           "${MINGW_PACKAGE_PREFIX}-python3-numpy")
  optdepends=("${MINGW_PACKAGE_PREFIX}-python3-nose: testsuite")

  cd ${MINGW_CHOST}-py3
  LDFLAGS="${LDFLAGS} -Wall -shared"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py config_fc --fcompiler=gnu95 install \
    --prefix=${MINGW_PREFIX} --root=${pkgdir} --optimize=1

  install -Dm644 LICENSE.txt \
    "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}
