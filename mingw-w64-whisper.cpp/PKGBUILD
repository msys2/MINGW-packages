# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=whisper.cpp
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.5.1
pkgrel=1
pkgdesc="Port of OpenAI's Whisper model in C/C++ (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://github.com/ggerganov/whisper.cpp'
license=('spdx:MIT')
depends=(
  "${MINGW_PACKAGE_PREFIX}-openblas"
  "${MINGW_PACKAGE_PREFIX}-SDL2"
  "${MINGW_PACKAGE_PREFIX}-gcc-libs"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-ninja"
  "${MINGW_PACKAGE_PREFIX}-pkgconf"
)
source=("https://github.com/ggerganov/whisper.cpp/archive/refs/tags/v${pkgver}.tar.gz"
        "0001-fix-importlib-location.patch"
        "0002-fix-openblas-lookup.patch"
        "0003-server-fix-missing-lib.patch"
        "0004-download-models-pwd.patch")
sha256sums=('ccf17e3283bcde3d09d333889528f3494e196f8a173d5023fec068a0dfae8f76'
            '30765001468dbc40893a2a63e675b2b40f290e27c3314e82dd7f72404d2d1ccf'
            '0314546c2ab5810ba84d4b2e14a84f992a282fc57698bd7a678756f8f6d13171'
            '0d29b3df33df73683c2f3c3c02e3e522d9a1dbd3eaf8ac5fd6c920d81364fdd2'
            '5d2290937b6407f97d60f8fb0bf0f270a95820611c951464a217e86a3a0f35e4')
noextract=("v${pkgver}.tar.gz")

prepare() {
  tar xzf "v${pkgver}.tar.gz" || tar xzf "v${pkgver}.tar.gz"
  cd "${srcdir}"/${_realname}-${pkgver}

  patch -p1 -i "${srcdir}/0001-fix-importlib-location.patch"
  patch -p1 -i "${srcdir}/0002-fix-openblas-lookup.patch"
  patch -p1 -i "${srcdir}/0003-server-fix-missing-lib.patch"

  # So that "whisper.cpp-download-ggml-model.sh base.en"
  # is all that's needed to get started
  patch -p1 -i "${srcdir}/0004-download-models-pwd.patch"
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  CFLAGS+=" $(pkgconf.exe --cflags openblas)" \
  LDFLAGS+=" -Wl,--export-all-symbols" \
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DWHISPER_SDL2=ON \
      -DWHISPER_OPENBLAS=ON \
      -DWHISPER_BUILD_EXAMPLES=ON \
      -DBLAS_LIBRARIES="`pkgconf --libs-only-l openblas`" \
      -DBLAS_INCLUDE_DIRS="`pkgconf --cflags-only-I openblas`" \
      -DBUILD_SHARED_LIBS=ON \
      -DWHISPER_ALL_WARNINGS=OFF \
      "${extra_config[@]}" \
      ../${_realname}-${pkgver}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .

  # Install the examples by prefixing them with whisper.cpp
  # except main.exe, which gets installed as plain whisper.cpp.exe
  for i in bin/*.exe; do
    install -Dm755 "${i}" \
      "${pkgdir}${MINGW_PREFIX}/bin/${_realname}-${i//bin\//}"
  done
  mv "${pkgdir}${MINGW_PREFIX}/bin/${_realname}-main.exe" \
    "${pkgdir}${MINGW_PREFIX}/bin/${_realname}.exe"

   install -Dm755 "${srcdir}/${_realname}-${pkgver}/models/download-ggml-model.sh" \
     "${pkgdir}${MINGW_PREFIX}/bin/${_realname}-download-ggml-model.sh"

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
