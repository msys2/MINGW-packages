_realname=onefetch
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.21.0
pkgrel=1
pkgdesc="Git repository summary on your terminal (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/o2sh/onefetch"
msys2_references=(
  'archlinux: onefetch'
  'purl: pkg:cargo/onefetch'
)
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-zstd")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-xz")
source=("${url}/archive/${pkgver}/${_realname}-${pkgver}.tar.gz"
        "link-zstd-dynamically.patch")
sha256sums=('a035bc44ef0c04a330b409e08ee61ac8a66a56cb672f87a824d4c0349989eaf2'
            'd26913e3bf3b5f0fc3dbf801ef684bdcc85424364ec8125b432102be3c307e96')

_env() {
  export PKG_CONFIG_ALL_DYNAMIC=1
  export ZSTD_SYS_USE_PKG_CONFIG=1
  export WINAPI_NO_BUNDLED_LIBRARIES=1
}

prepare() {
  cd "${_realname}-${pkgver}"

  cargo vendor --locked --versioned-dirs >> .cargo/config.toml

  patch -Np1 -i "${srcdir}/link-zstd-dynamically.patch"
}

build() {
  cd "${_realname}-${pkgver}"

  _env
  cargo build --release --all-features --frozen
}

check() {
  cd "${_realname}-${pkgver}"

  _env
  cargo test --frozen --all-features
}

package() {
  cd "${_realname}-${pkgver}"

  install -Dm755 "target/release/${_realname}" "${pkgdir}${MINGW_PREFIX}/bin/${_realname}.exe"

  install -Dm644 LICENSE.md -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
  install -Dm644 "docs/${_realname}.1" "${pkgdir}${MINGW_PREFIX}/share/man/man1/${_realname}.1"

  local _complete="./target/release/onefetch --generate"
  $_complete bash | install -Dm664 /dev/stdin "${pkgdir}${MINGW_PREFIX}/share/bash-completion/completions/${_realname}"
  $_complete fish | install -Dm664 /dev/stdin "${pkgdir}${MINGW_PREFIX}/share/fish/vendor_completions.d/${_realname}.fish"
  $_complete zsh | install -Dm664 /dev/stdin "${pkgdir}${MINGW_PREFIX}/share/zsh/site-functions/_${_realname}"
}
