# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=platformdirs
pkgbase=mingw-w64-python-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-${_realname}
pkgver=4.9.2
pkgrel=1
pkgdesc='A small Python module for determining appropriate platform-specific dirs (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'archlinux: python-platformdirs'
  'gentoo: dev-python/platformdirs'
  'purl: pkg:pypi/platformdirs'
)
msys2_repository_url='https://github.com/platformdirs/platformdirs'
msys2_changelog_url='https://platformdirs.readthedocs.io/en/latest/changelog.html'
url='https://platformdirs.readthedocs.io/'
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=(
    "${MINGW_PACKAGE_PREFIX}-python-build"
    "${MINGW_PACKAGE_PREFIX}-python-installer"
    "${MINGW_PACKAGE_PREFIX}-python-hatchling"
    "${MINGW_PACKAGE_PREFIX}-python-hatch-vcs"
)
checkdepends=(
    "${MINGW_PACKAGE_PREFIX}-python-appdirs"
    "${MINGW_PACKAGE_PREFIX}-python-pytest"
    "${MINGW_PACKAGE_PREFIX}-python-pytest-cov"
    "${MINGW_PACKAGE_PREFIX}-python-pytest-mock"
)
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('9a33809944b9db043ad67ca0db94b14bf452cc6aeaac46a88ea55b26e2e9d291')

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  python -m pytest || warning "Tests failed"
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
