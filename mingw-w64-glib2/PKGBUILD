# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ignacio Casal Quinteiro <icq@gnome.org>
# Contributor: Renato Silva <br.renatosilva@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=glib2
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.60.3
pkgrel=4
url="https://www.gtk.org/"
arch=('any')
pkgdesc="Common C routines used by GTK+ 2.4 and other libs (mingw-w64)"
license=(LGPL2)
install=glib2-${CARCH}.install
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gettext"
         "${MINGW_PACKAGE_PREFIX}-pcre"
         "${MINGW_PACKAGE_PREFIX}-libffi"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-python3")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-gtk-doc"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools")
source=("https://download.gnome.org/sources/glib/${pkgver%.*}/glib-${pkgver}.tar.xz"
        0001-Use-CreateFile-on-Win32-to-make-sure-g_unlink-always.patch
        0001-win32-Make-the-static-build-work-with-MinGW-when-pos.patch
        0001-disable-some-tests-when-static.patch
        0001-Revert-tests-W32-ugly-fix-for-sscanf-format.patch
        0002-add-and-use-g_get_console_charset.patch
        pyscript2exe.py)
sha256sums=('04ab0d560d45790d055f50db2d69974eab8b693a77390075462c56e652b760b9'
            'ff0d3df5d57cf621cac79f5bea8bd175e6c18b3fbf7cdd02df38c1eab9f40ac3'
            '838abaeab8ca4978770222ef5f88c4b464545dd591b2d532c698caa875b46931'
            '0f44135a139e3951c4b5fa7d4628d75226e0666d891faf524777e1d1ec3b440b'
            '601b4da43aeccfa522ea46fcb9c33ec9530b8c4b965b8964abd3f4972b769cdd'
            'c7da07a48604ecbba4d88cf1a5dc1f5d9384886af3ecfdcb9ad6e7bf6ddd29bb'
            '00e03a8b9d45e620c6fabbf96f5ccb41d1e49cc9b35ccb846d423f235ad5bec6')

prepare() {
  cd "${srcdir}/glib-${pkgver}"

  patch -Np1 -i "${srcdir}"/0001-Use-CreateFile-on-Win32-to-make-sure-g_unlink-always.patch
  patch -Np1 -i "${srcdir}"/0001-win32-Make-the-static-build-work-with-MinGW-when-pos.patch
  patch -Np1 -i "${srcdir}"/0001-disable-some-tests-when-static.patch
  patch -Np1 -i "${srcdir}"/0001-Revert-tests-W32-ugly-fix-for-sscanf-format.patch
  patch -Np1 -i "${srcdir}"/0002-add-and-use-g_get_console_charset.patch
}

build() {
  msg "Build static version"
  [[ -d "${srcdir}"/build-${CARCH}-static ]] && rm -rf "${srcdir}"/build-${CARCH}-static
  mkdir "${srcdir}"/build-${CARCH}-static && cd "${srcdir}"/build-${CARCH}-static

  meson \
    --buildtype=plain \
    --default-library=static \
    -Dforce_posix_threads=true \
    "../glib-${pkgver}"
  ninja

  msg "Build shared version"
  [[ -d "${srcdir}"/build-${CARCH}-shared ]] && rm -rf "${srcdir}"/build-${CARCH}-shared
  mkdir "${srcdir}"/build-${CARCH}-shared && cd "${srcdir}"/build-${CARCH}-shared

  meson \
    --buildtype=plain \
    -Ddefault_library=shared \
    -Dman=true \
    -Dforce_posix_threads=true \
    -Dgtk_doc=true \
    "../glib-${pkgver}"

  ninja
}

package() {
  cd "${srcdir}/build-${CARCH}-static"
  DESTDIR="${pkgdir}${MINGW_PREFIX}" ninja install

  cd "${srcdir}/build-${CARCH}-shared"
  DESTDIR="${pkgdir}${MINGW_PREFIX}" ninja install

  install -Dm644 "${srcdir}/glib-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"

  local _PRE_WIN="$(cygpath -m ${MINGW_PREFIX})"
  for name in glib-mkenums glib-genmarshal gdbus-codegen gtester-report; do
    ${MINGW_PREFIX}/bin/python3 \
      "${srcdir}/pyscript2exe.py" "${pkgdir}${MINGW_PREFIX}/bin/${name}"
    sed -e "s|${_PRE_WIN}|${MINGW_PREFIX}|g" \
     -i ${pkgdir}${MINGW_PREFIX}/bin/${name}-script.py
  done

  for pcfile in  "${pkgdir}${MINGW_PREFIX}"/lib/pkgconfig/*.pc; do
    sed -s "s|${_PRE_WIN}|${MINGW_PREFIX}|g" -i "${pcfile}"
    # https://github.com/Alexpux/MINGW-packages/issues/4364
    # https://gitlab.gnome.org/GNOME/glib/issues/1516
    sed -s "s| -lgiowin32||g" -i "${pcfile}"
    sed -s "s| -lgnulib||g" -i "${pcfile}"
    sed -s "s| -lcharset||g" -i "${pcfile}"
  done

  sed -s "s|${_PRE_WIN}|${MINGW_PREFIX}|g" -i "${pkgdir}${MINGW_PREFIX}/bin/glib-gettextize"
}
