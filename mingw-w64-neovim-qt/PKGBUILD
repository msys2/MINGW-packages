_realname=neovim-qt
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.2.17
pkgrel=1
pkgdesc="Neovim client library and GUI, in Qt5. (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/equalsraf/neovim-qt'
license=('spdx:ISC')
depends=("${MINGW_PACKAGE_PREFIX}-qt5-svg"
	 "${MINGW_PACKAGE_PREFIX}-neovim")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
	     "${MINGW_PACKAGE_PREFIX}-cc"
	     "${MINGW_PACKAGE_PREFIX}-qt5-tools")
source=("https://github.com/equalsraf/neovim-qt/archive/refs/tags/v${pkgver}.tar.gz"
		"fix-mingw-condition.patch")
sha256sums=('ac538c2e5d63572dd0543c13fafb4d428e67128ea676467fcda68965b2aacda1'
            'b3fd013330c24486d1c67bce01cb882d690fdfae490e047432c2a71d79a42f49')

prepare() {
	cd "${srcdir}"/${_realname}-${pkgver}

	patch -Np1 -i "${srcdir}"/fix-mingw-condition.patch
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      ../${_realname}-${pkgver}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  "${MINGW_PREFIX}"/bin/cmake.exe --build . --target test
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
