# Maintainer: @naveen521kk on Github, Naveen M K <naveen521kk@gmail.com>

_perlname=List-MoreUtils
_realname="${_perlname,,}"
pkgbase=mingw-w64-perl-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-perl-${_realname}")
pkgver=0.430
pkgrel=1
pkgdesc="Provide the stuff missing in List::Util (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://metacpan.org/release/${_perlname}"
groups=("${MINGW_PACKAGE_PREFIX}-perl-modules")
provides=(
  "${MINGW_PACKAGE_PREFIX}-perl-list-moreutils-pp"
)
depends=(
  "${MINGW_PACKAGE_PREFIX}-perl"
  "${MINGW_PACKAGE_PREFIX}-perl-exporter-tiny"
  "${MINGW_PACKAGE_PREFIX}-perl-list-moreutils-xs"
)
options=('!emptydirs')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-cc"
  $([[ ${MINGW_PACKAGE_PREFIX} != *-clang-* ]] || \
    echo "${MINGW_PACKAGE_PREFIX}-gcc-compat")
  "${MINGW_PACKAGE_PREFIX}-perl-extutils-makemaker"
)
license=('apache_2_0')
source=(
  'https://cpan.metacpan.org/authors/id/R/RE/REHSACK/List-MoreUtils-0.430.tar.gz'
  'patchmakefile.py'
)
sha256sums=('63b1f7842cd42d9b538d1e34e0330de5ff1559e4c2737342506418276f646527'
            '00fe5ced42aa635714909a8eba1347a6c132011a9bce68e9c75b27ebd64628f2')

build() {
  cd "$srcdir/${_perlname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL
  export pkgdir=$pkgdir
  python $srcdir/patchmakefile.py
  mingw32-make
}

package() {
  cd "$srcdir/${_perlname}-${pkgver}"
  mingw32-make DESTDIR="${pkgdir}/" install
}
