# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=onevpl
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2022.2.4
pkgrel=1
pkgdesc="oneAPI Video Processing Library (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='https://www.intel.com/content/www/us/en/developer/tools/oneapi/onevpl.html'
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cc")
source=("https://github.com/oneapi-src/oneVPL/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        0001-fix-pkgconfig-file.patch
        0002-cmake-install-vars-sh.patch
        0003-cmake-fix-32bit-install.patch)
sha256sums=('b8af78b0a59623d8a1dd556f5efd5b3ad395426615bec043289c34e729769d36'
            '1558d30f950e7ef33f06540fcb76ceccdd4a076eeee747da80be681a15191d2d'
            '5f95f749e61ecc38e14eba22c5ef86fa7bacf2fa545112ad7121c7cfe9e7cbab'
            '21e2ac28f021d47a648d1fcb3305b1e0af1fbbd9ad72874623c9ba000a437bf7')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  patch -p1 -i "${srcdir}"/0001-fix-pkgconfig-file.patch
  patch -p1 -i "${srcdir}"/0002-cmake-install-vars-sh.patch
  patch -p1 -i "${srcdir}"/0003-cmake-fix-32bit-install.patch
}

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  mkdir -p "${srcdir}/build-${MSYSTEM}-static" && cd "${srcdir}/build-${MSYSTEM}-static"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DBUILD_SHARED_LIBS=OFF \
      -DBUILD_TOOLS=OFF \
      -DINSTALL_EXAMPLE_CODE=OFF \
      ../${_realname}-${pkgver}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .

  mkdir -p "${srcdir}/build-${MSYSTEM}-shared" && cd "${srcdir}/build-${MSYSTEM}-shared"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DBUILD_SHARED_LIBS=ON \
      -DBUILD_TOOLS=OFF \
      -DINSTALL_EXAMPLE_CODE=OFF \
      ../${_realname}-${pkgver}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}-static"
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .

  cd "${srcdir}/build-${MSYSTEM}-shared"
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
