# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=coin-or-data-sample
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.2.12
pkgrel=1
pkgdesc="COIN-OR Sample data (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64')
url='https://github.com/coin-or-tools/Data-Sample'
msys2_references=(
  'archlinux: coin-or-data-sample'
)
license=('spdx:EPL-2.0')
groups=("${MINGW_PACKAGE_PREFIX}-coin-or")
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools")
source=("https://github.com/coin-or-tools/Data-Sample/archive/releases/${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('e9e67c04adfbd85523890b346326b106075df615e922c229277e138dbcb77e64')

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  ../"Data-Sample-releases-${pkgver}"/configure \
    --prefix="${MINGW_PREFIX}"

  make
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  make check
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  make install DESTDIR="${pkgdir}"
}
