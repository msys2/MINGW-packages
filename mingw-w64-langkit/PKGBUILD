# Maintainer: Philipp Smirnov <psmirnov@airmail.cc>

_realname=langkit
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.r8238.694c2b9
pkgrel=1
_branch='24.0'
provides=("${MINGW_PACKAGE_PREFIX}-langkit_support")
conflicts=("${MINGW_PACKAGE_PREFIX}-langkit_support")
pkgdesc="Language creation framework (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
msys2_references=(
  'aur: langkit'
)
license=('Apache-2.0 WITH LLVM-exception')
url="https://github.com/AdaCore/langkit"
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc-ada"
             "${MINGW_PACKAGE_PREFIX}-gprbuild")
depends=("${MINGW_PACKAGE_PREFIX}-gnatcoll"
         "${MINGW_PACKAGE_PREFIX}-iconv"
         "${MINGW_PACKAGE_PREFIX}-adasat")
source=("${_realname}::git+https://github.com/AdaCore/langkit.git#branch=${_branch}")
sha256sums=('SKIP')

_library_types="static static-pic relocatable"
_build_mode="prod"

pkgver() {
  cd ${_realname}
  printf "%s.r%s.%s" "$_branch" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
  cd "${srcdir}/${_realname}"

  ${MINGW_PREFIX}/bin/python setup.py build

  # Make the Langkit Python package tree available for import
  export PYTHONPATH="$srcdir/${_realname}"

  # Build the Langkit_Support library, used by all Langkit-generated
  # libraries.
  python manage.py build-langkit-support \
    --library-types=static,static-pic,relocatable \
    --build-mode=${_build_mode} \
    --gargs="-R"
}

package() {
  cd "${srcdir}/${_realname}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}" --optimize=1 --skip-build

  python manage.py install-langkit-support \
      --library-types=static,static-pic,relocatable \
      --build-mode=${_build_mode} \
      "$pkgdir/${MINGW_PREFIX}"

  install -Dm644 LICENSE.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.txt
}
