_realname=langkit
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0
pkgrel=1

provides=("${MINGW_PACKAGE_PREFIX}-langkit_support")
conflicts=("${MINGW_PACKAGE_PREFIX}-langkit_support")
pkgdesc="Language creation framework (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
msys2_references=(
  'aur: langkit'
)
license=('Apache-2.0 WITH LLVM-exception')
url="https://github.com/AdaCore/langkit"
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc-ada"
             "${MINGW_PACKAGE_PREFIX}-gprbuild")
depends=("${MINGW_PACKAGE_PREFIX}-gnatcoll-iconv"
         "${MINGW_PACKAGE_PREFIX}-iconv"
         "${MINGW_PACKAGE_PREFIX}-adasat")
source=("https://github.com/AdaCore/langkit/archive/${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('62d599791d2d339cc0f3b4625b20a317cf9260afd13552d1f22724a609109a42')

_library_types="static static-pic relocatable"
_build_mode="prod"

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python setup.py build

  # Make the Langkit Python package tree available for import
  export PYTHONPATH="$srcdir/${_realname}-${pkgver}"

  # Build the Langkit_Support library, used by all Langkit-generated
  # libraries.
  python manage.py build-langkit-support \
    --library-types=static,static-pic,relocatable \
    --build-mode=${_build_mode} \
    --gargs="-R"
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}" --optimize=1 --skip-build

  python manage.py install-langkit-support \
      --library-types=static,static-pic,relocatable \
      --build-mode=${_build_mode} \
      "$pkgdir/${MINGW_PREFIX}"

  install -Dm644 LICENSE.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.txt
}
