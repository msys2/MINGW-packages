# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>

_realname=zstandard
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.22.0
pkgrel=1
pkgdesc='Python bindings to the Zstandard (zstd) compression library (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
msys2_references=(
  'archlinux: python-zstandard'
  'pypi: zstandard'
)
msys2_repository_url='https://github.com/indygreg/python-zstandard/'
url='https://python-zstandard.readthedocs.io/'
license=('spdx:BSD-3-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python-wheel")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-hypothesis")
optdepends=("${MINGW_PACKAGE_PREFIX}-python-cffi")
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz"
        0001-python-zstandard-check-compiler-type.patch)
sha256sums=('8226a33c542bcb54cd6bd0a366067b610b41713b64c9abec1bc4533d69f51e70'
            '9b2affb073fb5f5d757701b98fa3ee87a84f90c9c58d17d06c8a4e8339041afe')

prepare() {
  cd "${_realname}-$pkgver"
  patch -p1 -i "${srcdir}/0001-python-zstandard-check-compiler-type.patch"
}

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m pytest || warning "Tests failed"
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
