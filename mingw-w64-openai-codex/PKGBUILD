# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=openai-codex
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.98.0
pkgrel=1
pkgdesc="Lightweight coding agent that runs in your terminal (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://github.com/openai/codex'
msys2_repository_url='https://github.com/openai/codex'
license=('spdx:Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-dotslash"
         "${MINGW_PACKAGE_PREFIX}-zstd")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-openssl"
             'git')
optdepends=(
  "${MINGW_PACKAGE_PREFIX}-ripgrep: accelerated large-repo search"
  "git: allow for repository actions"
)
options=('!strip')
source=("${url}/archive/rust-v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "zstd-sys.tar.gz::https://crates.io/api/v1/crates/zstd-sys/2.0.16+zstd.1.5.7/download"
        "zstd-sys-remove-statik.patch")
sha256sums=('a6e49133f46bb86788d4c998f4ee106a6374dec26c8dc0afa64cddb38fc6b8ce'
            '91e19ebc2adc8f83e43039e79776e3fda8ca919132d68a1fed6a5faca2683748'
            '48f4900ceb02d3aaf9a1020f33d56629156e96759f456c0e7ca18bfcf910767b')

_env() {
  export OPENSSL_NO_VENDOR=1
  export ZSTD_SYS_USE_PKG_CONFIG=1
}

prepare() {
  cd codex-rust-v${pkgver}/codex-rs

  patch -d "${srcdir}"/zstd-sys-2.0.16+zstd.1.5.7 -i "${srcdir}"/zstd-sys-remove-statik.patch
  sed -i '/\[patch\.crates-io\]/a zstd-sys.path = "../../zstd-sys-2.0.16+zstd.1.5.7"' Cargo.toml

  rm rust-toolchain.toml
  cargo update -p zstd-sys --config='net.git-fetch-with-cli=true'
  cargo fetch --locked --target "${RUST_CHOST}"
}

build() {
  cd codex-rust-v${pkgver}/codex-rs

  _env
  cargo build --release --frozen -p codex-cli -p codex-responses-api-proxy
}

package() {
  cd codex-rust-v${pkgver}/codex-rs

  install -Dm755 -t "${pkgdir}${MINGW_PREFIX}"/bin \
    target/release/codex \
    target/release/codex-responses-api-proxy

  install -Dm644 ../LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
