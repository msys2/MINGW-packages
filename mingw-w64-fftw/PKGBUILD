# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=fftw
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.3.5
pkgrel=1
pkgdesc="A library for computing the discrete Fourier transform (DFT) (mingw-w64)"
arch=('any')
url="http://www.fftw.org"
license=("GPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
options=('staticlibs' 'strip')
source=("${url}/${_realname}-${pkgver}.tar.gz")
sha256sums=('8ecfe1b04732ec3f5b7d279fdb8efcad536d555f9d1e8fabd027037d45ea8bcf')

precision="double float long_double quad"

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  for cur in ${precision}; do
    local _config="--enable-sse2 --enable-avx"
    if [ "${cur}" = "float" ]; then
       _config="${_config} --enable-float"
    elif [ "${cur}" = "long_double" ]; then
       _config="--enable-long-double"
    elif [ "${cur}" = "quad" ]; then
       _config="--enable-quad-precision"
    fi
    [[ -d ${MINGW_CHOST}-${cur} ]] && rm -rf ${MINGW_CHOST}-${cur}
    mkdir -p ${MINGW_CHOST}-${cur}
    pushd ${MINGW_CHOST}-${cur} > /dev/null
    msg "Building ${_realname} with ${cur} ..."

    ../configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      ${_config} \
      --enable-static \
      --enable-shared \
      --enable-threads \
      --with-combined-threads \
      --with-our-malloc \
      --with-g77-wrappers \
      --with-windows-f77-mangling
    make

    popd > /dev/null

  done
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  for cur in ${precision}; do
    pushd ${MINGW_CHOST}-${cur} > /dev/null
    msg "Instaling ${_realname} with ${cur} ..."
    make DESTDIR="${pkgdir}" install
    popd > /dev/null
  done
}
