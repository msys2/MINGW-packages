# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=fftw
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=3.3.3
pkgrel=1
pkgdesc="A library for computing the discrete Fourier transform (DFT) (mingw-w64)"
arch=('any')
url="http://www.fftw.org"
license=("GPL")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config")
depends=("${_mingw_suff}-crt")
options=('staticlibs' 'strip')
source=("$url/fftw-$pkgver.tar.gz")
md5sums=('0a05ca9c7b3bfddc8278e7c40791a1c2')

build() {
  cd "${srcdir}/fftw-${pkgver}"
    mkdir -p build-${MINGW_CHOST}-d && pushd build-${MINGW_CHOST}-d
    ../configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --enable-sse2 \
      --enable-static \
      --enable-shared \
      --enable-threads=win32 \
      --with-our-malloc \
      --with-g77-wrappers \
      --with-windows-f77-mangling
    make
	popd
	
    mkdir -p build-${MINGW_CHOST}-ld && pushd build-${MINGW_CHOST}-ld
    ../configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --enable-long-double \
      --enable-static \
      --enable-shared \
      --enable-threads=win32 \
      --with-our-malloc \
      --with-g77-wrappers \
      --with-windows-f77-mangling
    make
    popd

    mkdir -p build-${MINGW_CHOST}-f && pushd build-${MINGW_CHOST}-f
    ../configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --enable-float \
      --enable-sse \
      --enable-static \
      --enable-shared \
      --enable-threads=win32 \
      --with-our-malloc \
      --with-g77-wrappers \
      --with-windows-f77-mangling
    make
    popd
}

package() {
    cd "${srcdir}/fftw-${pkgver}/build-${MINGW_CHOST}-d"
    make DESTDIR="$pkgdir" install
    cd "${srcdir}/fftw-${pkgver}/build-${MINGW_CHOST}-ld"
    make DESTDIR="$pkgdir" install
    cd "${srcdir}/fftw-${pkgver}/build-${MINGW_CHOST}-f"
    make DESTDIR="$pkgdir" install
    
	rm "${pkgdir}"${MINGW_PREFIX}/bin/*.exe
    rm "${pkgdir}${MINGW_PREFIX}/bin/fftw-wisdom-to-conf"
    rm -r "${pkgdir}${MINGW_PREFIX}/share"
}
