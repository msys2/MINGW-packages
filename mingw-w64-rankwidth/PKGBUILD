# Contributor: Dirk Stolle

_realname=rankwidth
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.9
pkgrel=1
pkgdesc="A program that calculates rank-width and rank-decompositions (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://sourceforge.net/projects/rankwidth/'
msys2_references=(
  'archlinux: rankwidth'
  'gentoo: sci-mathematics/rw'
)
license=('spdx:GPL-2.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-igraph")
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc")
source=("https://master.dl.sourceforge.net/project/rankwidth/rw-${pkgver}.tar.gz"
        "0001-igraph-get-adjacency.patch")
sha256sums=('c1e03506fe25cdfcb428c051fc56b2d2affb5b06fba3f2ce756631466befb441'
            'e48c10ece7c23787a088fb35beb3411cabaa7f8384d0e66499ca0a83c6d6f5f5')

prepare() {
  cd "rw-${pkgver}"

  patch -Nbp2 -i "${srcdir}/0001-igraph-get-adjacency.patch"
}

build() {
  mkdir -p "build-${MSYSTEM}" && cd "build-${MSYSTEM}"

  ../"rw-${pkgver}"/configure \
    --prefix="${MINGW_PREFIX}" \
    --build="${MINGW_CHOST}" \
    --host="${MINGW_CHOST}" \
    --target="${MINGW_CHOST}" \
    --enable-static \
    --enable-shared

  make
}

check() {
  cd "build-${MSYSTEM}"

  make check
}

package() {
  cd "build-${MSYSTEM}"

  make install DESTDIR="${pkgdir}"

  install -Dm644 "${srcdir}/rw-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
