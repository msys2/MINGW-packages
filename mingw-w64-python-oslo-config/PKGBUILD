# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=oslo-config
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}"  "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=6.6.1
pkgrel=1
pkgdesc="parsing command line arguments and .ini style configuration files (mingw-w64)"
arch=('any')
url="https://pypi.python.org/pypi/oslo.config/${pkgver}"
license=('Apache')
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools" 
             "${MINGW_PACKAGE_PREFIX}-python3-netaddr" 
             "${MINGW_PACKAGE_PREFIX}-python2-netaddr"
             "${MINGW_PACKAGE_PREFIX}-python3-stevedore"
             "${MINGW_PACKAGE_PREFIX}-python2-stevedore" 
             "${MINGW_PACKAGE_PREFIX}-python3-debtcollector"
             "${MINGW_PACKAGE_PREFIX}-python2-debtcollector"
             "${MINGW_PACKAGE_PREFIX}-python3-oslo-i18n"
             "${MINGW_PACKAGE_PREFIX}-python2-oslo-i18n"
             "${MINGW_PACKAGE_PREFIX}-python3-rfc3986"
             "${MINGW_PACKAGE_PREFIX}-python2-rfc3986"
             "${MINGW_PACKAGE_PREFIX}-python3-yaml"
             "${MINGW_PACKAGE_PREFIX}-python2-yaml"
             "${MINGW_PACKAGE_PREFIX}-python2-enum34")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python3-oslotest" 
              "${MINGW_PACKAGE_PREFIX}-python2-oslotest"
              "${MINGW_PACKAGE_PREFIX}-python3-sphinx" 
              "${MINGW_PACKAGE_PREFIX}-python2-sphinx"
              "${MINGW_PACKAGE_PREFIX}-python3-testrepository"
              "${MINGW_PACKAGE_PREFIX}-python2-testrepository"
              "${MINGW_PACKAGE_PREFIX}-python3-requests-mock"
              "${MINGW_PACKAGE_PREFIX}-python2-requests-mock"
              "${MINGW_PACKAGE_PREFIX}-python3-oslo-log" 
              "${MINGW_PACKAGE_PREFIX}-python2-oslo-log")
options=('staticlibs' 'strip' '!debug')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/oslo.config/archive/$pkgver.tar.gz")
sha512sums=('280d9e23f5e16a8838b816b9a832936a28def00f0d36fa28df714c531ce9b22057f0b3f56bfb0760e00ad39f2579eb26e9f2998ad52c5275258e123c589ca3d8')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"
  sed -i '/argparse/d' oslo.config-$pkgver/requirements.txt

  for builddir in python{2,3}-build-${CARCH}; do
    rm -rf ${builddir} | true
    cp -r "oslo.config-${pkgver}" "${builddir}"
  done
#  find python2-build-${CARCH} -name \*.py -exec sed -i '1s/python$/&2/' {} +
#  find python3-build-${CARCH} -name \*.py -exec sed -i '1s/python$/&2/' {} +
  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

  export PBR_VERSION=$pkgver
}

# Note that build() is sometimes skipped because it's done in 
# the packages setup.py install for simplicity if you can do so.
# but sometimes, you want to do a check before install which would
# also trigger the build.
build() {
  for pver in {2,3}; do  
    msg "Python ${pver} build for ${CARCH}"  
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py build
  done  
}

check() {
  for pver in {2,3}; do
    msg "Python ${pver} test for ${CARCH}"
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py testr
  done
}

package_python3-oslo-config() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3"
           "${MINGW_PACKAGE_PREFIX}-python3-six"
           "${MINGW_PACKAGE_PREFIX}-python3-netaddr"
           "${MINGW_PACKAGE_PREFIX}-python3-stevedore"
           "${MINGW_PACKAGE_PREFIX}-python3-debtcollector"
           "${MINGW_PACKAGE_PREFIX}-python3-oslo-i18n"
           "${MINGW_PACKAGE_PREFIX}-python3-rfc3986"
           "${MINGW_PACKAGE_PREFIX}-python3-yaml")
  #This package install is needed for .fixups with .EXE's 
  #in the bit directory.  The install files "python-exe-installs"
  #and should be renamed to your _realname .
  install=${_realname}3-${CARCH}.install

  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"

# This entire section should be removed if the package does NOT install
# anything in the /mingw*/bin directory.
### begin section ###
  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  # fix python command in files
  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*.py; do
    sed -e "s|/usr/bin/env |${MINGW_PREFIX}|g" \
        -e "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" -i ${_f}
  done
#### end section ####
}

package_python2-oslo-config() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2"
           "${MINGW_PACKAGE_PREFIX}-python2-six"
           "${MINGW_PACKAGE_PREFIX}-python2-netaddr"
           "${MINGW_PACKAGE_PREFIX}-python2-stevedore"
           "${MINGW_PACKAGE_PREFIX}-python2-debtcollector"
           "${MINGW_PACKAGE_PREFIX}-python2-oslo-i18n"
           "${MINGW_PACKAGE_PREFIX}-python2-rfc3986"
           "${MINGW_PACKAGE_PREFIX}-python2-yaml"
           "${MINGW_PACKAGE_PREFIX}-python2-enum34")
  #This package install is needed for .fixups with .EXE's 
  #in the bit directory.  The install files "python-exe-installs"
  #and should be renamed to your _realname
  install=${_realname}2-${CARCH}.install

  cd "${srcdir}/python2-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE"

# This entire section should be removed if the package does NOT install
# anything in the /mingw*/bin directory. 
### begin section ###
  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  # fix python command in files
  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*.py; do
    sed -e "s|/usr/bin/env |${MINGW_PREFIX}|g" \
        -e "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" -i ${_f}
  done

# for Python2 packages, you want to rename some stuff from the bin directory 
# with the 2 suffix like in the mingw-w64-python-pygments package to avoid
# conflicts when installing both the Python2 and Python3 packages
  for f in oslo-config-generator oslo-config-validator; do
    mv "${pkgdir}${MINGW_PREFIX}"/bin/${f}{,2}.exe
    if [ -f "${pkgdir}${MINGW_PREFIX}"/bin/${f}.exe.manifest ]; then
      mv "${pkgdir}${MINGW_PREFIX}"/bin/${f}{,2}.exe.manifest
      sed -e "s|${f}|${f}2|g" -i "${pkgdir}${MINGW_PREFIX}"/bin/${f}2.exe.manifest
    fi
    mv "${pkgdir}${MINGW_PREFIX}"/bin/${f}{,2}-script.py
  done
#### end section ####
}

package_mingw-w64-i686-python2-oslo-config() {
  package_python2-oslo-config
}

package_mingw-w64-i686-python3-oslo-config() {
  package_python3-oslo-config
}

package_mingw-w64-x86_64-python2-oslo-config() {
  package_python2-oslo-config
}

package_mingw-w64-x86_64-python3-oslo-config() {
  package_python3-oslo-config
}
