# Maintainer: Philip Top <top1@llnl.gov>

_realname=helics
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.1.1
pkgrel=1
pkgdesc="Hierarchical Engine for Large-scale Infrastructure Co-Simulation (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://github.com/GMLC-TDC/HELICS"
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-msmpi"
         "${MINGW_PACKAGE_PREFIX}-zeromq")
makedepends=("${MINGW_PACKAGE_PREFIX}-boost"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
options=('!staticlibs' 'strip')
source=("https://github.com/GMLC-TDC/HELICS/releases/download/v${pkgver}/HELICS-v${pkgver}-source.tar.gz"
        "001-fix-linking-libs-order.patch"
        "002-asio-no-std-aligned-alloc.patch")
sha256sums=('65791ddede5f06aff58daa7cf0b9d244baa6c4e08e119ca010ddcc6137a479d9'
            '68d8774143263820acb6ee87555f317fdc2cdd456ca0bb3fb4775103f5d78173'
            '806f3ec5385dc28337a66efee1a8be79072c3e4775224a3b8f6586dba12e8d4c')

prepare() {
  patch -p1 -i ${srcdir}/001-fix-linking-libs-order.patch
  patch -p1 -i ${srcdir}/002-asio-no-std-aligned-alloc.patch
}

build() {
  [[ -d build-${MSYSTEM} ]] && rm -rf build-${MSYSTEM}
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DBUILD_SHARED_LIBS=ON \
    -DHELICS_ENABLE_MPI_CORE=ON \
    -DHELICS_DISABLE_GIT_OPERATIONS=ON \
    ${extra_config} \
    ..

  cmake --build .
}

package() {
  cd build-${MSYSTEM}
  DESTDIR=${pkgdir} cmake --install .

  # Remove zeromq import lib from the package
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/libzmq.dll.a

  install -Dm644 ${srcdir}/LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
