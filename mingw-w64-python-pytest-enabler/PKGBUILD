# Maintainer: @naveen521kk on Github, Naveen M K <naveen521kk@gmail.com>

_realname=pytest-enabler
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=3.3.0
pkgrel=2
pkgdesc='Enable installed pytest plugins (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/jaraco/pytest-enabler"
msys2_references=(
  'purl: pkg:pypi/pytest-enabler'
)
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-jaraco.context"
         "${MINGW_PACKAGE_PREFIX}-python-jaraco.functools"
         "${MINGW_PACKAGE_PREFIX}-python-importlib_resources"
         "${MINGW_PACKAGE_PREFIX}-python-toml")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python-build"
  "${MINGW_PACKAGE_PREFIX}-python-installer"
  "${MINGW_PACKAGE_PREFIX}-python-setuptools"
  "${MINGW_PACKAGE_PREFIX}-python-setuptools-scm"
)
source=("https://pypi.org/packages/source/${_realname::1}/${_realname/-/_}/${_realname/-/_}-${pkgver}.tar.gz")
sha256sums=('7780165bf20a1b67230c480e1ac0f3653c0d42947bd6d39505e5d9c883263bd1')

build() {
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
  cp -r "${_realname/-/_}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
