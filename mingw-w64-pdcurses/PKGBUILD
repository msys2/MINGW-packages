# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Simon Sobisch <simonsobisch@gnu.org>

_realname=pdcurses
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.8
pkgrel=1
pkgdesc="Public Domain Curses wincon port (mingw-w64)"
arch=('any')
url="https://github.com/wmcbrine/PDCurses"
license=('public domain')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
options=('staticlibs' 'strip')
source=(${_realname}-${pkgver}.tar.gz::"https://github.com/wmcbrine/PDCurses/archive/${pkgver}.tar.gz"
        001-mingw-pdcurses-3.8-build.patch)
sha256sums=('b88356684aa3e77069f07d8cfb1d35b6d146d1b1c711ad41fa56edc6ea046446'
            '78af3751394656eafa9bda383e866d12063c3036aeda90ed9162b538a3e53a10')

prepare() {
  cd PDCurses-${pkgver}
  patch -p1 -i ${srcdir}/001-mingw-pdcurses-3.8-build.patch
}

build() {
  cd "${srcdir}/PDCurses-${pkgver}/wincon"

  make \
    CC=${MINGW_PREFIX}/bin/gcc \
    LINK=${MINGW_PREFIX}/bin/gcc \
    STRIP=${MINGW_PREFIX}/bin/strip \
    AR=${MINGW_PREFIX}/bin/ar \
    WIDE=Y \
    UTF8=Y \
    DLL=Y
}

package() {
  cd "${srcdir}/PDCurses-${pkgver}"

  mkdir -p ${pkgdir}${MINGW_PREFIX}/{bin,include,lib}
  mkdir ${pkgdir}${MINGW_PREFIX}/include/pdcurses
 
  install wincon/*.exe ${pkgdir}${MINGW_PREFIX}/bin/
  install wincon/pdcurses.dll ${pkgdir}${MINGW_PREFIX}/bin/
  install wincon/libpdcurses.dll.a ${pkgdir}${MINGW_PREFIX}/lib/libpdcurses.dll.a

  echo '#include "pdcurses/curses.h"' > pdcurses.h
  install -m 0644 pdcurses.h ${pkgdir}${MINGW_PREFIX}/include/pdcurses.h
  install -m 0644 curses.h panel.h ${pkgdir}${MINGW_PREFIX}/include/pdcurses/
}
