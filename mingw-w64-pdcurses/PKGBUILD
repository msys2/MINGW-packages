# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Simon Sobisch <simonsobisch@gnu.org>

_realname=pdcurses
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
provides=("${MINGW_PACKAGE_PREFIX}-curses")
# No replace or conflict as the installed names are different
#replaces=("${MINGW_PACKAGE_PREFIX}-ncurses" "${MINGW_PACKAGE_PREFIX}-termcap")
#conflicts=("${MINGW_PACKAGE_PREFIX}-ncurses" "${MINGW_PACKAGE_PREFIX}-termcap")
pkgver=4.3.1
pkgrel=1
pkgdesc="Curses library on the Win32 API (mingw-w64)"
# ports that are available with this package
_pdcports="wincon wingui"  # vt is relevant especially for Windows Terminal - look at this later
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://www.projectpluto.com/win32a.htm"
license=('Public Domain')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
options=('staticlibs' 'strip')
source=(${_realname}-${pkgver}.tar.gz::"https://github.com/Bill-Gray/PDCursesMod/archive/v${pkgver}.tar.gz"
        001-mingw-pdcurses-4.3.1-build.patch
        002-fix-exports.patch)
sha256sums=('d3926643f7bab358ffd87e2c853a4e8f5f7555a01eae7e67256748e6208f8a68'
            '9a63f3c561d5a796965f5251e386d47371c0be86d42f97565394a7af930426b0'
            '246f93facdd2703f8b9d0bcd57e89688fd861d34a30facc60a48892b330b08bc')

prepare() {
  cd PDCursesMod-${pkgver}
  patch -p1 -i ${srcdir}/001-mingw-pdcurses-4.3.1-build.patch	# https://github.com/Bill-Gray/PDCursesMod/issues/222
  patch -p1 -i ${srcdir}/002-fix-exports.patch	# https://github.com/Bill-Gray/PDCursesMod/pull/221
}

build() {
  cd "${srcdir}/PDCursesMod-${pkgver}"

# Note that you should use something like -${CARCH}
# to prevent building i686 compiled binaries from
# being compiled with x86_64 compiled binaries and
# vice-versa.  That causes build failures - no surpise.

  for pdc_port in ${_pdcports}; do
    cp -rf ${pdc_port} ${pdc_port}-shared-${CARCH}
    pushd ${pdc_port}-shared-${CARCH}
      make -f Makefile \
        CC=${MINGW_PREFIX}/bin/gcc \
        STRIP=${MINGW_PREFIX}/bin/strip \
        AR=${MINGW_PREFIX}/bin/ar \
        WIDE=Y \
        UTF8=Y \
        DLL=Y
    popd

    cp -rf ${pdc_port} ${pdc_port}-static-${CARCH}
    pushd ${pdc_port}-static-${CARCH}
      make -f Makefile \
        CC=${MINGW_PREFIX}/bin/gcc \
        STRIP=${MINGW_PREFIX}/bin/strip \
        AR=${MINGW_PREFIX}/bin/ar \
        WIDE=Y \
        UTF8=Y
    popd
  done
}

# we use the check target to verify that the demos would link correctly
# and could then (manually) run the tests to verify the results *look* correct
check() {
  cd "${srcdir}/PDCursesMod-${pkgver}"

  for pdc_port in ${_pdcports}; do
    cp -rf ${pdc_port} ${pdc_port}-shared-${CARCH}
    pushd ${pdc_port}-shared-${CARCH}
      make -f Makefile \
        CC=${MINGW_PREFIX}/bin/gcc \
        STRIP=${MINGW_PREFIX}/bin/strip \
        AR=${MINGW_PREFIX}/bin/ar \
        WIDE=Y \
        UTF8=Y \
        DLL=Y \
        demos
    popd

    cp -rf ${pdc_port} ${pdc_port}-static-${CARCH}
    pushd ${pdc_port}-static-${CARCH}
      make -f Makefile \
        CC=${MINGW_PREFIX}/bin/gcc \
        STRIP=${MINGW_PREFIX}/bin/strip \
        AR=${MINGW_PREFIX}/bin/ar \
        WIDE=Y \
        UTF8=Y \
        demos
    popd
  done
}

package() {
  cd "${srcdir}/PDCursesMod-${pkgver}"

  mkdir -p ${pkgdir}${MINGW_PREFIX}/{bin,include,lib}
  mkdir ${pkgdir}${MINGW_PREFIX}/include/pdcurses

  # no exe - those are only demos and only run for check
  #install "wingui-shared-${CARCH}/*.exe ${pkgdir}${MINGW_PREFIX}/bin/
  
  # install dll and shared library of the "default port" (as in earlier versions of this package: wingui)
  install "wingui-shared-${CARCH}/libpdcurses.dll" "${pkgdir}${MINGW_PREFIX}/bin/"
  install "wingui-static-${CARCH}/libpdcurses.a"   "${pkgdir}${MINGW_PREFIX}/lib/"

  # as all share the same ABI and the binaries are intended to work only with the basename - only install one link library
  install "wingui-shared-${CARCH}/libpdcurses.dll.a" "${pkgdir}${MINGW_PREFIX}/lib/"
  
  # the additional static libraries and actual dll's go in with a rename
  for pdc_port in ${_pdcports}; do
    install "${pdc_port}-static-${CARCH}/libpdcurses.a"   "${pkgdir}${MINGW_PREFIX}/lib/libpdcurses_${pdc_port}.a"
    install "${pdc_port}-shared-${CARCH}/libpdcurses.dll" "${pkgdir}${MINGW_PREFIX}/bin/libpdcurses_${pdc_port}.dll"
  done

  install -m 0644 curses.h panel.h term.h ${pkgdir}${MINGW_PREFIX}/include/pdcurses/
  # needs defines matching the make step, see https://github.com/Bill-Gray/PDCursesMod/issues/133
  echo '/* if you want to use the DLL one: #define PDC_DLL_BUILD 1 */' > pdcurses.h
  echo '#define PDC_WIDE 1'           >> pdcurses.h
  echo '#define PDC_FORCE_UTF8 1'     >> pdcurses.h
  echo '#include "pdcurses/curses.h"' >> pdcurses.h
  install -m 0644 pdcurses.h ${pkgdir}${MINGW_PREFIX}/include/pdcurses.h
}
