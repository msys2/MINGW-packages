# Maintainer: David Macek <david.macek.0@gmail.com>

# todo -DBUILD_SHARED_LIBS=ON

_realname=badvpn
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.999.129
pkgrel=1
pkgdesc="NCD scripting language, tun2socks proxifier, P2P VPN (mingw-w64)"
arch=('any')
url="https://github.com/ambrop72/badvpn"
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cmake")
depends=("${MINGW_PACKAGE_PREFIX}-nss"
         "${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-nspr")
source=("https://github.com/ambrop72/badvpn/archive/${pkgver}.tar.gz")
md5sums=('e42f9c22fcef5a44498e80f23ceee0b3')

build() {
  mkdir -p "${srcdir}"/build-${CARCH}
  cd "${srcdir}"/build-${CARCH}

  cmake ../${_realname}-${pkgver} \
    -G "MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX="${pkgdir}${MINGW_PREFIX}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INCLUDE_PATH="${MINGW_PREFIX}"/include/nspr:"${MINGW_PREFIX}"/include/nss3 \
    -DBUILD_EXAMPLES=OFF \
    -DBUILD_TESTS=OFF
  make
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install
  install -Dm644 "${srcdir}"/$_realname-$pkgver/COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
