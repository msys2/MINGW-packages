# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=scikit-build
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.18.1
pkgrel=1
pkgdesc="Improved build system generator for Python C/C++/Fortran/Cython extensions (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/scikit-build/scikit-build'
msys2_references=(
  'purl: pkg:pypi/scikit-build'
)
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-distro"
         "${MINGW_PACKAGE_PREFIX}-python-packaging"
         "${MINGW_PACKAGE_PREFIX}-python-setuptools"
         "${MINGW_PACKAGE_PREFIX}-python-wheel"
         "${MINGW_PACKAGE_PREFIX}-cmake")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-hatchling"
             "${MINGW_PACKAGE_PREFIX}-python-hatch-fancy-pypi-readme"
             "${MINGW_PACKAGE_PREFIX}-python-hatch-vcs")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-pytest")
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname/-/_}-${pkgver}.tar.gz"
        "001-use-cygwin-for-mingw.patch")
sha256sums=('a4152ac5a084d499c28a7797be0628d8366c336e2fb0e1a063eb32e55efcb8e7'
            '4c22fde88b37fe674621d805348073dccdd65be88a9ad0cf0cf071120936353b')
noextract=("${_realname/-/_}-${pkgver}.tar.gz")

prepare() {
  echo "Extracting ${_realname/-/_}-${pkgver}.tar.gz ..."
  tar -xzf ${_realname/-/_}-${pkgver}.tar.gz || true

  cd "${srcdir}/${_realname/-/_}-${pkgver}"
  patch -p1 -i "${srcdir}"/001-use-cygwin-for-mingw.patch
}

build() {
  cp -r "${_realname/-/_}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/python -m pytest
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
