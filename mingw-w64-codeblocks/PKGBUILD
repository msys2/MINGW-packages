# Maintainer: Tim S <stahta01@gmail.com>

_realname='codeblocks'
_wx_basever=3.0
_wx_libname="wxmsw${_wx_basever}"
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-${_wx_libname}"
provides=(
  "${MINGW_PACKAGE_PREFIX}-${_realname}"
)
conflicts=(
  "${MINGW_PACKAGE_PREFIX}-${_realname}"
)
pkgver=17.12
pkgrel=1
url='http://codeblocks.org/'
pkgdesc='Cross-platform C/C++ IDE (mingw-w64)'
license=('GPL')
arch=(any)
depends=(
  "${MINGW_PACKAGE_PREFIX}-gcc-libs"
  "${MINGW_PACKAGE_PREFIX}-hunspell"
  "${MINGW_PACKAGE_PREFIX}-drmingw"
#  "${MINGW_PACKAGE_PREFIX}-${_wx_libname}"
  "${MINGW_PACKAGE_PREFIX}-wxWidgets"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-boost"
  "${MINGW_PACKAGE_PREFIX}-libtool"
  "${MINGW_PACKAGE_PREFIX}-gcc"
  "zip"
  "automake-wrapper"
  "autoconf"
  "make"
  "patch"
  "rsync"
)


source=(
  "https://downloads.sourceforge.net/codeblocks/${_realname}_$pkgver.tar.xz"
  # Fix-sdk-configure-make-build.patch is safe to apply to upstream CB SVN Repo.
  # Fix-sdk-configure-make-build.patch is safe to apply to upstream CB SVN Repo.
  011-cb-17.12-Fix-sdk-configure-make-build.patch
  # Fix-compiler-configure-make-build.patch is safe to apply to upstream CB SVN Repo.
  012-cb-16.01-Fix-compiler-configure-make-build.patch
  # Fix-coreapp-configure-make-build.patch is safe to apply to upstream CB SVN Repo.
  013-cb-16.01-Fix-coreapp-configure-make-build.patch

  # Not safe to apply to upstream CB SVN Repo.
  020-cb-16.01-Add-exe-to-auto_revision.patch
  # Not safe to apply to upstream CB SVN Repo.
  021-cb-16.01-Make-win32-pch-files.patch

  # Fix-ConsoleRunner-build-error.patch is safe to apply to upstream CB SVN Repo.
  201-cb-16.01-Fix-ConsoleRunner-build-error.patch
  # Fix-wxGTKwin-build.patch is safe to apply to upstream CB SVN Repo.
  202-cb-17.12-Fix-wxGTKwin-build.patch
  # Fix-win32-detect.patch is safe to apply to upstream CB SVN Repo.
  200-cb-16.01-Fix-win32-detect.patch

  # No idea if Fix-wxscintilla-h-DLL-macros.patch is safe to apply to upstream CB SVN Repo.
  301-cb-16.01-Fix-wxscintilla-h-DLL-macros.patch
  # No idea if Fix-cb-startup-issue.patch is safe to apply to upstream CB SVN Repo.
  302-cb-17.12-Fix-cb-startup-issue.patch
  # No idea if Fix-sdk-pch-issue.patch is safe to apply to upstream CB SVN Repo.
  300-cb-16.01-Fix-sdk-pch-issue.patch
)
# 000-009 Changes that must be done before bootstrap (configure.ac)
# 010-099 Build changes that can be done after bootstrap (Makefile.am)
# 200-299 Code changes that can be pushed to Code::Blocks upstream
# 300-399 Code changes that can *not* be pushed to Code::Blocks upstream
# 400-499 Add CB Projects for use under MSys2
# 990-999 Special patch files used to support patch file creation

sha256sums=('13881a0a72769694e82e531b8e7814d51fbf1fa122c73c5004e186560fbc57e0'
            '2bcea2501694f07665ef2dcac3521bd4b1f9c7f9461b80bc2b1da09e347d6bfa'
            'b9c57b333c67376fcd5b3ca598a2d7f5873455ac28f1e03bb305efcfde6c9678'
            'edb04577ce448c82634d2e7a41d2be949a842d588a35a0fbfad54d72b1a68324'
            '3152a93b562f7d6342c07a1382c673ff65c54ecc33da594dc09857877b5490ff'
            '49ff09b508dfe9f7c6fbaf4e19bdb75029dc6615542d42b27a52488f231c1efd'
            'b2c293ab7c96705f0d56c8c11f6da32f7341e0b1703969b16da9c323ecae23ca'
            '66663387eb759f3dfa4f85bc6cfb4c8a75c17fceaa7d11551dac51f1f43b8415'
            'e29795f80591cfa7aa21eda1554fd6d208310d093f77e75fc933396c78dfc149'
            '31ae0fefcc372677849e5445b80c323afb8d3f25ad9fdd574bd38a13cdc65d73'
            'f58d5e3f3a8d8cb27216dcecbe429bdc32c37437db7bd29029e5578e7156085d'
            '7cda901c48fe679cabf418cf353b5b188c3c3afa823628087297f160d053afd3')

options=('!strip')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

# Remove Folders that I think might cause MSys2 build issues
#  rm -fr src/exchndl

# Remove Folders to cause issues
#  rm -fr src/templates

  patch -p1 -i "${srcdir}"/011-cb-17.12-Fix-sdk-configure-make-build.patch
  patch -p1 -i "${srcdir}"/012-cb-16.01-Fix-compiler-configure-make-build.patch
  patch -p1 -i "${srcdir}"/013-cb-16.01-Fix-coreapp-configure-make-build.patch
  patch -p1 -i "${srcdir}"/020-cb-16.01-Add-exe-to-auto_revision.patch
  patch -p1 -i "${srcdir}"/021-cb-16.01-Make-win32-pch-files.patch

  patch -p1 -i "${srcdir}"/200-cb-16.01-Fix-win32-detect.patch
  patch -p1 -i "${srcdir}"/201-cb-16.01-Fix-ConsoleRunner-build-error.patch
  patch -p1 -i "${srcdir}"/202-cb-17.12-Fix-wxGTKwin-build.patch

  patch -p1 -i "${srcdir}"/300-cb-16.01-Fix-sdk-pch-issue.patch
  patch -p1 -i "${srcdir}"/301-cb-16.01-Fix-wxscintilla-h-DLL-macros.patch
  patch -p1 -i "${srcdir}"/302-cb-17.12-Fix-cb-startup-issue.patch
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  local _MINGW_PREFIX_WIN="$(cygpath -am "${MINGW_PREFIX}")"
  # This export needed if NOT using plain "wx-config"
  # export WX_CONFIG_PATH="${MINGW_PREFIX}/bin/wx-config-${_wx_basever} --prefix=${_MINGW_PREFIX_WIN}"
  export WX_CONFIG_PATH="${MINGW_PREFIX}/bin/wx-config --static=no --prefix=${_MINGW_PREFIX_WIN}"

  ##This export needed for spellchecker plugin
  export CB_HUNSPELL_LIBS="`${MINGW_PREFIX}/bin/pkg-config hunspell --libs-only-l`"
  # --host=${MSYSTEM_CHOST} does NOT work on MSys2; but, I think something should be used on Cygwin.
  PATH="${MINGW_PREFIX}/bin":"$PATH" ./configure --with-platform=win32 \
    --disable-pch \
    --prefix=${MINGW_PREFIX} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --with-contrib-plugins=yes,-Valgrind,-keybinder,-help,-exporter,-wxsmithcontrib,-wxsmithaui,-NassiShneiderman
  ###
  # The Valgrind program does NOT work under Win32
  # keybinder has major build problem
  #   wxMSW30: cbkeybinder.cpp:38:41: fatal error: wx/msw/private/keyboard.h: No such file or directory
  # exporter has minor build problem
  #   src/pdfannotation.cpp:24:10: fatal error: wx/pdfannotation.h: No such file or directory
  # wxsmithcontrib has build errors
  # wxsmithaui has build errors
  #   ../../../../src/plugins/contrib/wxSmith/wxwidgets/properties/../../properties/wxseditenumproperty.h:27:10: fatal error: ../wxwidgets/wxscodercontext.h: No such file or directory
  # help plugin has runtime issues; it crashes when enabled
  # NassiShneiderman has link errors because it does not to link to boost library
  ###
  PATH="${MINGW_PREFIX}/bin":"$PATH" make --jobs=1
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  make DESTDIR="$pkgdir" install

  # Rename files
  mv ${pkgdir}${MINGW_PREFIX}/bin/${MINGW_CHOST}-cb_console_runner.exe  ${pkgdir}${MINGW_PREFIX}/bin/cb_console_runner.exe
  mv ${pkgdir}${MINGW_PREFIX}/bin/${MINGW_CHOST}-cb_share_config.exe    ${pkgdir}${MINGW_PREFIX}/bin/cb_share_config.exe
  mv ${pkgdir}${MINGW_PREFIX}/bin/${MINGW_CHOST}-codeblocks.exe         ${pkgdir}${MINGW_PREFIX}/bin/codeblocks.exe

  # License files
  install -Dm644 COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING.txt"
}
