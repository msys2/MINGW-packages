# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_wx_basever=3.2

_realname=codeblocks
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
_basever=20.03
_revision=13431
pkgver=${_basever}.r${_revision}
pkgrel=1
pkgdesc="Cross-platform C/C++ IDE (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='http://codeblocks.org'
msys2_repository_url="https://sourceforge.net/p/codeblocks/code/HEAD/tree/trunk/"
msys2_references=(
  'archlinux: codeblocks'
)
license=('spdx:GPL-3.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-drmingw"
         "${MINGW_PACKAGE_PREFIX}-hunspell"
         "${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-msw-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-msw"
             "${MINGW_PACKAGE_PREFIX}-wxwidgets${_wx_basever}-msw-cb_headers"
             "${MINGW_PACKAGE_PREFIX}-boost"
             "zip"
             "subversion")
source=("$_realname::svn://svn.code.sf.net/p/codeblocks/code/trunk#revision=$_revision"
        "001-fix-build-shared-libs.patch"
        "002-make-codeblocks-dpi-aware.patch"
        "003-fix-loading-resources-and-plugins.patch"
        "004-disable-parallel-make-for-SmartIndent.patch")
sha256sums=('SKIP'
            '395f900f610d2bf149f9d9c0a118d0c91edd21e31d1bd6011b2082c9a3a63698'
            'bb653331ada819826b9cc3712f1e287a907e2e9abcf730d8faf4097989eb04ae'
            'dcc443841cfab987cd45d52b5d06b3e67eed0192a46ee908ae759050cc6e7119'
            '4dff5050d2fad37fe29cef40b354c39f70526f0ece3aa895b2e07885cc647089')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${_realname}"
  apply_patch_with_msg \
    001-fix-build-shared-libs.patch \
    002-make-codeblocks-dpi-aware.patch \
    003-fix-loading-resources-and-plugins.patch \
    004-disable-parallel-make-for-SmartIndent.patch

  ./bootstrap
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  export lt_cv_deplibs_check_method='pass_all'

  CXXFLAGS+=" -Wno-ignored-attributes" \
  ../${_realname}/configure \
    --prefix="${MINGW_PREFIX}" \
    --disable-pch \
    --with-contrib-plugins=all,-wxsmith,-wxsmithcontrib,-wxsmithaui

  make
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  make check
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  make install DESTDIR="${pkgdir}"

  mv "${pkgdir}"${MINGW_PREFIX}/lib/codeblocks/bin/*.dll "${pkgdir}"${MINGW_PREFIX}/bin

  install -Dm644 "${srcdir}/${_realname}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
