# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=goxel
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=0.13.0
pkgrel=1
pkgdesc="3D program that lets you create voxel volumes (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://goxel.xyz/"
msys2_repository_url="https://github.com/guillaumechereau/goxel"
license=('spdx:GPL-3.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-glfw"
         "${MINGW_PACKAGE_PREFIX}-glew"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread"
         "${MINGW_PACKAGE_PREFIX}-libtre")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-scons"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-winpthreads")
source=("https://github.com/guillaumechereau/goxel/archive/v$pkgver/$_realname-$pkgver.tar.gz"
        "001-goxel-0.11.0-msys-mingw.patch")
sha256sums=('33f92578bbd53c426ab577411aeba971f5164a0b7853ace1bad60386ebbb589e'
            '507c9429488871424b69a45f0e357f7d0762ec4f18581e0eaa779bb395ad993c')

prepare() {
  cd $_realname-$pkgver

  patch -Np1 -i "${srcdir}/001-goxel-0.11.0-msys-mingw.patch"
}

build() {
  msg "Build for ${MSYSTEM}"

  rm -rf build-${MSYSTEM} | true
  cp -r "${_realname}-${pkgver}" "build-${MSYSTEM}"
  cd "${srcdir}/build-${MSYSTEM}"

  make release
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  make DESTDIR="$pkgdir" PREFIX=${MINGW_PREFIX} install

  install -D -m644 COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname/LICENSE"
}
