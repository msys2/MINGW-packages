# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_basename=PyGLM
_realname=${_basename,,}
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
_pkgver=2.7.1-rev1
pkgver=${_pkgver/-rev1/}
_glmver=fcda03b54596adda543bcdccc6c1879e380c86a7
pkgrel=1
pkgdesc='OpenGL Mathematics library for Python (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64' 'clang32')
url='https://github.com/Zuzu-Typ/PyGLM'
license=('ZLIB')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-cc")
source=("${_basename}-${_pkgver}.tar.gz"::"${url}/archive/${_pkgver}.tar.gz"
        "glm-${_glmver}.tar.gz::https://github.com/Zuzu-Typ/glm/archive/${_glmver}.tar.gz")
sha256sums=('8ba02fcdc9eb78de189eb23c9e0f3b256d63b763ed212896927b94d572151828'
            '425cb233477847abc24f8655eff54fd8d3da4209162ef36acc0152736ce32976')

prepare() {
  cd "${srcdir}"
  rm -rf "python-build-${MSYSTEM}" | true
  rm -rf "${_basename}-${_pkgver}"/glm | true
  cp -r "${_basename}-${_pkgver}" "python-build-${MSYSTEM}"
  cp -r "glm-${_glmver}" "python-build-${MSYSTEM}/glm"
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1 --skip-build
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}

# vim: ts=2 sw=2 et:
