# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_basename=PyGLM
_realname=${_basename,,}
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=2.7.2
_glmver=fcda03b54596adda543bcdccc6c1879e380c86a7
pkgrel=1
pkgdesc='OpenGL Mathematics library for Python (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/Zuzu-Typ/PyGLM'
msys2_references=(
  'pypi: PyGLM'
)
license=('ZLIB')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-cc")
source=("${_basename}-${pkgver}.tar.gz"::"${url}/archive/${pkgver}.tar.gz"
        "glm-${_glmver}.tar.gz::https://github.com/Zuzu-Typ/glm/archive/${_glmver}.tar.gz")
sha256sums=('55f7a1968a3f318de69047c908a4abb6afdc9a0582d1e491e52493ef74511657'
            '425cb233477847abc24f8655eff54fd8d3da4209162ef36acc0152736ce32976')

prepare() {
  cd "${srcdir}"
  rm -rf "python-build-${MSYSTEM}" | true
  rm -rf "${_basename}-${pkgver}"/glm | true
  cp -r "${_basename}-${pkgver}" "python-build-${MSYSTEM}"
  cp -r "glm-${_glmver}" "python-build-${MSYSTEM}/glm"
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1 --skip-build
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}

# vim: ts=2 sw=2 et:
