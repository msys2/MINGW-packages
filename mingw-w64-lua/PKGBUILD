# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: David Macek <david.macek.0@gmail.com>
# Contributor: Felix Huettner <huettner94@gmx.de>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=lua
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=5.5.0
pkgrel=1
pkgdesc="A powerful light-weight programming language designed for extending applications. (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://www.lua.org/"
msys2_repository_url='https://github.com/lua/lua'
msys2_references=(
  "cpe: cpe:/a:lua:lua"
)
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc")
source=("${url}/ftp/lua-${pkgver}.tar.gz"
        'lua.pc'
        '0001-When-running-lua-on-mintty-or-other.patch'
        '0002-link-implib.patch'
        '0003-Fix-LUA_-DIR-for-MSYS2-FHS-layout.patch'
        '0004-fix-fseek-on-mingw.patch'
        'LICENSE')
sha256sums=('57ccc32bbbd005cab75bcc52444052535af691789dba2b9016d5c50640d68b3d'
            'ca9252633e782b8f85d6a94ea4f6babd4fe30bd759085b373160b1878e36ff78'
            '2f5c8c18f21922ee2306e1cf17fbdbc7479ffd5e5fd9bb94f32a2f6283451d77'
            'fa5a8284f1367e9d2a2482da41919918e036c020f1ceb74adbf3765ac03c3dbb'
            '53c549948b1953a96fb731ad1c7b4f6a739d4b1065ebc8ad90eb1e056c96a390'
            '1852f7d59a5fb74a1f5bfd647546688eaf60da7b8d8b561f98899982c93d5467'
            '142fb08b41a807b192b4b2c166696a1830a1c97967e5099ad0e579bf500e1da4')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  cp "${srcdir}/lua.pc" .
  rm -f src/iscygpty.{c,h} || true
  patch -p1 -i "${srcdir}/0001-When-running-lua-on-mintty-or-other.patch"
  patch -p1 -i "${srcdir}/0002-link-implib.patch"
  patch -p1 -i "${srcdir}/0003-Fix-LUA_-DIR-for-MSYS2-FHS-layout.patch"
  patch -p1 -i "${srcdir}/0004-fix-fseek-on-mingw.patch"
}

build() {
  cp -rf "${srcdir}/${_realname}-${pkgver}" "${srcdir}/build-${MSYSTEM}"
  cd "${srcdir}/build-${MSYSTEM}"
  sed -e "s|%VER%|${pkgver%.*}|g;s|%REL%|${pkgver}|g" \
    -e "s|/usr|${MINGW_PREFIX}|g" \
    -i lua.pc

  make \
    AR="ar rcu" \
    RANLIB="ranlib" \
    STRIP="strip" \
    CC="${CC}" \
    mingw
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"
  make test
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make \
    TO_BIN="lua.exe luac.exe lua55.dll" \
    TO_LIB="liblua.a liblua.dll.a" \
    INSTALL_TOP="${pkgdir}${MINGW_PREFIX}" \
    INSTALL_MAN="${pkgdir}${MINGW_PREFIX}"/share/man/man1 \
    install

  install -Dm644 lua.pc "${pkgdir}${MINGW_PREFIX}"/lib/pkgconfig/lua.pc
  install -Dm644 lua.pc "${pkgdir}${MINGW_PREFIX}"/lib/pkgconfig/lua5.5.pc

  # Install the documentation
  install -d "${pkgdir}${MINGW_PREFIX}"/share/doc/lua
  install -m644 doc/*.{png,css,html} "${pkgdir}${MINGW_PREFIX}"/share/doc/lua
  install -Dm644 ../LICENSE "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
