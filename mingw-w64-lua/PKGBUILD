# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: David Macek <david.macek.0@gmail.com>
# Contributor: Felix Huettner <huettner94@gmx.de>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=lua
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=5.4.6
pkgrel=1
pkgdesc="A powerful light-weight programming language designed for extending applications. (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://www.lua.org/"
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc")
source=("${url}/ftp/lua-${pkgver}.tar.gz"
        'lua.pc'
        '0001-When-running-Win32-version-of-luac-on-mintty-or-othe.patch'
        '0002-Add-Wl-out-implib-liblua.dll.a-to-AR.patch'
        '0003-Fix-LUA_-DIR-for-MSYS2-FHS-layout.patch'
        'link-implib.patch'
        'LICENSE')
sha256sums=('7d5ea1b9cb6aa0b59ca3dde1c6adcb57ef83a1ba8e5432c0ecd06bf439b3ad88'
            'ca9252633e782b8f85d6a94ea4f6babd4fe30bd759085b373160b1878e36ff78'
            'd4ada0484dc0e812903269f5cc0e8c13a994d7f640257d17fc26abc60161ad30'
            '5f0c29865f9645a61ca62b1860f26ffac8b3458bd5e7b7a9c92daae0927a1914'
            '2294a245794fac0ec6a6bae990ca2cb7c77b8426026192b5446480abd2a2fd21'
            'c325c516628207bf226e4e8bac5d3dcafca523d29fcd46327f49419416f46925'
            '142fb08b41a807b192b4b2c166696a1830a1c97967e5099ad0e579bf500e1da4')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  cp "${srcdir}/lua.pc" .
  rm -f src/iscygpty.{c,h} || true
  patch -p1 -i "${srcdir}/0001-When-running-Win32-version-of-luac-on-mintty-or-othe.patch"
  patch -p1 -i "${srcdir}/0002-Add-Wl-out-implib-liblua.dll.a-to-AR.patch"
  patch -p1 -i "${srcdir}/0003-Fix-LUA_-DIR-for-MSYS2-FHS-layout.patch"
  patch -p1 -i "${srcdir}/link-implib.patch"
}

build() {
  cp -rf "${srcdir}/${_realname}-${pkgver}" "${srcdir}/build-${MSYSTEM}"
  cd "${srcdir}/build-${MSYSTEM}"
  sed -e "s|%VER%|${pkgver%.*}|g;s|%REL%|${pkgver}|g" \
    -e "s|/usr|${MINGW_PREFIX}|g" \
    -i lua.pc

  make \
    AR="ar rcu" \
    RANLIB="ranlib" \
    STRIP="strip" \
    CC="${CC}" \
    mingw

  # Check we do not encounter:
  # "stdin is not a tty"
  set -x
  echo "print(\"Hello World\")" > hello-world.lua
  ./src/luac.exe - < hello-world.lua
  if [[ ! -f luac.out ]]; then
    echo "ERROR :: luac.exe stdin read failed to create luac.out"
    return 1
  fi
  rm hello-world.lua
  rm luac.out
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make \
    TO_BIN="lua.exe luac.exe lua54.dll" \
    TO_LIB="liblua.a liblua.dll.a" \
    INSTALL_TOP="${pkgdir}${MINGW_PREFIX}" \
    INSTALL_MAN="${pkgdir}${MINGW_PREFIX}"/share/man/man1 \
    install

  install -Dm644 lua.pc "${pkgdir}${MINGW_PREFIX}"/lib/pkgconfig/lua.pc
  install -Dm644 lua.pc "${pkgdir}${MINGW_PREFIX}"/lib/pkgconfig/lua5.4.pc

  # Install the documentation
  install -d "${pkgdir}${MINGW_PREFIX}"/share/doc/lua
  install -m644 doc/*.{gif,png,css,html} "${pkgdir}${MINGW_PREFIX}"/share/doc/lua
  install -Dm644 ../LICENSE "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
