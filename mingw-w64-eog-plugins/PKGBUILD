# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=eog-plugins
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.14.1
pkgrel=1
arch=('any')
pkgdesc="Eye of GNOME graphics viewer program - plugins (mingw-w64)"
options=(strip staticlibs)
depends=("${MINGW_PACKAGE_PREFIX}-eog"
         "${MINGW_PACKAGE_PREFIX}-python2"
         "${MINGW_PACKAGE_PREFIX}-libgdata"
         "${MINGW_PACKAGE_PREFIX}-libexif"
         "${MINGW_PACKAGE_PREFIX}-postr"
         "${MINGW_PACKAGE_PREFIX}-libchamplain")
makedepends=("${MINGW_PACKAGE_PREFIX}-gsettings-desktop-schemas")
options=('strip' 'staticlibs')
license=("GPL 2")
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver%.*}/${_realname}-$pkgver.tar.xz
        001-win.patch
        002-champlain.patch)
sha256sums=('12070173d875d6e9f70d8b918bbf3dfcc5af8c1681e4fabf212dae96b85318f5'
            '8dcb1e721785460d58f32911c52de3b41ddeb23cc3326ba07f5c95ec60471818'
            '3b7fab068ff3739fed247f72abbc18accb409b9008593e806f5c4e3df1d9b32d')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  patch -p1 -i ${srcdir}/001-win.patch
  patch -p1 -i ${srcdir}/002-champlain.patch

  autoreconf -isf
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib
  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install
}
