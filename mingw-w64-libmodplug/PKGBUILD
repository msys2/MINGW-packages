# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libmodplug

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.8.8.4
pkgrel=2
pkgdesc="A MOD playing library (mingw-w64)"
arch=('any')
url="http://modplug-xmms.sourceforge.net/"
license=("custom")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
options=('strip' 'staticlibs')
source=("http://downloads.sourceforge.net/modplug-xmms/${_realname}-${pkgver}.tar.gz"
    0001-stdafx-mingw-w64.mingw.patch
    libmodplug-CVE-2013-4233-Fix.patch
    libmodplug-CVE-2013-4234-Fix.patch)
md5sums=('fddc3c704c5489de2a3cf0fedfec59db'
         '0853de6307501e2f2e9c7aae4b6ca226'
         '5ca8a0159bb37bd9bd31ad9f83d4f79e'
         '0b12fa5592daab8519c34f5e8f4203ea')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-stdafx-mingw-w64.mingw.patch"
  patch -p2 -i "${srcdir}/libmodplug-CVE-2013-4233-Fix.patch"
  patch -p2 -i "${srcdir}/libmodplug-CVE-2013-4234-Fix.patch"
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  # Install license
  #install -m644 ${srcdir}/${_realname}-${pkgver}/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
