# Maintainer: Fabien Chouteau <chouteau@adacore.com>

_realname=libgpr
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.0
pkgrel=1

pkgdesc="Library for GPRbuild project files (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64')
url="https://github.com/AdaCore/gprbuild/"
msys2_references=(
  'aur: libgpr'
  'gentoo: dev-ada/libgpr'
)
license=('spdx:GPL-3.0-or-later WITH GCC-exception-3.1')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gcc-ada"
         "${MINGW_PACKAGE_PREFIX}-xmlada")
makedepends=("${MINGW_PACKAGE_PREFIX}-gprbuild")
source=("https://github.com/AdaCore/gprbuild/archive/v${pkgver}/gprbuild-${pkgver}.tar.gz")
sha256sums=('c7f3b1844556f7e85d4e7b6bcca6dc7039f415e5b9a8ae3593e48d2a6eea16b1')

build() {
  [[ -d "${srcdir}"/build-${MSYSTEM} ]] && rm -rf "${srcdir}"/build-${MSYSTEM}
  cp -r "${srcdir}/gprbuild-${pkgver}" "${srcdir}"/build-${MSYSTEM}
  cd "${srcdir}"/build-${MSYSTEM}

  make libgpr.build
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}

  make libgpr.install prefix="${pkgdir}${MINGW_PREFIX}"

  # Copy License Files
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/" COPYING*
}
