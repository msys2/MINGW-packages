# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=cargo-c
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.9.31
pkgrel=1
pkgdesc='A cargo subcommand to build and install C-ABI compatibile dynamic and static libraries (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang32' 'clang64' 'clangarm64')
url='https://github.com/lu-zero/cargo-c/'
msys2_references=(
  'archlinux: cargo-c'
)
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-sqlite3"
         "${MINGW_PACKAGE_PREFIX}-libssh2"
         "${MINGW_PACKAGE_PREFIX}-libgit2")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-openssl")
source=("https://github.com/lu-zero/cargo-c/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "${_realname}-${pkgver}.Cargo.lock"::"https://github.com/lu-zero/cargo-c/releases/download/v${pkgver}/Cargo.lock"
        001-add-lib-prefix-on-windows-gnu.patch)
sha256sums=('4a04db8fb17a55db403bc59572f05475a477fece7ab08cfb2de970e188b80b83'
            'd7960d508be22b1c050003970be0a1fa0b7ef6f3088fe6d29b330413a718a1ea'
            'b749ffdb37eb6016dca37c41c2d644156bbd4a7d8ffaa7bc18c7813f7af646c0')

prepare() {
    cp "${srcdir}/${_realname}-${pkgver}.Cargo.lock" "${_realname}-${pkgver}/Cargo.lock"
    cd "${srcdir}/${_realname}-${pkgver}"

    patch -p1 -i "${srcdir}"/001-add-lib-prefix-on-windows-gnu.patch

    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

_env() {
    export LIBZ_SYS_STATIC=0
    export LIBGIT2_NO_VENDOR=1
    export OPENSSL_NO_VENDOR=1
    export LIBSSH2_SYS_USE_PKG_CONFIG=1
    export LIBSQLITE3_SYS_USE_PKG_CONFIG=1
}

build() {
    cd "${srcdir}/${_realname}-${pkgver}"

    _env
    cargo build --release --frozen
}

check() {
    cd "${srcdir}/${_realname}-${pkgver}"

    _env
    cargo test --release --frozen
}

package() {
    cd "${srcdir}/${_realname}-${pkgver}"

    _env
    cargo install \
    --frozen \
    --offline \
    --no-track \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

    install -Dm644 "LICENSE" -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
}
