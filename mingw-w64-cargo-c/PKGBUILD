# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=cargo-c
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.9.21
pkgrel=1
pkgdesc='A cargo subcommand to build and install C-ABI compatibile dynamic and static libraries (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang32' 'clang64' 'clangarm64')
url='https://github.com/lu-zero/cargo-c/'
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-libgit2"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/lu-zero/cargo-c/archive/v${pkgver}.tar.gz"
        "${_realname}-${pkgver}.Cargo.lock"::"https://github.com/lu-zero/cargo-c/releases/download/v${pkgver}/Cargo.lock"
        001-add-lib-prefix-on-windows-gnu.patch)
sha256sums=('fbc199ca7da0514d18200a19630a9bbce6f4d5871c9c3ca5f0b6bca42ee99095'
            '30e60a7853ee4c6cc32239bc67fe55f8834d19cf70d49a4d35cece2c1d6931f3'
            'b749ffdb37eb6016dca37c41c2d644156bbd4a7d8ffaa7bc18c7813f7af646c0')

prepare() {
    cp "${srcdir}/${_realname}-${pkgver}.Cargo.lock" "${_realname}-${pkgver}/Cargo.lock"
    cd "${srcdir}/${_realname}-${pkgver}"

    patch -p1 -i "${srcdir}"/001-add-lib-prefix-on-windows-gnu.patch

    cargo fetch \
        --locked
}

build() {
    cd "${srcdir}/${_realname}-${pkgver}"

    cargo build \
        --release \
        --frozen \
        --offline
}

check() {
    cd "${srcdir}/${_realname}-${pkgver}"

    cargo test \
        --release \
        --frozen
}

package() {
    cd "${srcdir}/${_realname}-${pkgver}"

    cargo install \
    --frozen \
    --offline \
    --no-track \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

    install -D -m644 "LICENSE" -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
}
