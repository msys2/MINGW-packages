# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=cargo-c
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.10.7
pkgrel=2
pkgdesc='A cargo subcommand to build and install C-ABI compatibile dynamic and static libraries (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/lu-zero/cargo-c/'
msys2_references=(
  'archlinux: cargo-c'
  'purl: pkg:cargo/cargo-c'
)
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-sqlite3"
         "${MINGW_PACKAGE_PREFIX}-libssh2")
         #"${MINGW_PACKAGE_PREFIX}-libgit2")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-openssl")
options=('!strip')
source=("https://github.com/lu-zero/cargo-c/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "${_realname}-${pkgver}.Cargo.lock"::"https://github.com/lu-zero/cargo-c/releases/download/v${pkgver}/Cargo.lock"
        001-add-lib-prefix-on-windows-gnu.patch)
sha256sums=('c4532dd2bf23769df5f64649d5b0c037fb2a29467c74d16a54bad3054d9f3f3a'
            '7272f41442713ce20c42198fdf9128a77b539ded1649872ab88dc1411db8dee4'
            'a35e7fbfc0d4113154572ae204c0a6bdf21d1ec12ac6882a133b51e211467805')

prepare() {
    cp "${_realname}-${pkgver}.Cargo.lock" "${_realname}-${pkgver}/Cargo.lock"
    cd "${_realname}-${pkgver}"

    patch -p1 -i "${srcdir}"/001-add-lib-prefix-on-windows-gnu.patch

    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

_env() {
    # not compatible with libgit2 1.9+
    # export LIBGIT2_NO_VENDOR=1
    export OPENSSL_NO_VENDOR=1
    export LIBSSH2_SYS_USE_PKG_CONFIG=1
    export LIBSQLITE3_SYS_USE_PKG_CONFIG=1
}

build() {
    cd "${_realname}-${pkgver}"

    _env
    cargo build --frozen --profile release-strip
}

check() {
    cd "${_realname}-${pkgver}"

    _env
    cargo test --frozen --profile release-strip
}

package() {
    cd "${_realname}-${pkgver}"

    _env
    cargo install \
    --frozen \
    --offline \
    --no-track \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}" \
    --profile release-strip

    install -Dm644 "LICENSE" -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
}
