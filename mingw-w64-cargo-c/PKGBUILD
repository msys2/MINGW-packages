# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=cargo-c
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.10.9
pkgrel=3
pkgdesc='A cargo subcommand to build and install C-ABI compatibile dynamic and static libraries (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/lu-zero/cargo-c/'
msys2_references=(
  'archlinux: cargo-c'
  'purl: pkg:cargo/cargo-c'
)
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-sqlite3"
         "${MINGW_PACKAGE_PREFIX}-libssh2")
         #"${MINGW_PACKAGE_PREFIX}-libgit2")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-openssl")
options=('!strip')
source=("https://github.com/lu-zero/cargo-c/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "https://patch-diff.githubusercontent.com/raw/lu-zero/cargo-c/pull/444.patch"
        "${_realname}-${pkgver}.Cargo.lock"::"https://github.com/lu-zero/cargo-c/releases/download/v${pkgver}/Cargo.lock")
sha256sums=('4542e39aa67bf8712c60f21701cc8e8b5153d0344afe1b618f121f696b578a7f'
            '4c939d795be6f6645c24a4ff198183393915d490756eabdfbfd34c2446a71480'
            '387e25aa120f945c5455557ca7815b8085090b825d2d4fd0b46cc9fdce0c34bc')

prepare() {
    cp "${_realname}-${pkgver}.Cargo.lock" "${_realname}-${pkgver}/Cargo.lock"
    cd "${_realname}-${pkgver}"

    # https://github.com/lu-zero/cargo-c/issues/443
    patch -Np1 < ../444.patch
    cargo update -p implib

    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

_env() {
    # not compatible with libgit2 1.9+
    # export LIBGIT2_NO_VENDOR=1
    export OPENSSL_NO_VENDOR=1
    export LIBSSH2_SYS_USE_PKG_CONFIG=1
    export LIBSQLITE3_SYS_USE_PKG_CONFIG=1
}

build() {
    cd "${_realname}-${pkgver}"

    _env
    cargo build --frozen --profile release-strip
}

check() {
    cd "${_realname}-${pkgver}"

    _env
    cargo test --frozen --profile release-strip
}

package() {
    cd "${_realname}-${pkgver}"

    _env
    cargo install \
    --frozen \
    --offline \
    --no-track \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}" \
    --profile release-strip

    install -Dm644 "LICENSE" -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
}
