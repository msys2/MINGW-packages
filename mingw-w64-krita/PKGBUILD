# Maintainer: Naveen M K <naveen521kk@gmail.com>

_realname=krita
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=4.4.8
pkgrel=1
pkgdesc="Edit and paint images (mingw-w64)"
arch=('any')
url='https://krita.org'
license=(GPL3)
validpgpkeys=(
    '05D00A8B73A686789E0A156858B9596C722EA3BD'  # Boudewijn Rempt <foundation@krita.org>
    'E9FB29E74ADEACC5E3035B8AB69EB4CF7468332F'  # Dmitry Kazakov (main key) <dimula73@gmail.com>
    '064182440C674D9F8D0F6F8B4DA79EDA231C852B') # Stichting Krita Foundation <foundation@krita.org>

makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
    "${MINGW_PACKAGE_PREFIX}-ninja"
    "${MINGW_PACKAGE_PREFIX}-python"
    "${MINGW_PACKAGE_PREFIX}-extra-cmake-modules"
    "${MINGW_PACKAGE_PREFIX}-kdoctools-qt5"
    "${MINGW_PACKAGE_PREFIX}-boost"
    "${MINGW_PACKAGE_PREFIX}-eigen3"
    "${MINGW_PACKAGE_PREFIX}-poppler"
    "${MINGW_PACKAGE_PREFIX}-python-pyqt5"
    "${MINGW_PACKAGE_PREFIX}-libheif"
    "${MINGW_PACKAGE_PREFIX}-sip"
    "${MINGW_PACKAGE_PREFIX}-qt5-tools"
)
depends=(
    "${MINGW_PACKAGE_PREFIX}-qt5"
    "${MINGW_PACKAGE_PREFIX}-kitemviews-qt5"
    "${MINGW_PACKAGE_PREFIX}-kitemmodels-qt5"
    "${MINGW_PACKAGE_PREFIX}-ki18n-qt5"
    "${MINGW_PACKAGE_PREFIX}-kcompletion-qt5"
    "${MINGW_PACKAGE_PREFIX}-kguiaddons-qt5"
    "${MINGW_PACKAGE_PREFIX}-qt5-svg"
    "${MINGW_PACKAGE_PREFIX}-qt5-multimedia"
    "${MINGW_PACKAGE_PREFIX}-quazip"
    "${MINGW_PACKAGE_PREFIX}-gsl"
    "${MINGW_PACKAGE_PREFIX}-libraw"
    "${MINGW_PACKAGE_PREFIX}-exiv2"
    "${MINGW_PACKAGE_PREFIX}-openexr"
    "${MINGW_PACKAGE_PREFIX}-fftw"
    "${MINGW_PACKAGE_PREFIX}-giflib"
    "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
    "${MINGW_PACKAGE_PREFIX}-opencolorio"
    "${MINGW_PACKAGE_PREFIX}-hicolor-icon-theme"
    "${MINGW_PACKAGE_PREFIX}-kcoreaddons-qt5"
    "${MINGW_PACKAGE_PREFIX}-kwindowsystem-qt5"
    "${MINGW_PACKAGE_PREFIX}-icoutils"
)
source=(
    "https://download.kde.org/stable/krita/$pkgver/$_realname-$pkgver.tar.gz"
    "0001-fix-a-bug-with-libjpeg.patch"
    "0002-don-t-default-to-angle-on-mingw.patch"
    "0003-remove-egl-related-code.patch"
    "0004-fix-python.patch"
    "0005-don-t-install-installer.patch"
)
sha256sums=('bcc68a5711d92515d6553611a0bddd53f3259843fd3534b9b3e535d7ed430df8'
    'cece456da61a4a16fabfb5a04ea59f3228441d16d548b92f4c2203fa080fb9cc'
    'a095ecda834053eb0afd6d7dba90d1638941fdd5feba5ebd412538fa13511a2d'
    '524d8aae99086c51dd1255f483f1c6be7e02e7a250d4ab215eecc74ded83e073'
    '442a2d6197d38264a38ef1c893f9f0ba94b11e2d5f93156fe68d11cee66872e0'
    '5969a6a153b8f612122ffd2c63bdb115b86273c444054c2f4c9bb37b0fdc6ee6')

prepare() {
    cd "${srcdir}"/${_realname}-${pkgver}
    patch -Np1 -i "${srcdir}/0001-fix-a-bug-with-libjpeg.patch"
    patch -Np1 -i "${srcdir}/0002-don-t-default-to-angle-on-mingw.patch"
    patch -Np1 -i "${srcdir}/0003-remove-egl-related-code.patch"
    patch -Np1 -i "${srcdir}/0004-fix-python.patch"
    patch -Np1 -i "${srcdir}/0005-don-t-install-installer.patch"
}

build() {
    cd "${srcdir}"/${_realname}-${pkgver}
    [[ -d "${srcdir}"/build-${MSYSTEM} ]] && rm -rf "${srcdir}"/build-${MSYSTEM}
    mkdir -p "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

    declare -a extra_config
    if check_option "debug" "n"; then
        extra_config+=("-DCMAKE_BUILD_TYPE=Release")
    else
        extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
    fi

    MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
        ${MINGW_PREFIX}/bin/cmake.exe \
        -GNinja \
        -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
        "${extra_config[@]}" \
        -DBUILD_{SHARED,STATIC}_LIBS=ON \
        -DBUILD_TESTING=OFF \
        -DBUILD_KRITA_QT_DESIGNER_PLUGINS=ON \
        -Wno-dev \
        ../${_realname}-${pkgver}

    ${MINGW_PREFIX}/bin/cmake.exe --build .
}

check() {
    cd "${srcdir}"/build-${MSYSTEM}

    cmake --build . --target test
}

package() {
    cd "${srcdir}"/build-${MSYSTEM}

    DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install .

    install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
