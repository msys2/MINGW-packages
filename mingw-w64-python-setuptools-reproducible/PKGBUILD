# Maintainer: Joel Holdsworth <jholdsworth@nvidia.com>

_realname=setuptools-reproducible
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.1
pkgrel=1
pkgdesc="Extension of setuptools to support reproducible dists"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'archlinux: python-anytree'
  'purl: pkg:pypi/anytree'
)
license=('spdx:MIT')
url='https://github.com/wimglenn/setuptools-reproducible'
msys2_repository_url='https://github.com/wimglenn/setuptools-reproducible'
msys2_issue_tracker_url='https://github.com/wimglenn/setuptools-reproducible/issues'
depends=("${MINGW_PACKAGE_PREFIX}-python-setuptools")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-pytest")
options=('!strip')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname//-/_}/${_realname//-/_}-${pkgver}.tar.gz")
sha256sums=('1ae3c0dcca3f125e0fdfb7ce749c872f63d9fe6a0fd8b36954f639ee677f2f73')

build() {
  cp -r "${_realname//-/_}-${pkgver}" "build-${MSYSTEM}" && cd "build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "build-${MSYSTEM}"
  PYTHONPATH="build:$PYTHONPATH" ${MINGW_PREFIX}/bin/python -m pytest || warning "Tests failed"
}

package() {
  cd "build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
