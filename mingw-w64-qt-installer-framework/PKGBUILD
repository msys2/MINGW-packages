# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=installer-framework
pkgbase=mingw-w64-qt-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-qt-${_realname}"
pkgdesc="The Qt Installer Framework used for the Qt SDK installer (mingw-w64)"
pkgver=4.3.0
pkgrel=1
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='https://www.qt.io/developers/'
license=('GPL3' 'LGPL' 'FDL' 'custom')
conflicts=("${MINGW_PACKAGE_PREFIX}-qt-${_realname}-git")
replaces=("${MINGW_PACKAGE_PREFIX}-qt-${_realname}-git")
makedepends=("${MINGW_PACKAGE_PREFIX}-qt5-static"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-make"
             "${MINGW_PACKAGE_PREFIX}-libwebp"
             "${MINGW_PACKAGE_PREFIX}-jasper")
options=('strip' 'staticlibs')
#options=('!strip' 'debug' 'staticlibs')
source=(https://download.qt.io/official_releases/qt-${_realname}/${pkgver}/${_realname}-opensource-src-${pkgver}.tar.xz
        "0002-add-installation-stuff-to-pro-files.patch"
        "0003-add-recursive-option-to-rmdir-operation.patch"
        "0004-add-dirExists-fuction.patch"
        "0005-Fix-RunProgram-variable-substitution.patch"
        "0006-Don-t-remove-files-that-live-outside-of-path.patch"
        "0007-Don-t-prevent-qmake-linking-qmltooling-plugin.patch"
        "0008-Use-QTPLUGIN-qminimal-on-Windows-too.patch"
        "0010-fix-version-pass-to-windres.patch"
        "0011-fix-redefined-mode_t.patch")
sha256sums=('742924a36133da23215d9f68e8ce3c51d6dd448dde142f208866d43f324a87f6'
            '38ff350982e03aff66a70e87b0202eca7dae696d0b5312270204e8cc572e5aa6'
            '4db7a4c3e6ea3a26de6493e4e2b683251060c7c8cefacf6bfa458026e541e906'
            '2aaf51f346edb6db2367262d0d8e1bb77d0afee51fabd5b2766038a97ebf0ca6'
            '2fbd939417da1a2c0166c0ac47307b9c579f07a3e5b37aa73af1629caa38387f'
            '00857e9a654ecaed93ec79898389329799fdc01968b7562ae1889498b12c943d'
            'a82b4dbddebb42111832ab4dd79ec591d4a7196dea4b59f0dc77bb7615181b9e'
            'ef94ad9a00aca5ab66937bd3fe4a3c1714ce610bb59dbb338a92bef5d4beaebf'
            'f7293902981c69e642c59276cee2a24c0d0540f8fddfc4ac9f9a6b22cf0151ed'
            'feb3ce36535136dd7dcd457a99520b15605057b051fe481bc4073378e772560f')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${srcdir}"/${_realname}-opensource-src-${pkgver}

  apply_patch_with_msg \
    0002-add-installation-stuff-to-pro-files.patch \
    0003-add-recursive-option-to-rmdir-operation.patch \
    0004-add-dirExists-fuction.patch \
    0007-Don-t-prevent-qmake-linking-qmltooling-plugin.patch \
    0008-Use-QTPLUGIN-qminimal-on-Windows-too.patch \
    0010-fix-version-pass-to-windres.patch \
    0011-fix-redefined-mode_t.patch

  # Apply patch from https://bugreports.qt.io/browse/QTIFW-397
  apply_patch_with_msg \
    0005-Fix-RunProgram-variable-substitution.patch

  # This requires ../mingw-w64-qt5-static/0044-qt-5.4.1-Revert-Revert-fix-NTFS-mount-points.patch
  # to fix the problem for junction points. Together they fix:
  # https://github.com/msys2/msys2.github.io/issues/4
  apply_patch_with_msg \
    0006-Don-t-remove-files-that-live-outside-of-path.patch
}

build() {
  [[ -d build-${MSYSTEM} ]] && rm -rf build-${MSYSTEM}
  mkdir build-${MSYSTEM} && cd build-${MSYSTEM}

  local _config
  if check_option "debug" "n"; then
    _config="release"
  else
    _config="debug"
  fi

  ${MINGW_PREFIX}/qt5-static/bin/qmake.exe ../${_realname}-opensource-src-${pkgver}/installerfw.pro CONFIG+="${_config}" CONFIG+=no_testcase_installs

  make
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}
  make INSTALL_ROOT="${pkgdir}${MINGW_PREFIX}" install
  rm -rf "${pkgdir}${MINGW_PREFIX}"/lib

  install -d "$pkgdir${MINGW_PREFIX}"/share/licenses/${_realname}
  install -Dm644 "${srcdir}"/${_realname}-opensource-src-${pkgver}/LICENSE* "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}
}
