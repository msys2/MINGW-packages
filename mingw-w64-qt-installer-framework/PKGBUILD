# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=installer-framework
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-qt-${_realname}-git"
pkgdesc="The Qt Installer Framework used for the Qt SDK installer"
pkgver=r2084.b138cbf
pkgrel=1
arch=('any')
url='http://qt-project.org/'
license=('GPL3' 'LGPL' 'FDL' 'custom')
provides=("${_mingw_suff}-qt-${_realname}")
depends=("${_mingw_suff}-qt5-static")
makedepends=("${_mingw_suff}-gcc"
			"${_mingw_suff}-make")
options=('!strip' 'staticlibs')
source=("${_realname}"::"git://gitorious.org/${_realname}/${_realname}.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/${_realname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd ${srcdir}/${_realname}
}

build() {
	local _config
	cd ${srcdir}/${_realname}
	if check_option "strip" "n"; then
		_config="release"
	else
		_config="debug"
	fi
	${MINGW_PREFIX}/qt5-static/bin/qmake.exe installerfw.pro CONFIG+="${_config}"
	make
}

package() {
	cd ${srcdir}/${_realname}
	make INSTALL_ROOT="${pkgdir}${MINGW_PREFIX}" install
}
