# Maintainer: Mario Emmenlauer <memmenlauer@biodataanalysis.de>

_realname=thrift
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
pkgver=0.19.0
pkgrel=1
pkgdesc="Apache Thrift is a software framework for scalable cross-language services development (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://thrift.apache.org/'
license=('spdx:Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-boost"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
source=("https://archive.apache.org/dist/thrift/${pkgver}/${_realname}-${pkgver}.tar.gz"{,.asc}
        001-math-define-pi.patch
        002-thrift-add-stdint.patch)
sha256sums=('d49c896c2724a78701e05cfccf6cf70b5db312d82a17efe951b441d300ccf275'
            'SKIP'
            '1c84e10b84b3d44f5ab2e7be7505c9375f0a5516b906e2a52784d6c5c01fab08'
            'bb7b495b09a32bdb8058f9b5b19da22469d9e5a05a68b7e84b24d523b7342d77')
validpgpkeys=('8CD87F186F06E958EFCA963D76BD340FC4B75865') # Jens Geyer <jensgeyer@hotmail.com>

prepare() {
  cd "${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/001-math-define-pi.patch"

  # https://github.com/apache/thrift/pull/2861
  patch -p1 -i "${srcdir}/002-thrift-add-stdint.patch"
}

build() {
  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    "${_extra_config[@]}" \
    -DWITH_SHARED_LIB="OFF" \
    -DWITH_STATIC_LIB="ON" \
    -DBUILD_COMPILER="ON" \
    -DBUILD_TESTING="ON" \
    -DBUILD_TUTORIALS="OFF" \
    -DWITH_LIBEVENT="OFF" \
    -DWITH_OPENSSL="ON" \
    -DOPENSSL_USE_STATIC_LIBS="ON" \
    -DWITH_ZLIB="ON" \
    -DWITH_QT5="OFF" \
    -DWITH_CPP="ON" \
    -DWITH_C_GLIB="OFF" \
    -DWITH_JAVA="OFF" \
    -DWITH_JAVASCRIPT=OFF \
    -DWITH_NODEJS=OFF \
    -DWITH_PYTHON="OFF" \
    -S "${_realname}-${pkgver}" \
    -B "build-${MSYSTEM}"

  "${MINGW_PREFIX}"/bin/cmake.exe --build "build-${MSYSTEM}"
}

check() {
  "${MINGW_PREFIX}"/bin/ctest.exe --test-dir "build-${MSYSTEM}" || warning "Tests failed"
}

package() {
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install "build-${MSYSTEM}"

  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  for _f in "${pkgdir}${MINGW_PREFIX}"/lib/cmake/thrift/*.cmake; do
    sed -e "s|${PREFIX_WIN}|\$\{_IMPORT_PREFIX\}|g" -i ${_f}
    sed -e "s|${MINGW_PREFIX}|\$\{_IMPORT_PREFIX\}|g" -i ${_f}
  done

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.txt"
}
