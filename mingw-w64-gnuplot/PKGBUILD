# Maintainer: Peter Budai <peterbudai at hotmail.com>

_realname=gnuplot
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=5.2.0
pkgrel=1
pkgdesc="Plotting package which outputs to X11, PostScript, PNG, GIF, and others (mingw-w64)"
arch=('any')
url='http://www.gnuplot.info/'
license=('LICENSE')
validpgpkeys=('gpg_KEY')
depends=("${MINGW_PACKAGE_PREFIX}-readline"
          "${MINGW_PACKAGE_PREFIX}-libgd"
          "${MINGW_PACKAGE_PREFIX}-cairo"
          "${MINGW_PACKAGE_PREFIX}-pango"
          "${MINGW_PACKAGE_PREFIX}-lua"
          "${MINGW_PACKAGE_PREFIX}-wxWidgets"
          "${MINGW_PACKAGE_PREFIX}-libcerf"
          "${MINGW_PACKAGE_PREFIX}-gnutls")
makedepends=("texinfo")
options=('strip' 'staticlibs')
source=("http://downloads.sourceforge.net/sourceforge/$_realname/$_realname-$pkgver.tar.gz"
        "01-gnuplot.patch" )
sha256sums=('7dfe6425a1a6b9349b1fb42dae46b2e52833b13e807a78a613024d6a99541e43'
            '502743f65c2bea57f474192076c315717fcba1aa29fe2ebffff552a6c0be522c')


prepare() {
  cd "$srcdir"/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/01-gnuplot.patch
}

build() {
  cd "$srcdir"/${_realname}-${pkgver}

  ./configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST}

  # Do not build docs
  cd config/mingw
  make console windows pipes
}


package() {
  cd "${srcdir}"/${_realname}-${pkgver}/config/mingw
  make install DESTDIR="${pkgdir}"

  cd "${pkgdir}"
  mkdir -p "${pkgdir}${MINGW_PREFIX}/bin"
  mkdir -p "${pkgdir}${MINGW_PREFIX}/share/${_realname}"
  mkdir -p "${pkgdir}${MINGW_PREFIX}/share/${_realname}/demo"
  mkdir -p "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}"

  mv ${pkgdir}/bin/* ${pkgdir}${MINGW_PREFIX}/bin/
  mv ${pkgdir}/share/* ${pkgdir}${MINGW_PREFIX}/share/${_realname}/
  mv ${pkgdir}/demo/* ${pkgdir}${MINGW_PREFIX}/share/${_realname}/demo/
  mv ${pkgdir}/docs/* ${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/

  rm -rf "${pkgdir}/bin"
  rm -rf "${pkgdir}/share"
  rm -rf "${pkgdir}/demo"
  rm -rf "${pkgdir}/docs"

  for i in $(find . -maxdepth 1  -type f); do mv ${i} ${pkgdir}${MINGW_PREFIX}/share/${_realname}/; done
}
