# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ignacio Casal Quinteiro <icq@gnome.org>
# Contributor: Renato Silva <br.renatosilva@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=glib2
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=2.47.4.13.gea5ca11
provides=(${MINGW_PACKAGE_PREFIX}-${_realname}=$pkgver)
conflicts=(${MINGW_PACKAGE_PREFIX}-${_realname})
pkgrel=1
url="http://www.gtk.org/"
arch=('any')
pkgdesc="Common C routines used by GTK+ 2.4 and other libs (git) (mingw-w64)"
license=(LGPL2)
options=('debug' '!strip')
install=glib2-${CARCH}.install
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gettext"
         "${MINGW_PACKAGE_PREFIX}-libffi"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-python2")
source=("${_realname}::git://git.gnome.org/glib"
        0001-Use-CreateFile-on-Win32-to-make-sure-g_unlink-always.patch
        0003-g_abort.all.patch
        0004-glib-prefer-constructors-over-DllMain.patch
        0017-glib-use-gnu-print-scanf.patch
        0023-print-in-binary-more-for-testing.all.patch
        0027-no_sys_if_nametoindex.patch
        0028-inode_directory.patch
        0029-grand.all.patch)

md5sums=('SKIP'
         '10cf1e482b6a1367d79648466f2cf23c'
         '27e8188e764eed3b3d565e5fc5746654'
         'b3cf766d5e717010a7534c61e53f3204'
         '12d8f704e346f065f05914e9fd83f26b'
         'f6659fa19dd0395a7a285c84bb75472b'
         '4aa52b552a6a25b49a1b6f928b3cdafd'
         '27ad96697441aabc4a01f881ad9999ea'
         'b64390c406d32578e979103eed89232b')

pkgver() {
  cd "${srcdir}"/${_realname}
  git describe --tags | sed 's|-|.|g'
}

prepare() {
  cd "${srcdir}"/${_realname}

  patch -Np1 -i "${srcdir}"/0001-Use-CreateFile-on-Win32-to-make-sure-g_unlink-always.patch
  patch -Np1 -i "${srcdir}"/0003-g_abort.all.patch
  patch -Np1 -i "${srcdir}"/0004-glib-prefer-constructors-over-DllMain.patch
  patch -Np1 -i "${srcdir}"/0017-glib-use-gnu-print-scanf.patch
  patch -Np1 -i "${srcdir}"/0023-print-in-binary-more-for-testing.all.patch
  patch -Np1 -i "${srcdir}"/0027-no_sys_if_nametoindex.patch
  patch -Np1 -i "${srcdir}"/0028-inode_directory.patch
  patch -Np1 -i "${srcdir}"/0029-grand.all.patch
  
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  export MSYS2_ARG_CONV_EXCL="-//OASIS//DTD"
  msg "Build shared version"
  [[ -d "${srcdir}"/build-${CARCH}-shared ]] && rm -rf "${srcdir}"/build-${CARCH}-shared
  mkdir "${srcdir}"/build-${CARCH}-shared && cd "${srcdir}"/build-${CARCH}-shared
  ../${_realname}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --disable-static --enable-shared \
    --disable-libelf \
    --with-python=${MINGW_PREFIX}/bin/python.exe \
    --with-threads=posix \
    --with-xml-catalog=${MINGW_PREFIX}/etc/xml/catalog
  make

  msg "Build static version"
  mkdir "${srcdir}"/build-${CARCH}-static && cd "${srcdir}"/build-${CARCH}-static
  ../${_realname}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --disable-shared --enable-static \
    --disable-libelf \
    --with-python=${MINGW_PREFIX}/bin/python.exe \
    --with-threads=posix \
    --with-xml-catalog=${MINGW_PREFIX}/etc/xml/catalog
  make
}

package() {
  cd "${srcdir}"/build-${CARCH}-static
  make DESTDIR="${pkgdir}"/static install

  cd "${srcdir}"/build-${CARCH}-shared
  make DESTDIR="${pkgdir}" install

  mv "${pkgdir}"/static/${MINGW_PREFIX}/lib/*.a "${pkgdir}"/${MINGW_PREFIX}/lib/

  rm -rf "${pkgdir}"/static

  rm -f "${pkgdir}"${MINGW_PREFIX}/lib/charset.alias
  #rm -rf "${pkgdir}"${MINGW_PREFIX}/{share/{gtk-doc,man,bash-completion,gdb},lib/charset.alias,etc}
  pushd "${pkgdir}"${MINGW_PREFIX}/lib
    find . -maxdepth 1 -name "*.def" -exec rm -f {} \;
  popd

  #rm -f "${pkgdir}"${MINGW_PREFIX}/bin/gdbus-codegen
  #rm -rf "${pkgdir}"${MINGW_PREFIX}/lib/gdbus-2.0

  install -Dm644 "${srcdir}"/${_realname}/COPYING "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
