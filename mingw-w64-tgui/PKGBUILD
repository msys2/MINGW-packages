# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=tgui
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3.0
pkgrel=1
pkgdesc="Cross-platform C++ GUI library (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url='https://tgui.eu/'
msys2_repository_url='https://github.com/texus/TGUI'
license=('spdx:Zlib')
depends=("${MINGW_PACKAGE_PREFIX}-sfml")
makedepends=("${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-sfml"
             "${MINGW_PACKAGE_PREFIX}-doxygen")

source=("${msys2_repository_url}/archive/v${pkgver}/${_realname^^}-${pkgver}.tar.gz")
sha256sums=('641067026656518479ac8e7e1551fc0673836dfdb82c31b03474c27eb3bf0b05')

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  # static build
  [[ -d ${srcdir}/build-${MSYSTEM}-static ]] && rm -rf ${srcdir}/build-${MSYSTEM}-static
  mkdir -p ${srcdir}/build-${MSYSTEM}-static && cd ${srcdir}/build-${MSYSTEM}-static
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    ${extra_config} \
    -DTGUI_SHARED_LIBS=OFF \
    -DTGUI_BACKEND=SFML_GRAPHICS \
    ../${_realname^^}-${pkgver}
  ${MINGW_PREFIX}/bin/cmake --build .

  # shared build
  [[ -d ${srcdir}/build-${MSYSTEM}-shared ]] && rm -rf ${srcdir}/build-${MSYSTEM}-shared
  mkdir -p ${srcdir}/build-${MSYSTEM}-shared && cd ${srcdir}/build-${MSYSTEM}-shared
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    ${extra_config} \
    -DTGUI_SHARED_LIBS=ON \
    -DTGUI_BUILD_DOC=ON \
    -DTGUI_BACKEND=SFML_GRAPHICS \
    ../${_realname^^}-${pkgver}
  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  # Static Install
  cd "${srcdir}/build-${MSYSTEM}-static"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .

  # Shared Install
  cd "${srcdir}/build-${MSYSTEM}-shared"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .

  install -Dm644 "${srcdir}/${_realname^^}-${pkgver}/license.txt" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/license.txt"
}
