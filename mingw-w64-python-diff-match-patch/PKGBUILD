# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=diff-match-patch
pkgbase=mingw-w64-python-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-${_realname}
pkgver=20241021
pkgrel=1
pkgdesc='A high-performance library in multiple languages that manipulates plain text (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/diff-match-patch-python/diff-match-patch'
msys2_references=(
  'purl: pkg:pypi/diff-match-patch'
)
license=('spdx:Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-flit-core")
source=("https://pypi.io/packages/source/${_realname:0:1}/${_realname}/${_realname//-/_}-${pkgver}.tar.gz")
sha256sums=('beae57a99fa48084532935ee2968b8661db861862ec82c6f21f4acdd6d835073')

prepare() {
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname//-/_}-${pkgver}" "python-build-${MSYSTEM}"
}

build() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
