# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=flask-marshmallow
_pyname=flask_marshmallow
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.1.0
pkgrel=1
pkgdesc='Flask + marshmallow for beautiful APIs (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'archlinux: python-flask-marshmallow'
  'pypi: flask-marshmallow'
)
msys2_repository_url='https://github.com/marshmallow-code/flask-marshmallow/'
msys2_changelog_url='https://flask-marshmallow.readthedocs.io/en/latest/changelog.html'
msys2_documentation_url='https://flask-marshmallow.readthedocs.io/'
msys2_issue_tracker_url='https://github.com/marshmallow-code/flask-marshmallow/issues'
url='https://github.com/marshmallow-code/flask-marshmallow'
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python-flask-sqlalchemy"
         "${MINGW_PACKAGE_PREFIX}-python-marshmallow-sqlalchemy")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-flit-core"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-wheel")
source=("https://pypi.org/packages/source/${_pyname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha256sums=('2d186af7c8b4455b8a2c166c7470939c17d70353671ea5a287a14676846fa013')

build() {
  cp -r "${_pyname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
