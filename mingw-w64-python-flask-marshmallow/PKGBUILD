# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=flask-marshmallow
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.15.0
pkgrel=2
pkgdesc='Flask + marshmallow for beautiful APIs (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/marshmallow-code/flask-marshmallow'
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python-flask-sqlalchemy"
         "${MINGW_PACKAGE_PREFIX}-python-marshmallow-sqlalchemy")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools")
source=("${_realname}-$pkgver.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('51cbfc5472b534916cf722f928032e5eb62b3d4d1c93d6712753fb1c9794f230')

prepare() {
  cd "${srcdir}"
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}"
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    "${MINGW_PREFIX}"/bin/python setup.py install --optimize=1 --skip-build \
      --prefix=${MINGW_PREFIX} \
      --root="${pkgdir}"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
