# Maintainer: Wu, Zhenyu <whenyu@ustc.edu>

_realname=fq
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.12.0
pkgrel=1
pkgdesc="Tool, language and decoders for inspecting binary data (mingw-w64)"
arch=(any)
mingw_arch=('mingw32' 'mingw64' 'ucrt32' 'ucrt64' 'clang32' 'clang64' 'clangarm64')
url="https://github.com/wader/fq"
license=('MIT')
makedepends=("$MINGW_PACKAGE_PREFIX-go")
# LTO renders GO BUILDID unreproducible: https://gcc.gnu.org/bugzilla/show_bug.cgi?id=108534
options=(!lto)
source=("${url}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('8afba45acd45d294ab5421dd71e7224cfb66a7d9a2fb63a6e5d40d7943c827c6')

prepare() {
  cd "${_realname}-${pkgver}"
  mkdir -p dist/
}

build() {
  cd "${_realname}-${pkgver}"
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" ${_BUILDINFO}" \
    -o dist/fq.exe \
    .
}

package() {
  cd "${_realname}-${pkgver}"
  install -Dm 755 "dist/${_realname}.exe" -t "${pkgdir}/$MINGW_PREFIX/bin"
  install -Dm 644 LICENSE  -t "${pkgdir}/$MINGW_PREFIX/share/licenses/${_realname}"
  install -Dm 644 README.md -t "${pkgdir}/$MINGW_PREFIX/share/doc/${_realname}"
}

# vim:set ts=2 sw=2 et:
