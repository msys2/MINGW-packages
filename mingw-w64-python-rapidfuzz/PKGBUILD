# Contributor: Liu Pak Hin <wl@m-labs.hk>

_realname=rapidfuzz
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.9.1
pkgrel=1
pkgdesc="Rapid fuzzy string matching."
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=('MIT')
url="https://github.com/maxbachmann/RapidFuzz"
depends=(  
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-python-pandas"
  "${MINGW_PACKAGE_PREFIX}-python-numpy"
  "${MINGW_PACKAGE_PREFIX}-cc"
)
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-pytest-cov"
              "${MINGW_PACKAGE_PREFIX}-python-pytest-runner")
source=("https://github.com/maxbachmann/RapidFuzz/archive/refs/tags/v${pkgver}.tar.gz"
        "https://github.com/maxbachmann/rapidfuzz-cpp/archive/103674d.tar.gz"
        "https://github.com/taskflow/taskflow/archive/f69ae7c.tar.gz"
)
sha256sums=('e8053877d9aa12062e4c89930a848693f239a47d506101deb56b8ea3b8bce8fb'
            '6c5e3ed02a73e849f8488b6615cddd28c3d28331e66eeda692e1b23f49992305'
            '8d72c3d2ee8a6f03d2b759bf0e42158a15be91632e5f1be07d024dac26e92e7b'
)

prepare(){
  cd "${srcdir}"
  cp -r "RapidFuzz-${pkgver}" "python-build-${MSYSTEM}"
  rm -rf "python-build-${MSYSTEM}/extern/rapidfuzz-cpp"
  rm -rf "python-build-${MSYSTEM}/extern/taskflow"
  cp -r rapidfuzz-cpp-* "python-build-${MSYSTEM}/extern/rapidfuzz-cpp"
  cp -r taskflow-* "python-build-${MSYSTEM}/extern/taskflow"
}

build() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  "${MINGW_PREFIX}"/bin/python setup.py build
}

check() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python setup.py pytest || warning "Tests failed"
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
