# Contributor: Oleg A. Khlybov <fougas@mail.ru>

_realname=parmetis
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.0.3
pkgrel=1
pkgdesc="Parallel graph partitioning and fill-reducing matrix ordering library (mingw-w64)"
arch=('any')
url="http://glaros.dtc.umn.edu/gkhome/metis/parmetis/overview"
license=("Apache 2.0") # CHECKME
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-cmake" "${MINGW_PACKAGE_PREFIX}-metis>=5.0.1" "${MINGW_PACKAGE_PREFIX}-msmpi")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" "${MINGW_PACKAGE_PREFIX}-metis>=5.0.1")
options=('strip' 'staticlibs')
source=("http://glaros.dtc.umn.edu/gkhome/fetch/sw/parmetis/${_realname}-${pkgver}.tar.gz")
sha256sums=('f2d9a231b7cf97f1fee6e8c9663113ebf6c240d407d3c118c55b3633d6be6e5f')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  for p in ${srcdir}/../*.patch; do
	patch -p1 < $p
  done
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd ${srcdir}/build-${MINGW_CHOST}
  for shared in ON OFF; do
	  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
	  ${MINGW_PREFIX}/bin/cmake \
		-G"MSYS Makefiles" \
		-DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
		-DCMAKE_BUILD_TYPE=Release \
		-DSHARED=${shared} \
		-DGKLIB_PATH="../${_realname}-${pkgver}/metis/GKlib" \
		../${_realname}-${pkgver}
	  make
  done
}

_pc="
prefix=${MINGW_PREFIX}
libdir=\${prefix}/lib
includedir=\${prefix}/include
Name: ${_realname}
URL: ${url}
Version: ${pkgver}
Description: ${pkgdesc}
Requires: metis
Requires.private: msmpi
Cflags: -I\${includedir}
Libs: -L\${libdir} -l${_realname}
"

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install
  rm -rf "${pkgdir}${MINGW_PREFIX}/bin"
  mkdir -p "${pkgdir}${MINGW_PREFIX}/bin"
  mkdir -p "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig"
  lib=lib${_realname}
  cd ${lib}
  install -m644 ${lib}{.a,.dll.a} "${pkgdir}${MINGW_PREFIX}/lib"
  install -m644 ${lib}.dll "${pkgdir}${MINGW_PREFIX}/bin"
  echo "${_pc}" > "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/${_realname}.pc"
}
