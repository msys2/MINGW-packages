# Contributor (MSYS2): David Macek <david.macek.0@gmail.com>
# Maintainer (Arch Linux): Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor (Arch Linux): Angel Velasquez <angvp@archlinux.org>
# Contributor (Arch Linux): Ionut Biru  <ibiru@archlinux.ro>
# Contributor (Arch Linux): William Rea <sillywilly@gmail.com>
# Contributor (Arch Linux): Allan McRae <mcrae_allan@hotmail.com>

_realname=geany
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.0
_tarballver=2.0.0
pkgrel=1
pkgdesc='Fast and lightweight IDE (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://www.geany.org/'
msys2_repository_url="https://github.com/geany/geany"
msys2_references=(
  'archlinux: geany'
)
license=('spdx:GPL-2.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-gtk3"
         "${MINGW_PACKAGE_PREFIX}-adwaita-icon-theme"
         "${MINGW_PACKAGE_PREFIX}-gtk-update-icon-cache"
         "${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-doxygen"
             "${MINGW_PACKAGE_PREFIX}-python-docutils"
             "${MINGW_PACKAGE_PREFIX}-python-lxml"
             "${MINGW_PACKAGE_PREFIX}-python-rst2pdf")
optdepends=("${MINGW_PACKAGE_PREFIX}-geany-plugins: various extra features")
source=("https://github.com/geany/geany/archive/${_tarballver}/${_realname}-${pkgver}.tar.gz"
        "0001-mingw-build.patch"
        "0002-export.patch")
sha256sums=('a744ab9ae3e58b371de4b50990c44227c499f82e2a8ee6753307ef107748e4df'
            '5c9f7b90a889ededb3b1a8937aaccd6b3397872c7d5f24dfcc6c15d00b676793'
            '243af9ca00a88a6c4043c72285c62eaef2e9e796f1960f37cb99ee6b3c0e4c35')

prepare() {
  cd "${srcdir}/${_realname}-${_tarballver}"

  # Syntax highlighting for PKGBUILD files
  sed -i 's/Sh=/Sh=PKGBUILD;/' data/filetype_extensions.conf

  patch -p1 -i "${srcdir}/0001-mingw-build.patch"
  # https://github.com/geany/geany/pull/3067
  patch -p1 -i "${srcdir}/0002-export.patch"
}

build() {
  mkdir -p "build-${MSYSTEM}" && cd "build-${MSYSTEM}"

  # libiberty
  export LDFLAGS+=" -L${MINGW_PREFIX}/lib/binutils"

  # config.h
  export PLUGIN_CFLAGS+=" -I$(pwd)"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    meson setup \
      --prefix="${MINGW_PREFIX}" \
      --wrap-mode=nodownload \
      --auto-features=enabled \
      --buildtype=plain \
      -Dmac-integration=disabled \
      ../${_realname}-${_tarballver}

  meson compile
}

package() {
  cd "build-${MSYSTEM}"

  DESTDIR="${pkgdir}" meson install
  rm -rf "${pkgdir}${MINGW_PREFIX}/share/applications"
  rm -rf "${pkgdir}${MINGW_PREFIX}/share/icons/hicolor/index.theme"
}
