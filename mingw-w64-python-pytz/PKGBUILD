# Contributor: Runar Tenfjord < runar dot tenfjord at gmail dot com >

_realname=pytz
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}" "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=2016.10
pkgrel=1
pkgdesc="Cross platform time zone library for Python (mingw-w64)"
arch=('any')
url="https://pypi.python.org/pypi/pytz"
license=("MIT")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-python2")
_dtoken="42/00/5c89fc6c9b305df84def61863528e899e9dccb196f8438f6cbe960758fc5"
source=(https://pypi.python.org/packages/${_dtoken}/pytz-${pkgver}.tar.gz)
sha256sums=('9a43e20aa537cfad8fe7a1715165c91cb4a6935d40947f2d070e4c80f2dcd22b')

prepare() {
  cd "${srcdir}"
  cp -r pytz-${pkgver} build-python2
  cp -r pytz-${pkgver} build-python3
}

check(){
  cd ${srcdir}/build-python2/pytz/tests
  ${MINGW_PREFIX}/bin/python2 test_tzinfo.py
  cd ${srcdir}/build-python3/pytz/tests
  ${MINGW_PREFIX}/bin/python3 test_tzinfo.py
}

package_python3-pytz() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3")

  cd ${srcdir}/build-python3
  # Fix locale https://github.com/ipython/ipython/issues/2057
  export LC_ALL=en_US.UTF-8

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} --root=${pkgdir} --optimize=1

  install -D LICENSE.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE
}

package_python2-pytz() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2")

  cd ${srcdir}/build-python2
  # Fix locale https://github.com/ipython/ipython/issues/2057
  export LC_ALL=en_US.UTF-8

  # python 2 fix
  sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' pytz/tzfile.py

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} --root=${pkgdir} --optimize=1

  install -D LICENSE.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE
}

package_mingw-w64-i686-python2-pytz() {
  package_python2-pytz
}

package_mingw-w64-i686-python3-pytz() {
  package_python3-pytz
}

package_mingw-w64-x86_64-python2-pytz() {
  package_python2-pytz
}

package_mingw-w64-x86_64-python3-pytz() {
  package_python3-pytz
}
