# Maintainer: Vincent Torri <vincent.torri@gmail.com>

_realname=thorvg
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.0.0
pkgrel=1
pkgdesc="Lightweight vector graphic library (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://www.thorvg.org/'
msys2_repository_url='https://github.com/thorvg/thorvg'
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-openmp"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-libwebp")
makedepends=("${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-cc")
source=("${msys2_repository_url}/releases/download/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('42ba68b6ae770266e114a9f0fa56f70af8f843b63b59562f771c73c75b1d06af')

prepare() {
  cd "${srcdir}"/${_realname}
}

build() {
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    meson setup \
      --prefix="${MINGW_PREFIX}" \
      --wrap-mode=nodownload \
      --buildtype=release \
      -Ddefault_library=shared \
      -Dbindings=capi \
      -Dengines=sw,gl \
      -Dloaders=all \
      -Dsavers=all \
      -Dtools=all \
      ../${_realname}

  meson compile
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" meson install

  install -Dm644 "${srcdir}"/${_realname}/LICENSE \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
