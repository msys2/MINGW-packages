# Maintainer: Vincent Torri <vincent.torri@gmail.com>

_realname=thorvg
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.0.1
pkgrel=1
pkgdesc="Lightweight vector graphic library (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://www.thorvg.org/'
msys2_repository_url='https://github.com/thorvg/thorvg'
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-libwebp"
         "${MINGW_PACKAGE_PREFIX}-omp")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-pkgconf")
source=("${msys2_repository_url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('061343ed560f08ef2f7b48a7f6b684ae6bfd50b7904599d8581e466b2531844f')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
}

build() {
  MSYS2_ARG_CONV_EXCL="--prefix=" \
    meson setup \
      --prefix="${MINGW_PREFIX}" \
      --wrap-mode=nodownload \
      --buildtype=release \
      -Ddefault_library=shared \
      -Dbindings=capi \
      -Dengines=sw,gl \
      -Dloaders=all \
      -Dsavers=all \
      -Dsimd=true \
      -Dtools=all \
      build-${MSYSTEM} \
      ${_realname}-${pkgver}

  meson compile -C build-${MSYSTEM}
}

package() {
  DESTDIR="${pkgdir}" meson install -C build-${MSYSTEM}

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/LICENSE \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
