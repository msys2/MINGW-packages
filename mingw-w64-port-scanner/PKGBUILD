# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=port-scanner
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.3
pkgrel=4
pkgdesc="A multi threaded TCP port scanner from SecPoint.com (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://www.secpoint.com/multi-threaded-tcp-port-scanner.html"
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc")
source=("https://www.secpoint.com/freetools/threaded-port-scanner-${pkgver}.zip"
        "pathtools.c"
        "pathtools.h"
        "paths.patch")
sha256sums=('768c595fba7ba7e81da35e1bba1118bf08a1d689c6e419804d2109fc64177436'
            '6f1016e6647b6340fdceefaf24ff391f4c0ea3c785ddf70c9794ca2356797888'
            '6ce4dcf4ef6c4bce48dbcb6f1b5226baf79f74ac76719fb0c06419a0aadb37a3'
            'e27fad5161752247259e271c30aa49ea7eb18fd9f9b4f92553332c5cbb031db8')

prepare() {
  test ! -d "${startdir}/../mingw-w64-pathtools" || {
    cmp "${startdir}/../mingw-w64-pathtools/pathtools.c" "${srcdir}/pathtools.c" &&
    cmp "${startdir}/../mingw-w64-pathtools/pathtools.h" "${srcdir}/pathtools.h"
  } || exit 1

  cd "${srcdir}/threaded-port-scanner-${pkgver}"
  cp -fHv "${srcdir}"/pathtools.[ch] ./

  patch -p1 -i "${srcdir}/paths.patch" # also adds -lws2_32
  mkdir -p "${srcdir}/build-${CARCH}"
  cp * "${srcdir}/build-${CARCH}"
}

build() {
  cd "${srcdir}/build-${CARCH}"
  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  install -Dm755 pscan.exe "${pkgdir}${MINGW_PREFIX}/bin/pscan.exe"
  install -Dm644 oui.txt "${pkgdir}${MINGW_PREFIX}/share/port-scanner/oui.txt"
  install -Dm644 port-numbers.txt "${pkgdir}${MINGW_PREFIX}/share/port-scanner/port-numbers.txt"
}
