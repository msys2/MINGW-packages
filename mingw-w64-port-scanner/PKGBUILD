# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=port-scanner
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.3
pkgrel=2
pkgdesc="A multi threaded TCP port scanner from SecPoint.com (mingw-w64)"
arch=('any')
url="https://www.secpoint.com/multi-threaded-tcp-port-scanner.html"
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
source=("https://www.secpoint.com/freetools/threaded-port-scanner-${pkgver}.zip"
        "paths.patch")
md5sums=('e37be464831f874dc1c867fed4fdd680'
         'ee5682884609812aa6aafe3349148372')

prepare() {
  cd "${srcdir}/threaded-port-scanner-${pkgver}"
  rm -f pathtools.c pathtools.h
  patch -p1 -i "${srcdir}/paths.patch" # also adds -lws2_32
  mkdir -p "${srcdir}/build-${CARCH}"
  cp * "${srcdir}/build-${CARCH}"
}

build() {
  cd "${srcdir}/build-${CARCH}"
  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  install -Dm755 pscan.exe "${pkgdir}${MINGW_PREFIX}/bin/pscan.exe"
  install -Dm644 oui.txt "${pkgdir}${MINGW_PREFIX}/share/port-scanner/oui.txt"
  install -Dm644 port-numbers.txt "${pkgdir}${MINGW_PREFIX}/share/port-scanner/port-numbers.txt"
}
