# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.su>

_realname=kanri
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.8.2
pkgrel=1
pkgdesc="Modern offline Kanban board app made with Tauri and Nuxt (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64')
url='https://www.kanriapp.com/'
msys2_repository_url='https://github.com/kanriapp/kanri'
license=('spdx:GPL-3.0')
depends=("${MINGW_PACKAGE_PREFIX}-webview2-loader")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-cargo-tauri"
             #"${MINGW_PACKAGE_PREFIX}-nodejs"
             "${MINGW_PACKAGE_PREFIX}-yarn")
source=("${msys2_repository_url}/archive/app-v${pkgver}/${_realname}-app-v${pkgver}.tar.gz")
sha256sums=('1695e45c966540655dc53ddafef7176ca2e467c797fac1abd3357c7f4b94f569')

prepare() {
  cd "${_realname}-app-v${pkgver}"

  # Use cached NodeJS to avoid N-API errors
  PATH=/C/hostedtoolcache/windows/node/22.21.1/x64:$PATH \
    yarn install \
      --cache-folder "${srcdir}/yarn-cache" \
      --frozen-lockfile

  cargo fetch \
    --locked \
    --target "${RUST_CHOST}" \
    --manifest-path ./src-tauri/Cargo.toml
}

build() {
  cd "${_realname}-app-v${pkgver}"

  # Use cached NodeJS to avoid N-API errors
  PATH=/C/hostedtoolcache/windows/node/22.21.1/x64:$PATH \
    yarn generate

  cargo tauri build --no-bundle
}

package() {
  cd "${_realname}-app-v${pkgver}"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path ./src-tauri \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
