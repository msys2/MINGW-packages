# Maintainer: Andrew Chadwick <a.t.chadwick@gmail.com>

_realname=mypaint
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=1.3.0alpha.d20161023.g427c7e5
pkgrel=1
provides=(
    "${MINGW_PACKAGE_PREFIX}-${_realname}"
)
conflicts=(
    "${MINGW_PACKAGE_PREFIX}-${_realname}"
)
arch=('any')
pkgdesc="Simple drawing & painting program that works well with Wacom-style graphics tablets (git)"
depends=(
    "${MINGW_PACKAGE_PREFIX}-libmypaint"
    "${MINGW_PACKAGE_PREFIX}-gtk3"
    "${MINGW_PACKAGE_PREFIX}-python2-numpy"
    "${MINGW_PACKAGE_PREFIX}-json-c"
    "${MINGW_PACKAGE_PREFIX}-lcms2"
    "${MINGW_PACKAGE_PREFIX}-python2-cairo"
    "${MINGW_PACKAGE_PREFIX}-python2-gobject"
    "${MINGW_PACKAGE_PREFIX}-adwaita-icon-theme"
    "${MINGW_PACKAGE_PREFIX}-librsvg"
    "${MINGW_PACKAGE_PREFIX}-gcc-libs"
    "${MINGW_PACKAGE_PREFIX}-gsettings-desktop-schemas"
    "${MINGW_PACKAGE_PREFIX}-hicolor-icon-theme"
)
makedepends=(
    "${MINGW_PACKAGE_PREFIX}-swig"
    "${MINGW_PACKAGE_PREFIX}-pkg-config"
    "${MINGW_PACKAGE_PREFIX}-pygobject-devel"
    "${MINGW_PACKAGE_PREFIX}-python2"
    "scons"
    "swig"
)
options=('!strip' 'debug' 'staticlibs')
license=("GPL2")
url="http://mypaint.org"
install=mypaint-${CARCH}.install
source=("${_realname}::git+https://github.com/mypaint/mypaint.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}"/${_realname}
    eval `python2 lib/meta.py`
    echo "$MYPAINT_VERSION_CEREMONIAL" \
      | sed 's,-\(alpha\),\1.'`date +d%Y%m%d`',' \
      | sed 's,-\(beta\|rc\),\1,' \
      | sed 's,+git[a-z]*\.\?,.g,' \
      | sed 's,-,_,g'
}

prepare() {
    cd "${srcdir}"/${_realname}
}

build() {
    cd "${srcdir}"
    [[ -d "build-${MINGW_CHOST}" ]] && rm -rf "build-${MINGW_CHOST}"
    cp -a "${_realname}" "build-${MINGW_CHOST}"
    cd "build-${MINGW_CHOST}"
    scons prefix="${MINGW_PREFIX}"
}

package() {
    cd "${srcdir}/build-${MINGW_CHOST}"
    echo "prefix: ${MINGW_PREFIX}"
    echo "sandbox: ${pkgdir}"
    echo "target: ${pkgdir}${MINGW_PREFIX}"
    scons prefix="${MINGW_PREFIX}" --install-sandbox="${pkgdir}" \
        "${pkgdir}${MINGW_PREFIX}"
    install -Dm644 COPYING \
        "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
    install -Dm644 Licenses.md \
        "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/Licenses.md"
    install -Dm644 Licenses.dep5 \
        "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/Licenses.dep5"
}
