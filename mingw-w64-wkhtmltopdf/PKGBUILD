# Maintainer: Diego Sogari <diego.sogari@gmail.com>

_realname=wkhtmltopdf
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
_basever=0.13
pkgver=0.13.r1049.51f9658
pkgrel=1
pkgdesc="Convert HTML to PDF using QtWebkit (mingw-w64)"
arch=('any')
url="http://wkhtmltopdf.org/"
license=('LGPLv3')
makedepends=("git" "${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-qt5"
         "${MINGW_PACKAGE_PREFIX}-qtwebkit")
source=("${_realname}::git+https://github.com/${_realname}/${_realname}.git#branch=merge-${_basever}"
        "${_realname}-${_basever}.patch")
sha256sums=('SKIP'
            '17315508eb1fb4ef0241556b06926a62bc4f2b681612b80df26535744a6f4ec3')

pkgver() {
  cd "${srcdir}/${_realname}"
  printf "%s.r%s.%s" "${_basever}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_realname}"
  patch -Np1 -i "../${_realname}-${_basever}.patch"
}

build() {
  rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  ln -fs `which windres` "./${MINGW_CHOST}-windres"
  PATH=`pwd`:$PATH
  export WKHTMLTOX_VERSION=${_basever}
  ${MINGW_PREFIX}/bin/qmake -spec win32-g++ "../${_realname}/${_realname}.pro"
  make release
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"

  install -D ./include/wkhtmltox/dllbegin.inc "${pkgdir}${MINGW_PREFIX}/include/wkhtmltox/dllbegin.inc"
  install -D ./include/wkhtmltox/dllend.inc "${pkgdir}${MINGW_PREFIX}/include/wkhtmltox/dllend.inc"
  install -D ./include/wkhtmltox/image.h "${pkgdir}${MINGW_PREFIX}/include/wkhtmltox/image.h"
  install -D ./include/wkhtmltox/pdf.h "${pkgdir}${MINGW_PREFIX}/include/wkhtmltox/pdf.h"

  install -d "${pkgdir}${MINGW_PREFIX}/bin"
  install "./bin/"*.dll "${pkgdir}${MINGW_PREFIX}/bin"
  install "./bin/"*.exe "${pkgdir}${MINGW_PREFIX}/bin"

  install -d "${pkgdir}${MINGW_PREFIX}/lib"
  install "./bin/"*.a "${pkgdir}${MINGW_PREFIX}/lib"
}
