# Maintainer: Martell Malone <martell malone at g mail dot com >

_realname=libwebsockets
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.1.0
pkgrel=1
arch=('any')
pkgdesc="A lightweight pure C library for websockets (mingw-w64)"
url='https://libwebsockets.org/'
license=('LGPL 2.1 - custom')
depends=("${MINGW_PACKAGE_PREFIX}-zlib" "${MINGW_PACKAGE_PREFIX}-openssl")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake" "${MINGW_PACKAGE_PREFIX}-gcc")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/warmcat/${_realname}/archive/v${pkgver}.tar.gz"
        001-posix-layout.patch
        002-bzero-declaration-conflict.patch)
options=(!strip !buildflags staticlibs)
sha256sums=('db948be74c78fc13f1f1a55e76707d7baae3a1c8f62b625f639e8f2736298324'
            '9bdfdffc74e348692b20b08881ec682ee7aa7ba65b370199dd52f99331013cdc'
            'a54ba2399e94f6d0f5ccdb846c83f0380db2e17b89dd1fe767dce4d7f662085c')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-posix-layout.patch
  patch -p1 -i ${srcdir}/002-bzero-declaration-conflict.patch
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DLWS_WITHOUT_DAEMONIZE=ON \
    -DLWS_WITHOUT_TESTAPPS=ON \
    -DCMAKE_BUILD_TYPE=RELEASE \
    ../${_realname}-${pkgver}

  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} -j1 install
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
