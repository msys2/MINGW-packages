# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=typstudio
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.1.0dev.33
_pkgver=${pkgver/dev/-dev}
pkgrel=1
pkgdesc="desktop application for typst (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64')
url="https://github.com/Cubxity/typstudio"
license=('spdx:GPL-3.0-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-yarn"
             "${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-cargo-tauri"
             "${MINGW_PACKAGE_PREFIX}-jq"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-gtk3"
             "${MINGW_PACKAGE_PREFIX}-libsoup"
             'moreutils')
source=("${url}/archive/v${_pkgver}/${_realname}-${_pkgver}.tar.gz")
sha256sums=('698583c106fc235194b760ca3adc5caea678aa932aabe718374862b50c4bc731')

prepare() {
  cp -r "${_realname}-${_pkgver}" "build-${MSYSTEM}" && cd "build-${MSYSTEM}"

  local conf=src-tauri/tauri.conf.json

  jq '.build.beforeBuildCommand |= "yarn run build"' $conf | sponge $conf
  jq '.tauri.bundle.active |= false' $conf | sponge $conf

  yarn install

  cargo fetch \
    --locked \
    --manifest-path src-tauri/Cargo.toml \
    --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "build-${MSYSTEM}"

  cargo-tauri build
}

package() {
  cd "build-${MSYSTEM}"

  install -Dm755 "src-tauri/target/release/${_realname}.exe" "${pkgdir}${MINGW_PREFIX}/bin/${_realname}.exe"
  install -Dm755 "src-tauri/target/release/WebView2Loader.dll" "${pkgdir}${MINGW_PREFIX}/bin/WebView2Loader.dll"
  install -Dm644 "COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
