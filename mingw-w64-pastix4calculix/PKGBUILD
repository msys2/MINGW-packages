# Maintainer: Rafal Brzegowy <rafal.brzegowy@yahoo.com>

_realname=pastix4calculix
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.17
pkgrel=1
pkgdesc='High performance parallel solver for very large sparse linear systems based on direct methods for CalucliX (mingw-w64)'
arch=('any')
depends=("${MINGW_PACKAGE_PREFIX}-hwloc"
         "${MINGW_PACKAGE_PREFIX}-metis"
         "${MINGW_PACKAGE_PREFIX}-openblas"
         "${MINGW_PACKAGE_PREFIX}-python2"
         "${MINGW_PACKAGE_PREFIX}-scotch"
	 "${MINGW_PACKAGE_PREFIX}-starpu")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
provides=("${MINGW_PACKAGE_PREFIX}-spm") # also provide the SpM library
conflicts=("${MINGW_PACKAGE_PREFIX}-pastix")
license=('CeCILL-C')
url="https://gitlab.inria.fr/solverstack/pastix"
source=("https://github.com/Kabbone/PaStiX4CalculiX/archive/${pkgver}_cudaless.tar.gz"
        "pastix_mingw.patch")
sha256sums=('3A6232CB48D8775A097C82F64A5365047CD947E7CBDD111BE3E56A66DB2EBACD'
            '38A9CD3B4B5717731F5E683C496A5341DE8A54E8F47DC3B880D2525DBCB078D5')

prepare() {
    cd "${_realname}-${pkgver}_cudaless"
    patch -Np1 < ../pastix_mingw.patch
}

build() {
  local _arch_opt=""
  if [ "${CARCH}" = "i686" ]; then
    _arch_opt="-DBUILD_64bits=OFF"
  fi

  #Static Build
  [[ -d "${srcdir}/build-${MINGW_CHOST}-static" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}-static"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}-static" && cd "${srcdir}/build-${MINGW_CHOST}-static"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -G"Ninja" \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DCMAKE_SYSTEM_IGNORE_PATH=/usr/lib \
      -DPASTIX_INT64=OFF \
      -DCMAKE_BUILD_TYPE=Release \
      -DPASTIX_WITH_PARSEC=OFF \
      -DPASTIX_WITH_STARPU=ON \
      -DBUILD_DOCUMENTATION=OFF \
      -DPASTIX_ORDERING_METIS=ON \
      -DPASTIX_BLEND_DEEPEST_DISTRIB=OFF \
      ${_arch_opt} \
      ../${_realname}-${pkgver}_cudaless

  ${MINGW_PREFIX}/bin/cmake --build .
  
  #Shared Build
  [[ -d "${srcdir}/build-${MINGW_CHOST}-shared" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}-shared"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}-shared" && cd "${srcdir}/build-${MINGW_CHOST}-shared"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -G"Ninja" \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DCMAKE_SYSTEM_IGNORE_PATH=/usr/lib \
      -DPASTIX_INT64=OFF \
      -DCMAKE_BUILD_TYPE=Release \
      -DPASTIX_WITH_PARSEC=OFF \
      -DPASTIX_WITH_STARPU=ON \
      -DBUILD_DOCUMENTATION=OFF \
      -DPASTIX_ORDERING_METIS=ON \
      -DBUILD_SHARED_LIBS=ON \
      -DCMAKE_C_FLAGS="-fopenmp" \
      ${_arch_opt} \
      ../${_realname}-${pkgver}_cudaless

  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  #Static Install
  cd "${srcdir}/build-${MINGW_CHOST}-static"
  DESTDIR=${pkgdir} cmake --build . --target install

  #Shared Install
  cd "${srcdir}/build-${MINGW_CHOST}-shared"
  DESTDIR=${pkgdir} cmake --build . --target install
  install -Dm644 ${srcdir}/${_realname}-${pkgver}_cudaless/LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE  
  
  #Remove unwanted examples files
  rm -rf ${pkgdir}${MINGW_PREFIX}/examples
}
