# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=hunspell-en
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2018.04.16
pkgrel=1
pkgdesc="Hunspell dictionaries (mingw-w64)"
arch=('any')
url="https://wordlist.sourceforge.io/"
license=('LGPL' 'custom:scowl')
makedepends=("unzip")
optdepends=(${MINGW_PACKAGE_PREFIX}-hunspell)
source=(http://downloads.sourceforge.net/project/wordlist/speller/$pkgver/hunspell-en_US-large-$pkgver.zip
        http://downloads.sourceforge.net/project/wordlist/speller/$pkgver/hunspell-en_AU-large-$pkgver.zip
        http://downloads.sourceforge.net/project/wordlist/speller/$pkgver/hunspell-en_CA-large-$pkgver.zip
        http://downloads.sourceforge.net/project/wordlist/speller/$pkgver/hunspell-en_GB-large-$pkgver.zip)
sha256sums=('0c27a253da79c72b59c1241f2a90c43ee0bf0f795d4e8fce62e5eeb8f093a9f6'
            'cfc023fba724760373b025c1fb2243d12644a3a3b160ce85966182791be18192'
            '0a3aeaca7a31c28c066c0cd499ed3e011d33582a606c51a645d19abd7def0088'
            'b19e51a95d85c103b545f9d9a621febfdd8cf706358138748038c4bd4b3f8df8')

build() {
  /bin/true
}

package() {
  cd "$srcdir"
  install -dm755 ${pkgdir}${MINGW_PREFIX}/share/hunspell
  install -m644 en_*.dic en_*.aff ${pkgdir}${MINGW_PREFIX}/share/hunspell

  pushd ${pkgdir}${MINGW_PREFIX}/share/hunspell/
    en_GB_aliases="en_AG en_BS en_BW en_BZ en_DK en_GH en_HK en_IE en_IN en_JM en_NA en_NG en_NZ en_SG en_TT en_ZA en_ZW"
    for lang in $en_GB_aliases; do
      ln -s en_GB-large.aff $lang.aff
      ln -s en_GB-large.dic $lang.dic
    done

    en_AU_aliases="en_AU"
    for lang in $en_AU_aliases; do
      ln -s en_AU-large.aff $lang.aff
      ln -s en_AU-large.dic $lang.dic
    done

    en_US_aliases="en_PH en_US"
    for lang in $en_US_aliases; do
      ln -s en_US-large.aff $lang.aff
      ln -s en_US-large.dic $lang.dic
    done
    larges="en_CA en_GB"
    for lang in $larges; do
      mv $lang-large.aff $lang.aff
      mv $lang-large.dic $lang.dic
    done
  popd

  # the symlinks
  install -dm755 ${pkgdir}${MINGW_PREFIX}/share/myspell/dicts
  pushd ${pkgdir}${MINGW_PREFIX}/share/myspell/dicts
    for file in ${pkgdir}${MINGW_PREFIX}/share/hunspell/*; do
      ln -sv ${pkgdir}${MINGW_PREFIX}/share/hunspell/$(basename $file) .
    done
  popd

  # docs
  install -dm755 ${pkgdir}${MINGW_PREFIX}/share/doc/$_realname
  install -m644 README_en_??-large.txt ${pkgdir}${MINGW_PREFIX}/share/doc/$_realname

  # licenses
  install -D -m644 README_en_AU-large.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname/Copyright_en_AU
  install -D -m644 README_en_US-large.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname/Copyright_en_US
  install -D -m644 README_en_CA-large.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname/Copyright_en_CA
  install -D -m644 README_en_GB-large.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname/Copyright_en_GB
}
