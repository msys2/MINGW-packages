# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=insight

_gcc_ver=4.8.2
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=7.7
pkgrel=1
pkgdesc="GNU Debugger with Tcl/Tk interface(mingw-w64)"
arch=('any')
url="https://www.gnu.org/software/gdb/"
license=('GPL')
provides=("${MINGW_PACKAGE_PREFIX}-gdb")
conflicts=("${MINGW_PACKAGE_PREFIX}-gdb")
groups=("${MINGW_PACKAGE_PREFIX}-toolchain")
depends=("${MINGW_PACKAGE_PREFIX}-libiconv"
    "${MINGW_PACKAGE_PREFIX}-zlib"
    "${MINGW_PACKAGE_PREFIX}-expat"
    "${MINGW_PACKAGE_PREFIX}-libiconv"
    "${MINGW_PACKAGE_PREFIX}-python2"
    "${MINGW_PACKAGE_PREFIX}-tk")
checkdepends=('dejagnu' 'bc')
makedepends=("${MINGW_PACKAGE_PREFIX}-libiconv" "${MINGW_PACKAGE_PREFIX}-zlib" "${MINGW_PACKAGE_PREFIX}-ncurses" "${MINGW_PACKAGE_PREFIX}-xz")
options=('staticlibs' '!distcc' '!ccache')
source=("ftp://mirrors.kernel.org/sources.redhat.com/gdb/releases/gdb-${pkgver}.tar.bz2"
    "insight.tar.bz2"
    'gdbinit'
    'gdb-fix-display-tabs-on-mingw.patch'
    'gdb-mingw-gcc-4.7.patch'
    'gdb-perfomance.patch'
    'gdb-fix-using-gnu-print.patch'
    'gdb-tcl-m4.patch'
    'gdb-fix-python-stack-corruption.patch'
    'insight-fix-headers.patch')
md5sums=('271a18f41858a7e98b28ae4eb91287c9'
         '49a94d459a88feda36538e563ed094f3'
         'dec2439447cb1accf037457b459093f7'
         'e48292d553379a9765c82e80e618ffd2'
         '7cc5df72d958ef3e4b1ffa9f7b069e6f'
         'e9b7c252fedfa950998f5f3b32536461'
         '7f24fa76a81f23f462789a50891d9c88'
         '3dbea59906882108f89b65f47e980605'
         '3e8c305a3855f01b1ebfd608f2378ffe'
         'c0b7612c2dd0d3673164f2b9faf97dd2')

prepare() {
  cd ${srcdir}
  cp -rf insight/* ${srcdir}/gdb-${pkgver}/
  cd ${srcdir}/gdb-${pkgver}

  # https://sourceware.org/ml/gdb-patches/2013-11/msg00224.html
  #patch -p1 -i ${srcdir}/gdb-fix-display-tabs-on-mingw.patch
  # https://sourceware.org/bugzilla/show_bug.cgi?id=15559
  patch -p1 -i ${srcdir}/gdb-mingw-gcc-4.7.patch
  # http://sourceware.org/bugzilla/show_bug.cgi?id=15412
  patch -p1 -i ${srcdir}/gdb-perfomance.patch

  patch -p1 -i ${srcdir}/gdb-fix-using-gnu-print.patch
  patch -p1 -i ${srcdir}/gdb-tcl-m4.patch
  patch -p1 -i ${srcdir}/gdb-fix-python-stack-corruption.patch
  patch -p1 -i ${srcdir}/insight-fix-headers.patch

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  cd libgui
  autoreconf
  cd ../gdb
  aclocal -Ignulib/import/m4
  autoconf
  cd gdbtk
  autoreconf
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}

  CFLAGS+=" -I${MINGW_PREFIX}/include/tcl8.6/tcl-private/win -I${MINGW_PREFIX}/include/tk8.6/tk-private/win -D__USE_MINGW_ANSI_STDIO=1"
  CXXFLAGS+=" -I${MINGW_PREFIX}/include/tcl8.6/tcl-private/win -I${MINGW_PREFIX}/include/tk8.6/tk-private/win -D__USE_MINGW_ANSI_STDIO=1"

  ${srcdir}/gdb-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --enable-targets="i686-w64-mingw32,x86_64-w64-mingw32" \
    --enable-64-bit-bfd \
    --disable-werror \
    --disable-win32-registry \
    --disable-rpath \
    --with-system-gdbinit=${MINGW_PREFIX}/etc/gdbinit \
    --with-python=${MINGW_PREFIX}/bin/python-config.sh \
    --with-tcl=${MINGW_PREFIX}/lib \
    --with-tk=${MINGW_PREFIX}/lib \
    --enable-gdbtk \
    --with-expat \
    --with-libiconv-prefix=${MINGW_PREFIX} \
    --with-zlib \
    --with-lzma \
    --disable-tui

  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install

  # Remove unwanted files
  rm -rf ${pkgdir}${MINGW_PREFIX}/share/{man,info}

  # install "custom" system gdbinit
  install -D -m644 ${srcdir}/gdbinit ${pkgdir}${MINGW_PREFIX}/etc/gdbinit
  sed -i 's|%GCC_NAME%|gcc-'${_gcc_ver}'|g' ${pkgdir}${MINGW_PREFIX}/etc/gdbinit

  rm -f ${pkgdir}${MINGW_PREFIX}/include/*.h
  rm -f ${pkgdir}${MINGW_PREFIX}/lib/*.a

}
