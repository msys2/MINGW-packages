# Maintainer: Philipp Smirnov <psmirnov@airmail.cc>

_realname=ada_spawn
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.r163.2642bd6
pkgrel=1
_branch=24.0

pkgdesc="Ada API to spawn processes and communicate with them (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
msys2_references=(
  'aur: ada_spawn'
)

url="https://github.com/AdaCore/spawn"
license=('Apache-2.0 WITH LLVM-exception')
makedepends=("${MINGW_PACKAGE_PREFIX}-gprbuild")
depends=("${MINGW_PACKAGE_PREFIX}-gtkada")
source=("${_realname}::git+https://github.com/AdaCore/spawn.git#branch=${_branch}"
        'fix-imports-in-test-projects.patch')
sha256sums=('SKIP'
            '4037b889e74743bb089cd2cdae87db8f4bafde820b0a0d001618b1e244419256')

pkgver() {
  cd ${_realname}
  printf "%s.r%s.%s" "$_branch" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

prepare() {
  cd "${srcdir}/${_realname}"

  # Workaround for https://github.com/AdaCore/spawn/issues/33
  patch -p0 -i ${srcdir}/fix-imports-in-test-projects.patch
}

build() {
  cd "${srcdir}/${_realname}"

  make all

  LIBRARY_TYPE=relocatable \
  gprbuild -P gnat/spawn_glib.gpr
}

# #
# # Failed for me:
# #  * raised PROGRAM_ERROR : wait_all.adb:17 finalize/adjust raised exception
# #  * raised CONSTRAINT_ERROR : spawn-internal-monitor__windows.adb:149 access check failed
# #
# check() {
#   cd "${srcdir}/${_realname}"
#   make check
# }

package() {
  cd "${srcdir}/${_realname}"
  make install PREFIX="${pkgdir}${MINGW_PREFIX}"

  # Copy License Files
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" \
    "${srcdir}/${_realname}"/LICENSE*

  LIBRARY_TYPE=relocatable \
  gprinstall -p --prefix="${pkgdir}${MINGW_PREFIX}" gnat/spawn_glib.gpr
}
