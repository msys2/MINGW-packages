_realname=ada_spawn
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.0
pkgrel=1

pkgdesc="Ada API to spawn processes and communicate with them (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
msys2_references=(
  'aur: ada_spawn'
)

url="https://github.com/AdaCore/spawn"
license=('spdx:Apache-2.0 WITH LLVM-exception')
makedepends=("${MINGW_PACKAGE_PREFIX}-gprbuild")
depends=("${MINGW_PACKAGE_PREFIX}-gtkada")
_repo_name=spawn
source=("https://github.com/AdaCore/${_repo_name}/archive/v${pkgver}/${_repo_name}-${pkgver}.tar.gz"
        'fix-imports-in-test-projects.patch')
sha256sums=('f59715132f12e39d691ddfad583bea19d6f0aa786208a8de027a9113d5849e3d'
            '1e3811aa5ef7f888c7b0dcee0d36dbbcea9b7e00f2380fd267d637728266ea93')

prepare() {
  cd "${srcdir}/${_repo_name}-${pkgver}"

  # Workaround for https://github.com/AdaCore/spawn/issues/33
  patch -p0 -i ${srcdir}/fix-imports-in-test-projects.patch
}

build() {
  cd "${srcdir}/${_repo_name}-${pkgver}"

  make all

  LIBRARY_TYPE=relocatable \
  gprbuild -P gnat/spawn_glib.gpr
}

# #
# # Failed for me:
# #  * raised PROGRAM_ERROR : wait_all.adb:17 finalize/adjust raised exception
# #  * raised CONSTRAINT_ERROR : spawn-internal-monitor__windows.adb:149 access check failed
# #
# check() {
#   cd "${srcdir}/${_repo_name}-${pkgver}"
#   make check
# }

package() {
  cd "${srcdir}/${_repo_name}-${pkgver}"
  make install PREFIX="${pkgdir}${MINGW_PREFIX}"

  # Copy License Files
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" \
    "${srcdir}/${_repo_name}-${pkgver}"/LICENSE*

  LIBRARY_TYPE=relocatable \
  gprinstall -p --prefix="${pkgdir}${MINGW_PREFIX}" gnat/spawn_glib.gpr
}
