_realname=pexports
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.47
pkgrel=1
pkgdesc="A program to extract exported symbols from a PE image (mingw-w64)"
arch=('any')
url="https://sourceforge.net/projects/mingw/files/MinGW/Extension/pexports/"
license=('GPL')
depends=()
makedepends=()
options=('!strip')
source=(https://sourceforge.net/projects/mingw/files/MinGW/Extension/pexports/${_realname}-${pkgver}/${_realname}-${pkgver}-mingw32-src.tar.xz)
sha256sums=('fe12853979bd7335aa771c934ddee913c987554f9dc11266262b7fd68e0aa884')

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX}

  make
}

package() {
  mkdir -p ${pkgdir}${MINGW_PREFIX}/bin
  cp ${srcdir}/build-${MINGW_CHOST}/pexports.exe ${pkgdir}${MINGW_PREFIX}/bin
  
  mkdir -p ${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/${pkgver}
  cp ${srcdir}/${_realname}-${pkgver}/{ChangeLog,COPYING,README} ${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/${pkgver}
}
