# Based on https://aur.archlinux.org/packages/mingw-w64-capnproto/
#
# Maintainer: Igor Matuszewski <xanewok@gmail.com>
# Contributors: Dave Reisner <dreisner@archlinux.org>
#               Matthias Blaicher <matthias@blaicher.com>
#               Severen Redwood <severen@shrike.me>
#               Igor Matuszewski <xanewok@gmail.com>

_realname=capnproto
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.7.0
pkgrel=1
pkgdesc="Cap'n Proto serialization/RPC system (mingw-w64)"
arch=('any')
url='https://capnproto.org/'
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-openssl")
source=("https://capnproto.org/capnproto-c++-${pkgver}.tar.gz"
        "0001-mingw-w64-capnproto-Define-_WIN32_WINNT-before-other.patch")
sha512sums=('9f8fb5753155798fcf9377a87f984a54d9fc5157c41aa11cd94108a773ca22d6e6952657e2d8079c9806f7de06f316c94957329fa52dbab6207aaa3b52348f04'
            '3b3fdbfeac4273bc776133eb5015a55c58c5d083aea0e200b72c8bc7d3cca8d85cac38f373a2707cedeb44316b5f1bc64a78f135f9cd9c2735a9ec623399d884')

prepare() {
  cd "${srcdir}/capnproto-c++-${pkgver}"

  patch -Np1 -i ${srcdir}/0001-*.patch
}

build() {
  [[ -d "${srcdir}/build-${CARCH}" ]] && rm -rf "${srcdir}/build-${CARCH}"
  mkdir -p "${srcdir}/build-${CARCH}" && cd "${srcdir}/build-${CARCH}"

  ${srcdir}/capnproto-c++-${pkgver}/configure \
    --disable-shared \
    --enable-static \
    --with-openssl

  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2:
