# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=spglib
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.7.0
pkgrel=2
pkgdesc="C library for finding and handling crystal symmetries (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://spglib.readthedocs.io/en/latest/'
msys2_repository_url="https://github.com/spglib/spglib"
msys2_references=(
  'archlinux: spglib'
)
license=('spdx:BSD-3-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-omp")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-fc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-pybind11"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-python-numpy"
             "${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-scikit-build-core"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools-scm")
optdepends=("${MINGW_PACKAGE_PREFIX}-python-numpy: Python bindings")
if [[ ${MINGW_PACKAGE_PREFIX} != *-clang-* ]]; then
  optdepends+=("${MINGW_PACKAGE_PREFIX}-gcc-lbgfortran: Fortran Interface")
fi
source=("https://github.com/spglib/spglib/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        001-no-duplicate-libs.patch
        002-fix-pkgconfig-file.patch)
sha256sums=('b22fc9abae9716c574fbc6d55cfc53ed654a714fccc5657a26ff5d18114bd8bd'
            '9cdf115eb93477ee5c5b88b11c0b309b7242e1910610c52f6ae0c9c6a01dd85a'
            'eb764f1f605ca7aca02b193e68e073b4d9212d39056bfc597bd4219af78f99a2')
noextract=("${_realname}-${pkgver}.tar.gz")

prepare() {
  echo "Extracting ${_realname}-${pkgver}.tar.gz ..."
  tar -xzf ${_realname}-${pkgver}.tar.gz || true

  cd "${srcdir}"/${_realname}-${pkgver}

  patch -p1 -i "${srcdir}"/001-no-duplicate-libs.patch
  patch -p1 -i "${srcdir}"/002-fix-pkgconfig-file.patch
}

build() {
  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  if [[ ${MSYSTEM} == CLANG64 ]]; then
    _extra_config+=("-DSPGLIB_WITH_Fortran=OFF")
    # flang/lib/Optimizer/CodeGen/Target.cpp:105: not yet implemented: returning BIND(C) derived type for this target
  else
    _extra_config+=("-DSPGLIB_WITH_Fortran=ON")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DCMAKE_DLL_NAME_WITH_SOVERSION=ON \
      "${_extra_config[@]}" \
      -DSPGLIB_SHARED_LIBS=ON \
      -DSPGLIB_INSTALL=ON \
      -DSPGLIB_USE_OMP=ON \
      -DSPGLIB_WITH_TESTS=OFF \
      -DSPGLIB_COMPILATION_WARNING=OFF \
      -S "${_realname}-${pkgver}" \
      -B "build-${MSYSTEM}"

  "${MINGW_PREFIX}"/bin/cmake.exe --build "build-${MSYSTEM}"

  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install "build-${MSYSTEM}"

  cd "${srcdir}"/python-build-${MSYSTEM}

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/COPYING \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
