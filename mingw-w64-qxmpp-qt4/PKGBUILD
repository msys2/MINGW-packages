# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: AlexWMF <alexxwmf@gmail.com>

_realname=qxmpp
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-qt4"
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.8.3
pkgrel=1
pkgdesc="Cross-platform C++ XMPP client and server library"
arch=('any')
license=("LGPL")
url="https://github.com/qxmpp-project/qxmpp"
depends=("${MINGW_PACKAGE_PREFIX}-qt4"
        "${MINGW_PACKAGE_PREFIX}-speex"
        "${MINGW_PACKAGE_PREFIX}-libtheora"
        "${MINGW_PACKAGE_PREFIX}-libvpx")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-make")
options=(!strip staticlibs)
source=("${_realname}-$pkgver".tar.gz::"https://github.com/qxmpp-project/${_realname}/archive/v${pkgver}.tar.gz")
md5sums=('e945edf90eafbc8fcaaa9dde1a7b357a')

build() {
  cd ${srcdir}/${_realname}-${pkgver}

  mkdir -p ${pkgdir}${MINGW_PREFIX}
  local PREFIX_WIN=$(cygpath -am ${pkgdir}${MINGW_PREFIX})

  ${MINGW_PREFIX}/bin/qmake \
    PREFIX=${PREFIX_WIN} \
    QXMPP_USE_SPEEX=1 \
    QXMPP_USE_THEORA=1 \
    QXMPP_USE_VPX=1 \
    "CONFIG+=release" \
    qxmpp.pro

  ${MINGW_PREFIX}/bin/mingw32-make # VERBOSE=1
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  ${MINGW_PREFIX}/bin/mingw32-make install
  mkdir -p ${pkgdir}${MINGW_PREFIX}/bin/
  mv "${pkgdir}${MINGW_PREFIX}"/lib/*.dll "${pkgdir}${MINGW_PREFIX}"/bin/

  local PREFIX_WIN=$(cygpath -am ${pkgdir}${MINGW_PREFIX})
  local PREFIX_DEP=$(cygpath -am ${MINGW_PREFIX})

  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/qxmpp.pc
  sed -s "s|${PREFIX_DEP}|${MINGW_PREFIX}|gi" \
    -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/qxmpp.pc
}
