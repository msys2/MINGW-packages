# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>
# Contributor: wirx6 <wirx654@gmail.com>

_realname=Vulkan-LoaderAndValidationLayers
pkgbase=mingw-w64-vulkan
pkgname=("${MINGW_PACKAGE_PREFIX}-vulkan")
conflicts=("${MINGW_PACKAGE_PREFIX}-vulkan-headers")
replaces=("${MINGW_PACKAGE_PREFIX}-vulkan-headers")
pkgver=1.0.46.0
pkgrel=1
pkgdesc='Khronos official ICD loader and validation layers for Vulkan (mingw-w64)'
arch=('any')
url="https://www.khronos.org/vulkan/"
groups=("${MINGW_PACKAGE_PREFIX}-vulkan-devel")
license=('Apache')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-glslang"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-spirv-tools")
source=(${_realname}-sdk-${pkgver}.tar.gzls::"https://github.com/KhronosGroup/${_realname}/archive/sdk-${pkgver}.tar.gz"
        build-fix.patch)
sha256sums=('439db567f03448f57d7899c4a78986669dc4d930af9699986a85bf2fc3d3acb8'
            '96a9fb5ce2165eb20a6094589eaa735f992f8f4fed6c562a2c8a612015f2d67d')

prepare() {
  cd ${srcdir}/${_realname}-sdk-${pkgver}
  patch -p1 -i ${srcdir}/build-fix.patch
}

build() {
  [[ -d ${srcdir}/build-${CARCH} ]] && rm -rf ${srcdir}/build-${CARCH}
  mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G'MSYS Makefiles' \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS} -fpermissive -D_WIN32_WINNT=0x0600" \
    -DBUILD_DEMOS=OFF \
    -DBUILD_TESTS=OFF \
    ../${_realname}-sdk-${pkgver}

  make -j1
}

package() {
  cd ${srcdir}/build-${CARCH}
  #make DESTDIR="${pkgdir}" install

  mkdir -p ${pkgdir}${MINGW_PREFIX}/{bin,lib,include}

  cd ${srcdir}/${_realname}-sdk-${pkgver}
  cp -rf include/* ${pkgdir}${MINGW_PREFIX}/include/
  install -Dm644 LICENSE.TXT ${pkgdir}${MINGW_PREFIX}/share/licenses/vulkan/LICENSE

  cd ${srcdir}/build-${CARCH}
  cp -rf */*.dll ${pkgdir}${MINGW_PREFIX}/bin/
  cp -rf */*.a ${pkgdir}${MINGW_PREFIX}/lib/
}
