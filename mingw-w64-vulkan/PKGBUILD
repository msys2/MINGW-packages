# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>
# Contributor: wirx6 <wirx654@gmail.com>

_realname=Vulkan-LoaderAndValidationLayers
pkgbase=mingw-w64-vulkan
pkgname=("${MINGW_PACKAGE_PREFIX}-vulkan")
conflicts=("${MINGW_PACKAGE_PREFIX}-vulkan-headers")
replaces=("${MINGW_PACKAGE_PREFIX}-vulkan-headers")
pkgver=1.0.51.0
pkgrel=1
pkgdesc='Khronos official ICD loader and validation layers for Vulkan (mingw-w64)'
arch=('any')
url="https://www.khronos.org/vulkan/"
groups=("${MINGW_PACKAGE_PREFIX}-vulkan-devel")
license=('Apache')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-glslang"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-spirv-tools")
source=(${_realname}-sdk-${pkgver}.tar.gz::"https://github.com/KhronosGroup/${_realname}/archive/sdk-${pkgver}.tar.gz"
        001-build-fix.patch
        002-proper-def-files-for-32bit.patch
        003-generate-pkgconfig-files.patch
        004-installation-commands.patch)
sha256sums=('20c07764e3f69d0d2158070a24faa6b8c5242af0bf8088ac538281c42c2ca119'
            '72cbf3860f93150c051043705961fca26230d1aa13f1419db842a4ae67a8375a'
            '3a0ce08d5b1b59a4661d770f6da71d1fb1f9805a3f5f670766c5202d1a50756c'
            '86b93e12a0817655699fea7e6faaee45c04d7c10c4df2e4db2098e18e00b5040'
            '3ff685bbeb4a888ab9c05012306bcccaf06629af436a53a5994ec7e5f902e26a')

prepare() {
  cd ${srcdir}/${_realname}-sdk-${pkgver}
  patch -p1 -i ${srcdir}/001-build-fix.patch
  patch -p1 -i ${srcdir}/002-proper-def-files-for-32bit.patch
  patch -p1 -i ${srcdir}/003-generate-pkgconfig-files.patch
  patch -p1 -i ${srcdir}/004-installation-commands.patch
}

build() {
  [[ -d ${srcdir}/build-${CARCH} ]] && rm -rf ${srcdir}/build-${CARCH}
  mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}

  CFLAGS+=" -D_WIN32_WINNT=0x0600 -D__STDC_FORMAT_MACROS"
  CPPFLAGS+=" -D_WIN32_WINNT=0x0600 -D__STDC_FORMAT_MACROS"
  CXXFLAGS+=" -D__USE_MINGW_ANSI_STDIO -D__STDC_FORMAT_MACROS -fpermissive -D_WIN32_WINNT=0x0600"
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DBUILD_DEMOS=OFF \
    -DBUILD_TESTS=OFF \
    ../${_realname}-sdk-${pkgver}

  make
}

package() {
  cd ${srcdir}/build-${CARCH}
  make DESTDIR="${pkgdir}" install

  install -Dm644 ${srcdir}/${_realname}-sdk-${pkgver}/LICENSE.TXT ${pkgdir}${MINGW_PREFIX}/share/licenses/vulkan/LICENSE
}
