# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=vulkan
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}-headers" "${MINGW_PACKAGE_PREFIX}-${_realname}-man-pages" "${MINGW_PACKAGE_PREFIX}-${_realname}-html-docs")
_pkgname=Vulkan-Docs
pkgver=1.0.38
_pkgver=${pkgver}-core
epoch=1
pkgrel=1
arch=('any')
url="https://www.khronos.org/vulkan/"
groups=("${MINGW_PACKAGE_PREFIX}-vulkan-devel")
makedepends=("asciidoc" "git" "python3" "${MINGW_PACKAGE_PREFIX}-cmake" "${MINGW_PACKAGE_PREFIX}-tinyxml2")
license=('custom')
source=("https://github.com/KhronosGroup/${_pkgname}/archive/v${_pkgver}.tar.gz"
        git+https://github.com/leethomason/tinyxml2.git#commit=c8dad95
        git+https://github.com/KhronosGroup/Vulkan-Hpp.git#commit=09e169c)
sha256sums=('65c7673ae10e4b96dabe77edb65fe4fbb4b0d98db79c9d21e0d7187bf325cf02'
            'SKIP'
            'SKIP')

prepare() {
  cd ${srcdir}/Vulkan-Hpp
  git submodule init
  git config submodule.tinyxml2.url $srcdir/tinyxml2
  git submodule update
}

build() {
  # we need this to build hpp headers
  cd ${srcdir}/Vulkan-Hpp

  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} 
  cp -rf ${srcdir}/Vulkan-Hpp/* ${srcdir}/build-${CARCH}
  cd "${srcdir}"/build-${CARCH}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -G'MSYS Makefiles' \
     ./ && make
  ./VulkanHppGenerator "${srcdir}/${_pkgname}-${pkgver}-core/src/spec/vk.xml"
  cd "${srcdir}/${_pkgname}-${pkgver}-core/doc/specs/vulkan"
  #./makeKHR manpages
  # Broken, see https://github.com/KhronosGroup/Vulkan-Docs/issues/367
  # workaround :( (yes, twice the same command)
  LANG="en_US.UTF8" make manpages
  LANG="en_US.UTF8" make manpages
  ./makeKHR chunked
}

package_vulkan-headers() {
  pkgdesc="Vulkan header files (mingw-w64)"
  cd "${srcdir}/${_pkgname}-${_pkgver/+/-}"
  install -dm755 "${pkgdir}${MINGW_PREFIX}/include/vulkan"
  install -dm755 "${pkgdir}${MINGW_PREFIX}/share/vulkan"
  install -dm755 "${pkgdir}${MINGW_PREFIX}/share/licenses/${pkgname}"

  install -m644 src/vulkan/vk_platform.h "${pkgdir}${MINGW_PREFIX}"/include/vulkan/
  install -m644 src/vulkan/vulkan.h "${pkgdir}${MINGW_PREFIX}"/include/vulkan

  install -m644 ${srcdir}/build-${CARCH}/vulkan/vulkan.hpp ${pkgdir}${MINGW_PREFIX}/include/vulkan
  install -m644 src/spec/vk.xml ${pkgdir}${MINGW_PREFIX}/share/vulkan

  install -m644 doc/specs/vulkan/copyright-ccby.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${pkgname}/copyright-ccby.txt
  install -m644 doc/specs/vulkan/copyright-spec.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${pkgname}/copyright-spec.txt
}

package_vulkan-man-pages() {
  pkgdesc="Vulkan man pages (mingw-w64)"
  cd "${srcdir}/${_pkgname}-${_pkgver/+/-}"
  install -dm755 "${pkgdir}${MINGW_PREFIX}/share/man/man3"
  install -dm755 "${pkgdir}${MINGW_PREFIX}/share/licenses/${pkgname}"

  install -m644 out/1.0/man/3/*.3 "${pkgdir}${MINGW_PREFIX}/share/man/man3/"

  install -m644 doc/specs/vulkan/copyright-ccby.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${pkgname}/copyright-ccby.txt
  install -m644 doc/specs/vulkan/copyright-spec.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${pkgname}/copyright-spec.txt
}

package_vulkan-html-docs() {
  pkgdesc="Vulkan html documentation (mingw-w64)"
  cd "${srcdir}/${_pkgname}-${pkgver}-core"
  install -dm755 "${pkgdir}${MINGW_PREFIX}/share/doc/vulkan"
  install -dm755 "${pkgdir}${MINGW_PREFIX}/share/licenses/${pkgname}"

  mv -v out/1.0/chunked/* "${pkgdir}${MINGW_PREFIX}/share/doc/vulkan/"

  install -m644 doc/specs/vulkan/copyright-ccby.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${pkgname}/copyright-ccby.txt
  install -m644 doc/specs/vulkan/copyright-spec.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${pkgname}/copyright-spec.txt
}

package_mingw-w64-x86_64-vulkan-headers() {
  package_vulkan-headers
}

package_mingw-w64-i686-vulkan-headers() {
  package_vulkan-headers
}

package_mingw-w64-x86_64-vulkan-man-pages() {
  package_vulkan-man-pages
}

package_mingw-w64-i686-vulkan-man-pages() {
  package_vulkan-man-pages
}

package_mingw-w64-x86_64-vulkan-html-docs() {
  package_vulkan-html-docs
}

package_mingw-w64-i686-vulkan-html-docs() {
  package_vulkan-html-docs
}
