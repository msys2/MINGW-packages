# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=pytest-xdist
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}"  "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=1.24.0
pkgrel=1
pkgdesc="py.test xdist plugin for distributed testing and loop-on-failing modes (mingw-w64)"
arch=('any')
url='https://bitbucket.org/pytest-dev/pytest-xdist'
validpgpkeys=('gpg_KEY')
makedepends=("${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-python3-pytest"
             "${MINGW_PACKAGE_PREFIX}-python2-pytest"
             "${MINGW_PACKAGE_PREFIX}-python3-pytest-forked"
             "${MINGW_PACKAGE_PREFIX}-python2-pytest-forked"
             "${MINGW_PACKAGE_PREFIX}-python3-execnet"
             "${MINGW_PACKAGE_PREFIX}-python2-execnet"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools-scm"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools-scm")
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-$pkgver.tar.gz"::"https://github.com/pytest-dev/pytest-xdist/archive/v$pkgver.tar.gz")
sha512sums=('90e86e09ae7ca6c0e8cab50cc72591f0d062b4457adddb3325ef10e5f899f160def60430b95aaf4efa84b12c403e063a1126cf231a576991c90b42ee04b3cbd4')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"
#  pushd "${_realname}-${pkgver}"
#    apply_patch_with_msg 0001-A-really-important-fix.patch \
#      0002-A-less-important-fix.patch
#  popd 
  for builddir in python{2,3}-build-${CARCH}; do
    rm -rf ${builddir} | true
    cp -r "${_realname}-${pkgver}" "${builddir}"
  done
  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

# Note that build() is sometimes skipped because it's done in 
# the packages setup.py install for simplicity if you can do so.
# but sometimes, you want to do a check before install which would
# also trigger the build.
build() {
  for pver in {2,3}; do  
    msg "Python ${pver} build for ${CARCH}"  
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py build
  done  
}

check() {
# Hack entry points by installing it
  local OLD_PYTHON_PATH=$PYTHONPATH
  msg "Python 2 test for ${CARCH}"
  cd "${srcdir}/python2-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python2 setup.py install --root="$PWD/tmp_install" --optimize=1 --skip-build
  _PATHONPATH1="${srcdir}/python2-build-${CARCH}/tmp_install${MINGW_PREFIX}/lib/python2.7/site-packages"
  _PATHONPATH2="${OLD_PYTHON_PATH};${srcdir}/python2-build-${CARCH}/tests"
  PYTHONPATH="${_PATHONPATH1};${_PATHONPATH2}" py.test2 || true

  msg "Python 3 test for ${CARCH}"
  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py install --root="$PWD/tmp_install" --optimize=1 --skip-build
  _PATHONPATH1="${srcdir}/python3-build-${CARCH}/tmp_install${MINGW_PREFIX}/lib/python3.7/site-packages"
  _PATHONPATH2="${OLD_PYTHON_PATH};${srcdir}/python3-build-${CARCH}/tests"
  PYTHONPATH="${_PATHONPATH1};${_PATHONPATH2}" py.test || true

}

package_python3-pytest-xdist() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3")

  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}

package_python2-pytest-xdist() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2")

  cd "${srcdir}/python2-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE"
}

package_mingw-w64-i686-python2-pytest-xdist() {
  package_python2-pytest-xdist
}

package_mingw-w64-i686-python3-pytest-xdist() {
  package_python3-pytest-xdist
}

package_mingw-w64-x86_64-python2-pytest-xdist() {
  package_python2-pytest-xdist
}

package_mingw-w64-x86_64-python3-pytest-xdist() {
  package_python3-pytest-xdist
}
