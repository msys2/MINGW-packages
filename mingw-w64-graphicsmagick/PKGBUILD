# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=graphicsmagick
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.3.19
pkgrel=1
pkgdesc="An image viewing/manipulation program (mingw-w64)"
arch=('any')
url="http://www.graphicsmagick.org/"
license=("custom")
makedepends=("${_mingw_suff}-gcc"
		"${_mingw_suff}-pkg-config"
		"${_mingw_suff}-libxml2"
		"${_mingw_suff}-jasper"
		"${_mingw_suff}-libpng"
		"${_mingw_suff}-libtiff"
		"${_mingw_suff}-libwebp")
depends=("${_mingw_suff}-libtool"
		"${_mingw_suff}-lcms2"
		"${_mingw_suff}-fontconfig"
		"${_mingw_suff}-freetype"
		"${_mingw_suff}-bzip2"
		"${_mingw_suff}-zlib"
		"${_mingw_suff}-fontconfig"
		"${_mingw_suff}-xz"
		"${_mingw_suff}-glib2"
		#"${_mingw_suff}-perl"
		)
optdepends=("${_mingw_suff}-ghostscript: for Ghostscript support"
			"${_mingw_suff}-libxml2: for XML support"
			"${_mingw_suff}-jasper: for JPEG-2000 support"
			"${_mingw_suff}-libpng: for PNG support"
			"${_mingw_suff}-libtiff: for PNG support"
			"${_mingw_suff}-libwebp: for WEBP support")
options=('staticlibs' 'strip')
source=(http://sourceforge.net/projects/graphicsmagick/files/${_realname}/${pkgver}/GraphicsMagick-${pkgver}.tar.xz)
md5sums=('e2795d7bdc2f3917804e40c8cae1993e')

prepare() {
	cd GraphicsMagick-${pkgver}
}

build() {
	export lt_cv_deplibs_check_method='pass_all'
	cd GraphicsMagick-${pkgver}
	[[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
	mkdir -p ${srcdir}/build-${MINGW_CHOST}
	cd ${srcdir}/build-${MINGW_CHOST}

	../GraphicsMagick-${pkgver}/configure \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--enable-shared \
		--disable-static \
		--with-modules \
		--with-wmf \
		--with-xml \
		--with-lcms2 \
		--with-jp2 \
		--with-webp \
		--with-gslib \
		--without-jbig \
		--without-fpx \
		--without-dps \
		--without-perl \
		--without-x
    make	
}

package() {
	cd ${srcdir}/build-${MINGW_CHOST}
	make -j1 DESTDIR="${pkgdir}" install
}
