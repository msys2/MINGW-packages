# Maintainer: luadebug lin@sz.cn.eu.org

_realname=libmem
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=5.1.0
pkgrel=1
pkgdesc='Advanced Game Hacking Library for C, Modern C++'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/rdbo/libmem/'
msys2_repository_url='https://github.com/rdbo/libmem/'
license=('spdx:AGPL-3.0-only')
depends=("${MINGW_PACKAGE_PREFIX}-capstone"
         "${MINGW_PACKAGE_PREFIX}-keystone")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-llvm")
source=("https://github.com/rdbo/libmem/archive/refs/tags/${pkgver}.tar.gz"
        "0002-debundle-dependencies.patch"
        "0003-add-pc-in.patch")
sha256sums=('9f61b53ce86fd59afb13bc4f48db40e8c8dc156f56879b9e9929014924f95495'
            '47b6c5da3ca8e56bb7842b46f63b15193a66d25f154e08628ce8b0faec1b7c9f'
            'd91b44a7e1cd92ba2d9deb24400719d9233a3cca39eeb8f9c42e7de77d14ea8e')
noextract=("${pkgver}.tar.gz")

prepare() {
  tar -xzf "${srcdir}/${pkgver}.tar.gz" -C "${srcdir}" --exclude="${_realname}-${pkgver}/compile_commands.json" || true
  cd "${srcdir}/${_realname}-${pkgver}"
  rm "PreLoad.cmake"
  patch -p1 -i "${srcdir}"/0002-debundle-dependencies.patch
  patch -p1 -i "${srcdir}"/0003-add-pc-in.patch
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}-shared" && cd "${srcdir}/build-${MSYSTEM}-shared"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  CFLAGS+=" -Wno-int-conversion -Wno-incompatible-pointer-types"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_DLL_NAME_WITH_SOVERSION=ON \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DLIBMEM_BUILD_STATIC=OFF \
      -DPACKAGE_VERSION="${pkgver}" \
      "${extra_config[@]}" \
      "../${_realname}-${pkgver}"

  "${MINGW_PREFIX}"/bin/cmake.exe --build .

  mkdir -p "${srcdir}/build-${MSYSTEM}-static" && cd "${srcdir}/build-${MSYSTEM}-static"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DLIBMEM_BUILD_STATIC=ON \
      -DPACKAGE_VERSION="${pkgver}" \
      "${extra_config[@]}" \
      "../${_realname}-${pkgver}"

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}-shared"
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .

  cd "${srcdir}/build-${MSYSTEM}-static"
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .
  
  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/LICENSE \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
