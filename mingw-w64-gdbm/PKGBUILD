# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gdbm
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.11
pkgrel=3
pkgdesc="GNU database library (mingw-w64)"
arch=('any')
url="https://www.gnu.org/software/gdbm/gdbm.html"
license=('GPL3')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gettext"
         "${MINGW_PACKAGE_PREFIX}-libiconv")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('staticlibs')
source=(https://ftp.gnu.org/gnu/gdbm/${_realname}-${pkgver}.tar.gz{,.sig}
        'gdbm-1.10-zeroheaders.patch'
        'gdbm-win32-support.patch')
sha256sums=('8d912f44f05d0b15a4a5d96a76f852e905d051bb88022fcdfd98b43be093e3c3'
            'SKIP'
            'ff33700f7919039037e5f56452c56ca00e968637f6f38485c9b8ae586770ee5a'
            'b4ed23df66b4f8ea08a9156dd2ccd1a03c3595e6aad8c236cc1809f618c22d3d')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  patch -p1 -i ${srcdir}/gdbm-1.10-zeroheaders.patch
  patch -p0 -i ${srcdir}/gdbm-win32-support.patch

  autoreconf --install --force
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --enable-static \
    --enable-shared \
    --disable-libgdbm-compat

  make
}

check() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make check
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install
}
