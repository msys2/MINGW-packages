# Contributor: dragon-archer <dragon-archer@outlook.com>

_realname=pypdfium2
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=5.4.0
pkgrel=1
pkgdesc='Python bindings to PDFium, reasonably cross-platform. (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'purl: pkg:pypi/pypdfium2'
)
url='https://github.com/pypdfium2-team/pypdfium2'
msys2_repository_url='https://github.com/pypdfium2-team/pypdfium2'
msys2_documentation_url='https://pypdfium2.readthedocs.io/'
license=('spdx:Apache-2.0 OR BSD-3-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-github-cli"
             "${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-packaging"
             "${MINGW_PACKAGE_PREFIX}-python-pypdfium2-ctypesgen"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools")
options=('!strip')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('7219e55048fb3999fc8adcaea467088507207df4676ff9e521a3ae15a67d99c4')

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  "${MINGW_PREFIX}"/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    "${MINGW_PREFIX}"/bin/python -m installer --prefix="${MINGW_PREFIX}" \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSES/Apache-2.0.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/Apache-2.0.txt"
  install -Dm644 LICENSES/BSD-3-Clause.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/BSD-3-Clause.txt"
  install -Dm644 LICENSES/CC-BY-4.0.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/CC-BY-4.0.txt"
}
