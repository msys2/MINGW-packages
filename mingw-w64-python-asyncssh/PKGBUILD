# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.su>

_realname=asyncssh
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=2.22.0
pkgrel=3
pkgdesc='Asynchronous SSHv2 client and server library (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://asyncssh.readthedocs.io/'
msys2_repository_url='https://github.com/ronf/asyncssh'
msys2_changelog_url='https://asyncssh.readthedocs.io/en/latest/changes.html'
msys2_references=(
  'archlinux: python-asyncssh'
  'gentoo: dev-python/asyncssh'
  'purl: pkg:pypi/asyncssh'
)
license=('spdx:EPL-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-cryptography"
         "${MINGW_PACKAGE_PREFIX}-python-typing_extensions")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools")
options=('!strip')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('c3ce72b01be4f97b40e62844dd384227e5ff5a401a3793007c42f86a5c8eb537')

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
