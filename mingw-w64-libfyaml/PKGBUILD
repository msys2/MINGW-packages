# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=libfyaml
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.9.5
pkgrel=1
pkgdesc='Fully feature complete YAML parser and emitter (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://pantoniou.github.io/libfyaml/'
msys2_repository_url='https://github.com/pantoniou/libfyaml'
license=("spdx:MIT")
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-ninja"
)
source=("https://github.com/pantoniou/libfyaml/releases/download/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "0001-disable-unused-unix-code.patch"
        "0002-dont-hardcode-dll-name.patch")
sha256sums=('9acbc9737808b2833a51be0d4b361987ab6bbbb19cbda7c0c5c83148c8addd8a'
            '014de7ad50a9a2758faea6cdc0fbd25d9745d0a180c3de388a3e3d953ae470a2'
            '17de3a01069087620b346d59a8353f9883a3ad84e7d460a360f874ef25de73a5')

prepare() {
  cd "${_realname}-${pkgver}"

  patch -p1 -i "${srcdir}/0001-disable-unused-unix-code.patch"
  patch -p1 -i "${srcdir}/0002-dont-hardcode-dll-name.patch"
}

build() {
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DCMAKE_DLL_NAME_WITH_SOVERSION=ON \
      -DBUILD_TESTING=OFF \
      -B "build-${MSYSTEM}" \
      -S "${_realname}-${pkgver}"

  cmake --build "build-${MSYSTEM}"
}

package() {
  DESTDIR="${pkgdir}" cmake --install "build-${MSYSTEM}"
  install -Dm644 "${_realname}-${pkgver}/LICENSE" -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
}
