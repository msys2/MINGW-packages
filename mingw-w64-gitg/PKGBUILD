# Maintainer: Ignacio Casal Quinteiro <icq@gnome.org>

_realname=gitg
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.2.7
pkgrel=1
arch=('any')
pkgdesc="git repository viewer for GTK+/GNOME (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-gtksourceview3"
         "${MINGW_PACKAGE_PREFIX}-libpeas"
         "${MINGW_PACKAGE_PREFIX}-enchant"
         "${MINGW_PACKAGE_PREFIX}-iso-codes"
         "${MINGW_PACKAGE_PREFIX}-python3-gobject"
         "${MINGW_PACKAGE_PREFIX}-gsettings-desktop-schemas"
         "${MINGW_PACKAGE_PREFIX}-libgit2-glib")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-vala"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             "intltool"
             "itstool"
             "gettext-devel")
options=('strip' 'staticlibs')
license=("GPL2+")
url="https://wiki.gnome.org/Apps/Gitg"
install=${_realname}-${CARCH}.install
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver:0:3}/${_realname}-$pkgver.tar.xz
        001-win.patch)
sha256sums=('c07fc4fc6cef31a06f0b9daa2ed23a5d0f6cb0a10a728324970675fc7001d095'
            'a765e59ba670ba2a6c2383e47beda69be848358700823b3f1c53323c9523365f')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  patch -b -V simple -p1 -i ${srcdir}/001-win.patch

  AUTOPOINT='intltoolize --automake --copy' autoreconf -f -i
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --disable-schemas-compile \
    --enable-glade-catalog
  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
