# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=allegro
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=5.0.10
pkgrel=1
pkgdesc="Portable library mainly aimed at video game and multimedia programming (mingw-w64)"
arch=(any)
url="http://alleg.sourceforge.net"
license=("custom")
makedepends=("${_mingw_suff}-gcc"
			"${_mingw_suff}-cmake"
			"${_mingw_suff}-freetype"
			"${_mingw_suff}-physfs"
			"${_mingw_suff}-libvorbis"
			"${_mingw_suff}-flac"
			"${_mingw_suff}-libpng"
			"${_mingw_suff}-openal"
			"${_mingw_suff}-dumb"
			"${_mingw_suff}-libjpeg-turbo")
depends=("${_mingw_suff}-crt")
optdepends=(
"${_mingw_suff}-dumb: allegro_audio"
"${_mingw_suff}-flac: allegro_audio"
"${_mingw_suff}-freetype: allegro_font"
"${_mingw_suff}-libjpeg-turbo: allegro_image"
"${_mingw_suff}-libpng: allegro_image"
"${_mingw_suff}-libvorbis: allegro_audio"
"${_mingw_suff}-openal: allegro_audio"
"${_mingw_suff}-physfs: allegro_physfs")
options=('strip' 'staticlibs')
source=("http://downloads.sourceforge.net/alleg/allegro-$pkgver.tar.gz")
conflicts=("${_mingw_suff}-allegro-static")
provides=("${_mingw_suff}-allegro-static")
sha256sums=('71b81080f34f6e485edd0c51f22923c18ff967d5db438e591e6f3885d5bdcda1')

prepare() {
	cd "$srcdir/allegro-$pkgver"
	sed -i "s,DS3DALG_DEFAULT,GUID_NULL,g" "addons/audio/dsound.cpp"
}

build() {
	mkdir -p ${pkgdir}${MINGW_PREFIX}
	pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
	export PREFIX_WIN=`pwd -W`
	popd > /dev/null
	
	pushd ${MINGW_PREFIX} > /dev/null
	export PREFIX_DEPS=`pwd -W`
	popd > /dev/null

	export DXSDK_DIR=${PREFIX_DEPS}/${MINGW_CHOST}
	mkdir "$srcdir/build-${MINGW_CHOST}" && cd "$srcdir/build-${MINGW_CHOST}"
	${MINGW_PREFIX}/bin/cmake \
		-G"MSYS Makefiles" \
		-DCMAKE_INSTALL_PREFIX:PATH=${PREFIX_WIN} \
		-DCMAKE_BUILD_TYPE=Release \
		-DWANT_DEMO=OFF \
		-DWANT_DOCS=OFF \
		-DWANT_EXAMPLES=OFF \
		-DINSTALL_PKG_CONFIG_FILES=ON \
		../${_realname}-${pkgver}
	make
}

package() {
    cd "$srcdir/build-${MINGW_CHOST}"
    make install
    find "${pkgdir}${MINGW_PREFIX}" -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
}