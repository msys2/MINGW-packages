# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=tinymist
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
# WARNING: Odd-numbered versions (e.g. 0.12.11) use not stable typst!
pkgver=0.14.0
pkgrel=1
pkgdesc="An integrated language service for Typst (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://myriad-dreamin.github.io/tinymist'
msys2_repository_url='https://github.com/Myriad-Dreamin/tinymist'
license=('spdx:Apache-2.0')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust" 'git')
source=("${msys2_repository_url}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('0a5dbd53e9bc3334b153f996d9fdafb52bed76fa59b27e759e9f9721dd490c1f')

prepare() {
  cd "${_realname}-${pkgver}"

  rm rust-toolchain.toml
  cargo fetch --locked \
    --config='net.git-fetch-with-cli=true' \
    --target "${RUST_CHOST}"
}

build() {
  cd "${_realname}-${pkgver}"

  cargo build --release --frozen -p tinymist-cli
}

check() {
  cd "${_realname}-${pkgver}"

  cargo test --release --frozen -p tinymist-cli
}

package() {
  cd "${_realname}-${pkgver}"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path crates/tinymist-cli \
    --root "${pkgdir}${MINGW_PREFIX}"

  local _target="target/release/tinymist completion"
  install -Dm644 <($_target bash) "${pkgdir}${MINGW_PREFIX}/share/bash-completion/completions/${_realname}"
  install -Dm644 <($_target fish) "${pkgdir}${MINGW_PREFIX}/share/fish/vendor_completions.d/${_realname}.fish"
  install -Dm644 <($_target zsh) "${pkgdir}${MINGW_PREFIX}/share/zsh/site-functions/_${_realname}"
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
