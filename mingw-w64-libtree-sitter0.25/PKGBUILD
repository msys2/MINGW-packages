# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>
# Contributor: Shiwei Wang <wsw0108@qq.com>

_realname=tree-sitter
pkgbase=mingw-w64-lib${_realname}0.25
pkgname=("${MINGW_PACKAGE_PREFIX}-lib${_realname}0.25-devel"
         "${MINGW_PACKAGE_PREFIX}-lib${_realname}0.25")
pkgver=0.25.10
pkgrel=1
pkgdesc="An incremental parsing system for programming tools (0.25.x version) (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://tree-sitter.github.io/"
msys2_repository_url="https://github.com/tree-sitter/tree-sitter"
msys2_references=(
  'archlinux: tree-sitter'
  'purl: pkg:cargo/tree-sitter'
)
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
checkdepends=('git')
source=("https://github.com/tree-sitter/tree-sitter/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('ad5040537537012b16ef6e1210a572b927c7cdc2b99d1ee88d44a7dcdc3ff44c')
noextract=("${_realname}-${pkgver}.tar.gz")

prepare() {
  tar -xzf "${_realname}-${pkgver}.tar.gz" || true
}

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DBUILD_SHARED_LIBS=ON \
      -DCMAKE_DLL_NAME_WITH_SOVERSION=ON \
      -S "${_realname}-${pkgver}"/lib \
      -B "build-${MSYSTEM}"

  cmake --build "build-${MSYSTEM}"
}

package_libtree-sitter0.25-devel() {
  pkgdesc+='(Development package)'
  depends=("${MINGW_PACKAGE_PREFIX}-libtree-sitter0.25")
  conflicts=("${MINGW_PACKAGE_PREFIX}-libtree-sitter")

  DESTDIR="${pkgdir}" cmake --install "build-${MSYSTEM}"
  mkdir -p dest-${MSYSTEM}-lib && mv "${pkgdir}${MINGW_PREFIX}"/bin/ dest-${MSYSTEM}-lib
}

package_libtree-sitter0.25() {
  mkdir -p "${pkgdir}${MINGW_PREFIX}"
  cp -a dest-${MSYSTEM}-lib/* "${pkgdir}${MINGW_PREFIX}"
  install -Dm644 "${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/lib${_realname}0.25/LICENSE"
}

# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
