# Maintainer: Antoine Martin <totaam@xpra.org>

_realname=lzo
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.14
pkgrel=1
pkgdesc="This package provides python bindings for the LZO compression library (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='https://github.com/jd-boyd/python-lzo'
license=('GPL2')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-lzo2")
#checkdepends=("${MINGW_PACKAGE_PREFIX}-python-nose")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-cc")
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/jd-boyd/python-lzo/archive/v${pkgver}.tar.gz")
sha512sums=('0a541c247fe86007662ca107889e7d84da0c33881d74078bab0c178c2de0d72d27bc8bc1907ac750efd61fa3fd0a09f05188b5fe1c17f5dacc3428a24379da46')

prepare() {
  cd "${srcdir}"

  rm -rf python-build-${CARCH} | true
  cp -r "python-${_realname}-${pkgver}" "python-build-${CARCH}"
  sed -i -e 's+lzo2.lib+lib/liblzo2.a+g' "python-build-${CARCH}/setup.py"
}

build() {
  msg "Python build for ${CARCH}"
  cd "${srcdir}/python-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  LZO_DIR="${MINGW_PREFIX}" \
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  LZO_DIR="${MINGW_PREFIX}" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}" --skip-build

  install -Dm644 COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
