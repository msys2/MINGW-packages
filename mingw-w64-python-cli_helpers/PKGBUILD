# Contributor: kreijstal <kreijstal@hotmail.com>

_realname=python-cli_helpers
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.3.0
pkgrel=1
pkgdesc='Python helpers for common CLI tasks (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/dbcli/cli_helpers'
license=('BSD')
depends=(
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-python-tabulate"
  "${MINGW_PACKAGE_PREFIX}-python-configobj"
)
makedepends=(
  'git'
  "${MINGW_PACKAGE_PREFIX}-python-build"
  "${MINGW_PACKAGE_PREFIX}-python-installer"
  "${MINGW_PACKAGE_PREFIX}-python-wheel"
  "${MINGW_PACKAGE_PREFIX}-python-setuptools"
)
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-pytest")
optdepends=("${MINGW_PACKAGE_PREFIX}-python-pygments: for styles extras")
_commit='58c7d280338748a6de6317a5e7d3104b6a27b8fb'
source=("$pkgname::git+https://github.com/dbcli/cli_helpers.git#commit=$_commit") # https://pypi.org/packages/source/c/cli-helpers/cli_helpers-2.3.0.tar.gz  look at the underscore -
b2sums=('SKIP')
sha512sums=('SKIP')

build() {
  cd "$pkgname"

  ${MINGW_PREFIX}/bin/python -m build --wheel --no-isolation --skip-dependency-check 
}

check() {
  cd "$pkgname"
  pytest -v \
    --deselect tests/tabular_output/test_tabulate_adapter.py::test_tabulate_wrapper || true
}

package() {
  cd "$pkgname"
  MSYS2_ARG_CONV_EXCL="--prefix=" ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} --destdir="$pkgdir" dist/*.whl
  # license
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
