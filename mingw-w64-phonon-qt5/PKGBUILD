# Maintainer (ArchLinux): Andrea Scarpino <andrea@archlinux.org>
# Maintainer (MSYS2): Mateusz Miku³a <mati865@gmail.com>

_realname=phonon
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-qt5"
pkgver=4.9.0
pkgrel=1
arch=('any')
url='http://phonon.kde.org/'
license=('LGPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake" "${MINGW_PACKAGE_PREFIX}-extra-cmake-modules" "${MINGW_PACKAGE_PREFIX}-qt5")
options=('!emptydirs')
source=("http://download.kde.org/stable/${_realname}/${pkgver}/${_realname}-${pkgver}.tar.xz"
        "fix-paths.patch")
sha256sums=('bb74b40f18ade1d9ab89ffcd7aeb7555be797ca395f1224c488b394da6deb0e0'
            '528e8bd38fb5f922e8a626b8e6b4c95c2ea0df8a89afe3ca9f894d0053d43bfd')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/fix-paths.patch
}

build() {
  [[ -d "build-${CARCH}" ]] && rm -rf "build-${CARCH}"
  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"
  
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=;-DECM_MKSPECS_INSTALL_DIR=" \
    cmake ../${_realname}-${pkgver} \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DPHONON_BUILD_PHONON4QT5=ON \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DPHONON_INSTALL_QT_EXTENSIONS_INTO_SYSTEM_QT=ON \
      -G'MSYS Makefiles'  
  make
}

package(){
  pkgdesc="The multimedia framework for KF5"
  depends=("${MINGW_PACKAGE_PREFIX}-qt5")

  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install
}
