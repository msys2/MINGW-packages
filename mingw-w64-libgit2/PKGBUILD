# Maintainer: Martell Malone <martellmalone@gmail.com>

_realname=libgit2
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-winhttp")
pkgver=1.3.0
pkgrel=1
pkgdesc='A linkable library for Git (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='https://github.com/libgit2/libgit2'
license=('GPL2' 'custom')
options=('strip')
depends=("${MINGW_PACKAGE_PREFIX}-http-parser"
         "${MINGW_PACKAGE_PREFIX}-libssh2"
         "${MINGW_PACKAGE_PREFIX}-zlib")
_openssl_depends=("${MINGW_PACKAGE_PREFIX}-openssl")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-ninja"
  "${MINGW_PACKAGE_PREFIX}-cc"
)
source=(${_realname}-${pkgver}.tar.gz::"https://github.com/libgit2/libgit2/archive/v${pkgver}.tar.gz"
        "0001-fix-libssh2-pc-dep.patch")
noextract=(${_realname}-${pkgver}.tar.gz)
sha256sums=('192eeff84596ff09efb6b01835a066f2df7cd7985e0991c79595688e6b36444e'
            '8b5354d4bfba6f1e5cd256f0182996862deb0a7b71ec62645072af6bd75ddc86')

prepare() {
  tar --exclude tests/resources/testrepo-worktree/link_to_new.txt -zxf ${_realname}-${pkgver}.tar.gz
  cd ${_realname}-${pkgver}

  patch -Np1 -i "${srcdir}/0001-fix-libssh2-pc-dep.patch"
}

do_build() {
  _variant=$1
  _destdir="${srcdir}/build-${MINGW_CHOST}"
  if [ "${_variant}" != "-openssl" ]; then
    _destdir="${_destdir}${_variant}"
  fi
  [[ -d "${_destdir}" ]] && rm -rf "${_destdir}"
  mkdir -p "${_destdir}" && cd "${_destdir}"

  local -a _variant_config
  if [ "${_variant}" = "-winhttp" ]; then
    _variant_config+=("-DWINHTTP=ON")
  elif [ "${_variant}" = "-openssl" ]; then
    _variant_config+=("-DWINHTTP=OFF")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DUSE_HTTP_PARSER=system \
    -DTHREADSAFE=ON \
    "${_variant_config[@]}" \
    ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake --build .
}

build() {
  do_build -openssl
  do_build -winhttp
}

check() {
  cd "${srcdir}"/build-${MINGW_CHOST}
  ${MINGW_PREFIX}/bin/ctest . || true

  cd "${srcdir}"/build-${MINGW_CHOST}-winhttp
  ${MINGW_PREFIX}/bin/ctest . || true
}

package_libgit2() {
  cd "${srcdir}"/build-${MINGW_CHOST}
  depends=("${depends[@]}"
           "${_openssl_depends[@]}")

  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --build . --target install
}

package_libgit2-winhttp() {
  cd "${srcdir}"/build-${MINGW_CHOST}-winhttp
  depends=("${depends[@]}")
  provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
  conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")

  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --build . --target install
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
