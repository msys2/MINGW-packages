# Maintainer: Martell Malone <martellmalone@gmail.com>

_realname=libgit2
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.21.5
pkgrel=1
pkgdesc='A linkable library for Git (mingw-w64)'
arch=('any')
url='https://github.com/libgit2/libgit2'
license=('GPL2' 'custom')
_pkgfqn=v${pkgver}.tar.gz
source=("https://github.com/libgit2/libgit2/archive/${_pkgfqn}"
        001-fix-pkg-config.patch)
md5sums=('6f1d64b2d50239f0c518cb45fbe3da4a'
         '341f5b8f48d1a489b157dc5300628960')
options=('strip')
depends=("${MINGW_PACKAGE_PREFIX}-http-parser"
        "${MINGW_PACKAGE_PREFIX}-libssh2"
        "${MINGW_PACKAGE_PREFIX}-openssl"
        "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake" 'p7zip')

noextract=(${_pkgfqn})

prepare() {
  cd ${srcdir}
  msg "Extracting ${_pkgfqn} due to git folder tag"
  7z x "$startdir/${_pkgfqn}" -so | 7z x -aoa -si -ttar -o"${srcdir}"
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-fix-pkg-config.patch
}

build() {
  mkdir -p ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}

  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DTHREADSAFE:BOOL=1 \
    -DCMAKE_INSTALL_PREFIX:PATH=${pkgdir}${MINGW_PREFIX} \
    ${srcdir}/${_realname}-${pkgver}

  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make install

  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/libgit2.pc
}
