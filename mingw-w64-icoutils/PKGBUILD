# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=icoutils
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.32.0
pkgrel=1
arch=('any')
pkgdesc="Create and extract MS Windows icons and cursors (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-libpng")
options=('strip' 'staticlibs')
license=("GPL 2")
url="http://www.nongnu.org/icoutils/"
source=(https://savannah.nongnu.org/download/${_realname}/${_realname}-${pkgver}.tar.bz2)
sha256sums=('17234d6e922f5dcd2dc8351a4b3535a2f348bb9b92c17ca3e438132147019ca6')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
}

build() {
  [[ -d "${srcdir}"/build-${MINGW_CHOST} ]] && rm -rf "${srcdir}"/build-${MINGW_CHOST}
  mkdir -p "${srcdir}"/build-${MINGW_CHOST} && cd "${srcdir}"/build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install
}
