# Maintainer: Andrew Sun <adsun701@gmail.com>

_realname=shine
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.1.1
pkgrel=2
pkgdesc="Super fast fixed-point MP3 encoder (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/savonet/shine"
msys2_repository_url='https://github.com/savonet/shine'
license=('spdx:LGPL-2.0-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc")
source=("https://github.com/toots/${_realname}/archive/${pkgver}/${_realname}-${pkgver}.tar.gz"
        "0001-shine-3.1.1-fix-include.patch")
noextract=("${_realname}-${pkgver}.tar.gz")
sha256sums=('565b87867d6f8e6616a236445d194e36f4daa9b4e7af823fcf5010af7610c49e'
            'bc4e9b812611c2a06c7fb58d5e284fa1978a97348c99db52d239edabca26e792')

prepare() {
  tar -xzf "${_realname}-${pkgver}.tar.gz" || true

  cd "$srcdir"/${_realname}-${pkgver}
  patch -Np1 -i "${srcdir}/0001-shine-3.1.1-fix-include.patch"
  ./bootstrap
}

build() {
  [[ -d "${srcdir}"/build-${MSYSTEM} ]] && rm -rf "${srcdir}"/build-${MSYSTEM}
  mkdir -p "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}
  cp -R "${srcdir}"/${_realname}-${pkgver}/* "${srcdir}"/build-${MSYSTEM}
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared

  make
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}
  make install DESTDIR="${pkgdir}"
}
