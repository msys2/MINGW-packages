# Maintainer: David SEGUELA <david.seguela@delair-tech.com>

_realname=zbar
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.23
pkgrel=1
pkgdesc="Application and library for reading bar codes from various sources (mingw-w64)"
arch=('any')
url="https://github.com/mchehab/zbar"
license=('LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-imagemagick")
source=(${_realname}-${pkgver}.tar.gz::https://github.com/mchehab/zbar/archive/${pkgver}.tar.gz
        "no-undefined.patch")
sha256sums=('a4fb80c05fb05af6490f62f62df7000b8287f6f27bf8b723080a5b57556f7911'
            'fd2b2e1d099e2100ab7374931715025c5ceedd338ba36c769bb622afda393e36')
options=('strip' 'staticlibs')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/no-undefined.patch
  autoreconf -fiv
}

build() {
  [[ -d ${srcdir}/build-${CARCH} ]] && rm -rf ${srcdir}/build-${CARCH}
  cp -rf ${srcdir}/${_realname}-${pkgver} ${srcdir}/build-${CARCH}
  cd ${srcdir}/build-${CARCH}
  ./configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST} \
      --without-qt \
      --without-gtk \
      --with-python=no \
      --disable-video
  make
}

package() {
  cd ${srcdir}/"build-"${CARCH}/
  make install DESTDIR="${pkgdir}"
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
