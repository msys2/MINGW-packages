# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=napi-rs-lzma
pkgbase=mingw-w64-nodejs-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-nodejs-${_realname}"
pkgver=1.2.1
pkgrel=1
pkgdesc='lzma-rs binding to Node.js via napi.rs (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url='https://napi.rs/'
msys2_repository_url='https://github.com/Brooooooklyn/lzma'
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-nodejs"
         "${MINGW_PACKAGE_PREFIX}-nodejs-napi-rs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-rust")
source=(https://github.com/Brooooooklyn/lzma/archive/refs/tags/v${pkgver}.tar.gz)
sha256sums=('b0e2a7ff5c0982740c611ceb48d2792892b505cbed706ebf64be0df41b7041be')

package() {
  cd ${srcdir}
  npm install -g @napi-rs/lzma@1.2.1 \
    --prefix "${pkgdir}${MINGW_PREFIX}"

  local _arch=x64
  if [[ ${CARCH} == aarch64 ]]; then
    _arch=arm64
  fi

  cd ${srcdir}/lzma-${pkgver}
  npm install --ignore-scripts
  npm run build --verbose

  install -Dm755 lzma.win32-${_arch}-gnu.node ${pkgdir}${MINGW_PREFIX}/lib/node_modules/@napi-rs/lzma/node_modules/@napi-rs/lzma-win32-${_arch}-msvc/lzma.win32-${_arch}-msvc.node
}
