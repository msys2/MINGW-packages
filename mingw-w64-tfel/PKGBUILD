# Maintainer: Rafal Brzegowy <rafal.brzegowy@yahoo.com>

_realname=tfel
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=3.3.0
pkgrel=1
pkgdesc='MFront: a code generation tool dedicated to material knowledge (mingw-w64)'
arch=('any')
provides=('mfront')
depends=("${MINGW_PACKAGE_PREFIX}-gnuplot")
makedepends=("unzip"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cmake")
#optdepends=("")
license=('GPL')
url='http://tfel.sourceforge.net'
source=(https://github.com/thelfer/${_realname}/archive/TFEL-${pkgver}.zip
	0000-fix-gcc-10.patch)
sha256sums=('FD591A74E1E943E6C307D822872E4F1588A7E7D70E573FDECB1F44B79A7BD737'
            '7E85A1F5BAE743C5DAF019051BF743C25CDC84F4BB5D97E22278D527E816B23A')
noextract=("TFEL-${pkgver}.zip") # bsdtar could not unpack this archive

prepare() {  
  rm -rf "${srcdir}/TFEL-${pkgver}r"
  unzip -oq TFEL-${pkgver}.zip -d ${srcdir}
  cd "${srcdir}/${_realname}-TFEL-${pkgver}"
  patch -p1 -i "${srcdir}/0000-fix-gcc-10.patch"
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -G'Ninja' \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DCMAKE_BUILD_TYPE=Release \
      -Denable-fortran=ON \
      -Denable-aster=ON \
      -Denable-abaqus=ON \
      -Denable-ansys=ON \
      -Denable-europlexus=ON \
      -Denable-zmat=ON \
      -Denable-cyrano=ON \
      -Denable-calculix=ON \
      -Denable-comsol=ON \
      -Denable-diana-fea=ON \
      -Denable-lsdyna=ON \
      -Denable-cxx-17=ON \
      -Denable-portable-build=ON \
      ../${_realname}-TFEL-${pkgver}
  ninja
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  DESTDIR=${pkgdir} ninja install
  install -Dm644 ${srcdir}/${_realname}-TFEL-${pkgver}/LICENCE-GNU-GPL ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}

# vim: ts=2 sw=2 et:
