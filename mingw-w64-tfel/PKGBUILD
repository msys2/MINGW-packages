# Maintainer: Rafa≈Ç Brzegowy <rafal.brzegowy@yahoo.com>

_realname=tfel
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=5.1.0
pkgrel=2
pkgdesc='MFront: a code generation tool dedicated to material knowledge (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://thelfer.github.io/tfel/web/index.html'
msys2_repository_url='https://github.com/thelfer/tfel'
license=('spdx:GPL-3.0-or-later OR CECILL-2.1')
provides=("${MINGW_PACKAGE_PREFIX}-mfront")
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-gnuplot")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-fc")
source=(${msys2_repository_url}/archive/TFEL-${pkgver}/${_realname}-TFEL-${pkgver}.tar.gz)
sha256sums=('1afd98200de332e97e86d109ce0e1aaa8f18cc6c6c81daec3218809509cdfad7')
noextract=("${_realname}-TFEL-${pkgver}.tar.gz")

prepare() {
  tar -xf "${_realname}-TFEL-${pkgver}.tar.gz" || true
}

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -Denable-python=OFF \
      -Denable-python-bindings=OFF \
      -Denable-aster=ON \
      -Denable-abaqus=ON \
      -Denable-ansys=ON \
      -Denable-europlexus=ON \
      -Denable-zmat=ON \
      -Denable-cyrano=ON \
      -Denable-calculix=ON \
      -Denable-comsol=ON \
      -Denable-diana-fea=ON \
      -Denable-lsdyna=ON \
      -Denable-cxx-17=ON \
      -Denable-fortran=ON \
      -Denable-portable-build=ON \
      -Denable-reference-doc=OFF \
      "${extra_config[@]}" \
      -S ${_realname}-TFEL-${pkgver} \
      -B build-${MSYSTEM}

  ${MINGW_PREFIX}/bin/cmake --build build-${MSYSTEM}
}

package() {
  DESTDIR="${pkgdir}" cmake --install build-${MSYSTEM}

  install -Dm644 "${srcdir}"/${_realname}-TFEL-${pkgver}/LICENCE-GNU-GPL \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE-GNU-GPL
  install -Dm644 "${srcdir}"/${_realname}-TFEL-${pkgver}/LICENCE-CECILL-A-EN \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE-CECILL-A-EN
}

# vim: ts=2 sw=2 et:
