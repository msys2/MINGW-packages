# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=libimagequant
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.10.2
pkgrel=1
pkgdesc="Palette quantization library (mingw-w64)"
arch=('any')
url='https://github.com/ImageOptim/libimagequant'
license=("GPL3" "MIT")
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/ImageOptim/${_realname}/archive/${pkgver}.tar.gz"
        "2.10.0-fix-configure.patch")
sha256sums=('c7a70caa87a72095f44d5b0e53737a1cb42c0b12ec599b42982224816f45abeb'
            '6879b967137bd7e5f1b80c387e03981d0571e7dcf2e659e23f4f371786c42397')

prepare() {
  cd $srcdir/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/2.10.0-fix-configure.patch
}

build() {
  cd "$srcdir"/${_realname}-${pkgver}
  rm -rf "${srcdir}"/build-${CARCH} || true
  cp -r "${srcdir}/${_realname}-${pkgver}" "${srcdir}/build-${CARCH}"
  cd "${srcdir}"/build-${CARCH}
  msg2 "PWD $(pwd)"
  if [ "${CARCH}" = "i686" ]; then
    SSE="--enable-sse"
  else
    SSE="--disable-sse"
  fi
  ./configure --prefix=${MINGW_PREFIX} \
    CC="${MINGW_PREFIX}/bin/gcc" \
    ${SSE}  --with-openmp=static
  make --jobs=1 static
}

package() {
  cd "${srcdir}"/build-${CARCH}
   mkdir -p ${pkgdir}${MINGW_PREFIX}/lib
   mkdir -p ${pkgdir}${MINGW_PREFIX}/include/libimagequant
   mkdir -p ${pkgdir}${MINGW_PREFIX}/share/doc/libimagequant
   mkdir -p ${pkgdir}${MINGW_PREFIX}/share/licenses/libimagequant
   install -m 0644 -p libimagequant.a \
     ${pkgdir}${MINGW_PREFIX}/lib/libimagequant.a
   for _file in *.h
   do
     install -m 0644 -p ${_file} \
        ${pkgdir}${MINGW_PREFIX}/include/libimagequant/${_file}
   done
   install -m 0644 -p CHANGELOG \
      ${pkgdir}${MINGW_PREFIX}/share/doc/libimagequant/CHANGELOG
   install -m 0644 -p README.md \
      ${pkgdir}${MINGW_PREFIX}/share/doc/libimagequant/README.md
   install -m 0644 -p COPYRIGHT \
       ${pkgdir}${MINGW_PREFIX}/share/licenses/libimagequant/COPYRIGHT
}
