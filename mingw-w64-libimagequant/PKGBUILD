# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=libimagequant
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.8.2
pkgrel=1
pkgdesc="Palette quantization library (mingw-w64)"
arch=('any')
url='https://github.com/ImageOptim/libimagequant'
license=("GPL3" "MIT")
source=("https://github.com/ImageOptim/${_realname}/archive/${pkgver}.tar.gz"
"0001-min-build-fix.patch")
sha256sums=('0c1532456f273cc759932b3869e76161ac87b1fcbbd525c2478b0d0262a98d5f'
            '9e8833679949ec2e51d9ccdc7005262898de02ba4a8f5f8463e07501c6878447')

prepare() {
  cd $srcdir/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-min-build-fix.patch
}

build() {
  cd "$srcdir"/${_realname}-${pkgver}
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  cp -r "${srcdir}/${_realname}-${pkgver}" "${srcdir}/build-${CARCH}" && \
    cd "${srcdir}"/build-${CARCH}
  if [ "${CARCH}" = "i686" ]; then
    SSE="--disable-sse"
  else
    SSE="--enable-sse"
  fi
  ./configure --prefix=${MINGW_PREFIX} \
    CC="${CARCH}-w64-mingw32-gcc.exe" \
    ${SSE} \
    --extra-cflags="${CFLAGS}" \
    --extra-ldflags="${LDFLAGS}"
  make dll shared static
}

package() {
  cd "${srcdir}"/build-${CARCH}
   mkdir -p ${pkgdir}${MINGW_PREFIX}/bin
   mkdir -p ${pkgdir}${MINGW_PREFIX}/lib
   mkdir -p ${pkgdir}${MINGW_PREFIX}/include/libimagequant
   mkdir -p ${pkgdir}${MINGW_PREFIX}/share/doc/libimagequant
   mkdir -p ${pkgdir}${MINGW_PREFIX}/share/licenses/libimagequant
   mv libimagequant_dll.a libimagequant.dll.a
   install -m 0644 -p libimagequant.dll \
     ${pkgdir}${MINGW_PREFIX}/bin/libimagequant.dll
   install -m 0644 -p libimagequant.dll.a \
     ${pkgdir}${MINGW_PREFIX}/lib/libimagequant.dll.a
   install -m 0644 -p libimagequant.a \
     ${pkgdir}${MINGW_PREFIX}/lib/libimagequ.a
   for _file in *.h
   do
     install -m 0644 -p ${_file} \
        ${pkgdir}${MINGW_PREFIX}/include/libimagequant/${_file}
   done
   install -m 0644 -p CHANGELOG \
      ${pkgdir}${MINGW_PREFIX}/share/doc/libimagequant/CHANGELOG
   install -m 0644 -p README.md \
      ${pkgdir}${MINGW_PREFIX}/share/doc/libimagequant/README.md
   install -m 0644 -p COPYRIGHT \
       ${pkgdir}${MINGW_PREFIX}/share/licenses/libimagequant/COPYRIGHT
}

