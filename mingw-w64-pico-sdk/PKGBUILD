# Contributer: thewh1teagle
_realname=pico-sdk
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.5.1
pkgrel=1

pkgdesc="Libraries and tools for C/C++ development on RP2040 microcontrollers."
arch=("any")
mingw_arch=("mingw64" "ucrt64" "clang64")
url="https://github.com/raspberrypi/pico-sdk"
license=("BSD-3-Clause")

depends=("${MINGW_PACKAGE_PREFIX}-arm-none-eabi-binutils"
    "${MINGW_PACKAGE_PREFIX}-arm-none-eabi-gcc"
    "${MINGW_PACKAGE_PREFIX}-arm-none-eabi-newlib"
    "${MINGW_PACKAGE_PREFIX}-cmake"
)
makedepends=("git")
optdepends=("${MINGW_PACKAGE_PREFIX}-arm-none-eabi-gdb: Debugger")

options=("!strip")
install=${_realname}-${MSYSTEM}.install
source=("git+${url}.git#tag=${pkgver}")
sha256sums=("SKIP")

prepare() {
    cd "${srcdir}/${_realname}"
    git submodule update --init --depth=1
}

package() {
    # Install sdk
    mkdir -p "${pkgdir}${MINGW_PREFIX}/share"
    cp -r "${srcdir}/pico-sdk/" "${pkgdir}${MINGW_PREFIX}/share/${pkgname}"
    rm -rf "${pkgdir}${MINGW_PREFIX}/share/${pkgname}"/{.git*,README.md,CONTRIBUTING.md,LICENSE.TXT}

    # install profile script
    mkdir -p "${pkgdir}${MINGW_PREFIX}"/etc/profile.d
    echo "export PICO_SDK_PATH=${pkgdir}${MINGW_PREFIX}/share/${pkgname}" >"${pkgdir}${MINGW_PREFIX}"/etc/profile.d/${_realname}.sh

    # Install docs
    install -Dm644 "${srcdir}/pico-sdk/README.md" -t "${pkgdir}${MINGW_PREFIX}/share/doc/${pkgname}"

    # Install license
    install -Dm644 "${srcdir}/pico-sdk/LICENSE.TXT" -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${pkgname}"
}
