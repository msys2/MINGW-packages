# Contributer: thewh1teagle
_realname=pico-sdk
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.5.1
pkgrel=1
pkgdesc="Libraries for C/C++ development on RP2040 microcontrollers."
arch=("any")
mingw_arch=("mingw64" "ucrt64" "clang64")
url="https://github.com/raspberrypi/pico-sdk"
license=("BSD-3-Clause")
makedepends=("git")
options=("!strip")
install=${_realname}-${MSYSTEM}.install
source=("git+${url}.git#tag=${pkgver}")
sha256sums=("SKIP")

prepare() {
    cd "${srcdir}/${_realname}"
    git submodule update --init --depth=1
}

package() {
    # Install docs
    install -Dm644 "${srcdir}/${_realname}/README.md" -t "${pkgdir}/share/doc/${pkgname}"

    # Install license
    install -Dm644 "${srcdir}/${_realname}/LICENSE.TXT" -t "${pkgdir}/share/licenses/${pkgname}"

    # Clean SDK
    rm -rf "${srcdir}/${_realname}/"{.git*,README.md,CONTRIBUTING.md,LICENSE.TXT}
    
    # Install SDK files
    cp -r "${srcdir}/${_realname}" "${pkgdir}/share/${pkgname}"

    # Install profile script
    mkdir -p "${pkgdir}/etc/profile.d"
    echo "export PICO_SDK_PATH=/share/${pkgname}" > "${pkgdir}/etc/profile.d/${_realname}.sh"
}