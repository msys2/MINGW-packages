# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=meld
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}3"
pkgver=3.11.0
pkgrel=3
pkgdesc="Visual diff and merge tool (mingw-w64)"
arch=('any')
url="http://meldmerge.org/"
license=("GPL" "LGPL")
options=('strip' 'staticlibs')
depends=(
		"${_mingw_suff}-gtk3"
		"${_mingw_suff}-libxml2"
		"${_mingw_suff}-python2-gobject"
		"${_mingw_suff}-gtksourceview3"
		"${_mingw_suff}-hicolor-icon-theme"
		"${_mingw_suff}-gsettings-desktop-schemas"
		)
makedepends=("${_mingw_suff}-gcc"
		"${_mingw_suff}-pkg-config"
		'intltool'
		'itstool')
install=meld-${CARCH}.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${_realname}/${pkgver%.*}/${_realname}-$pkgver.tar.xz
        '0001-fixes-for-MinGW-w64-symlinks-and-use-sh-to-spawn.patch')
md5sums=('795fb159d0842c6ff237856b98ad7642'
         'e388ea44b5ceebb9c327f59fb12a2761')

prepare() {
	cd "${srcdir}/${_realname}-${pkgver}"
	patch -p1 -i "${srcdir}/0001-fixes-for-MinGW-w64-symlinks-and-use-sh-to-spawn.patch"
	sed -i 's/env python/env python2/' bin/meld
}

build() {
    cd "${srcdir}/${_realname}-${pkgver}"
	
	#export PYTHON=${MINGW_PREFIX}/bin/python2
	DISTUTILS_DEBUG=1 ${MINGW_PREFIX}/bin/python2 setup.py build 
	
}

package() {
    cd "${srcdir}/${_realname}-${pkgver}"

    MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
	${MINGW_PREFIX}/bin/python2 setup.py install --root=${pkgdir} --prefix=${MINGW_PREFIX}
	
	# Missing files; required
	install -d "${pkgdir}${MINGW_PREFIX}"/share/{glib-2.0/schemas,icons}
	cp -r data/icons/hicolor data/icons/HighContrast "${pkgdir}${MINGW_PREFIX}/share/icons"
	cp data/org.gnome.meld.gschema.xml "${pkgdir}${MINGW_PREFIX}/share/glib-2.0/schemas"
}
