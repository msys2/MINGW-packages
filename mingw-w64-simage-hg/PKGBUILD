# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=simage
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}-hg"
pkgver=r671.2dab4dadbb2e
pkgrel=1
pkgdesc="Provides support for loading and saving images, sound and video (mingw-w64)"
arch=('any')
license=('BSD')
depends=("${_mingw_suff}-libjpeg-turbo"
    "${_mingw_suff}-libpng"
    "${_mingw_suff}-libsndfile"
    "${_mingw_suff}-libtiff"
    "${_mingw_suff}-giflib"
    "${_mingw_suff}-libvorbis"
    "${_mingw_suff}-jasper"
    "${_mingw_suff}-zlib")
makedepends=("mercurial" "${_mingw_suff}-cmake" "${_mingw_suff}-gcc")
provides=("${_mingw_suff}-${_realname}")
conflicts=("${_mingw_suff}-${_realname}")
url=('https://bitbucket.org/Coin3D/simage/overview')
source=("${_realname}"::"hg+https://bitbucket.org/Alexpux/simage")
options=(!strip staticlibs)
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_realname"
  printf "r%s.%s" "$(hg identify -n)" "$(hg identify -i)"
}

prepare() {
  cd ${srcdir}/${_realname}
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
  ../"${_realname}"/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --disable-static \
    --enable-optimization \
    --disable-html \
    --disable-debug \
    --with-targa \
    --with-rgb \
    --with-xwd \
    --with-eps \
    --with-gif=${MINGW_PREFIX} \
    --with-jpeg=${MINGW_PREFIX} \
    --with-zlib=${MINGW_PREFIX} \
    --with-png=${MINGW_PREFIX} \
    --with-tiff=${MINGW_PREFIX} \
    --with-jasper=${MINGW_PREFIX} \
    --with-oggvorbis=${MINGW_PREFIX} \
    --with-libsndfile=${MINGW_PREFIX} \
    --without-x \
    --without-gdiplus \
    --without-doxygen
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install
}
