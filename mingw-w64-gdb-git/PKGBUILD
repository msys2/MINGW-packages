# Maintainer: Ray Donnelly <mingw.android@gmail.com>
# Contributor: Alexey Pavlov <alexpux@gmail.com>

_realname=gdb
_gcc_ver=5.2.0
pkgbase=mingw-w64-${_realname}
_base_ver=7.10
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}=${_base_ver}")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}=${pkgver}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=7.10.r85475.f5cc5a5
pkgrel=1
pkgdesc="GNU Debugger (mingw-w64)"
arch=('any')
url="https://www.gnu.org/software/gdb/"
license=('GPL')
#groups=("${MINGW_PACKAGE_PREFIX}-toolchain")
depends=("${MINGW_PACKAGE_PREFIX}-expat"
         "${MINGW_PACKAGE_PREFIX}-libiconv"
         "${MINGW_PACKAGE_PREFIX}-python2"
         "${MINGW_PACKAGE_PREFIX}-zlib")
#checkdepends=('dejagnu' 'bc')
makedepends=("${MINGW_PACKAGE_PREFIX}-iconv"
             "${MINGW_PACKAGE_PREFIX}-ncurses"
             "${MINGW_PACKAGE_PREFIX}-xz"
             "git")
options=('staticlibs' '!distcc' '!ccache' 'debug' '!strip')
source=("${_realname}"::"git://sourceware.org/git/binutils-gdb.git"
        gdbinit
        0001-MinGW-w64-Two-fixes-for-unusual-files.patch
        0002-MinGW-w64-Fix-libiberty-makefile.patch
        0003-MinGW-w64-Fix-libibery-configure.patch
        0004-MinGW-w64-Use-gnu-printf.patch
        0005-GDB-performance.patch)
md5sums=('SKIP'
         'be1c16674d98189a6f18dc0f5922c739'
         '8f21d3c0c2d07a8e7717625fca4410ab'
         '52b62ba647ee0f27d70bcfa6c7bac5b9'
         '6c854b3f155f82c14048338b250d7c63'
         'f614db2fdc6c6bf06a0797f395a847bc'
         '4e9d7f210eaa3c50b6bfd2f941454f4f')

pkgver() {
  cd "$srcdir/$_realname"
  printf "%s.r%s.%s" "${_base_ver}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${srcdir}/${_realname}
  git am "${srcdir}"/0001-MinGW-w64-Two-fixes-for-unusual-files.patch
# Does not apply currently; probably want to sync libiberty across from GCC.
# git am "${srcdir}"/0002-MinGW-w64-Fix-libiberty-makefile.patch
  git am "${srcdir}"/0003-MinGW-w64-Fix-libibery-configure.patch
  git am "${srcdir}"/0004-MinGW-w64-Use-gnu-printf.patch
  git am "${srcdir}"/0005-GDB-performance.patch
}

build() {
  declare -a _extra_conf
  if [ "${CARCH}" = "x86_64" ]; then
    _extra_conf+=(--enable-64-bit-bfd)
  else
    LDFLAGS+=" -Wl,--large-address-aware"
  fi

  if check_option "debug" "y"; then
    CFLAGS+=" -O0"
  fi

  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}

  mkdir ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
  ../${_realname}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --enable-targets="i686-w64-mingw32,x86_64-w64-mingw32" \
    "${_extra_conf[@]}" \
    --disable-werror \
    --disable-win32-registry \
    --disable-rpath \
    --with-system-gdbinit=${MINGW_PREFIX}/etc/gdbinit \
    --with-python=${MINGW_PREFIX}/bin/python-config-u.sh \
    --with-expat \
    --with-libiconv-prefix=${MINGW_PREFIX} \
    --with-zlib \
    --with-lzma \
    --disable-tui

  make all-gdb
}

# check() {
#   cd "${srcdir}"/build-${MINGW_CHOST}
#
#   # unset LDFLAGS as testsuite makes assumptions about which ones are active
#   # do not abort on errors - manually check log files
#   make LDFLAGS="" -k check || true
# }

package() {
  cd "${srcdir}"/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install-gdb

  # Remove unwanted files
  rm -rf ${pkgdir}${MINGW_PREFIX}/share/{man,info}

  # install "custom" system gdbinit
  install -D -m644 ${srcdir}/gdbinit ${pkgdir}${MINGW_PREFIX}/etc/gdbinit
  sed -i 's|%GCC_NAME%|gcc-'${_gcc_ver}'|g' ${pkgdir}${MINGW_PREFIX}/etc/gdbinit
}
