# Maintainer: Andrew Sun <adsun701@gmail.com>

_realname=ffcall
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.2
pkgrel=1
pkgdesc="C library for implementing foreign function calls in embedded interpreters (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://www.gnu.org/software/libffcall/"
license=('GPL2')
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools" "${MINGW_PACKAGE_PREFIX}-cc")
options=('!makeflags' 'staticlibs')
source=("https://ftp.gnu.org/gnu/libffcall/lib${_realname}-${pkgver}.tar.gz")
sha256sums=('ebfa37f97b6c94fac24ecf3193f9fc829517cf81aee9ac2d191af993d73cb747')

build() {
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}
  ../lib${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared

  make
}

check() {
  cd "${srcdir}"/build-${CARCH}
  make check || true
}

package() {
  cd "${srcdir}"/build-${CARCH}
  install -d "${pkgdir}"${MINGW_PREFIX}/share/{man,doc/ffcall}
  make install DESTDIR="${pkgdir}" htmldir=${MINGW_PREFIX}/share/doc/ffcall
}
