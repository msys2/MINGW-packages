# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

_realname=jbigkit
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.1
pkgrel=3
pkgdesc="Data compression library/utilities for bi-level high-resolution images (mingw-w64)"
arch=('any')
url="http://www.cl.cam.ac.uk/~mgk25/jbigkit/"
license=(GPL2)
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('staticlibs' 'strip')
source=("http://www.cl.cam.ac.uk/~mgk25/download/${_realname}-${pkgver}.tar.gz"
        allNewMainMakefile.all.patch
        manfiles.all.patch
        pbmtoolsMakefile.all.patch
        useLibtool.all.patch
        zzz_debug.mingw.patch)
md5sums=('ebcf09bed9f14d7fa188d3bd57349522'
         '8b71b6de344a29212b14b02b287d11f7'
         '644209f4f1546fe5156f686b9939d60b'
         '8bdf22501c4f9618517d2f1621a4824b'
         '36ff8ef0482bc2bd98b4d62dba71396f'
         '0c788e2d231f07dd1e9ebb7e764746ce')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/allNewMainMakefile.all.patch
  patch -p1 -i ${srcdir}/manfiles.all.patch
  patch -p1 -i ${srcdir}/pbmtoolsMakefile.all.patch
  patch -p1 -i ${srcdir}/useLibtool.all.patch
  patch -p1 -i ${srcdir}/zzz_debug.mingw.patch
}

build() {
  cp -rf ${_realname}-${pkgver} build-${MINGW_CHOST}
  cd "${srcdir}/build-${MINGW_CHOST}"
  unset CFLAGS CPPFLAGS LDFLAGS
  make prefix=${MINGW_PREFIX}
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make prefix=${MINGW_PREFIX} DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
