# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=phodav
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.5
pkgrel=1
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
pkgdesc="A WebDAV server using libsoup (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-libsoup"
         "${MINGW_PACKAGE_PREFIX}-libxml2")
makedepends=("${MINGW_PACKAGE_PREFIX}-asciidoc"
             "${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-gtk-doc"
             "${MINGW_PACKAGE_PREFIX}-cc")
options=('strip' 'staticlibs')
license=("LGPL 2.1")
url="https://wiki.gnome.org/phodav"
source=(https://download.gnome.org/sources/${_realname}/${pkgver}/${_realname}-${pkgver}.tar.xz
        001-skip-man-for-win.patch
        002-enable-gtk-doc-for-win.patch
        003-meson-check-version-script.patch)
sha256sums=('71f0a9cd70afd4dd1412a0298331dbb8ac71c0377f52117afc15eb88dc6fb910'
            'd5efa5820e37f305f3508baf4948ee8f0611e810a86b24489955c41afedc1595'
            'b24b7bc59c309faf6cf3c47f7e928fbb45273616b20aad49f05a785bdf0f2f68'
            'e6345bf6d857e471a86739999a871a10cdff232cfe142fea9d7c40769593f98a')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  # xmlto write in wrong path when pass Windows style directory from meson
  patch -p1 -i ${srcdir}/001-skip-man-for-win.patch
  patch -p1 -i ${srcdir}/002-enable-gtk-doc-for-win.patch
  patch -p1 -i ${srcdir}/003-meson-check-version-script.patch
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="--prefix=" \
  ${MINGW_PREFIX}/bin/meson.exe \
    --prefix="${MINGW_PREFIX}" \
    --wrap-mode=nodownload \
    --default-library=both \
    --buildtype=plain \
    -Dgtk_doc=enabled \
    "../${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/meson.exe compile
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/meson.exe install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/lib${_realname}/COPYING"
}
