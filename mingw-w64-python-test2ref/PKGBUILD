# Maintainer: Joel Holdsworth <jholdsworth@nvidia.com>

_realname=test2ref
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.1.1
pkgrel=1
pkgdesc="Python Testing Against Learned Reference Data"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'archlinux: python-test2ref'
  'purl: pkg:pypi/test2ref'
)
license=('spdx:MIT')
url='https://github.com/nbiotcloud/test2ref'
msys2_repository_url='https://github.com/nbiotcloud/test2ref'
msys2_issue_tracker_url='https://github.com/nbiotcloud/test2ref/issues'
msys2_documentation_url='https://test2ref.readthedocs.io/en/stable/'
depends=("${MINGW_PACKAGE_PREFIX}-python-binaryornot")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-pdm-backend")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-binaryornot"
              "${MINGW_PACKAGE_PREFIX}-python-pytest"
              "${MINGW_PACKAGE_PREFIX}-python-pytest-cov")
options=('!strip')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('f8911debf088e745717b7fc0e2edaa1be2292377e0efcf927e03fbcb7163a7cb')

build() {
  cp -r "${_realname}-${pkgver}" "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m pytest || warning "Tests failed"
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
