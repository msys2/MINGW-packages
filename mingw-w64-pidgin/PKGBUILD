# Maintainer: Renato Silva <br.renatosilva@gmail.com>
# Contributor: Zach Bacon <11doctorwhocanada@gmail.com>

_realname='pidgin'
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.14.8
pkgrel=1
pkgdesc='Multi-protocol instant messaging client (mingw-w64)'
url='https://pidgin.im'
license=(GPL2 partial:'GPL2+') # GPL2+, but Novell and SILC are GPL2-only
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')

options=(!libtool strip staticlibs)
source=("https://downloads.sourceforge.net/${_realname}/${_realname}-${pkgver}.tar.bz2"
        001-autotools-and-fhs.patch
        002-build-fixes.patch
        003-build-fixes.patch)
sha256sums=('3f8085c0211c4ca1ba9f8a03889b3d60738432c1673b57b0086070ef6e094cca'
            '5852d78f97e344226e598f2342b7a1b276f25eec71903bacdc9f68849804df85'
            'bdc8fc0e707206ec340711c25c7ef5be453be1cff93b1678908ebe71907600d7'
            '1cc23086ab40bed7df176015cbd7f3cc0cd4dc72ae814dea9120f5e2d2ce9faa')
provides=(${MINGW_PACKAGE_PREFIX}-libpurple)
depends=(${MINGW_PACKAGE_PREFIX}-adwaita-icon-theme
         ${MINGW_PACKAGE_PREFIX}-ca-certificates
         #${MINGW_PACKAGE_PREFIX}-cyrus-sasl
         #${MINGW_PACKAGE_PREFIX}-dbus
         ${MINGW_PACKAGE_PREFIX}-farstream
         ${MINGW_PACKAGE_PREFIX}-freetype
         ${MINGW_PACKAGE_PREFIX}-fontconfig
         ${MINGW_PACKAGE_PREFIX}-gettext
         ${MINGW_PACKAGE_PREFIX}-gnutls
         ${MINGW_PACKAGE_PREFIX}-gsasl
         ${MINGW_PACKAGE_PREFIX}-gst-plugins-base
         ${MINGW_PACKAGE_PREFIX}-gst-plugins-good
         ${MINGW_PACKAGE_PREFIX}-gtk2
         ${MINGW_PACKAGE_PREFIX}-gtkspell
         ${MINGW_PACKAGE_PREFIX}-libgadu
         ${MINGW_PACKAGE_PREFIX}-libidn
         #${MINGW_PACKAGE_PREFIX}-libxml2
         ${MINGW_PACKAGE_PREFIX}-meanwhile
         #${MINGW_PACKAGE_PREFIX}-nspr
         ${MINGW_PACKAGE_PREFIX}-nss
         ${MINGW_PACKAGE_PREFIX}-ncurses
         ${MINGW_PACKAGE_PREFIX}-silc-toolkit
         ${MINGW_PACKAGE_PREFIX}-zlib)
makedepends=(gtk-doc intltool rsync
             #${MINGW_PACKAGE_PREFIX}-dbus-glib
             ${MINGW_PACKAGE_PREFIX}-doxygen
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             ${MINGW_PACKAGE_PREFIX}-python)

prepare() {
    cd "${srcdir}/${_realname}-${pkgver}"

    # From https://build.opensuse.org/package/view_file/windows:mingw:win32/mingw32-pidgin/pidgin-2.10.11-autotools.patch
    rm -f pidgin/plugins/win32/Makefile.am
    rm -f pidgin/plugins/win32/winprefs/Makefile.am
    patch -p1 < "${srcdir}/001-autotools-and-fhs.patch"
    patch -p1 < "${srcdir}/002-build-fixes.patch"
    patch -p1 < "${srcdir}/003-build-fixes.patch"
    libtoolize --force --copy --install
    aclocal -I m4macros
    automake --add-missing
    autoreconf -f -i
}

build() {
    msg2 'Synchronizing build directory'
    rsync --recursive --times "${srcdir}/${_realname}-${pkgver}"/* "${srcdir}/${_realname}-${pkgver}.build.${MSYSTEM}"
    cd "${srcdir}/${_realname}-${pkgver}.build.${MSYSTEM}"

    INTLTOOL_PERL=/usr/bin/perl ./configure \
        --prefix=${MINGW_PREFIX} \
        --build=${MINGW_CHOST} \
        --host=${MINGW_CHOST} \
        --sysconfdir=${MINGW_PREFIX}/etc \
        --with-python3=${MINGW_PREFIX}/bin/python \
        --with-system-ssl-certs=${MINGW_PREFIX}/ssl/certs \
        --without-x \
        --disable-avahi \
        --disable-schemas-install \
        --disable-screensaver \
        --disable-perl \
        --disable-tcl \
        --disable-tk \
        --disable-sm \
        --disable-nm \
        --disable-dbus \
        --disable-gevolution \
        --disable-cyrus-sasl

    make
}

package() {
    cd "${srcdir}/${_realname}-${pkgver}.build.${MSYSTEM}"
    make DESTDIR="${pkgdir}" install
    install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
