# $Id$
# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=mysql
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}")
pkgver=1.2.5
pkgrel=2
pkgdesc="MySQL support for Python (mingw-w64)"
arch=(any)
url="http://mysql-python.sourceforge.net/"
license=('GPL2')
depends=("${MINGW_PACKAGE_PREFIX}-python2" "${MINGW_PACKAGE_PREFIX}-libmariadbclient")
makedepends=("${MINGW_PACKAGE_PREFIX}-python2-setuptools")
source=("https://pypi.python.org/packages/a5/e9/51b544da85a36a68debe7a7091f068d802fc515a3a202652828c73453cad/MySQL-python-1.2.5.zip"
        fix-mingw.patch)
sha512sums=('37521c6fd855c4cde495cc8ec085aca79c2d441a6e3710759385bf2e2c17f43d3311cf1166663892829d2e3999c419443c358c7031cdda225ac44611ced188d0'
            '000a6de060e4446f18c23cdb79aa32e772eed33804c0373b7dc7ecfb5d52aac66ac9b7b0600276009dc8ddeacc6d7b24ef219bdcf735b4fdb77a6f47a73be1e7')


# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp0 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"
  apply_patch_with_msg fix-mingw.patch
  rm -rf "python2-build-${CARCH}" | true
  cp -r "MySQL-python-${pkgver}" "python2-build-${CARCH}"

}

build() {
  cd "${srcdir}/python2-build-${CARCH}"

  sed "s|connector = [[:alpha:]]\+$|connector = ${MINGW_PREFIX}|" -i site.cfg
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py build
}

package() {
  cd "${srcdir}/python2-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
}
