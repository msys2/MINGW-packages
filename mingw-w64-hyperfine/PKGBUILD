# Maintainer: Wu Zhenyu <wuzy01@qq.com>

_realname=hyperfine
pkgbase=mingw-w64-$_realname
pkgname=$MINGW_PACKAGE_PREFIX-$_realname
pkgver=1.18.0
pkgrel=1
pkgdesc='A command-line benchmarking tool (mingw-w64)'
arch=(any)
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/sharkdp/hyperfine'
license=('spdx:MIT OR Apache-2.0')
makedepends=($MINGW_PACKAGE_PREFIX-rust)
source=("$url/archive/v$pkgver/${_realname}-${pkgver}.tar.gz")
sha256sums=('fea7b92922117ed04b9c84bb9998026264346768804f66baa40743c5528bed6b')

prepare() {
  cd "${_realname}-${pkgver}"

  local _target="${CARCH}-pc-windows-gnu"
  if [[ $MINGW_PACKAGE_PREFIX == *-clang-aarch64 ]]; then
    _target="${CARCH}-pc-windows-gnullvm"
  fi
  cargo fetch --locked --target "${_target}"
}

build() {
  cd "$srcdir/$_realname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$srcdir/$_realname-$pkgver"
  cargo test --release --frozen
}

package() {
  cd "$srcdir/$_realname-$pkgver"
  install -Dm644 "LICENSE-APACHE" "$pkgdir$MINGW_PREFIX/share/licenses/$_realname/LICENSE-APACHE"
  install -Dm644 "LICENSE-MIT" "$pkgdir$MINGW_PREFIX/share/licenses/$_realname/LICENSE-MIT"
  install -Dm755 "target/release/$_realname" "$pkgdir$MINGW_PREFIX/bin/$_realname"

  install -Dm644 target/release/build/hyperfine-*/out/hyperfine.bash "$pkgdir$MINGW_PREFIX/share/bash-completion/completions/hyperfine"
  install -Dm644 target/release/build/hyperfine-*/out/hyperfine.fish "$pkgdir$MINGW_PREFIX/share/fish/vendor_completions.d/hyperfine.fish"
  install -Dm644 target/release/build/hyperfine-*/out/_hyperfine "$pkgdir$MINGW_PREFIX/share/zsh/site-functions/_hyperfine"

  install -Dm644 doc/hyperfine.1 "$pkgdir$MINGW_PREFIX/share/man/man1/hyperfine.1"
}
