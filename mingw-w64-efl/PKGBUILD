# Maintainer: Carsten Haitzler <raster@rasterman.com>
# Contributor: Ryan Gammon <ryan@gamnation.net>

_pkgname=efl
pkgbase=mingw-w64-${_pkgname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_pkgname}"
pkgver=1.25.0.r669.g6efb60be59
pkgrel=1
pkgdesc="Enlightenment Foundation Libraries - Development version"
arch=('any')
mingw_arch=('mingw32' 'mingw64')
url="https://www.enlightenment.org"
license=('BSD' 'LGPL2.1' 'GPL2' 'MIT' 'custom')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" "${MINGW_PACKAGE_PREFIX}-openjpeg2"
         "${MINGW_PACKAGE_PREFIX}-lz4" "${MINGW_PACKAGE_PREFIX}-luajit" 
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo" "${MINGW_PACKAGE_PREFIX}-dbus"
         "${MINGW_PACKAGE_PREFIX}-libsndfile" "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libtiff" "${MINGW_PACKAGE_PREFIX}-libavif"
         "${MINGW_PACKAGE_PREFIX}-libheif" "${MINGW_PACKAGE_PREFIX}-libjxl"
         "${MINGW_PACKAGE_PREFIX}-freetype" "${MINGW_PACKAGE_PREFIX}-fontconfig"
         "${MINGW_PACKAGE_PREFIX}-fribidi" "${MINGW_PACKAGE_PREFIX}-pixman"
         "${MINGW_PACKAGE_PREFIX}-gstreamer" "${MINGW_PACKAGE_PREFIX}-gst-plugins-base"
         "${MINGW_PACKAGE_PREFIX}-libraw" "${MINGW_PACKAGE_PREFIX}-openssl")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-meson"
  "${MINGW_PACKAGE_PREFIX}-pkgconf"
  "git"
)
_commit="6efb60be59c2ab2c000fb7980e82782d32f4e544"
source=("git+https://git.enlightenment.org/enlightenment/efl.git#commit=${_commit}"
        "install.patch")
sha256sums=('SKIP'
            '5d5133479efc6d58819153c790702a10cae7b8fce6759acdc179cb1a96970b57')

pkgver() {
  cd "$_pkgname"

  git describe --long "${_commit}" | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//g'
}

prepare() {
    cd "$_pkgname"

    patch --forward --strip=1 --input="${srcdir}/install.patch"
}

build() {
  mkdir -p "build-${MSYSTEM}" && cd "build-${MSYSTEM}"

  # EFL_RUN_IN_TREE needs to be set manually on Windows. See:
  # https://github.com/Enlightenment/efl-ci/commit/18ce3e8066f954bd32f2ba890e308def86c6a668
  export EFL_RUN_IN_TREE=1

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    meson setup \
      --prefix="${MINGW_PREFIX}" \
      --default-library shared \
      -Dsystemd=false \
      -Dpulseaudio=false \
      -Dv4l2=false \
      -Dlibmount=false \
      -Deeze=false \
      -Dx11=false \
      -Dxinput2=false \
      -Devas-loaders-disabler='pdf','ps','rsvg','json' \
      -Dopengl=none \
      -Dpixman=true \
      -Dembedded-lz4=false \
      -Dfribidi=true \
      -Dinput=false \
      -Dbuild-examples=false \
      -Dbuild-tests=false \
      -Dbindings= \
      -Dbuild-examples=false \
      -Dbuild-tests=false \
      "../${_pkgname}"

   meson compile
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="$pkgdir" meson install

  cd "${srcdir}/${_pkgname}"
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/doc/$_pkgname/" README.md
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/$pkgname/" AUTHORS COMPLIANCE COPYING
}
