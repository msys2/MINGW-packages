# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=cegui
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=0.8.3
pkgrel=2
pkgdesc="A free library providing windowing and widgets for graphics APIs/engines (mingw-w64)"
arch=('any')
url="http://crayzedsgui.sourceforge.net"
license=("MIT")
#makedepends=('doxygen' 'gtk2' 'graphviz' 'irrlicht' 'glm' 'tolua++')
makedepends=("${_mingw_suff}-gcc"
    "${_mingw_suff}-pkg-config"
    "${_mingw_suff}-cmake"
    "${_mingw_suff}-python2"
    "mercurial")
depends=("${_mingw_suff}-tinyxml"
    "${_mingw_suff}-boost"
    "${_mingw_suff}-DevIL"
    "${_mingw_suff}-expat"
    "${_mingw_suff}-FreeImage"
    "${_mingw_suff}-freetype"
    "${_mingw_suff}-fribidi"
    "${_mingw_suff}-glew"
    #"${_mingw_suff}-glfw"
    "${_mingw_suff}-glm"
    "${_mingw_suff}-irrlicht"
    "${_mingw_suff}-libxml2"
    "${_mingw_suff}-libiconv"
    "${_mingw_suff}-lua51"
    "${_mingw_suff}-ogre3d"
    "${_mingw_suff}-ois"
    "${_mingw_suff}-pcre"
    #"${_mingw_suff}-tolua"
    "${_mingw_suff}-xerces-c"
    "${_mingw_suff}-zlib")
options=('staticlibs' 'strip')
source=("http://downloads.sourceforge.net/crayzedsgui/cegui-${pkgver}.tar.gz"
    freetype.patch
    mingw-w64-fix.patch
    fix-finding-glew.patch
    fix-finding-directx.patch
    fix-finding-glfw3.patch
    fix-dxerr-libname.patch)
md5sums=('142cca3648cee034e04d0f61bd9863ce'
         '4c5f81f109f36718045bb4ba5ba25602'
         'ac605f26c068cf0b90bc24add094ca6d'
         'b60459d7cf5854dee5b8bbdd519778ea'
         'db4b135df45db6716c242042a8003893'
         '3a754a1caf9ab0cb0c732bd68f6f29a0'
         '77649c26d817dc7d4d57b9c54d3641f4')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/freetype.patch
  patch -p1 -i ${srcdir}/mingw-w64-fix.patch
  patch -p1 -i ${srcdir}/fix-finding-glew.patch
  patch -p1 -i ${srcdir}/fix-finding-directx.patch
  #patch -p1 -i ${srcdir}/fix-finding-glfw3.patch
  patch -p1 -i ${srcdir}/fix-dxerr-libname.patch
}

build() {
  mkdir -p ${pkgdir}${MINGW_PREFIX}
  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null

  pushd ${MINGW_PREFIX} > /dev/null
  export PREFIX_DEPS=`pwd -W`
  popd > /dev/null

  mkdir $srcdir/build-${MINGW_CHOST}
  cd "$srcdir/build-${MINGW_CHOST}"

  export DXSDK_DIR=${PREFIX_DEPS}/${MINGW_CHOST}
  export CFLAGS="-I${PREFIX_DEPS}/include"
  export CXXFLAGS="-I${PREFIX_DEPS}/include"
  #cd "${srcdir}/${_realname}-${pkgver}"
  ${MINGW_PREFIX}/bin/cmake.exe \
    ../${_realname}-${pkgver} \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX:PATH=${PREFIX_WIN} \
    -DCEGUI_LIB_INSTALL_DIR=lib \
    -DCEGUI_BUILD_PYTHON_MODULES=OFF # currently broken :(
    # -PYTHON_EXECUTABLE=/usr/bin/python2
  make
  #make html
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make -j1 install
}
