# Maintainer: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=qt6
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
_qtver=6.0.2
pkgver=${_qtver/-/}
pkgrel=1
arch=(any)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
depends=("${MINGW_PACKAGE_PREFIX}-double-conversion"
         "${MINGW_PACKAGE_PREFIX}-dbus"
         "${MINGW_PACKAGE_PREFIX}-freetype"
         "${MINGW_PACKAGE_PREFIX}-fontconfig"
         "${MINGW_PACKAGE_PREFIX}-icu"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-pcre2"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-sqlite3"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-assimp")
optdepends=("${MINGW_PACKAGE_PREFIX}-clang"
            "${MINGW_PACKAGE_PREFIX}-libmariadbclient"
            "${MINGW_PACKAGE_PREFIX}-firebird2"
            "${MINGW_PACKAGE_PREFIX}-postgresql")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-clang"
             "${MINGW_PACKAGE_PREFIX}-clang-tools-extra"
             "${MINGW_PACKAGE_PREFIX}-polly"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-xmlstarlet"
             "${MINGW_PACKAGE_PREFIX}-libmariadbclient"
             "${MINGW_PACKAGE_PREFIX}-firebird2"
             "${MINGW_PACKAGE_PREFIX}-postgresql")
conflicts=("${MINGW_PACKAGE_PREFIX}-qt5"
           "${MINGW_PACKAGE_PREFIX}-qt5-debug"
           "${MINGW_PACKAGE_PREFIX}-qt5-static")
groups=(qt6)
_pkgfn="${_realname/6/}-everywhere-src-${_qtver}"
source=("https://download.qt.io/official_releases/qt/${pkgver%.*}/$_qtver/single/$_pkgfn.tar.xz")
sha256sums=('67a076640647783b95a907d2231e4f34cec69be5ed338c1c1b33124cadf10bdf')

build() {
  cd ${srcdir}
  # Base
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -GNinja \
    -DCMAKE_OBJECT_PATH_MAX=2000 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DINSTALL_ARCHDATADIR=share/qt6 \
    -DINSTALL_DATADIR=share/qt6 \
    -DINSTALL_MKSPECSDIR=share/qt6/mkspecs \
    -DINSTALL_DESCRIPTIONSDIR=share/qt6/modules \
    -DQT_FEATURE_system_doubleconversion=ON \
    -DQT_FEATURE_system_freetype=ON \
    -DQT_FEATURE_system_jpeg=ON \
    -DQT_FEATURE_system_pcre2=ON \
    -DQT_FEATURE_system_png=ON \
    -DQT_FEATURE_system_sqlite=ON \
    -DQT_FEATURE_system_zlib=ON \
    -DQT_FEATURE_openssl=ON \
    -DQT_FEATURE_openssl_linked=ON \
    -DQT_FEATURE_opengl=ON \
    -DQT_FEATURE_opengl_desktop=ON \
    -DQT_FEATURE_clang=ON \
    -DQT_FEATURE_clangcpp=ON \
    -DQT_FEATURE_dbus=ON \
    -DQT_FEATURE_icu=ON \
    -DQT_FEATURE_fontconfig=ON \
    -DQT_FEATURE_pkg_config=ON \
    ../${_pkgfn}

  ninja
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  DESTDIR=${pkgdir} cmake --install .

  install -D -m644 ${srcdir}/${_pkgfn}/LICENSE.FDL    "${pkgdir}${MINGW_PREFIX}"/share/licenses/qt6/LICENSE.FDL
  install -D -m644 ${srcdir}/${_pkgfn}/LICENSE.GPL3-EXCEPT "${pkgdir}${MINGW_PREFIX}"/share/licenses/qt6/LICENSE.GPL3-EXCEPT
  install -D -m644 ${srcdir}/${_pkgfn}/LICENSE.GPLv2 "${pkgdir}${MINGW_PREFIX}"/share/licenses/qt6/LICENSE.GPLv2
  install -D -m644 ${srcdir}/${_pkgfn}/LICENSE.GPLv3 "${pkgdir}${MINGW_PREFIX}"/share/licenses/qt6/LICENSE.GPLv3
  install -D -m644 ${srcdir}/${_pkgfn}/LICENSE.LGPLv3 "${pkgdir}${MINGW_PREFIX}"/share/licenses/qt6/LICENSE.LGPLv3
  install -D -m644 ${srcdir}/${_pkgfn}/LICENSE.LGPLv21 "${pkgdir}${MINGW_PREFIX}"/share/licenses/qt6/LICENSE.LGPLv21
  install -D -m644 ${srcdir}/${_pkgfn}/LICENSE.QT-LICENSE-AGREEMENT  "${pkgdir}${MINGW_PREFIX}"/share/licenses/qt6/LICENSE.QT-LICENSE-AGREEMENT
}
