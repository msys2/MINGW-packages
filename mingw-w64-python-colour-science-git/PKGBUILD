# Contributor: Brien Dieterle <briend@gmail.com>

_realname=colour-science
pkgbase=mingw-w64-python-${_realname}-git
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}-git" "${MINGW_PACKAGE_PREFIX}-python3-${_realname}-git")
#pkgname="${MINGW_PACKAGE_PREFIX}-python2-${_realname}-git"
pkgver=0.3.11.217.g95a375d4
pkgrel=1
pkgdesc="Python library for a multitude of colour science applications (git) (mingw-w64)"
arch=('any')
license=('BSD-3-Clause')
url="https://pypi.python.org/pypi/colour-science"
makedepends=("${MINGW_PACKAGE_PREFIX}-python2-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python2-numpy"
             "${MINGW_PACKAGE_PREFIX}-python3-numpy"
             "${MINGW_PACKAGE_PREFIX}-python2-scipy"
             "${MINGW_PACKAGE_PREFIX}-python3-scipy"
             "${MINGW_PACKAGE_PREFIX}-python2-six"
             "${MINGW_PACKAGE_PREFIX}-python3-six")
makedepends+=("git")
options=('staticlibs')
source=("${_realname}::git+https://github.com/colour-science/colour.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}"/${_realname}
  git describe --tags | sed 's,-\(alpha\|beta\),\1,' | sed 's,-,.,g' | sed 's,^v,,'
}

prepare() {
  cd ${srcdir}
  cp -r colour-science python2-build
  cp -r colour-science python3-build
}

build() {
  cd "${srcdir}"
  for builddir in python{2,3}-build; do
    pushd ${builddir}
    ${MINGW_PREFIX}/bin/${builddir%-build} setup.py build
    popd
  done
}

package_python2-colour-science() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2")

  cd "${srcdir}/python2-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/COPYING"
}

package_python3-colour-science() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3")

  cd "${srcdir}/python3-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/COPYING"
}

package_mingw-w64-i686-python2-colour-science-git() {
  package_python2-colour-science
}

package_mingw-w64-i686-python3-colour-science-git() {
  package_python3-colour-science
}

package_mingw-w64-x86_64-python2-colour-science-git() {
  package_python2-colour-science
}

package_mingw-w64-x86_64-python3-colour-science-git() {
  package_python3-colour-science
}
