# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Xin Sun <sun.simpson@gmail.com>

_realname=hdf5
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
_ver=2.0.0
__patch=
pkgver=${_ver}${_patch//-/.}
pkgrel=2
pkgdesc="General purpose library and file format for storing scientific data (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://www.hdfgroup.org/HDF5/"
msys2_repository_url="https://github.com/HDFGroup/hdf5"
msys2_references=(
  "cpe: cpe:/a:hdfgroup:hdf5"
)
license=('spdx:BSD-3-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-aws-c-auth"
         "${MINGW_PACKAGE_PREFIX}-aws-c-common"
         "${MINGW_PACKAGE_PREFIX}-aws-c-http"
         "${MINGW_PACKAGE_PREFIX}-aws-c-io"
         "${MINGW_PACKAGE_PREFIX}-aws-c-s3"
         "${MINGW_PACKAGE_PREFIX}-aws-c-sdkutils"
         $([[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]] || echo "${MINGW_PACKAGE_PREFIX}-gcc-libgfortran")
         "${MINGW_PACKAGE_PREFIX}-libaec"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         )
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             $([[ ${MSYSTEM} == CLANG64 ]] || echo "${MINGW_PACKAGE_PREFIX}-fc")
             )
_filever=${_ver//./_}
source=("https://support.hdfgroup.org/releases/hdf5/v${_filever%_*}/v${_filever}/downloads/hdf5-${_ver}${__patch}.tar.gz"
        "hdf5-proper-library-names-mingw.patch"
        "hdf5-fix-fortran-module-directory-leak.patch"
        "0002-dynamic-allocation-functions.patch"
        "0005-fix-extracted-library-names.patch"
        "0006-link-static-libs.patch")
sha256sums=('6e45a4213cb11bb5860e1b0a7645688ab55562cc2d55c6ff9bcb0984ed12b22b'
            'bd27243196dfa5ae90c710187ddfa58a7f7e0a75a804e86fc29f09bd514de2f2'
            '02b5d7335a9db628ed03b29c62d8f0e5f5618fa39de8e453aa02b530d28317a8'
            'aca3d88c6a9733ad61708ac8f540bfb6874d6877263e1ea110b3365e5fbb9907'
            '8218e254dc890864640dbcab0e6ca1e8c9145dd08d94a20b0586ce571d701687'
            '617e10fa95eec21207339ed8a1df194bb32f2e0ffd7701e51febbabeead07a54')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${srcdir}/${_realname}-${_ver}${__patch}"

  apply_patch_with_msg \
    hdf5-proper-library-names-mingw.patch \
    hdf5-fix-fortran-module-directory-leak.patch \
    0002-dynamic-allocation-functions.patch \
    0005-fix-extracted-library-names.patch \
    0006-link-static-libs.patch
}

build() {
  mkdir -p "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  if [[ ${MSYSTEM} == CLANG64 ]]; then
    _extra_config+=("-DHDF5_BUILD_FORTRAN=OFF")
  else
    _extra_config+=("-DHDF5_BUILD_FORTRAN=ON")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -Wno-dev \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DCMAKE_DLL_NAME_WITH_SOVERSION=ON \
      -DHDF5_BUILD_EXAMPLES=OFF \
      -DBUILD_TESTING=OFF \
      "${_extra_config[@]}" \
      -DCMAKE_SKIP_RPATH=ON \
      -DHDF5_BUILD_CPP_LIB=ON \
      -DHDF5_BUILD_HL_LIB=ON \
      -DHDF5_BUILD_TOOLS=ON \
      -DHDF5_ENABLE_DEPRECATED_SYMBOLS=ON \
      -DHDF5_ENABLE_SZIP_SUPPORT=ON \
      -DHDF5_ENABLE_ZLIB_SUPPORT=ON \
      -DHDF5_ENABLE_ROS3_VFD=ON \
      -DHDF5_INSTALL_CMAKE_DIR="lib/cmake" \
      -DHDF5_INSTALL_DATA_DIR="share/doc/hdf5" \
      -DHDF5_INSTALL_MODULE_DIR="include" \
      -DHDF5_MSVC_NAMING_CONVENTION=OFF \
      -DHDF5_ENABLE_ALL_WARNINGS=OFF \
      ../${_realname}-${_ver}${__patch}

  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .

  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  for _f in "${pkgdir}${MINGW_PREFIX}"/lib/cmake/*.cmake; do
    sed -e "s|${PREFIX_WIN}|\$\{_IMPORT_PREFIX\}|g" -i ${_f}
  done

  for pcfile in "${pkgdir}${MINGW_PREFIX}"/lib/pkgconfig/*.pc; do
    sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" -i "${pcfile}"
  done
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" -i "${pkgdir}${MINGW_PREFIX}"/lib/libhdf5.settings

  install -d "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}
  install -Dm644 "${srcdir}"/${_realname}-${_ver}${__patch}/LICENSE \
    -t "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}
}
