# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Xin Sun <sun.simpson@gmail.com>

_realname=hdf5
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
_ver=1.14.5
__patch=
pkgver=${_ver}${_patch//-/.}
pkgrel=2
pkgdesc="General purpose library and file format for storing scientific data (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64' 'mingw32')
url="https://www.hdfgroup.org/HDF5/"
msys2_repository_url="https://github.com/HDFGroup/hdf5"
msys2_references=(
  "cpe: cpe:/a:hdfgroup:hdf5"
)
license=('spdx:BSD-3-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-libaec"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-curl"
         $([[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]] || echo "${MINGW_PACKAGE_PREFIX}-gcc-libgfortran")
         )
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-fc"
             )
_filever=${_ver//./_}
source=("https://support.hdfgroup.org/releases/hdf5/v${_filever%_*}/v${_filever}/downloads/hdf5-${_ver}${__patch}.tar.gz"
        "hdf5-proper-library-names-mingw.patch"
        "hdf5-fix-static-lib.patch"
        "hdf5-fix-fortran-module-directory-leak.patch"
        "0001-hdf5-pkgconfig-use-requires-private.patch"
        "0002-dynamic-allocation-functions.patch"
        "0003-fix-find-libaec.patch"
        "0004-fix-linker-flag-for-zlib.patch::https://github.com/HDFGroup/hdf5/commit/e64e1ea8.patch"
        "0005-fix-extracted-library-names.patch")
sha256sums=('ec2e13c52e60f9a01491bb3158cb3778c985697131fc6a342262d32a26e58e44'
            'e236f5805152b25065c206922d7ec1bab05b233adac51bbd0fb1e546326162f6'
            '9d172a7f6b8f54fdbf840e032708acade4ab88c81262b45bfa85d203810962a9'
            '02b5d7335a9db628ed03b29c62d8f0e5f5618fa39de8e453aa02b530d28317a8'
            '101b6b41bd2e3fad9be2b3a72d5a9235fa90d4a900879b687e26521ee1a2fd8c'
            '4119aebc5d8afab3594c58821a104f252fb8c1bcc5bc12292deb53ac48778577'
            '5b9f30f989b6d299de3205d7a8ad47a6ee89a02d8623a27542a2ab6e3e9e7ed7'
            '9fd9a54d36acfccd9f71e1bb516bee21951d348667198af08ec715c83615de63'
            'b367217ccddafae8eaa471ce108d3d62a4ad869951647f32e9efcb3dabf1e3c9')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${srcdir}/${_realname}-${_ver}${__patch}"

  # 0001: curl and openssl in Requires.private, zlib, libaec in Libs.private
  apply_patch_with_msg \
    hdf5-proper-library-names-mingw.patch \
    hdf5-fix-static-lib.patch \
    hdf5-fix-fortran-module-directory-leak.patch \
    0001-hdf5-pkgconfig-use-requires-private.patch \
    0002-dynamic-allocation-functions.patch \
    0003-fix-find-libaec.patch \
    0004-fix-linker-flag-for-zlib.patch \
    0005-fix-extracted-library-names.patch
}

build() {
  mkdir -p "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  # Fails with flang-19:
  # error: loc("C:/_/B/src/hdf5-1.14.5/fortran/src/H5VLff.F90":456:32):
  # C:/M/B/src/flang-19.1.4.src/lib/Optimizer/CodeGen/Target.cpp:102:
  # not yet implemented: passing VALUE BIND(C) derived type for this target
  if [[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]]; then
    _extra_config+=("-DHDF5_BUILD_FORTRAN=OFF")
  else
    _extra_config+=("-DHDF5_BUILD_FORTRAN=ON")
  fi

  CFLAGS+=" -Wno-implicit-function-declaration" \
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -Wno-dev \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DCMAKE_DLL_NAME_WITH_SOVERSION=ON \
      -DCMAKE_C_FLAGS="-Wno-error=incompatible-pointer-types" \
      -DBUILD_TESTING=OFF \
      "${_extra_config[@]}" \
      -DCMAKE_SKIP_RPATH=ON \
      -DHDF5_BUILD_CPP_LIB=ON \
      -DHDF5_BUILD_HL_LIB=ON \
      -DHDF5_BUILD_TOOLS=ON \
      -DHDF5_ENABLE_DEPRECATED_SYMBOLS=ON \
      -DHDF5_ENABLE_SZIP_SUPPORT=ON \
      -DHDF5_ENABLE_Z_LIB_SUPPORT=ON \
      -DZLIB_ROOT=${MINGW_PREFIX} \
      -DHDF5_ENABLE_ROS3_VFD=ON \
      -DHDF5_INSTALL_CMAKE_DIR="lib/cmake" \
      -DHDF5_INSTALL_DATA_DIR="share/doc/hdf5" \
      -DHDF5_INSTALL_MODULE_DIR="include" \
      -DHDF5_MSVC_NAMING_CONVENTION=OFF \
      ../${_realname}-${_ver}${__patch}

  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .

  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  for _f in "${pkgdir}${MINGW_PREFIX}"/lib/cmake/*.cmake; do
    sed -e "s|${PREFIX_WIN}|\$\{_IMPORT_PREFIX\}|g" -i ${_f}
  done

  for pcfile in "${pkgdir}${MINGW_PREFIX}"/lib/pkgconfig/*.pc; do
    sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" -i "${pcfile}"
  done
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" -i "${pkgdir}${MINGW_PREFIX}"/lib/libhdf5.settings

  install -d "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}
  install -Dm644 "${srcdir}"/${_realname}-${_ver}${__patch}/COPYING* \
    -t "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}
}
