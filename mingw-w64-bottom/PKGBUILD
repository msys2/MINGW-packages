# Maintainer: Wu Zhenyu <wuzy01@qq.com>
_realname=bottom

pkgbase=mingw-w64-$_realname
pkgname=$MINGW_PACKAGE_PREFIX-$_realname
pkgver=0.8.0
pkgrel=1
pkgdesc="A graphical process/system monitor (mingw-w64)"
url="https://github.com/ClementTsang/bottom"
arch=(any)
mingw_arch=(mingw32 mingw64 ucrt64 clang64)
license=('MIT')
makedepends=($MINGW_PACKAGE_PREFIX-rust tree)
source=("$url/archive/$pkgver/$_realname-$pkgver.tar.gz"
        "$url/releases/download/$pkgver/completion.tar.gz"
        "$url/releases/download/$pkgver/manpage.tar.gz")
sha256sums=('0fe6a826d18570ab33b2af3b26ce28c61e3aa830abb2b622f2c3b81da802437a'
            '0d66631e718caafab29b1daec00394342cc2460cb8f7c706bd4cc85143d73928'
            'e0bcd05ddfd7e084a27d978f66688aeceba48a2acb5156f9c4dbb73a65e25a95')

prepare() {
  cd "$_realname-$pkgver"
  cargo fetch --locked
}

build() {
  cd "$_realname-$pkgver"
  cargo build --frozen --release --all-features
}

check() {
  cd "$_realname-$pkgver"
  CARGO_HUSKY_DONT_INSTALL_HOOKS=true cargo test --frozen --all-features
}

package() {
  cd "$_realname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir$MINGW_PREFIX/bin" target/release/btm

  # license
  install -vDm644 -t "$pkgdir$MINGW_PREFIX/share/licenses/$_realname" LICENSE

  # documentation
  install -vDm644 -t "$pkgdir$MINGW_PREFIX/share/doc/$_realname" *.md
  cp -r docs sample_configs "$pkgdir$MINGW_PREFIX/share/doc/$_realname"
  rm "$pkgdir$MINGW_PREFIX/share/doc/bottom/docs/.gitignore"

  cd "${srcdir}"
  # manpage
  install -Dm644 btm.1.gz "$pkgdir$MINGW_PREFIX/share/man/man1/btm.1.gz"

  # completions
  install -Dm644 _btm "$pkgdir$MINGW_PREFIX/share/zsh/site-functions/_btm"
  install -Dm644 btm.bash "$pkgdir$MINGW_PREFIX/share/bash-completion/completions/btm"
  install -Dm644 btm.fish "$pkgdir$MINGW_PREFIX/share/fish/vendor_completions.d/btm.fish"
}
