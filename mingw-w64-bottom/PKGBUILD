# Maintainer: Wu Zhenyu <wuzy01@qq.com>

_realname=bottom
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.9.7
pkgrel=1
pkgdesc="A graphical process/system monitor (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/ClementTsang/bottom"
msys2_documentation_url="https://clementtsang.github.io/bottom/${pkgver}"
msys2_references=(
  'purl: pkg:cargo/bottom'
)
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "tree"
             "git")
options=('!strip' '!lto')
source=("git+${url}.git#tag=${pkgver}"
        "ntapi-rs.tar.gz::https://crates.io/api/v1/crates/ntapi/0.4.1/download"
        "${url}/releases/download/${pkgver}/completion.tar.gz"
        "${url}/releases/download/${pkgver}/manpage.tar.gz"
        "ntapi-link-ntdll-arm64.patch")
sha256sums=('b1a8b62d9d1513a4b38928997ee13cf55fb3643b5d54d28f675a4c19f9d9af0d'
            'e8a3895c6391c39d7fe7ebc444a87eb2991b2a0bc718fdabd071eec617fc68e4'
            '721b3921157cbc3f31f20f0aaaea468a235b3d0fa8f876cbf9d3867578db8203'
            'e828123c48bfaec804db06dd211c7e4bb2c9899100c9a68522607832789a3ddb'
            '9994d5b61f020f85f67df54c56c793bd4faf41e39b62ddd4e67c482b9a11b73e')

prepare() {
  cd "${_realname}"

  cat >> Cargo.toml <<END

[patch.crates-io]
ntapi = { path = "../ntapi-0.4.1" }
END

  patch -d ../ntapi-0.4.1 -Np1 -i "${srcdir}/ntapi-link-ntdll-arm64.patch"
  cargo update -p ntapi

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${_realname}"

  cargo build --frozen --release --all-features
}

check() {
  cd "${_realname}"

  CARGO_HUSKY_DONT_INSTALL_HOOKS=true cargo test --frozen --release --all-features
}

package() {
  # binary
  install -vDm755 -t "${pkgdir}${MINGW_PREFIX}/bin" "${_realname}/target/release/btm"

  # license
  install -vDm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" "${_realname}/LICENSE"

  # manpage
  install -Dm644 btm.1.gz "${pkgdir}${MINGW_PREFIX}/share/man/man1/btm.1.gz"

  # completions
  install -Dm644 _btm "${pkgdir}${MINGW_PREFIX}/share/zsh/site-functions/_btm"
  install -Dm644 btm.bash "${pkgdir}${MINGW_PREFIX}/share/bash-completion/completions/btm"
  install -Dm644 btm.fish "${pkgdir}${MINGW_PREFIX}/share/fish/vendor_completions.d/btm.fish"
}
