_realname=gpr-unit-provider
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=r15.57b221a
pkgrel=1
_branch=master

pkgdesc="A unit provider for Libadalang based on GPR project analysis library (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
msys2_references=(
  'aur: gpr-unit-provider'
)

url="https://github.com/AdaCore/gpr-unit-provider"
license=('GPL-3.0-or-later WITH GCC-exception-3.1')
makedepends=("${MINGW_PACKAGE_PREFIX}-gprbuild")
depends=("${MINGW_PACKAGE_PREFIX}-gpr"
         "${MINGW_PACKAGE_PREFIX}-libadalang")
source=("${_realname}::git+https://github.com/AdaCore/gpr-unit-provider#branch=${_branch}")
sha256sums=('SKIP')

pkgver() {
  cd "${_realname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
  cd "${srcdir}/${_realname}"

  make \
       ENABLE_SHARED=yes                  \
       GPR_UNIT_PROVIDER_BUILD=release    \
       PROCESSORS=0                       \
       prefix="${pkgdir}/${MINGW_PREFIX}"
}

package() {
  cd "${srcdir}/${_realname}"

  make -j1 \
       ENABLE_SHARED=yes                  \
       GPR_UNIT_PROVIDER_BUILD=release    \
       PROCESSORS=0                       \
       prefix="${pkgdir}/${MINGW_PREFIX}" \
       install

  # Copy License Files
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" \
    "${srcdir}/${_realname}"/COPYING*
}
