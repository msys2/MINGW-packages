# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=libspectre
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.2.7
pkgrel=3
arch=('any')
pkgdesc="libspectre is a small library for rendering PostScript documents. It provides a convenient easy to use API for handling and rendering PostScript documents (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-ghostscript"
         "${MINGW_PACKAGE_PREFIX}-cairo")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-libidn"
            "${MINGW_PACKAGE_PREFIX}-pkg-config"
            "libtool"
            "automake-wrapper"
            "autoconf"
            "make")
options=('strip' 'staticlibs')
license=("GPL 2")
url="http://libspectre.freedesktop.org/"
source=(http://libspectre.freedesktop.org/releases/${_realname}-${pkgver}.tar.gz
        gs-error-codes.patch)
sha256sums=('e81b822a106beed14cf0fec70f1b890c690c2ffa150fa2eee41dc26518a6c3ec'
            '4be333af80378aba74c7bc3804727e3a3b49ccfaee0a8f9173094607e01b9b17')

prepare() {
  cd ${_realname}-${pkgver}

  patch -p1 -i ${srcdir}/gs-error-codes.patch

  autoreconf -fi
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install
}
