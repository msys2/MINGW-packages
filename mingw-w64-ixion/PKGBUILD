# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=ixion
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.19.0
pkgrel=3
pkgdesc="A general purpose formula parser & interpreter. (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://gitlab.com/ixion/ixion'
msys2_references=(
  'archlinux: libixion'
)
license=('spdx:MPL-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-boost")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-mdds"
             "${MINGW_PACKAGE_PREFIX}-python")
optdepends=("${MINGW_PACKAGE_PREFIX}-python: Python bindings")
source=("https://gitlab.com/ixion/ixion/-/archive/${pkgver}/${_realname}-${pkgver}.tar.bz2"
        "001-fix-build-on-mingw.patch")
sha256sums=('c8b6315bae2d33a0e18b60004d625e4a5ea8a7326364f9f0b51cd6b49bdf93e8'
            '5e7949e70a0f9c86411f75d9755a06ca56cad58e059d1d7af078d836a4b1f277')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/001-fix-build-on-mingw.patch

  autoreconf -fiv
}

build() {
  mkdir -p "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX}

  make
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  make check || true
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" make install

  # Use *.pyd extension for python module
  mv "${pkgdir}${MINGW_PREFIX}"/lib/python3.11/site-packages/ixion.{dll,pyd}
  # remove unused import library
  rm -f "${pkgdir}${MINGW_PREFIX}"/lib/python3.11/site-packages/ixion.dll.a

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
