# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=postr
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.13
pkgrel=1
arch=('any')
pkgdesc="Upload photos to Flickr (mingw-w64)"
options=(strip staticlibs)
depends=("${MINGW_PACKAGE_PREFIX}-python2-pygtk")
makedepends=("${MINGW_PACKAGE_PREFIX}-gnome-doc-utils")
options=('strip' 'staticlibs')
license=("GPL 2")
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver}/${_realname}-$pkgver.tar.xz
        001-win.patch)
sha256sums=('3d5d4515892c8b81494257d7fc35d1c13a857bbdaf7b01ba67bde62ebddb962c'
            '7f963d3703a7e0fc678622a69961a6dabb00af050086520fd122fdcc37fbc292')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  patch -p1 -i ${srcdir}/001-win.patch

  AUTOPOINT='intltoolize --automake --copy' autoreconf -f -i
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib
  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install
  rm ${pkgdir}/postrExtension.py
}
