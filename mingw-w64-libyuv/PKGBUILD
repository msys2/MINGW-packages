# Maintainer: Antoine Martin <totaam@xpra.org>

_realname=libyuv
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.31.20180904git9a07219
pkgrel=1
pkgdesc="YUV conversion and scaling library"
arch=("any")
url="https://chromium.googlesource.com/libyuv/libyuv"
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-cmake")
_srchash="cb9a58f25fbdf8382d68680f022959022f746ef2"
source=("https://xpra.org/src/${_realname}-${_srchash}.tar.xz"
        "libyuv-nojpeg.patch"
        "cmake-mingw.patch")
options=('strip' 'staticlibs')
sha256sums=('eb76578955cfd005fc4964b06374cee0f125472bf433ebf038a377c5b82906d6'
            'f93d432a69a5c84259a3502d1dc1d7a04a41005e2cc819b53582135a6c482722'
            'd81d9abb695a5cb8939ef153a6a1ad4016f1a337a71ae785b33d4555b7c79297')

prepare() {
  cd "${srcdir}/${_realname}-${_srchash}"
  patch -p1 -i "$srcdir"/libyuv-nojpeg.patch
  patch -p1 -i "$srcdir"/cmake-mingw.patch
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd ${srcdir}/build-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake -Wno-dev \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    ../${_realname}-${_srchash}
  echo "cmake done"
  make
}

package() {
  cd build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
  mkdir -p ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/
  cat > ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/libyuv.pc << EOF
prefix=${MINGW_PREFIX}
libdir=${MINGW_PREFIX}/bin
includedir=${MINGW_PREFIX}/include

Name: ${_realname}
Description: YUV conversion and scaling library
Version: 0
Libs: -lyuv
EOF
  install -Dm644 ${srcdir}/${_realname}-${_srchash}/LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
