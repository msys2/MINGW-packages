# $Id$
# Maintainer (ArchLinux): Andrea Scarpino <andrea@archlinux.org>
# Maintainer (MSYS2): Ray Donnelly <mingw.android@gmail.com>

_realname=extra-cmake-modules
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=5.20.0
pkgrel=1
pkgdesc='Extra CMake modules (mingw-w64)'
arch=('any')
url='https://projects.kde.org/projects/kdesupport/extra-cmake-modules'
license=('LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-cmake"
         "${MINGW_PACKAGE_PREFIX}-png2ico")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-sphinx") # qt5-tools for QtHelp pages
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${_realname}-${pkgver}.tar.xz"
        "set-AUTOSTATICPLUGINS.patch")
sha256sums=('2c8ff0c7289edfaa95007e622c87a5fa95e9e2a2ed291eb25a61f06ea92ac34c'
            '30bdcedab402c69ea0db3460f5a23cbd226a5cd1e12b13926b8a65df773e14a0')

prepare() {
  mkdir -p build-${CARCH}

  # Apply patches
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/set-AUTOSTATICPLUGINS.patch
}

build() {
  mkdir -p build-${CARCH}
  cd build-${CARCH}
  MSYS2_ARG_CONV_EXCL=-DCMAKE_INSTALL_PREFIX= \
    cmake ../${_realname}-${pkgver} \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DCMAKE_BUILD_TYPE=Release \
      -DBUILD_HTML_DOCS=OFF \
      -G'MSYS Makefiles'
  make
}

package() {
  cd build-${CARCH}
  make DESTDIR="${pkgdir}" install
}
