# $Id$
# Maintainer (ArchLinux): Andrea Scarpino <andrea@archlinux.org>
# Maintainer (MSYS2): Ray Donnelly <mingw.android@gmail.com>

_realname=extra-cmake-modules
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=5.26.0
pkgrel=1
pkgdesc='Extra CMake modules (mingw-w64)'
arch=('any')
url='https://projects.kde.org/projects/kdesupport/extra-cmake-modules'
license=('LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-cmake"
         "${MINGW_PACKAGE_PREFIX}-png2ico")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-sphinx") # qt5-tools for QtHelp pages
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${_realname}-${pkgver}.tar.xz"
        "set-AUTOSTATICPLUGINS.patch"
        "use-UNIX-paths-for-MSYS.patch")
sha256sums=('cd529cc10cc4a4fc20a962329ffc8cc93cc200b7dc681aa4ddfc9e9cc88f79ec'
            'a246d25065ac7472b3a4e5995b3c6cb32081ffa21c7de7455006398431e6c886'
            '5032bdc8d894f6e4e5a46e178a0905ba87b38115d1f3e303472a13b0b8eee70a')

prepare() {
  mkdir -p build-${CARCH}

  # Apply patches
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/set-AUTOSTATICPLUGINS.patch
  patch -p1 -i "${srcdir}"/use-UNIX-paths-for-MSYS.patch

  # repalce old QStandardPaths functions with our new *Msys functions
  find . -type f -name "*.cpp*" -print0 | xargs -0 sed -i -e "s/QStandardPaths::locateAll(/QStandardPaths::locateAllMsys(/g" \
                                                         -e "s/QStandardPaths::locate(/QStandardPaths::locateMsys(/g" \
                                                         -e "s/QStandardPaths::standardLocations(/QStandardPaths::standardLocationsMsys(/g" \
                                                         -e "s/QStandardPaths::writableLocation(/QStandardPaths::writableLocationMsys(/g"
}

build() {
  mkdir -p build-${CARCH}
  cd build-${CARCH}
  MSYS2_ARG_CONV_EXCL=-DCMAKE_INSTALL_PREFIX= \
    cmake ../${_realname}-${pkgver} \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DCMAKE_BUILD_TYPE=Release \
      -DBUILD_HTML_DOCS=OFF \
      -G'MSYS Makefiles'
  make
}

package() {
  cd build-${CARCH}
  make DESTDIR="${pkgdir}" install
}
