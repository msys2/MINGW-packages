# $Id$
# Maintainer (ArchLinux): Andrea Scarpino <andrea@archlinux.org>
# Maintainer (MSYS2): Ray Donnelly <mingw.android@gmail.com>

_realname=extra-cmake-modules
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=5.12.0
pkgrel=1
pkgdesc='Extra CMake modules (mingw-w64)'
arch=('any')
url='https://projects.kde.org/projects/kdesupport/extra-cmake-modules'
license=('LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-cmake")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-sphinx") # qt5-tools for QtHelp pages
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${_realname}-${pkgver}.tar.xz"
        "set-AUTOSTATICPLUGINS.patch")
md5sums=('e4042cf258b545267d74b0cf7d756150'
         '0d249754247382f3b8dda80d6833aac4')

prepare() {
  mkdir -p build-${CARCH}

  # Apply patches
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/set-AUTOSTATICPLUGINS.patch
}

build() {
  cd build-${CARCH}
  MSYS2_ARG_CONV_EXCL=-DCMAKE_INSTALL_PREFIX= \
    cmake ../${_realname}-${pkgver} \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DCMAKE_BUILD_TYPE=Release \
      -DBUILD_HTML_DOCS=OFF \
      -G'MSYS Makefiles'
  make
}

package() {
  cd build-${CARCH}
  make DESTDIR="${pkgdir}" install
}
