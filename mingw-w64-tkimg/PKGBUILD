# Based on the Arch package
# Don't building with installed jbigkit package as we build tiff without jbigkit support

_pkgname=tkimg
pkgbase=mingw-w64-${_pkgname}
pkgname=${MINGW_PACKAGE_PREFIX}-$_pkgname
_pkgver=1.4
pkgver=${_pkgver}.2
pkgrel=3
pkgdesc="Adds support to Tk for many other Image formats: BMP, XBM, XPM, GIF, PNG, JPEG, TIFF and postscript (mingw-64)"
url="https://tkimg.sourceforge.io/"
arch=('any')
license=('BSD')
depends=(#"${MINGW_PACKAGE_PREFIX}-jbigkit"
         "${MINGW_PACKAGE_PREFIX}-libjpeg"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libtiff"
         "${MINGW_PACKAGE_PREFIX}-tk"
         "${MINGW_PACKAGE_PREFIX}-zlib")
# Uses dtplite for generating the man pages:
makedepends=("${MINGW_PACKAGE_PREFIX}-tcllib")
source=("https://downloads.sourceforge.net/tkimg/${_pkgname}${pkgver}.tar.gz"
        001-compile-tif_jbig.patch
        002-dtplite_tcl.patch)
sha256sums=('38d2c1341f331e0713d0e7774178290bec50ab58268a98eb277d66309322759e'
            'b05c8996d794f7e174037a08537e6596b7a2fbadd226f8e7d6521f4f32cb4a39'
            'eb39aa5f628852b8030041c082e1ede89e2516930125572e5b1f8bc1bcbe8fca')

prepare() {
  cd "${srcdir}/${_pkgname}${_pkgver}"
  #patch -Np1 -i "${srcdir}/001-compile-tif_jbig.patch"
  patch -Np1 -i "${srcdir}/002-dtplite_tcl.patch"
}

build() {
  if check_option "debug" "y"; then
    extra_config+=( --enable-symbols )
  fi

  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"

  ../${_pkgname}${_pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --mandir=${MINGW_PREFIX}/share/man \
    --disable-jbig \
    "${extra_config[@]}"
  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make INSTALL_ROOT="${pkgdir}" install
  rm -f ${pkgdir}/${MINGW_PREFIX}/lib/*.sh
}
