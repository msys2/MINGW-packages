# Based on the Arch package
# Don't building with installed jbigkit package as we build tiff without jbigkit support

_pkgname=tkimg
pkgbase=mingw-w64-${_pkgname}
pkgname=${MINGW_PACKAGE_PREFIX}-$_pkgname
pkgver=1.4.9
pkgrel=1
pkgdesc="Adds support to Tk for many other Image formats: BMP, XBM, XPM, GIF, PNG, JPEG, TIFF and postscript (mingw-64)"
url="https://tkimg.sourceforge.io/"
arch=('any')
license=('BSD')
depends=(#"${MINGW_PACKAGE_PREFIX}-jbigkit"
         "${MINGW_PACKAGE_PREFIX}-libjpeg"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libtiff"
         "${MINGW_PACKAGE_PREFIX}-tk"
         "${MINGW_PACKAGE_PREFIX}-zlib")
# Uses dtplite for generating the man pages:
makedepends=("${MINGW_PACKAGE_PREFIX}-tcllib")
source=("https://downloads.sourceforge.net/tkimg/Img-${pkgver}-Source.tar.gz"
        001-compile-tif_jbig.patch
        002-dtplite_tcl.patch
        003-msys_mingw-no-need-cygpath.patch)
sha256sums=('89aa029d9352de02c483857bc85b27af52f1b77907ee276047e13e894d1e3629'
            'b05c8996d794f7e174037a08537e6596b7a2fbadd226f8e7d6521f4f32cb4a39'
            'eb39aa5f628852b8030041c082e1ede89e2516930125572e5b1f8bc1bcbe8fca'
            '8f26a5670c4e5bea92a3dbbf5b635d342c7b05168ec2619bd80e5e08b134c175')

prepare() {
  cd "${srcdir}/Img-${pkgver}"
  #patch -Np1 -i "${srcdir}/001-compile-tif_jbig.patch"
  patch -Np1 -i "${srcdir}/002-dtplite_tcl.patch"
  patch -Np1 -i "${srcdir}/003-msys_mingw-no-need-cygpath.patch"

  autoreconf -fiv
}

build() {
  if check_option "debug" "y"; then
    extra_config+=( --enable-symbols )
  fi

  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"

  ../Img-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --mandir=${MINGW_PREFIX}/share/man \
    --disable-jbig \
    "${extra_config[@]}"
  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make INSTALL_ROOT="${pkgdir}" install
  rm -f ${pkgdir}/${MINGW_PREFIX}/lib/*.sh
}
