# Maintainer: Martell Malone <martellmalone@gmail.com>

_realname=chipmunk
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
# Please don't update chipmunk until cocos2d-x support 7.x :)
pkgver=6.2.2
pkgrel=1
pkgdesc="A high-performance 2D rigid body physics library (mingw-w64)"
arch=('any')
url="http://chipmunk-physics.net/"
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-cmake")
source=("http://github.com/slembcke/Chipmunk2D/archive/Chipmunk-${pkgver}.tar.gz"
        001-build-fix.patch
        001-fix-install-locations.patch)
md5sums=('e989b9ef00750e56493f91f118705970'
         '457ee3f8fb9a4779d8f7eaaab7eb2697'
         'ae50c9ed773b2858435ca7c6dbb8d524')
options=(staticlibs !buildflags !strip)

prepare() {
  cd ${srcdir}/Chipmunk2D-Chipmunk-${pkgver}
  #patch -p1 -i ${srcdir}/001-build-fix.patch
  patch -p1 -i ${srcdir}/001-fix-install-locations.patch
}

build() {

  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DBUILD_DEMOS=OFF \
    -DBUILD_SHARED=ON \
    -DINSTALL_STATIC=ON \
    ../Chipmunk2D-Chipmunk-${pkgver}

  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR=${pkgdir} install
}
