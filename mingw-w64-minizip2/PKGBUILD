# Maintainer: Andrew Sun <adsun701@gmail.com>

_realname=minizip
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}2")
pkgver=3.0.6
pkgrel=1
pkgdesc="Minizip zlib contribution fork with latest bug fixes that supports PKWARE disk splitting, AES encryption, and IO buffering (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='https://github.com/nmoinvaz/minizip/'
license=('ZLIB')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-cc")
depends=("${MINGW_PACKAGE_PREFIX}-bzip2"
         "${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-zstd")
source=("${_realname}-${pkgver}.tar.gz"::https://github.com/nmoinvaz/minizip/archive/${pkgver}.tar.gz)
sha256sums=('383fa1bdc28c482828a8a8db53f758dbd44291b641182724fda5df5b59cce543')

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  [[ -d "${srcdir}"/shared-${MSYSTEM} ]] && rm -rf "${srcdir}"/shared-${MSYSTEM}
  mkdir -p "${srcdir}"/shared-${MSYSTEM} && cd "${srcdir}"/shared-${MSYSTEM}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
      -G'Ninja' \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DMZ_ZLIB=ON \
      -DMZ_BZIP2=ON \
      -DMZ_LZMA=ON \
      -DMZ_PKCRYPT=ON \
      -DMZ_WZAES=ON \
      -DMZ_ZSTD=ON \
      "${extra_config[@]}" \
      -DBUILD_SHARED_LIBS=ON \
      -DMZ_FETCH_LIBS=OFF \
      -DMZ_PROJECT_SUFFIX="2" \
      ../${_realname}-ng-${pkgver}
  ${MINGW_PREFIX}/bin/cmake --build .

  [[ -d "${srcdir}"/static-${MSYSTEM} ]] && rm -rf "${srcdir}"/static-${MSYSTEM}
  mkdir -p "${srcdir}"/static-${MSYSTEM} && cd "${srcdir}"/static-${MSYSTEM}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
      -G'Ninja' \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DMZ_ZLIB=ON \
      -DMZ_BZIP2=ON \
      -DMZ_LZMA=ON \
      -DMZ_PKCRYPT=ON \
      -DMZ_WZAES=ON \
      -DMZ_ZSTD=ON \
      "${extra_config[@]}" \
      -DBUILD_SHARED_LIBS=OFF \
      -DMZ_FETCH_LIBS=OFF \
      -DMZ_PROJECT_SUFFIX="2" \
      ../${_realname}-ng-${pkgver}
  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  cd "${srcdir}"/static-${MSYSTEM}
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .

  cd "${srcdir}"/shared-${MSYSTEM}
 DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .

  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  for _f in "${pkgdir}${MINGW_PREFIX}"/lib/cmake/minizip2/*.cmake; do
    sed -e "s|${PREFIX_WIN}|\$\{_IMPORT_PREFIX\}|g" -i ${_f}
  done

  install -Dm0644 ${srcdir}/${_realname}-ng-${pkgver}/LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
