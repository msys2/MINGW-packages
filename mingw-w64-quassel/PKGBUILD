# $Id$
# Maintainer (ArchLinux): Vesa Kaihlavirta <vegai@iki.fi>
# Maintainer (ArchLinux): Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Maintainer (MSYS2): Ray Donnelly <mingw.android@gmail.com>

pkgbase=quassel
pkgname=("${MINGW_PACKAGE_PREFIX}-${pkgbase}")
pkgver=0.11.0
pkgrel=2
pkgdesc="Next-generation distributed IRC client (mingw-w64)"
arch=('any')
url="http://quassel-irc.org/"
license=('GPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-qt5")
source=(http://quassel-irc.org/pub/$pkgbase-$pkgver.tar.bz2
        "0001-MinGW-w64-Adopt-FHS-Layout.patch")
sha256sums=('99a191b8bc2a410f7020b890ec57e0be49313f539da9f4843675bb108b0f4504'
            'e7bdf5b0e613dc001f50f6e017abeabb48e4f3409c8cbe3e6d8154c8c6f72cf0')

prepare() {
  cd "${srcdir}"/quassel-${pkgver}
  patch -p1 -i "${srcdir}"/0001-MinGW-w64-Adopt-FHS-Layout.patch
}

build() {
  cd "${srcdir}"
  [ -d build-${CARCH} ] && rm -rf build-${CARCH}
  mkdir build-${CARCH}

  # -DWIN32_LEAN_AND_MEAN=1 because "interface" is used in:
  # /mingw64/include/QtDBus/qdbusconnection.h
  cd "${srcdir}"/build-${CARCH}
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DCMAKE_CXX_FLAGS="${CFLAGS} -DWIN32_LEAN_AND_MEAN=1" \
      -DUSE_QT5=ON \
      -DWITH_KDE=OFF \
      -DWITH_OPENSSL=ON \
      -DCMAKE_BUILD_TYPE="Release" \
      -DWANT_CORE=OFF \
      -DWANT_QTCLIENT=OFF \
      -DWANT_MONO=ON \
      ../quassel-${pkgver}/ \
      -Wno-dev \
      -G'MSYS Makefiles'
  make
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make DESTDIR="${pkgdir}" install
}
