# Maintainer: WarBorinks

_realname=pybind11-json
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.2.15
pkgrel=1
pkgdesc="Using nlohmann::json with pybind11 (mingw-w64)"
arch=("any")
mingw_arch=("mingw64" "ucrt64" "clang64")
url="https://github.com/pybind/pybind11_json"
license=("spdx:BSD-3-Clause")
depends=("${MINGW_PACKAGE_PREFIX}-pybind11"
         "${MINGW_PACKAGE_PREFIX}-nlohmann-json")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cc")
source=("${url}/archive/refs/tags/0.2.15.tar.gz")
sha256sums=('9a4a1494549a2db27bc4c5a16743a5c88ebc18e13c73019c56f399ae0310baf2')

build() {
  cd "pybind11_json-${pkgver}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DCMAKE_BUILD_TYPE=Release \
      -B build
  cmake --build build
}

package() {
  cd "pybind11_json-${pkgver}"

  DESTDIR="${pkgdir}" cmake --install build

  local WINPREFIX=$(cygpath -wm ${MINGW_PREFIX})
  find "${pkgdir}${MINGW_PREFIX}/share/cmake" -type f -name '*.cmake' \
    -exec sed -i -e "s|${WINPREFIX}|\$\{_IMPORT_PREFIX\}|g" {} \;
}
