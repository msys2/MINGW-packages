_realname=blackmagic-decklink-sdk
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=10.9.5
pkgrel=1
pkgdesc='Blackmagic DeckLink SDK (mingw-w64)'
arch=('any')
url='https://www.blackmagicdesign.com/support/family/capture-and-playback/'
license=('custom')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config" "curl")
source=('LICENSE')
sha256sums=('25fafed93b109e5bf1d107f5e9a629c30f7bf3568f1c40c5d4554140c903947b')

_srcfile="Blackmagic_DeckLink_SDK_${pkgver}.zip"
_downloadid='627a3b1bcfdb4996a2bcc0d70af9ee3b'
_referid='c4985c77ccb24b48b4be5ae7e309e92d'
_srcurl="https://www.blackmagicdesign.com/api/register/us/download/${_downloadid}"
_expected_sha256sum='1aa0fd45714ee955badbe986254cd6c7537f25e6ff596cbc1b9499bf5dcaf32b'

_useragent="User-Agent: Mozilla/5.0 (X11; Windows x86_64) \
                        AppleWebKit/537.36 (KHTML, like Gecko) \
                        Chrome/60.0.3112.90 \
                        Safari/537.36"
_reqjson="{ \
    \"platform\": \"Windows\", \
    \"country\": \"us\", \
    \"firstname\": \"Mingw\", \
    \"lastname\": \"w64\", \
    \"email\": \"someone@windows.org\", \
    \"phone\": \"202-555-0194\", \
    \"state\": \"New York\", \
    \"city\": \"MSYS2\", \
    \"hasAgreedToTerms\": true, \
    \"product\": \"Desktop Video ${pkgver} SDK\" \
}"
_useragent="$(printf '%s' "$_useragent" | sed 's/[[:space:]]\+/ /g')"
_reqjson="$(  printf '%s' "$_reqjson"   | sed 's/[[:space:]]\+/ /g')"

_exit_makepkg() {
    printf '%s\n' "error: failed to ${1} ${_srcfile}"
    exit 1
}

prepare() {
    # check if decklink sdk zip file was already downloaded
    if ! [ -f "../${_srcfile}" ] 
    then
        # download decklink sdk zip file from website
        msg2 "Downloading ${_srcfile} from website..."
        curl \
            -o "../${_srcfile}" \
            -H 'Host: sw.blackmagicdesign.com' \
            -H 'Upgrade-Insecure-Requests: 1' \
            -H "$_useragent" \
            -H 'Accept: text/html,application/xhtml+xml,application/xml;q=0.9,image/webp,image/apng,*/*;q=0.8' \
            -H 'Accept-Language: en-US,en;q=0.8' \
            --compressed \
            "$(curl \
                -s \
                -H 'Host: www.blackmagicdesign.com' \
                -H 'Accept: application/json, text/plain, */*' \
                -H 'Origin: https://www.blackmagicdesign.com' \
                -H "$_useragent" \
                -H 'Content-Type: application/json;charset=UTF-8' \
                -H "Referer: https://www.blackmagicdesign.com/support/download/${_referid}/Windows" \
                -H 'Accept-Language: en-US,en;q=0.8' \
                -H 'Cookie: _ga=GA1.3.853760154.1498322710; _gid=GA1.3.1693019090.1500064482' \
                --data-binary "$_reqjson" \
                --compressed \
                "$_srcurl" \
            )" || _exit_makepkg 'download'
    else
        msg2 "Found ${_srcfile}"
    fi
    
    # check integrity of the decklink sdk zip file (file validation)
    msg2 "Validating ${_srcfile} with sha256sum..."
    local _real_sha256sum="$(openssl dgst -sha256 "../${_srcfile}" || _exit_makepkg 'calculate SHA256 of')"
    _real_sha256sum="${_real_sha256sum##* }"
    printf '%s' "       ${_srcfile} ... "
    if [ "$_expected_sha256sum" = "$_real_sha256sum" ] 
    then
        printf '%s\n' 'Passed'
    else
        printf '%s\n' 'FAILED'
        exit 1
    fi
    
    # create symbolic link of decklink sdk zip file in $srcdir
    ln -sf "../${_srcfile}" "${srcdir}/${_srcfile}" || _exit_makepkg 'create symbolic link of'
    
    # extract decklink sdk zip file
    msg2 "Extracting ${_srcfile} with bsdtar..."
    bsdtar -xf "${_srcfile}" || _exit_makepkg 'extract'
	
}

package() {
    # directories creation
    mkdir -p "${pkgdir}/${MINGW_PREFIX}/include"
    mkdir -p "${pkgdir}/${MINGW_PREFIX}/share/doc/${_realname}"
    
    # convert idl file to header file
    cd "${srcdir}/Blackmagic DeckLink SDK ${pkgver}/Win/include"
    ${MINGW_PREFIX}/bin/widl -h "DeckLinkAPI.idl" -o "${pkgdir}/${MINGW_PREFIX}/include/DeckLinkAPI.h"
   
    
    # includes
    cd "${srcdir}/Blackmagic DeckLink SDK ${pkgver}/Win/include"
    install -D -m644 *.h "${pkgdir}/${MINGW_PREFIX}/include" 
    
    # documentation
    cd "${srcdir}/Blackmagic DeckLink SDK ${pkgver}"
    install -D -m644 *.pdf "${pkgdir}/${MINGW_PREFIX}/share/doc/${_realname}"
    
    # license
    install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
