# Maintainer: @naveen521kk on Github Naveen M K <naveen521kk@gmail.com>

_realname=poetry-core
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.0.8
pkgrel=3
pkgdesc="Poetry PEP 517 Build Backend (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://github.com/python-poetry/poetry-core"
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-build")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-pep517"
              "${MINGW_PACKAGE_PREFIX}-python-pytest-mock"
              "${MINGW_PACKAGE_PREFIX}-python-pytest")
depends=("${MINGW_PACKAGE_PREFIX}-python")
source=("${_realname}-$pkgver.tar.gz"::"https://github.com/python-poetry/${_realname}/archive/${pkgver}.tar.gz"
        "0002-msys-git-support.patch")
sha256sums=('b8795006e169a24e0e114ba1c03217716cd190e8d2716c796ddc5ed0382dbca8'
            '189a2ef48a9e952cc3d1b7c46c8a9745afe0a682d2c01cb067c41bb79edd6845')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -p1 -i ${srcdir}/0002-msys-git-support.patch

  # be a proper namespace package, python3-only
  rm poetry/__init__.py
}

check(){
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYSTEM= ${MINGW_PREFIX}/bin/python -m pytest || echo "test failed"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -D -m644 LICENSE "${pkgdir}${MINGW_PREFIX}"/share/licenses/python-${_realname}/LICENSE
}
