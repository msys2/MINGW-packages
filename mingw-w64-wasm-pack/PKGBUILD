# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=wasm-pack
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.12.1
pkgrel=2
pkgdesc="Your favorite rust -> wasm workflow tool! (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url="https://github.com/rustwasm/wasm-pack"
msys2_references=(
  'archlinux: wasm-pack'
  'purl: pkg:cargo/wasm-pack'
)
license=('spdx:MIT OR Apache-2.0')
depends=(
  "${MINGW_PACKAGE_PREFIX}-curl"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-rust"
  "${MINGW_PACKAGE_PREFIX}-mdbook"
  "${MINGW_PACKAGE_PREFIX}-pkgconf"
  "${MINGW_PACKAGE_PREFIX}-bzip2"
  "${MINGW_PACKAGE_PREFIX}-zstd"
)
source=("${url}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('afa164fec0b119e2c47e38aad9e83351cb414e8ca3c062de292ec8008a45ac09')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  export ZSTD_SYS_USE_PKG_CONFIG=1
  ${MINGW_PREFIX}/bin/cargo build \
    --release \
    --frozen

  cd docs
  ${MINGW_PREFIX}/bin/mdbook build
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  export ZSTD_SYS_USE_PKG_CONFIG=1
  ${MINGW_PREFIX}/bin/cargo test \
    --release \
    --frozen
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  install -Dm755 "target/release/wasm-pack.exe" "${pkgdir}${MINGW_PREFIX}/bin/wasm-pack.exe"

  install -d "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/html/"
  cp -r -t "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/html/" docs/book/*

  install -d "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/"
  cp -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/" LICENSE-{APACHE,MIT}
}
