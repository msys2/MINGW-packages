# Maintainer: Pellegrino Prevete <pellegrinoprevete@gmail.com>

_realname=libhandy
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.0.6
pkgrel=1
pkgdesc="A library full of GTK+ widgets for mobile phones (mingw-w64)"
url="https://source.puri.sm/Librem5/libhandy"
license=("LGPL2.1")
arch=('any')
depends=("${MINGW_PACKAGE_PREFIX}-gtk3")
makedepends=("${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-vala"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection")
options=('strip' 'staticlibs' '!debug')
source=('https://source.puri.sm/Librem5/libhandy/-/archive/v0.0.6/libhandy-v0.0.6.tar.gz')
sha256sums=('9d22e94fbc16043b59d9e2ea6e12a2ecc7ce821943379c1f024afcabb56674a4')

build() {
  rm -rf build
  ${MINGW_PREFIX}/bin/meson "libhandy-v${pkgver}" build -Dexamples=false -Dgtk_doc=false -Dglade_catalog=false
  ${MINGW_PREFIX}/bin/ninja -C build
}

package() {
  DESTDIR="${pkgdir}${MINGW_PREFIX}" ${MINGW_PREFIX}/bin/ninja -C build install
}
