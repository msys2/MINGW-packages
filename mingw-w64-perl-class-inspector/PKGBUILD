# Maintainer: @naveen521kk on Github, Naveen M K <naveen521kk@gmail.com>

_perlname=Class-Inspector
_realname="${_perlname,,}"
pkgbase=mingw-w64-perl-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-perl-${_realname}")
pkgver=1.36
pkgrel=1
pkgdesc="Get information about a class and its structure (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://metacpan.org/release/${_perlname}"
groups=("${MINGW_PACKAGE_PREFIX}-perl-modules")
provides=(
  "${MINGW_PACKAGE_PREFIX}-perl-class-inspector-functions"
)
depends=(
  "${MINGW_PACKAGE_PREFIX}-perl"
  "${MINGW_PACKAGE_PREFIX}-perl-base"
  "${MINGW_PACKAGE_PREFIX}-perl-file-spec"
)
options=('!emptydirs')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-cc"
  $([[ ${MINGW_PACKAGE_PREFIX} != *-clang-* ]] || \
    echo "${MINGW_PACKAGE_PREFIX}-gcc-compat")
)
license=('perl_5')
source=(
  'https://cpan.metacpan.org/authors/id/P/PL/PLICEASE/Class-Inspector-1.36.tar.gz'
  'patchmakefile.py'
)
sha256sums=('cc295d23a472687c24489d58226ead23b9fdc2588e522f0b5f0747741700694e'
            '00fe5ced42aa635714909a8eba1347a6c132011a9bce68e9c75b27ebd64628f2')

build() {
  cd "$srcdir/${_perlname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL
  export pkgdir=$pkgdir
  python $srcdir/patchmakefile.py
  mingw32-make
}

package() {
  cd "$srcdir/${_perlname}-${pkgver}"
  mingw32-make DESTDIR="${pkgdir}/" install
}
