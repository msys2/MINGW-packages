# Contributor: Oleg A. Khlybov <fougas@mail.ru>

_realname=msmpi
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=10.1.1
pkgrel=1
pkgdesc="Microsoft MPI SDK (mingw-w64)"
arch=('any')
url="https://docs.microsoft.com/en-us/message-passing-interface/microsoft-mpi"

license=('MIT')

makedepends=("${MINGW_PACKAGE_PREFIX}-binutils")
depends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-gcc-fortran")
options=('strip')

rootdir=`pwd`

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}
  mkdir -p bin include lib
  # Import library
  ${MINGW_PREFIX}/bin/dlltool -k -d "$rootdir/$CARCH/lib/msmpi.def" -l lib/libmsmpi.a
  # Headers
  cp $rootdir/include/* $rootdir/$CARCH/include/* include
  # Compiler wrappers
  mpic="$rootdir/mpi.c"
  cflags="-s -O2 -DNDEBUG"
  gcc ${cflags} -o bin/mpicc.exe -DCC "${mpic}"
  gcc ${cflags} -o bin/mpicxx.exe -DCXX "${mpic}"
  cp bin/mpicxx.exe bin/mpic++.exe
  gcc ${cflags} -o bin/mpifort.exe -DFC "${mpic}"
  cp bin/mpifort.exe bin/mpif77.exe
  cp bin/mpifort.exe bin/mpif90.exe
}

package() {
  cd "${srcdir}/build-${CARCH}"
  mkdir -p "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig"
  cp -r * "${pkgdir}${MINGW_PREFIX}"
  echo "
    prefix=${MINGW_PREFIX}
    libdir=\${prefix}/lib
    includedir=\${prefix}/include
    Name: ${_realname}
    URL: ${url}
    Version: ${pkgver}
    Description: ${pkgdesc}
    Cflags: -I\${includedir}
    Libs: -L\${libdir} -l${_realname}
  " | sed '/^\s*$/d;s/^\s*//' > "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/${_realname}.pc"
}
