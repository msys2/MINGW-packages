# Maintainer: Philipp Smirnov <psmirnov@airmail.cc>

_realname=gnatcoll-sql
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.0
pkgrel=1

pkgdesc="GNAT Component Collection - SQL database interface (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
msys2_references=(
  'aur: gnatcoll-sql'
)

url="https://github.com/AdaCore/gnatcoll-db"
license=('GPL-3.0-or-later WITH GCC-exception-3.1')
makedepends=("${MINGW_PACKAGE_PREFIX}-gprbuild")
depends=("${MINGW_PACKAGE_PREFIX}-gnatcoll-core")
_repo_name=gnatcoll-db
source=("https://github.com/AdaCore/${_repo_name}/archive/v${pkgver}/${_repo_name}-${pkgver}.tar.gz")
sha256sums=('d284ea84051a2338cc467e7b58ea10334c423dadf3402b473a82a7a5b7f768c6')

_makeopts=(BUILD=PROD PROCESSORS="$(nproc)" ENABLE_SHARED=yes)

build() {
  cd "${srcdir}/${_repo_name}-${pkgver}/sql"
  make "${_makeopts[@]}"
}

package() {
  cd "${srcdir}/${_repo_name}-${pkgver}/sql"
  make install "${_makeopts[@]}" prefix="${pkgdir}${MINGW_PREFIX}"
  install -D -m644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" \
    "${srcdir}/${_repo_name}-${pkgver}"/COPYING*
}
