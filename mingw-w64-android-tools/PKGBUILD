# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=android-tools
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=33.0.3
_tag=${pkgver}p2
pkgrel=1
pkgdesc='Android platform tools (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='http://tools.android.com/'
license=('spdx:Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-brotli"
         "${MINGW_PACKAGE_PREFIX}-libusb"
         "${MINGW_PACKAGE_PREFIX}-lz4"
         "${MINGW_PACKAGE_PREFIX}-pcre2"
         "${MINGW_PACKAGE_PREFIX}-protobuf"
         "${MINGW_PACKAGE_PREFIX}-zstd")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-go"
             "${MINGW_PACKAGE_PREFIX}-gtest"
             "${MINGW_PACKAGE_PREFIX}-nasm"
             "${MINGW_PACKAGE_PREFIX}-ninja")
source=("https://github.com/nmeum/android-tools/releases/download/${_tag}/${_realname}-${_tag}.tar.xz"
        "https://github.com/msys2/adbwinapi/releases/download/${pkgver}/adbwinapi.zip"
        "https://github.com/msys2/adbwinapi/releases/download/${pkgver}/adbwinapi_sources.zip"
        "AdbWinApi.def"
        "0001-android-tools-cmake.patch"
        "0002-android-tools-vendor.patch")
noextract=("${_realname}-${_tag}.tar.xz")
sha256sums=('6bf6b52d7389e79fc92b63cc206451ee42fc4f7da769d76922193e98d75f5604'
            '34edca31f99e744bd396b09639955b68f234a0ebea88bd3d22b721c35dd056b1'
            '465a8cce0e93f79e5b9b44eeb0eb7239e8de3a09bc8cc3f7dce46d0a61699a92'
            'e6fc31c148e120fc69be53e0464434c492ab0eaa0dbc8f18dd3c19eb2dd2577b'
            '1130b36340f640639a040aa2514ae477b5f5f060dd7c02be4100676c78543eb8'
            '1f1a0567ef6f0f895c9f18202133e344350208c1aac91384ea9b119dd75ec9d3')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -p1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  tar -xf "${_realname}-${_tag}.tar.xz" || true

  cd "${srcdir}/${_realname}-${_tag}"
  cp "${srcdir}"/AdbWinApi.def ./vendor/
  cp -r "${srcdir}"/development ./vendor/

  apply_patch_with_msg \
    0001-android-tools-cmake.patch \
    0002-android-tools-vendor.patch
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DCMAKE_CXX_FLAGS="${CXXFLAGS}" \
      -DCMAKE_C_FLAGS="${CFLAGS}" \
      ../${_realname}-${_tag}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .
  install -Dm644 "${srcdir}/${_realname}-${_tag}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"

  if [ "${MSYSTEM}" = "CLANGARM64" ]; then
    cp -r "${srcdir}/arm64"/*.dll "${pkgdir}${MINGW_PREFIX}/bin"
  elif  [ "${CARCH}" = "x86_64" ]; then
    cp -r "${srcdir}/x64"/*.dll "${pkgdir}${MINGW_PREFIX}/bin"
  else
    cp -r "${srcdir}/win32"/*.dll "${pkgdir}${MINGW_PREFIX}/bin"
  fi
}
