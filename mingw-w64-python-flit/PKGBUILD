# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=flit
pkgbase=mingw-w64-python-${_realname}
pkgname=(${MINGW_PACKAGE_PREFIX}-python-$_realname)
pkgver=3.9.0
pkgrel=3
pkgdesc='Simplified packaging of Python modules (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/takluyver/flit'
license=('spdx:BSD-3-Clause')
depends=(
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-python-flit-core"
  "${MINGW_PACKAGE_PREFIX}-python-requests"
  "${MINGW_PACKAGE_PREFIX}-python-tomli-w"
  "${MINGW_PACKAGE_PREFIX}-python-docutils"
)
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-coverage"
              "${MINGW_PACKAGE_PREFIX}-python-responses"
              "${MINGW_PACKAGE_PREFIX}-python-testpath"
              "${MINGW_PACKAGE_PREFIX}-python-pytest"
              "${MINGW_PACKAGE_PREFIX}-python-pytest-cov")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-pygments-github-lexers"
             "${MINGW_PACKAGE_PREFIX}-python-sphinx"
             "${MINGW_PACKAGE_PREFIX}-python-sphinx_rtd_theme"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-sphinxcontrib-github-alt")
options=('!strip')
source=("$url/archive/$pkgver/${_realname}-$pkgver..tar.gz")
sha256sums=('55e28d0953726c4c744f02779b85f733f65da5bffb6a5d2fe11072d19ca2d148')

prepare() {
  cd "${_realname}-${pkgver}"
  rm -f tests/test_sdist.py
  cd "flit_core"

  python build_dists.py
}

build() {
  cd "${_realname}-${pkgver}"

  PYTHONPATH=flit_core python -m flit build --format wheel

  cd doc
  make dirhtml man
}

check() {
  cd "${_realname}-${pkgver}"

  # 9 failed, 172 passed, 5 skipped, 1 warning in 13.73s
  PYTHONPATH=flit_core ${MINGW_PREFIX}/bin/python -m pytest  --cov=flit --cov=flit_core/flit_core || true
}

package() {
  cd "${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    python -m installer --prefix=${MINGW_PREFIX} --destdir="${pkgdir}" dist/*.whl

  install -Dm 644 LICENSE "$pkgdir${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSE

  mkdir -p "$pkgdir${MINGW_PREFIX}"/share/doc/${_realname}
  cp -r doc/_build/dirhtml/* "$pkgdir${MINGW_PREFIX}"/share/doc/${_realname}
  
  mkdir -p "$pkgdir${MINGW_PREFIX}"/share/man/man1
  cp -r doc/_build/man/*.1 "$pkgdir${MINGW_PREFIX}"/share/man/man1
}
