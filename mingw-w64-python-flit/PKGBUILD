# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=flit
pkgbase=mingw-w64-python-${_realname}
pkgname=(${MINGW_PACKAGE_PREFIX}-python-$_realname{,-core})
pkgver=3.6.0
pkgrel=2
pkgdesc='Simplified packaging of Python modules (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://github.com/takluyver/flit'
license=('spdx:BSD-3-Clause')
options=('!strip')
depends=("${MINGW_PACKAGE_PREFIX}-python")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-coverage"
              "${MINGW_PACKAGE_PREFIX}-python-responses"
              "${MINGW_PACKAGE_PREFIX}-python-testpath"
              "${MINGW_PACKAGE_PREFIX}-python-pytest"
              "${MINGW_PACKAGE_PREFIX}-python-pytest-cov")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-pygments-github-lexers"
             "${MINGW_PACKAGE_PREFIX}-python-sphinx"
             "${MINGW_PACKAGE_PREFIX}-python-sphinx_rtd_theme"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-sphinxcontrib-github-alt")
source=("${_realname}-$pkgver.tar.gz"::"$url/archive/$pkgver.tar.gz")
sha256sums=('2a0495922f6b964adbe59f19f09e4c660dfd0cb788df8946e20abf6eb9390492')

prepare() {
  cd "${_realname}-${pkgver}"
  rm -f tests/test_sdist.py
  cd "flit_core"

  python build_dists.py
}

build() {
  cd "${_realname}-${pkgver}"

  PYTHONPATH=flit_core python -m flit build --format wheel

  cd doc
  make dirhtml man
}

check() {
  cd "${_realname}-${pkgver}"

  # 9 failed, 172 passed, 5 skipped, 1 warning in 13.73s
  PYTHONPATH=flit_core ${MINGW_PREFIX}/bin/python -m pytest  --cov=flit --cov=flit_core/flit_core || true
}

package_python-flit() {
  pkgdesc+=' (mingw-w64)'
  depends+=("${MINGW_PACKAGE_PREFIX}-python-flit-core"
            "${MINGW_PACKAGE_PREFIX}-python-requests"
            "${MINGW_PACKAGE_PREFIX}-python-tomli"
            "${MINGW_PACKAGE_PREFIX}-python-tomli-w"
            "${MINGW_PACKAGE_PREFIX}-python-docutils")

  cd "${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    python -m installer --prefix=${MINGW_PREFIX} --destdir="${pkgdir}" dist/*.whl

  install -Dm 644 LICENSE "$pkgdir${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSE

  mkdir -p "$pkgdir${MINGW_PREFIX}"/share/doc/${_realname}
  cp -r doc/_build/dirhtml/* "$pkgdir${MINGW_PREFIX}"/share/doc/${_realname}
  
  mkdir -p "$pkgdir${MINGW_PREFIX}"/share/man/man1
  cp -r doc/_build/man/*.1 "$pkgdir${MINGW_PREFIX}"/share/man/man1
}

package_python-flit-core() {
  pkgdesc+=' (core backend) (mingw-w64)'

  cd "${_realname}-${pkgver}/flit_core"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    python -m installer --prefix=${MINGW_PREFIX} --destdir="${pkgdir}" dist/*.whl

  install -Dm 644 ../LICENSE "$pkgdir${MINGW_PREFIX}"/share/licenses/${_realname}-core/LICENSE

  # installing tests to a subdir of your project might not conflict the toplevel, but
  # don't do it anyway...
  rm -rf "$pkgdir"${MINGW_PREFIX}/lib/python*/site-packages/flit_core/tests/
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
