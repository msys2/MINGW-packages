# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=pycurl
pkgbase=mingw-w64-python-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-${_realname}
pkgver=7.45.7
pkgrel=3
pkgdesc='A Python 3.x interface to libcurl (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/pycurl/pycurl'
msys2_changelog_url='http://pycurl.io/docs/latest/release-notes.html'
msys2_references=(
  'archlinux: python-pycurl'
  'gentoo: dev-python/pycurl'
  'purl: pkg:pypi/pycurl'
)
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-openssl")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-pkgconf")
source=(https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz
        001-pycurl-setup-fix.patch
        002-pycurl-docdir-path-fix.patch)
sha256sums=('9d43013002eab2fd6d0dcc671cd1e9149e2fc1c56d5e796fad94d076d6cb69ef'
            'cd2ea373f576b35967f99e6e183a92faf3fb0908456be2157cbf4ec44ba648d8'
            '4c9cc653e3037cc89b851958cb193d05f7580d654cf57a15fe69f5e4a3e7727a')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/001-pycurl-setup-fix.patch
  patch -p1 -i "${srcdir}"/002-pycurl-docdir-path-fix.patch
}

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation \
    -C--with-openssl \
    -C--curl-config=${MINGW_PREFIX}/bin/curl-config
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 COPYING-MIT "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
