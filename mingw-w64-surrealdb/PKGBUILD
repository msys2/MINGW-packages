# Maintainer: Maksim Bondarenkov <maksapple@gmail.com>

_realname=surrealdb
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.0.1
pkgrel=1
pkgdesc="A scalable, distributed, collaborative, document-graph database, for the realtime web (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://surrealdb.com'
msys2_repository_url='https://github.com/surrealdb/surrealdb'
msys2_references=(
  'aur: surrealdb'
  'purl: pkg:cargo/surrealdb'
)
license=('spdx:BUSL-1.1')
depends=("${MINGW_PACKAGE_PREFIX}-bzip2"
         "${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-zstd")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-clang"
             "${MINGW_PACKAGE_PREFIX}-nasm"
             "${MINGW_PACKAGE_PREFIX}-rust-bindgen"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-cargo-edit"
             'git')
options=('!strip')
source=("git+${msys2_repository_url}#tag=v${pkgver}"
        "bzip2-sys.tar.gz::https://crates.io/api/v1/crates/bzip2-sys/0.1.13+1.0.8/download"
        "zstd-sys.tar.gz::https://crates.io/api/v1/crates/zstd-sys/2.0.16+zstd.1.5.7/download"
        "bzip2-sys-use-pkgconfig.patch"
        "zstd-sys-remove-statik.patch")
sha256sums=('64ae62becdeaad210d75a511bb0336526821ef2842685ca50f37206ec1c73505'
            '225bff33b2141874fe80d71e07d6eec4f85c5c216453dd96388240f96e1acc14'
            '91e19ebc2adc8f83e43039e79776e3fda8ca919132d68a1fed6a5faca2683748'
            'f73309fb98b89b2085bccee6ffb66820082628d096bc989922b25d33a1f5f378'
            '48f4900ceb02d3aaf9a1020f33d56629156e96759f456c0e7ca18bfcf910767b')

prepare() {
  cd "${_realname}"

  patch -d ../bzip2-sys-0.1.13+1.0.8 -i ../bzip2-sys-use-pkgconfig.patch
  patch -d ../zstd-sys-2.0.16+zstd.1.5.7 -i ../zstd-sys-remove-statik.patch
  cat >> Cargo.toml <<END

[patch.crates-io]
bzip2-sys.path = "../bzip2-sys-0.1.13+1.0.8"
zstd-sys.path = "../zstd-sys-2.0.16+zstd.1.5.7"
END

  rm rust-toolchain.toml
  cargo update -p aws-lc-rs --precise 1.15.4
  # cargo update -p bzip2-sys -p zstd-sys
  cargo fetch --locked --target "${RUST_CHOST}"
}

build() {
  cd "${_realname}"

  export BINDGEN_EXTRA_CLANG_ARGS="--target=${CARCH}-pc-windows-gnu"
  export LIBCLANG_PATH="${MINGW_PREFIX}/bin"
  export ZSTD_SYS_USE_PKG_CONFIG=1
  export RUSTFLAGS="${RUSTFLAGS/+crt-static/-crt-static}"
  # workaround for multiple definitions in wasm-opt crate
  if [[ ${MINGW_PACKAGE_PREFIX} != *-clang-* ]]; then
    export RUSTFLAGS+=" -C link-arg=-Wl,--allow-multiple-definition"
  fi
  cargo build --release --frozen
}

package() {
  cd "${_realname}"

  install -Dm755 target/release/surreal "${pkgdir}${MINGW_PREFIX}/bin/surreal"
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
