# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libxslt
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.1.30
pkgrel=2
pkgdesc="XML stylesheet transformation library (mingw-w64)"
arch=('any')
url="http://xmlsoft.org/XSLT/"
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-libgcrypt")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")
options=('strip' 'staticlibs')
install=${_realname}-${CARCH}.install
source=("http://xmlsoft.org/sources/${_realname}-${pkgver}.tar.gz"
        "libxslt-1.1.26-w64.patch"
        "libxslt-1.1.27-disable_static_modules.patch"
        "libxslt-1.1.28-win32-shared.patch"
        "libxslt.m4-libxslt-1.1.26.patch"
        '0002-python-linking-on.mingw.patch'
        '0003-fix-concurrent-directory-creation.all.patch'
        '0004-add-missing-include-for-python.all.patch'
        '0007-use-mingw-C99-compatible-functions-{v}snprintf.patch'
        '0008-fix-warnings.patch')
sha256sums=('ba65236116de8326d83378b2bd929879fa185195bc530b9d1aba72107910b6b3'
            '887d7f22b2af770340d8f5b29bb490504d1ca904e289f03f9800f08f2b36e9ac'
            'e62f27315f2a25db3b6bfe5695d885998aeb3ad87cce6aebbd035c404fe8d378'
            '94fc53c9563221c0fa2e896d294d33951fa011ff3126f469c7a5d2c0aecdbe2d'
            'c02cd8437064db0921c08e63245c403dad12410d2997715aa7aa36600bfaa895'
            '2090ec6269e8e8b68a36922fca24f334872b702d81be8c9436fd9b25f506fc42'
            'fc23bd3c9e62a28cb1c020cebc83ca7532a5a7a21c2e4748b72d13ac7716a851'
            '1da234751e2bd337a0991a0f90272bbf83b6cbd8945208825fa3d9dd580020e6'
            '06e99e9c48fedd23fa9be704f227c437356aac14dc402a4ddf73b888d849cdd3'
            'b848692b0c59259692b94923c0c8157b95df97e3c5ba7881c0adb2b047c168db')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _fname in "$@"
  do
    msg2 "Applying ${_fname}"
    patch -Nbp1 -i "${srcdir}"/${_fname}
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ] || [ -d $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  apply_patch_with_msg "libxslt-1.1.26-w64.patch" \
    "libxslt-1.1.27-disable_static_modules.patch" \
    "libxslt-1.1.28-win32-shared.patch" \
    "libxslt.m4-libxslt-1.1.26.patch" \
    "0002-python-linking-on.mingw.patch" \
    "0003-fix-concurrent-directory-creation.all.patch" \
    "0004-add-missing-include-for-python.all.patch" \
    "0007-use-mingw-C99-compatible-functions-{v}snprintf.patch"  \
    "0008-fix-warnings.patch"

  autoreconf -fi
}

build() {
  [[ -d "${srcdir}/build-${CARCH}" ]] && rm -rf "${srcdir}/build-${CARCH}"
  mkdir -p "${srcdir}/build-${CARCH}" && cd "${srcdir}/build-${CARCH}"
 ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --with-crypto \
    --with-plugins \
    --with-libxml-prefix=${MINGW_PREFIX} \
    --with-python=${MINGW_PREFIX}/bin/python2

  # Hack for mingw python
  local PREFIX_DEPS=$(cygpath -am ${MINGW_PREFIX})
  find . -type f -name "Makefile" -exec sed -i "s|${PREFIX_DEPS}|${MINGW_PREFIX}|g" {} \;
  CC=${MINGW_PREFIX}/bin/gcc make
}

package() {
  export MSYS2_ARG_CONV_EXCL="-//OASIS"
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install

  rm "${pkgdir}${MINGW_PREFIX}"/lib/python2.7/site-packages/*.a

  local PREFIX_DEPS=$(cygpath -am ${MINGW_PREFIX})
  pushd "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig" > /dev/null
  sed -s "s|${PREFIX_DEPS}|${MINGW_PREFIX}|g" -i ./libexslt.pc
  sed -s "s|${PREFIX_DEPS}|${MINGW_PREFIX}|g" -i ./libxslt.pc
  popd > /dev/null
}

