# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=pidgin
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-hg"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=r36348.9d62816617b9
pkgrel=1
pkgdesc="Multi-protocol instant messaging client (mingw-w64)"
arch=('any')
url="http://pidgin.im/"
license=("GPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "doxygen"
             "gtk-doc"
             "mercurial")
depends=("${MINGW_PACKAGE_PREFIX}-ca-certificates"
         "${MINGW_PACKAGE_PREFIX}-farstream"
         "${MINGW_PACKAGE_PREFIX}-freetype"
         "${MINGW_PACKAGE_PREFIX}-fontconfig"
         "${MINGW_PACKAGE_PREFIX}-gettext"
         "${MINGW_PACKAGE_PREFIX}-gnutls"
         "${MINGW_PACKAGE_PREFIX}-gsasl"
         "${MINGW_PACKAGE_PREFIX}-gtk3"
         "${MINGW_PACKAGE_PREFIX}-gtkspell"
         "${MINGW_PACKAGE_PREFIX}-hicolor-icon-theme"
         "${MINGW_PACKAGE_PREFIX}-libgadu"
         "${MINGW_PACKAGE_PREFIX}-libidn"
         "${MINGW_PACKAGE_PREFIX}-nss"
         "${MINGW_PACKAGE_PREFIX}-ncurses"
         "${MINGW_PACKAGE_PREFIX}-webkitgtk3"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=(!libtool strip staticlibs)

source=(${_realname}::"hg+https://hg.pidgin.im/pidgin/main"
        #"http://downloads.sourceforge.net/${_realname}/${_realname}-${pkgver}.tar.bz2"
        )
md5sums=('SKIP')

pkgver() {
  cd "$_realname"
  printf "r%s.%s" "$(hg identify -n)" "$(hg identify -i)"
}

prepare() {
  cd ${_realname}
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "${srcdir}/${_realname}"
  sed -i "s#env python#env python2#" */plugins/*.py
  sed -i "s#env python#env python2#" libpurple/purple-{remote,notifications-example,url-handler}
  ./configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --sysconfdir=${MINGW_PREFIX}/etc \
      --disable-schemas-install \
      --disable-meanwhile \
      --disable-screensaver \
      --disable-sm \
      --disable-nm \
      --disable-vv \
      --disable-tcl \
      --with-python=${MINGW_PREFIX}/bin/python2 \
      --with-system-ssl-certs=${MINGW_PREFIX}/ssl/certs \
      --with-win32-dirs=fhs
      #--enable-cyrus-sasl

  make
}

package() {
  cd "${srcdir}/${_realname}"
  make DESTDIR="${pkgdir}" install
}
