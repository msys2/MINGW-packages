# Maintainer: Jeroen Ooms <jeroen@berkeley.edu>

_realname=clustal-omega
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.2.4
pkgrel=4
pkgdesc="Fast, scalable generation multiple sequence alignments (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=("custom")
url="http://www.clustal.org/omega"
source=("http://www.clustal.org/omega/${_realname}-${pkgver}.tar.gz")
sha256sums=('8683d2286d663a46412c12a0c789e755e7fd77088fb3bc0342bb71667f05a3ee')
depends=("${MINGW_PACKAGE_PREFIX}-argtable"
         "${MINGW_PACKAGE_PREFIX}-gcc-libs"
         $([[ ${MINGW_PACKAGE_PREFIX} != *-clang-* ]] || echo "${MINGW_PACKAGE_PREFIX}-openmp"))
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc")

options=('staticlibs' 'strip')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  autoreconf -fiv
}

build() {
  [[ -d ${srcdir}/build-${MSYSTEM} ]] && rm -rf ${srcdir}/build-${MSYSTEM}
  cp -Rf ${srcdir}/${_realname}-${pkgver} ${srcdir}/build-${MSYSTEM} && cd ${srcdir}/build-${MSYSTEM}

  CPPFLAGS="-DSRE_STRICT_ANSI" \
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared

  make
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
}
