# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=codebook-lsp
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.3.30
pkgrel=1
pkgdesc="Code-aware spell checker with language server implementation (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://github.com/blopker/codebook'
msys2_repository_url='https://github.com/blopker/codebook'
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust" 'git')
source=("git+${url}.git#tag=v${pkgver}")
sha256sums=('7346456a95b17654029af5128fc09c52a8b7ce909243a6866d0e22fcceabdf46')

prepare() {
  cd codebook

  cargo fetch --locked --target "${RUST_CHOST}"
}

build() {
  cd codebook

  cargo build --release --frozen -p codebook-lsp
}

check() {
  cd codebook

  cargo test --release --frozen -p codebook-lsp
}

package() {
  cd codebook

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path crates/codebook-lsp \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
