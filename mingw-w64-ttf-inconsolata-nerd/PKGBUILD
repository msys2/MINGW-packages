# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_font=inconsolata
_realname=ttf-${_font}-nerd
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgdesc="Patched font Inconsolata from nerd fonts library (mingw-w64)"
pkgver=3.0.2
pkgrel=1
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://github.com/ryanoasis/nerd-fonts'
_urlbase="${url}/releases/download/v${pkgver}"
license=('spdx:OFL-1.1')
options=('!debug' '!strip')
groups=("${MINGW_PACKAGE_PREFIX}-nerd-fonts")
source=("${_font}-${pkgver}.tar.xz::${_urlbase}/Inconsolata.tar.xz")
sha256sums=('a5bfad20f01d4f13261f3b1270e14ac53af57169679ff17bef77ad3f9ec92a0b')
noextract=("${_font}-${pkgver}.tar.xz")

prepare() {
  rm -rf ${_font}
  mkdir ${_font}
  bsdtar xf ${_font}-${pkgver}.tar.xz -C ${_font}
}

package() {
  install -Dm644 inconsolata/*.ttf -t "${pkgdir}${MINGW_PREFIX}/share/fonts/TTF"
  install -Dm644 inconsolata/OFL.txt -t "${pkgdir}${MINGW_PREFIX}/share/licenses/ttf-inconsolata-nerd/"
}
