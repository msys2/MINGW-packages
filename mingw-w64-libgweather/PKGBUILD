# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=libgweather
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.24.1
pkgrel=1
arch=('any')
pkgdesc="GWeather shared library (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-gtk3"
         "${MINGW_PACKAGE_PREFIX}-libsoup"
         "${MINGW_PACKAGE_PREFIX}-libsystre"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-geocode-glib")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-glade"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             "${MINGW_PACKAGE_PREFIX}-vala"
             "gtk-doc"
             "intltool")
options=('strip' 'staticlibs')
license=("LGPL 2.1")
url="https://www.gnome.org/"
install=${_realname}-${CARCH}.install
source=(https://download.gnome.org/sources/${_realname}/${pkgver%.*}/${_realname}-${pkgver}.tar.xz
        001-win.patch
        002-relocation.patch)
sha256sums=('1227316037b63dac4153031a22abccb3d2ac679a7409e29cd303eec499eb653c'
            '33bbe3b4ae6e3897ecab23cbd3b9eb5aefd5b14584adc3a16261e34395d7d0c5'
            'b84ead0a59db1d315e7de7bea76a162e69fa43242bc5480e6149271304991381')

prepare() {
  cd ${_realname}-${pkgver}

  patch -p1 -i ${srcdir}/001-win.patch
  patch -p1 -i ${srcdir}/002-relocation.patch

  AUTOPOINT='intltoolize --automake --copy' autoreconf -fiv
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  mkdir -p doc/html
  cp -rf ../${_realname}-${pkgver}/doc/html/* doc/html

  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --enable-introspection \
    --enable-shared \
    --enable-static

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
