# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=libgweather
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.13.91
pkgrel=1
arch=('any')
pkgdesc="GWeather shared library  (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-gtk3"
         "${MINGW_PACKAGE_PREFIX}-libsoup"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-geocode-glib")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-gtk-doc"
             "${MINGW_PACKAGE_PREFIX}-vala"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             "${MINGW_PACKAGE_PREFIX}-glade"
             "intltool")
options=('strip' 'staticlibs')
license=("LGPL 2.1")
url="http://www.gnome.org/"
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver:0:4}/${_realname}-$pkgver.tar.xz
        001-win.patch
        002-localedir-fix.patch
        003-hack-unixy-paths.patch)
sha256sums=('f4871d5d19b463e130651665fc922c981f592c244db3074e4444689319deeb00'
            '54aafa1ec47f818855cb8d17bb4f7f4458505a54b08c9fc51bfd4bbc1d6c37a2'
            '6677acc97b0322170339479df46435f14eb232df2d36e607029af3bd1f1a4eb0'
            'bfc78c118d966b8fcd557b3d5a2d372532fc5ec47884a44df6fbe5c987e01592')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-win.patch
  patch -p1 -i ${srcdir}/002-localedir-fix.patch

  AUTOPOINT='intltoolize --automake --copy' autoreconf -fi
  
  patch -p1 -i ${srcdir}/003-hack-unixy-paths.patch
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --enable-introspection
  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install
}
