####
# Based partly on https://www.archlinux.org/packages/extra/x86_64/gnome-calculator/
####
# Past Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>
# Contributor: Tim Stahlhut <stahta01@gmail.com>

_realname=gnome-calculator
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=42.1
pkgrel=2
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
pkgdesc="GNOME desktop calculator (mingw-w64)"
options=(strip staticlibs)
depends=("${MINGW_PACKAGE_PREFIX}-gtksourceview5"
         "${MINGW_PACKAGE_PREFIX}-libadwaita"
         "${MINGW_PACKAGE_PREFIX}-libgee"
         "${MINGW_PACKAGE_PREFIX}-libhandy"
         "${MINGW_PACKAGE_PREFIX}-libsoup"
         "${MINGW_PACKAGE_PREFIX}-mpc"
         "${MINGW_PACKAGE_PREFIX}-mpfr")
makedepends=("${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-vala"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             "${MINGW_PACKAGE_PREFIX}-appstream-glib"
             "${MINGW_PACKAGE_PREFIX}-gmp"
             "${MINGW_PACKAGE_PREFIX}-yelp-tools" # Used in package step
             "${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-cc")
license=('spdx:GPL-3.0-or-later')
url="https://wiki.gnome.org/Apps/Calculator"
install=${_realname}-${MSYSTEM}.install
source=(https://download.gnome.org/sources/${_realname}/${pkgver%.*}/${_realname}-${pkgver}.tar.xz
        001-win.patch
        5b387d03f434922e1811919aedb2d475cc874da4.patch
        84ea10dc61fb156341bc7eba4d0b2025db801e9b.patch
)
sha256sums=('ef4d24e42a65dc860e6206f3b470b7d233c2ae2a4d4965d8859aa9ea8a395acd'
            '4cf4eefa6cfef044ec4f068daa8d427f58d65acc3b6d854eb4a1dea1bb4ad6f2'
            'eab07acf04de697b3c381b22b05464588b92f2ea0357ed4d6b68a03c54a6c411'
            '1834f771ed0177798302eb3b807c96a8bba980509d22e5dbfa2b7b1d44b43d99')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  del_file_exists "${srcdir}/${_realname}-${pkgver}/lib/windows.c"
  apply_patch_with_msg 001-win.patch

  # https://gitlab.gnome.org/GNOME/gnome-calculator/-/commit/5b387d03f434922e1811919aedb2d475cc874da4
  apply_patch_with_msg 5b387d03f434922e1811919aedb2d475cc874da4.patch
  # https://gitlab.gnome.org/GNOME/gnome-calculator/-/commit/84ea10dc61fb156341bc7eba4d0b2025db801e9b
  apply_patch_with_msg 84ea10dc61fb156341bc7eba4d0b2025db801e9b.patch
}

build() {
  [[ -d build-${MSYSTEM} ]] && rm -rf build-${MSYSTEM}
  mkdir -p build-${MSYSTEM}
  cd build-${MSYSTEM}

  MSYS2_ARG_CONV_EXCL="--prefix=" \
  ${MINGW_PREFIX}/bin/meson.exe \
    --prefix="${MINGW_PREFIX}" \
    --wrap-mode=nodownload \
    --buildtype plain \
    ../${_realname}-${pkgver}

  # Both mpc and mpfr needs the gmp library or linker error happens.
  sed -e 's|"-lmpc" "-lmpfr"|"-lmpc" "-lmpfr" "-lgmp"|g' -e 's|-lmpc -lmpfr|-lmpc -lmpfr -lgmp|g' -i build.ninja

  ${MINGW_PREFIX}/bin/meson.exe compile
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/meson.exe test || true
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/meson.exe install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
