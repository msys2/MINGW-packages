# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Miloš Komarčević <miloskomarcevic@aim.com>

_realname=LibRaw
pkgbase=mingw-w64-libraw
pkgname="${MINGW_PACKAGE_PREFIX}-libraw"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.22.0
pkgrel=1
pkgdesc="Library for reading RAW files obtained from digital photo cameras (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_repository_url='https://github.com/LibRaw/LibRaw'
msys2_references=(
  'cpe: cpe:/a:libraw:libraw'
)
url="https://www.libraw.org/"
license=("spdx:LGPL-2.1 OR CDDL-1.0")
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools" "${MINGW_PACKAGE_PREFIX}-cc")
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-lcms2"
         "${MINGW_PACKAGE_PREFIX}-libjpeg"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread"
         "${MINGW_PACKAGE_PREFIX}-omp"
         "${MINGW_PACKAGE_PREFIX}-zlib")
source=("https://www.libraw.org/data/${_realname}-${pkgver}.tar.gz"
        "0001-libraw-pkgconfig-remove-stdcxx.patch"
        "0002-libraw-datastream-utf8-api.patch"
        "0003-libraw-win32-dlldefs.patch")
sha256sums=('1071e6e8011593c366ffdadc3d3513f57c90202d526e133174945ec1dd53f2a1'
            'f3b9cc8eb9e557ad6298cc05285c7f3a91b15ccbc65577f03505e0a38f62c8e8'
            '262c29b7e51d0916340b827fe1810ce1581b267886a98b4d6bccf720bd2789d3'
            '4222276a8d5c95ddd5e4c719a8d79e028e611b930dd4fbddeca3631a8624a995')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-libraw-pkgconfig-remove-stdcxx.patch"

  # https://github.com/LibRaw/LibRaw/issues/632
  patch -p1 -i "${srcdir}/0002-libraw-datastream-utf8-api.patch"

  # https://github.com/LibRaw/LibRaw/issues/709
  patch -p1 -i "${srcdir}/0003-libraw-win32-dlldefs.patch"

  autoreconf -ifv
}

build() {
  if [[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]]; then
    LDFLAGS+=" -lomp"
  fi

  # This would enable more formats (X3F (Foveon), Rapsberry Pi RAW+JPEG),
  # but upstream recommends against it for security reasons, see
  # https://github.com/LibRaw/LibRaw/blob/27144e42000/Changelog.txt#L417
  # CPPFLAGS+=" -DUSE_6BY9RPI -DUSE_X3FTOOLS"

  mkdir -p "${srcdir}/build-${MSYSTEM}-static" && cd "${srcdir}/build-${MSYSTEM}-static"

  CPPFLAGS+=" -DLIBRAW_FORCE_OPENMP -DLIBRAW_NODLL"
  ../${_realname}-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --enable-shared=yes \
      --enable-static=yes

  make

  mkdir -p "${srcdir}/build-${MSYSTEM}-shared" && cd "${srcdir}/build-${MSYSTEM}-shared"

  CPPFLAGS+=" -ULIBRAW_NODLL -DLIBRAW_BUILDLIB"
  ../${_realname}-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --enable-shared=yes \
      --enable-static=no

  make
}

package() {
  cd "${srcdir}/build-${MSYSTEM}-static"

  make install DESTDIR="${pkgdir}"

  cd "${srcdir}/build-${MSYSTEM}-shared"

  make install DESTDIR="${pkgdir}"

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE.CDDL" "${pkgdir}${MINGW_PREFIX}/share/licenses/libraw/LICENSE.CDDL"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE.LGPL" "${pkgdir}${MINGW_PREFIX}/share/licenses/libraw/LICENSE.LGPL"
}
