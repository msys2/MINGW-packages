# Maintainer: Antoine Martin <antoine@xpra.org>
# Contributor: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>

_realname=zeroconf
_srcname=python-zeroconf
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
provides=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
replaces=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=0.39.4
pkgrel=1
pkgdesc="A pure python implementation of multicast DNS service discovery"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://github.com/jstasiak/python-zeroconf"
license=('spdx:LGPL-2.1-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-async-timeout"
         "${MINGW_PACKAGE_PREFIX}-python-ifaddr"
         "${MINGW_PACKAGE_PREFIX}-python-netifaces"
         "${MINGW_PACKAGE_PREFIX}-python-six")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-pytest")
source=("${_srcname}-${pkgver}.tar.gz"::"https://github.com/jstasiak/${_srcname}/archive/${pkgver}.tar.gz")
sha256sums=('32783c9a583419abe7223ba199e2ddebe9533011caecf6a268852e0fbea1186a')

prepare() {
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_srcname}-${pkgver}" "python-build-${MSYSTEM}"
}

build() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
      --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 COPYING    "${pkgdir}${MINGW_PREFIX}/share/licenses/python-zeroconf/COPYING"
  install -Dm644 README.rst "${pkgdir}${MINGW_PREFIX}/share/doc/python-zeroconf/README"
}

check() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  pytest -k 'not test_integration_with_listener_ipv6' tests || warning "Tests failed"
}
