# Maintainer: J. Peter <jpetermugaas@suddenlink.net>

_realname=libunwind
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.8.0
pkgrel=1
pkgdesc='A new implementation of a stack unwinder for C++ exceptions'
url='http://llvm.org'
license=('custom:University of Illinois/NCSA Open Source License')
arch=('any')
makedepends=("${MINGW_PACKAGE_PREFIX}-llvm")
source=(http://llvm.org/releases/${pkgver}/${_realname}-${pkgver}.src.tar.xz{,.sig}
        0001-libunwind-add-support-for-mingw-w64.patch)
sha256sums=('af3eaf39ecdc3b9e89863fb62e1aa3c135cfde7e9415424e4e396d7486a9422b'
            'SKIP'
            'c05162c426e90a01cef4a9d34d09e6314b966f2fea5a121223c98e51f5b6bd93')
validpgpkeys=('B6C8F98282B944E3B0D5C2530FC3042E345AD05D') # Hans Wennborg, Google.

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}.src
  patch -p1 -i "${srcdir}/"0001-libunwind-add-support-for-mingw-w64.patch
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DLLVM_CONFIG="${MINGW_PREFIX}/bin/llvm-config.exe" \
    -DCMAKE_C_COMPILER="${MINGW_PREFIX}/bin/gcc.exe" \
    -DCMAKE_CXX_COMPILER="${MINGW_PREFIX}/bin/g++.exe" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIBUNWIND_ENABLE_SHARED=OFF \
    ../${_realname}-${pkgver}.src
  make

}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="$pkgdir" install
}
