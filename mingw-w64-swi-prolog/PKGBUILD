# Maintainer: Matthias Gondan <Matthias.Gondan-Rochon@uibk.ac.at>

_realname=swi-prolog
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=9.0.2
pkgrel=1
pkgdesc="Prolog environment (mingw-w64)"
arch=(any)
mingw_arch=('mingw64' 'mingw32' 'ucrt64' 'clang64' 'clang32')
url="https://www.swi-prolog.org/"
license=("FreeBSD")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread-git"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-gmp"
             "${MINGW_PACKAGE_PREFIX}-libarchive"
             "${MINGW_PACKAGE_PREFIX}-libyaml"
             "${MINGW_PACKAGE_PREFIX}-db"
             "${MINGW_PACKAGE_PREFIX}-pcre"
             "${MINGW_PACKAGE_PREFIX}-xpm-nox"
             "${MINGW_PACKAGE_PREFIX}-openjpeg"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")
source=("https://www.swi-prolog.org/download/stable/src/swipl-${pkgver}.tar.gz"
        "01-skip-installation-of-system-dlls.patch"
        "02-unix-style-directory-structure.patch"
        "03-semweb-ntriples.patch"
        "04-semweb-turtle.patch")
sha256sums=('33b5de34712d58f14c1e019bd1613df9a474f5e5fd024155a0f6e67ebb01c307'
            '680acefdecaaab9b4b964620c136d379f06b5659fe78b5c1e844e861bbf88e2c'
            '696ef1b4b097ce28770a64fe4d8a108a760267d9bf35527db0890aec384ee0ab'
            '4e244b597f318c8aee01fa19275208125c77f6f63a89e8db31e0cc1d95bf7d0e'
            'ad2ff034117a03ff57cba92ef4df0da3c452850aef71d98346491a08106167ad')

prepare() {
  cd ${srcdir}/swipl-${pkgver}
  patch -p1 < ${srcdir}/01-skip-installation-of-system-dlls.patch
  patch -p1 < ${srcdir}/02-unix-style-directory-structure.patch
  patch -p1 < ${srcdir}/03-semweb-ntriples.patch
  patch -p1 < ${srcdir}/04-semweb-turtle.patch
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G Ninja \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DMSYS2=1 \
    -DMINGW_ROOT=${MINGW_PREFIX} \
    "${extra_config[@]}" \
    -DWIN32_DLLS="" \
    -DSWIPL_PACKAGES_JAVA=OFF \
    -DINSTALL_DOCUMENTATION=OFF \
    -DSWIPL_{SHARED,STATIC}_LIB=ON \
    ../swipl-${pkgver}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .
  install -Dm644 "${srcdir}/swipl-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  "${MINGW_PREFIX}"/bin/cmake.exe --build . --target test
}
