# Maintainer: Matthias Gondan <Matthias.Gondan-Rochon@uibk.ac.at>

_realname=swi-prolog
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=9.2.5
pkgrel=2
pkgdesc="Prolog environment (mingw-w64)"
arch=(any)
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url="https://www.swi-prolog.org/"
msys2_references=("cpe: cpe:/a:swi-prolog:swi-prolog")
license=('spdx:BSD-2-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gmp"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread-git"
         "${MINGW_PACKAGE_PREFIX}-zlib")
optdepends=("${MINGW_PACKAGE_PREFIX}-db: support for Berkeley db"
            "${MINGW_PACKAGE_PREFIX}-libarchive: libarchive support"
            "${MINGW_PACKAGE_PREFIX}-libyaml: yaml support"
            "${MINGW_PACKAGE_PREFIX}-openssl: Crypto/SSL support"
            "${MINGW_PACKAGE_PREFIX}-pcre2: support for regular expressions"
            "${MINGW_PACKAGE_PREFIX}-python: python interface")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-db"
             "${MINGW_PACKAGE_PREFIX}-libarchive"
             "${MINGW_PACKAGE_PREFIX}-libyaml"
             "${MINGW_PACKAGE_PREFIX}-openssl"
             "${MINGW_PACKAGE_PREFIX}-pcre2"
             "${MINGW_PACKAGE_PREFIX}-python")
source=("https://www.swi-prolog.org/download/stable/src/swipl-${pkgver}.tar.gz")
sha256sums=('b9f40771906c7e04be80ae4cfaa4463aeb44c52010a478edd8c7a4c022fe8781')

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  "${MINGW_PREFIX}"/bin/cmake \
    -G Ninja \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -DCMAKE_DLL_NAME_WITH_SOVERSION=ON \
    -DMSYS2=1 \
    "${extra_config[@]}" \
    -DUSE_GMP=ON \
    -DSWIPL_PACKAGES_JAVA=OFF \
    -DSWIPL_PACKAGES_ODBC=OFF \
    -DSWIPL_PACKAGES_X=OFF \
    -DINSTALL_DOCUMENTATION=OFF \
    -DBUILD_TESTING=OFF \
    -DINSTALL_TESTS=OFF \
    -DSWIPL_{SHARED,STATIC}_LIB=ON \
    -DPython_EXECUTABLE=${MINGW_PREFIX}/bin/python \
    ../swipl-${pkgver}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

check() {
  cd "${srcdir}"/build-${MSYSTEM}
  "${MINGW_PREFIX}"/bin/cmake.exe -DBUILD_TESTING=ON ../swipl-${pkgver}
  "${MINGW_PREFIX}"/bin/cmake.exe --build .

  PYTHONHOME="${MINGW_PREFIX}" "${MINGW_PREFIX}"/bin/ctest
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .

  # Remove path reference in cmake and .pc files
  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  for _f in "${pkgdir}${MINGW_PREFIX}"/lib/cmake/**/*.cmake; do
    sed -e "s|${PREFIX_WIN}|\$\{_IMPORT_PREFIX\}|g" -i ${_f}
    sed -e "s|${MINGW_PREFIX}|\$\{_IMPORT_PREFIX\}|g" -i ${_f}
  done
  for pcfile in  "${pkgdir}${MINGW_PREFIX}"/share/pkgconfig/*.pc; do
    sed -s "s|${MINGW_PREFIX}/lib|\${prefix}/lib|g" -i "${pcfile}"
  done

  install -Dm644 "${srcdir}"/swipl-${pkgver}/LICENSE "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
