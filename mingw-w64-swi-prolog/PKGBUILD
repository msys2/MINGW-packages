# Maintainer: Matthias Gondan <Matthias.Gondan-Rochon@uibk.ac.at>

_realname=swi-prolog
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=9.2.0
pkgrel=1
pkgdesc="Prolog environment (mingw-w64)"
arch=(any)
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url="https://www.swi-prolog.org/"
license=('spdx:BSD-2-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread-git"
         "${MINGW_PACKAGE_PREFIX}-zlib")
optdepends=("${MINGW_PACKAGE_PREFIX}-gmp: support for large integers"
            "${MINGW_PACKAGE_PREFIX}-libyaml: yaml support"
            "${MINGW_PACKAGE_PREFIX}-db: support for Berkeley db"
            "${MINGW_PACKAGE_PREFIX}-pcre2: support for regular expressions"
            "${MINGW_PACKAGE_PREFIX}-python: python interface")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-gmp"
             "${MINGW_PACKAGE_PREFIX}-libarchive"
             "${MINGW_PACKAGE_PREFIX}-libyaml"
             "${MINGW_PACKAGE_PREFIX}-db"
             "${MINGW_PACKAGE_PREFIX}-pcre2"
             "${MINGW_PACKAGE_PREFIX}-pkgconf")
source=("https://www.swi-prolog.org/download/stable/src/swipl-${pkgver}.tar.gz"
        "01-libpython.patch"
        "03-unixdirs-src-cmakelists.patch")

sha256sums=('10d90b15734d14d0d7972dc11a3584defd300d65a9f0b1185821af8c3896da5e'
            'a6d965538f28fbf260de062fb68000d3a59a85f6d0a78d82d579c1828145da5a')

prepare() {
  cd ${srcdir}/swipl-${pkgver}
  patch -p1 < ${srcdir}/01-libpython.patch
  patch -p1 < ${srcdir}/03-unixdirs-src-cmakelists.patch
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G Ninja \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -DMSYS2=1 \
    -DSWIPL_INSTALL_AS_LINK=ON \
    -DWIN32_DLLS="" \
    -DMINGW_ROOT="${MINGW_PREFIX}" \
    "${extra_config[@]}" \
    -DSWIPL_PACKAGES_JAVA=OFF \
    -DSWIPL_PACKAGES_X=OFF \
    -DINSTALL_DOCUMENTATION=OFF \
    -DSWIPL_{SHARED,STATIC}_LIB=ON \
    ../swipl-${pkgver}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .
  install -Dm644 "${srcdir}/swipl-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"
  "${MINGW_PREFIX}"/bin/cmake.exe --build . --target test
}

