# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libgadu
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.12.0
pkgrel=1
pkgdesc="This library implements the client side of the Gadu-Gadu protocol (mingw-w64)"
arch=('any')
url="http://libgadu.net"
license=("LGPL2.1")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-gnutls" "${MINGW_PACKAGE_PREFIX}-protobuf-c")
groups=("${MINGW_PACKAGE_PREFIX}")
options=('staticlibs' 'strip')
source=("http://github.com/wojtekka/libgadu/releases/download/${pkgver}/${_realname}-${pkgver}.tar.gz"
        protobuf-c.patch)
md5sums=('e4569a0ed26136c4634fa8c4013910e3'
         '893ce50093458407ad688f12f999b106')

prepare() {
  cd "$srcdir/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/protobuf-c.patch

  ./protobufgen.sh
}

build() {
  mkdir -p $srcdir/build-${CARCH} && cd $srcdir/build-${CARCH}
  ../${_realname}-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST} \
      --enable-shared \
      --enable-static \
      --disable-tests
  make
}

check() {
  cd "${srcdir}/build-${CARCH}"
  make check
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="$pkgdir" install
}
