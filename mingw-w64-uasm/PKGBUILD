# Maintainer: Adrian Pop <adrian.pop@liu.se>

_realname=uasm
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.57
pkgrel=1
pkgdesc="UASM is a free MASM-compatible assembler based on JWasm (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
license=('spdx:Watcom-1.0')
url="https://github.com/Terraspace/UASM"
msys2_repository_url='https://github.com/Terraspace/UASM'
depends=("${MINGW_PACKAGE_PREFIX}-cc")
source=("https://github.com/Terraspace/UASM/archive/v${pkgver}/${_realname}-${pkgver}r.tar.gz")
sha256sums=('03963d2eb4e5071fb708fd9784dcc1226c9732509f31477814908ce7dac1def7')

prepare() {
  cd "UASM-${pkgver}"
  rm -rf DJGPPr
  sed -i -e 's/^extra_c_flags = .*$/& -std=c11 -D__NT__ -Wno-incompatible-pointer-types -Wno-int-conversion -Wno-implicit-function-declaration -Wno-incompatible-function-pointer-types'"$CFLAGS"'/' Makefile-DOS-GCC.mak
  sed -i -e 's|gcc\.exe|cc.exe|g' Makefile-DOS-GCC.mak
}

build() {
  cd "UASM-${pkgver}"
  make -f Makefile-DOS-GCC.mak
}

package() {
  install -Dm744 "${srcdir}"/UASM-${pkgver}/DJGPPr/hjwasm.exe "${pkgdir}${MINGW_PREFIX}/bin/jwasm.exe"
  install -Dm744 "${srcdir}"/UASM-${pkgver}/DJGPPr/hjwasm.exe "${pkgdir}${MINGW_PREFIX}/bin/${_realname}.exe"
  install -Dm644 "${srcdir}"/UASM-${pkgver}/License.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
