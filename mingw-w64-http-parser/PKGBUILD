# Maintainer: Martell Malone <martellmalone@gmail.com>
# Contributor: Alethea Rose <alethea@alethearose.com>

_realname=http-parser
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.7.1
pkgrel=2
pkgdesc='Parser for HTTP Request/Response written in C (mingw-w64)'
arch=('any')
url='https://github.com/joyent/http-parser'
license=('MIT')
source=(${_realname}-${pkgver}.tar.gz::"https://github.com/joyent/http-parser/archive/v${pkgver}.tar.gz")
options=('strip' 'staticlibs')
sha256sums=('70409ad324e5de2da6a0f39e859e566d497c1ff0a249c0c38a5012df91b386b3')

build() {
  cd ${srcdir}/${_realname}-${pkgver}

  CC=gcc \
  AR=ar \
  SONAME=libhttp_parser-2.dll \
  LDFLAGS+=' -Wl,--out-implib=libhttp_parser.dll.a' \
  make -j1 \
    library package
}

check() {
  cd ${srcdir}/${_realname}-${pkgver}
  make test
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  mkdir -p ${pkgdir}/${MINGW_PREFIX}/{bin,include,lib}
  install -Dm644 http_parser.h "${pkgdir}/${MINGW_PREFIX}/include/http_parser.h"
  cp *.a ${pkgdir}/${MINGW_PREFIX}/lib/
  cp *.dll ${pkgdir}/${MINGW_PREFIX}/bin/
}
