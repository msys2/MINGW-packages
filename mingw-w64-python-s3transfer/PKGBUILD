# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>

_realname=s3transfer
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.4.2
pkgrel=1
pkgdesc='Amazon S3 Transfer Manager for Python (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64')
url="https://github.com/boto/s3transfer"
license=('Apache')
depends=("${MINGW_PACKAGE_PREFIX}-python-botocore")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools")
source=("$url/archive/$pkgver.tar.gz")
sha256sums=('c559bf00e6c543f2eb2e84966dc3890d4836ee15447264b9cffca6855e84ce56')

prepare() {  
  cd "$srcdir"
  rm -rf python-build-${CARCH} | true
  cp -r "${_realname}-${pkgver}" "python-build-${CARCH}"
}

build() {
  msg "Python build for ${CARCH}"  
  cd "${srcdir}/python-build-${CARCH}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE.txt"
}
