# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>

_realname=s3transfer
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.5.2
pkgrel=1
pkgdesc='Amazon S3 Transfer Manager for Python (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://github.com/boto/s3transfer"
license=('Apache')
depends=("${MINGW_PACKAGE_PREFIX}-python-botocore")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools")
source=("$url/archive/$pkgver.tar.gz")
sha256sums=('c083e9207d3d47826acdabe722c622c2baeb992af8a5933056d9ef0d577dd263')

prepare() {
  cd "$srcdir"
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}"
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE.txt"
}
