# Maintainer: Kreijstal <kreijstal@hotmail.com>

_realname=solvespace
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=3.1
pkgrel=1
pkgdesc="Parametric 2d/3d CAD (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://solvespace.com"
license=('GPL3')
depends=("${MINGW_PACKAGE_PREFIX}-mimalloc"
  "${MINGW_PACKAGE_PREFIX}-angleproject"
  "${MINGW_PACKAGE_PREFIX}-pixman"
  "${MINGW_PACKAGE_PREFIX}-cairo"
  "${MINGW_PACKAGE_PREFIX}-libpng"
  "${MINGW_PACKAGE_PREFIX}-freetype"
  "${MINGW_PACKAGE_PREFIX}-eigen3"
  "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-ninja"
  "${MINGW_PACKAGE_PREFIX}-libbacktrace"
  "${MINGW_PACKAGE_PREFIX}-cc")
source=("https://github.com/solvespace/solvespace/releases/download/v${pkgver}/${_realname}-${pkgver}.tar.xz"
  "1487.patch"
  "002-not-in-msys2-temp.patch")
sha256sums=('34a273ce642d0c77b8f101463730ed4eade7d90cfabfe486f3e7cbf494ff132a'
            '76b7711320430440e646d3a93c4128ae86db797c4089cc08bbfb813aac70527e'
            '519cda45a5e5c76d1c5a3904b9ff35fcb058a8f12a72ec06d703576290092a54')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -N -p1 -i "${srcdir}/1487.patch" || true
  patch -N -p1 -i "${srcdir}/002-not-in-msys2-temp.patch" || true
}

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    "${extra_config[@]}" \
    -DENABLE_TESTS=OFF \
    -DENABLE_COVERAGE=OFF \
    ../${_realname}-${pkgver}

  cmake --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  DESTDIR="${pkgdir}" cmake --install .

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING.txt" \
    "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING.txt"
}
