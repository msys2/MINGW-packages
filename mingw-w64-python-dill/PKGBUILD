# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>

_realname=dill
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.3.9
pkgrel=2
pkgdesc='Serialize all of python (almost) (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://dill.readthedocs.io/en/latest/"
msys2_repository_url="https://github.com/uqfoundation/dill"
msys2_references=(
  'purl: pkg:pypi/dill'
)
license=('spdx:BSD-3-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools")
options=('!strip')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('81aa267dddf68cbfe8029c42ca9ec6a4ab3b22371d1c450abc54422577b4512c')

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  local _PREFIX_WIN=$(cygpath -m ${MINGW_PREFIX})
  for file in get_gprof get_objgraph undill; do
    sed -s '1 { s/^#!.*python.*$/#!\/usr\/bin\/env python3/ }' -i "${pkgdir}${MINGW_PREFIX}/bin/$file"
  done

  install -D -m644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
