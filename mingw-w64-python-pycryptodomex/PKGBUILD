# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=pycryptodomex
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=3.22.0
pkgrel=1
pkgdesc='A self-contained Python package of low-level cryptographic primitives (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'archlinux: python-pycryptodomex'
  'purl: pkg:pypi/pycryptodomex'
  "cpe: cpe:2.3:a:pycryptodome:pycryptodomex"
)
msys2_repository_url='https://github.com/Legrandin/pycryptodome/'
msys2_changelog_url='https://www.pycryptodome.org/src/changelog/'
url='https://www.pycryptodome.org/'
license=('PublicDomain')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools")
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('a1da61bacc22f93a91cbe690e3eb2022a03ab4123690ab16c46abb693a9df63d')

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE.rst "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE.rst"
}
