# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=colobot-gold
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.2.2_alpha
_pkgver=${pkgver/_/-}
_dataver=${pkgver}
pkgrel=1
pkgdesc='Colobot GOLD is a real-time strategy game with programmable bots (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://colobot.info/'
msys2_repository_url='https://github.com/colobot/colobot'
license=('spdx:GPL3')
depends=("${MINGW_PACKAGE_PREFIX}-libsndfile"
         "${MINGW_PACKAGE_PREFIX}-glew"
         "${MINGW_PACKAGE_PREFIX}-libvorbis"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libogg"
         "${MINGW_PACKAGE_PREFIX}-gettext"
         "${MINGW_PACKAGE_PREFIX}-SDL2_image"
         "${MINGW_PACKAGE_PREFIX}-SDL2_ttf"
         "${MINGW_PACKAGE_PREFIX}-boost-libs"
         "${MINGW_PACKAGE_PREFIX}-openal"
         "${MINGW_PACKAGE_PREFIX}-physfs"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-data=${_dataver}")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-boost"
             "${MINGW_PACKAGE_PREFIX}-glm"
             "${MINGW_PACKAGE_PREFIX}-nlohmann-json"
             "${MINGW_PACKAGE_PREFIX}-gtest")
source=("${msys2_repository_url}/archive/${_realname}-${_pkgver}/colobot-${_realname}-${_pkgver}.tar.gz")
sha256sums=('047eb2bc96725598d918c5b500928e3a08fa3e6e7e730a82169da4524b03ddad')

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=('-DCMAKE_BUILD_TYPE=Release'
                   '-DDEV_BUILD=0')
  else
    extra_config+=('-DCMAKE_BUILD_TYPE=Debug'
                   '-DDEV_BUILD=1')
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=;-DCOLOBOT_INSTALL" \
    cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DOPENAL_SOUND=1 \
      -DASSERTS=1 \
      -DTESTS=1 \
      -DUSE_RELATIVE_PATHS=ON \
      -DCOLOBOT_INSTALL_BIN_DIR="${MINGW_PREFIX}"/bin \
      -DCOLOBOT_INSTALL_LIB_DIR="${MINGW_PREFIX}"/bin \
      -S "colobot-${_realname}-${_pkgver}" \
      -B "build-${MSYSTEM}"

  cmake --build "build-${MSYSTEM}"
}

check() {
  cmake --build "build-${MSYSTEM}" --target test
}

package() {
  DESTDIR="${pkgdir}" cmake --install "build-${MSYSTEM}"

  install -Dm644 "${srcdir}/colobot-${_realname}-${_pkgver}/LICENSE.txt" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.txt"
}
