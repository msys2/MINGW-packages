# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=ruff
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.1.9
pkgrel=1
pkgdesc="An extremely fast Python linter, written in Rust (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url='https://github.com/astral-sh/ruff'
license=('spdx:MIT')
options=('!strip')
depends=(
  "${MINGW_PACKAGE_PREFIX}-python"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-rust"
  "${MINGW_PACKAGE_PREFIX}-python-maturin"
  "${MINGW_PACKAGE_PREFIX}-python-installer"
)
source=("${url}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "${url}/commit/a3e06e5a.patch")
sha256sums=('422b84742188a722684f0659c875a343065f9edb78350cc4ef63352e68c4339b'
            'ae118785b52eab2e5b2b2a81c28cdb82bf59ef1a856995a1ee7fb2ed8561932c')
noextract=("${_realname}-${pkgver}.tar.gz")

prepare() {
  tar -xf "${_realname}-${pkgver}.tar.gz" || true
  cd "${srcdir}/${_realname}-${pkgver}"

  # https://github.com/astral-sh/ruff/issues/9234
  patch -p1 -i "${srcdir}/a3e06e5a.patch"

  cargo fetch --locked
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  export WINAPI_NO_BUNDLED_LIBRARIES=1
  maturin build --release --strip --locked --all-features

  for _shell in bash zsh fish; do
    ./target/release/ruff --generate-shell-completion ${_shell} > ruff.${_shell}
  done
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  cargo test --release --frozen --all-features
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" target/wheels/*.whl

  install -Dm644 LICENSE -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/"
  install -Dm644 README.md -t "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/"
  install -Dm644 ruff.bash "${pkgdir}${MINGW_PREFIX}/share/bash-completion/completions/ruff"
  install -Dm644 ruff.fish "${pkgdir}${MINGW_PREFIX}/share/fish/vendor_completions.d/ruff.fish"
  install -Dm644 ruff.zsh "${pkgdir}${MINGW_PREFIX}/share/zsh/site-functions/_ruff"
}
