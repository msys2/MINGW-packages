_realname=gnatcoll-python
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.0
pkgrel=1

pkgdesc="GNAT Components Collection - Interface to the Python 3 interpreter (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64')
provides=("${MINGW_PACKAGE_PREFIX}-gnatcoll-python3")
url="https://github.com/AdaCore/gnatcoll-bindings"
msys2_references=(
  'aur: gnatcoll-python'
  'gentoo: dev-ada/gnatcoll-bindings'
)
license=('spdx:GPL-3.0-or-later WITH GCC-exception-3.1')
makedepends=("${MINGW_PACKAGE_PREFIX}-gprbuild"
             "${MINGW_PACKAGE_PREFIX}-python3")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gcc-ada"
         "${MINGW_PACKAGE_PREFIX}-gnatcoll-core")
source=("https://github.com/AdaCore/gnatcoll-bindings/archive/v${pkgver}/gnatcoll-bindings-${pkgver}.tar.gz"
        '001-Python-3.11-frame-accessors.diff'
        '002-Fix-setup.py-for-cross-volume-builds.patch')
sha256sums=('d123733ca9a9206572e77bfbffbe0cd50e96ea8f8e276055048dc209e5327791'
            'c743bec4329ac7c487add1b4217c9ce24c3d501734cdc732eeea4949fbc509d2'
            '0add78c928a6b065f793c2d96af51a1dbef3ee0402c4dd00f64b42e9c7ea199a')
options=('strip')

prepare() {
  echo "${pkgver}" > "${srcdir}/gnatcoll-bindings-${pkgver}/version_information"

  cd "${srcdir}/gnatcoll-bindings-${pkgver}"

  # https://github.com/AdaCore/gnatcoll-bindings/issues/18
  # https://github.com/AdaCore/gnatcoll-bindings/pull/17#issuecomment-1784920061
  # https://salsa.debian.org/debian/libgnatcoll-bindings/-/blob/debian/23.0.0-4/debian/patches/python3.11-frame-accessors.diff
  patch -p1 -i "${srcdir}/001-Python-3.11-frame-accessors.diff"

  # Fix setup.py, which caused the CI pipeline in github.com/MSYS2/MINGW-packages to fail
  # when the MSYS2 installation and package sources were on different volumes:
  #
  #   Traceback:
  #     ...
  #     File "C:/_/B/src/gnatcoll-bindings-24.0.0/python3/setup.py", line 45, in relocate
  #       rel_path = os.path.relpath(path, build_prefix)
  #                    ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
  #     File "<frozen ntpath>", line 778, in relpath
  #   ValueError: path is on mount 'D:', start on mount 'C:'
  #
  patch -p1 -i "${srcdir}/002-Fix-setup.py-for-cross-volume-builds.patch"
}

build() {
  cd "${srcdir}/gnatcoll-bindings-${pkgver}/python3"
  ${MINGW_PREFIX}/bin/python3 setup.py build --jobs="$(nproc)" --reconfigure
}

package() {
  cd "${srcdir}/gnatcoll-bindings-${pkgver}/python3"
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix="${pkgdir}${MINGW_PREFIX}"

  # Copy License Files
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" \
    "${srcdir}/gnatcoll-bindings-${pkgver}"/COPYING*
}
