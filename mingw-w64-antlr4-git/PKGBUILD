# Maintainer: Diego Sogari <diego.sogari@gmail.com>

_realname=antlr4
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=4.5.3.r4904.6f62821
pkgrel=1
pkgdesc="ANother Tool for Language Recognition v4 (mingw-w64)"
arch=('any')
url="http://www.antlr.org/"
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
source=("${_realname}::git+https://github.com/DanMcLaughlin/antlr4.git"
        "${_realname}-${pkgver}.patch")
sha256sums=('SKIP'
            'e555d8dd44c5c71af1ec1b653c311f51766feaa54a20fa689f0e5db16a69bebd')

pkgver() {
  cd "${srcdir}/${_realname}"
  printf "4.5.3.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_realname}"
  patch -Np1 -i "../${_realname}-${pkgver}.patch"
}

build() {
  rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    "../${_realname}/runtime/Cpp"

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
