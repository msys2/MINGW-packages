# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_bootstrapping=yes

_pyname=wheel
_realname=${_pyname}
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
provides=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
replaces=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=0.37.1
pkgrel=3
pkgdesc="A built-package format for Python (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://pypi.python.org/pypi/wheel"
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python")
if [[ "${_bootstrapping}" == "no" ]]; then
  makedepends=("${MINGW_PACKAGE_PREFIX}-python-installer")
fi
options=('!strip')
source=("https://pypi.debian.net/wheel/wheel-${pkgver}-py2.py3-none-any.whl"
        "install_wheel.py")
sha256sums=('4bdcd7d840138086126cd09254dc6195fb4fc6f01c050a1d7236f2630db1d22a'
            '668e64f912d6360330c403edb6100b21f6aa6dd6a7803d342fab8c35d8176ad9')

package() {

  if [[ "${_bootstrapping}" == "no" ]]; then
    MSYS2_ARG_CONV_EXCL="--prefix=" \
      python -m installer --prefix=${MINGW_PREFIX} --destdir="${pkgdir}" *.whl
  else
    _pythonpath=`python -c "import sysconfig; print(sysconfig.get_path('platlib'))"`
    _site_packages="${pkgdir}$(cygpath ${_pythonpath})"
    mkdir -p "$_site_packages"
    python "${srcdir}/install_wheel.py" -i"${_site_packages}" *.whl
    MSYS2_ARG_CONV_EXCL="-p" python -m compileall \
      -o 0 -o 1 -q -s"${pkgdir}" -p"/" "${pkgdir}${MINGW_PREFIX}/lib/python"*
  fi

  install -Dm644 *.dist-info/LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/COPYING"
}
