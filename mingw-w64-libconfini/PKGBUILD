# Maintainer: nobody

_realname=libconfini
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.16.4
pkgrel=1
pkgdesc='Yet another INI parser (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://madmurphy.github.io/libconfini'
license=('GPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools" "${MINGW_PACKAGE_PREFIX}-cc")
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/madmurphy/${_realname}/archive/${pkgver}.tar.gz")
sha256sums=('b19095b0bbf63731d97e595205b305f821aa522868d50a3e9b8e75428c66f701')
noextract=(${_realname}-${pkgver}.tar.gz)

prepare() {
  [ -d "${srcdir}/${_realname}-${pkgver}" ] && rm -rf "${srcdir}/${_realname}-${pkgver}"
  tar -xzf "${srcdir}/${_realname}-${pkgver}.tar.gz" -C "${srcdir}"
  cd "${srcdir}/${_realname}-${pkgver}"
  ./bootstrap --noconfigure
}

build() {
  [ -d "${srcdir}/build-${MSYSTEM}" ] && rm -rf "${srcdir}/build-${MSYSTEM}"
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"
  ../${_realname}-${pkgver}/configure \
      --prefix="${MINGW_PREFIX}" \
      --build="${MINGW_CHOST}" \
      --host="${MINGW_CHOST}" \
      --enable-shared \
      --enable-static
  make
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"
  make check
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
}
