_realname='libconfini'
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver='1.10.2'
pkgrel=1
pkgdesc='Yet another INI parser'
arch=('i686' 'x86_64')
url='https://madmurphy.github.io/libconfini'
license=('GPL')
provides=("${_realname}")
conflicts=("${pkgname}-git" "${pkgname}-bin")
source=("https://github.com/madmurphy/${_realname}/archive/${pkgver}.tar.gz")
sha256sums=('e54dec793f12d8bc259a8c1d7c4c94f81d32a309b84d36c02d46e36f417abf4f')

prepare() {

	  [ -d "${srcdir}/build-${MINGW_CHOST}" ] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
	  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

	  ../${_realname}-${pkgver}/autogen.sh \
		--prefix="${MINGW_PREFIX}" \
		--build="${MINGW_CHOST}" \
		--host="${MINGW_CHOST}" \
		--target="${MINGW_CHOST}"

}

build() {

	cd "${srcdir}/build-${MINGW_CHOST}"
	make

}

check() {

	cd "${srcdir}/build-${MINGW_CHOST}"
	make check

}

package() {

	cd "${srcdir}/build-${MINGW_CHOST}"
	make DESTDIR="${pkgdir}" install

}

