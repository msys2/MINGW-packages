_libname='libconfini'
pkgname="${MINGW_PACKAGE_PREFIX}-${_libname}"
pkgver='1.10.2'
pkgrel=1
pkgdesc='Yet another INI parser'
arch=('i686' 'x86_64')
url='https://madmurphy.github.io/libconfini'
license=('GPL')
provides=("${_libname}")
conflicts=("${_libname}" "${_libname}-git" "${_libname}-bin" "${pkgname}-git" "${pkgname}-bin")
source=("https://github.com/madmurphy/${_libname}/archive/${pkgver}.tar.gz")
sha256sums=('e54dec793f12d8bc259a8c1d7c4c94f81d32a309b84d36c02d46e36f417abf4f')

prepare() {

	cd "${srcdir}/${_libname}-${pkgver}"
	./autogen.sh \
		--prefix="${MINGW_PREFIX}" \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--target=${MINGW_CHOST}

}

build() {

	cd "${srcdir}/${_libname}-${pkgver}"
	make

}

check() {

	cd "${srcdir}/${_libname}-${pkgver}"
	make check

}

package() {

	cd "${srcdir}/${_libname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

}

