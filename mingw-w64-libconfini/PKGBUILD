# Maintainer: nobody

_realname='libconfini'
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver='1.10.4'
pkgrel=1
pkgdesc='Yet another INI parser (mingw-w64)'
arch=('any')
url='https://madmurphy.github.io/libconfini'
license=('GPL')
options=('staticlibs')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/madmurphy/${_realname}/archive/${pkgver}.tar.gz")
sha256sums=('b249955eb43a0dae79bc1e0d5939f709ce2849cfcaafe19eee198316dc8ca749')

prepare() {
	[ -d "${srcdir}/build-${MINGW_CHOST}" ] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
	mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"
	../${_realname}-${pkgver}/bootstrap --noconfigure
}

build() {
	cd "${srcdir}/build-${MINGW_CHOST}"
	../${_realname}-${pkgver}/configure \
		--prefix="${MINGW_PREFIX}" \
		--build="${MINGW_CHOST}" \
		--host="${MINGW_CHOST}" \
		--enable-shared \
		--enable-static
	make
}

package() {
	cd "${srcdir}/build-${MINGW_CHOST}"
	make DESTDIR="${pkgdir}" install
}

