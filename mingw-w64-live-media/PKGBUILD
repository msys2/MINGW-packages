# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Maintainer: Zach Bacon <11doctorwhocanada@gmail.com>
# Contributor: Gilles CHAUVIN <gcnweb@gmail.com>

_realname=live-media
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2015.06.25
pkgrel=1
pkgdesc="A set of C++ libraries for multimedia streaming (mingw-w64)"
arch=('any')
license=('LGPL')
url="http://live555.com/liveMedia"
depends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('staticlibs')
source=("http://live555.com/liveMedia/public/live.${pkgver}.tar.gz")
md5sums=('7c13d58c020efa42369b5f23a839be0c')

build() {
  cd ${srcdir}/live
  ./genMakefiles mingw

  make CC=gcc
}

package() {
  cd ${srcdir}/live

  for dir in BasicUsageEnvironment groupsock liveMedia UsageEnvironment; do
    install -dm755 ${pkgdir}${MINGW_PREFIX}/{bin,lib,include/${dir}}
    install -m644 ${dir}/*.a "${pkgdir}${MINGW_PREFIX}/lib"
    install -m644 ${dir}/include/*.h* "${pkgdir}${MINGW_PREFIX}/include/${dir}"
  done

  for testprog in `find testProgs -type f -perm 755`; do
    install ${testprog} "${pkgdir}${MINGW_PREFIX}/bin"
  done
}
