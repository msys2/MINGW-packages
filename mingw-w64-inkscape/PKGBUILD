# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=inkscape
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=0.48.4
pkgrel=4
pkgdesc="Vector graphics editor using the SVG file format (mingw-w64)"
arch=('any')
url="http://inkscape.sourceforge.net/"
license=("GPL" "LGPL")
install=inkscape-${CARCH}.install
makedepends=("${_mingw_suff}-gcc"
    "${_mingw_suff}-pkg-config"
    "${_mingw_suff}-boost"
    "intltool")
depends=(
    "${_mingw_suff}-gc"
    "${_mingw_suff}-ghostscript"
    "${_mingw_suff}-gsl"
    "${_mingw_suff}-gtkmm"
    #"${_mingw_suff}-gtkspell"
    "${_mingw_suff}-hicolor-icon-theme"
    "${_mingw_suff}-imagemagick"
    "${_mingw_suff}-lcms2"
    "${_mingw_suff}-libxml2"
    "${_mingw_suff}-libxslt"
    "${_mingw_suff}-libwpg"
    "${_mingw_suff}-poppler"
    "${_mingw_suff}-popt"
    "${_mingw_suff}-python2"
    )
optdepends=(#"${_mingw_suff}-pstoedit: latex formulas"
            #"${_mingw_suff}-texlive-core: latex formulas"
            "${_mingw_suff}-python2-numpy: some extensions"
            #"${_mingw_suff}-python2-lxml: some extensions and filters"
            #"${_mingw_suff}-uniconvertor: reading/writing to some proprietary formats"
      )
options=('staticlibs' 'strip')
source=("http://downloads.sourceforge.net/project/${_realname}/${_realname}/${pkgver}/${_realname}-${pkgver}.tar.bz2"
    inkscape-0.48.4-automake-1.13.patch
    inkscape-0.48.4-fix-member-decl.patch
    inkscape-0.48.4-freetype.patch
    inkscape-0.48.4-gc74.patch
    inkscape-0.48.4-headers-fix.patch
    inkscape-0.48.4-python.patch
    inkscape-0.48.4-use-GStatBuf-insted-stat.patch
    inkscape-0.48.4-poppler-includes-conflicts.patch
    inkscape-0.48.4-mingw-dont-need-dl.patch
    inkscape-0.48.4-win32-relocatable.patch
    inkscape-0.48.4-win32-file-dialog.patch
    inkscape-0.48.4-python-extensions-location.patch
    poppler-0.26.patch)
sha1sums=('5f26f6ad191d1e7c2a9fb69a438722beb172224c'
          '6b82b0a9f31d4da453e77665098c13dec1535d5b'
          '71f071627b53efaa0a7a66bae0e2ef194803cbbf'
          '88bf1e15507db24868fd766e471ba6dde225af55'
          'eb05940d770aef9f31c2db9e96883ebfd9aea9a8'
          '12fddb0a0b34b4308de1979d13b26d05a6ec8f77'
          '4dd0da65e538ba7e73a42ce76efd5e5a155d3bf0'
          '0585754e2ff81d8e7a1cd6c648ff2d7c149f48fb'
          '8d65767ed2b291ccae70d8718f0b7a764632c445'
          'db9c1ce6372e8670f9949d2aaef3d27f8ded32ec'
          '6a65e95e9d310b48b3caebc98f323183cef60e0c'
          'e4841ec881085400b787b90acab959f8fe67a77c'
          '185cd8f6e4ad0b55862720c47f80da9dfa8a82ff'
          'b60843f43e6d3b6cfcf4e253abb3f7ea7bc69c2a')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  #patch -p1 -i ${srcdir}/inkscape-0.48.4-automake-1.13.patch
  patch -p1 -i ${srcdir}/inkscape-0.48.4-fix-member-decl.patch
  patch -p1 -i ${srcdir}/inkscape-0.48.4-freetype.patch
  patch -p1 -i ${srcdir}/inkscape-0.48.4-gc74.patch
  patch -p1 -i ${srcdir}/inkscape-0.48.4-headers-fix.patch
  patch -p1 -i ${srcdir}/inkscape-0.48.4-python.patch
  patch -p1 -i ${srcdir}/inkscape-0.48.4-use-GStatBuf-insted-stat.patch
  patch -p1 -i ${srcdir}/inkscape-0.48.4-poppler-includes-conflicts.patch
  patch -p1 -i ${srcdir}/inkscape-0.48.4-mingw-dont-need-dl.patch
  patch -p1 -i ${srcdir}/inkscape-0.48.4-win32-relocatable.patch
  patch -p1 -i ${srcdir}/inkscape-0.48.4-win32-file-dialog.patch
  patch -p1 -i ${srcdir}/inkscape-0.48.4-python-extensions-location.patch
  patch -p0 -i ${srcdir}/poppler-0.26.patch
  
  sed -i 's|/usr/bin/python\>|/usr/bin/python2|g' cxxtest/*.py
  sed -i 's|/usr/bin/env python\>|/usr/bin/env python2|g' share/*/{test/,}*.py
  #sed -i 's|"python" },|"python2" },|g' src/extension/implementation/script.cpp
  sed -i 's|python -c|python2 -c|g' configure share/extensions/uniconv*.py
  sed -i '/^#include <g.kmm/i #include <glibmm.h>' src/*{,/*{,/*{,/*}}}.{h,cpp}
  sed -e "s|export-dynamic|export-all-symbols|g" -i src/Makefile.am
  
  autoreconf -fi -I m4
}

build() {
  # export lt_cv_deplibs_check_method='pass_all'
  rm -rf $srcdir/cd build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}

  CPPFLAGS+=" -D__USE_MINGW_ANSI_STDIO=1"
  CFLAGS+=" -D__USE_MINGW_ANSI_STDIO=1"
  CXXFLAGS+=" -D__USE_MINGW_ANSI_STDIO=1"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --with-python \
    --without-perl \
    --enable-lcms \
    --enable-poppler-cairo \
    --disable-dependency-tracking
  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
}
