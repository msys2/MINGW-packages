# Maintainer: Frederik Carlier <frederik.carlier@keysight.com>

_realname=gnustep-base
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.30.0
pkgrel=11
pkgdesc="GNUstep Base library (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url='https://gnustep.github.org/'
license=('spdx:LGPL-2.1-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-clang"
             "${MINGW_PACKAGE_PREFIX}-lld"
             "${MINGW_PACKAGE_PREFIX}-gnustep-make"
             "${MINGW_PACKAGE_PREFIX}-curl"
             patchutils
             rsync)
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gnutls"
         "${MINGW_PACKAGE_PREFIX}-icu"
         "${MINGW_PACKAGE_PREFIX}-libffi"
         "${MINGW_PACKAGE_PREFIX}-libiconv"
         "${MINGW_PACKAGE_PREFIX}-libobjc2"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-libxslt"
         "${MINGW_PACKAGE_PREFIX}-zlib"
)
source=("https://github.com/gnustep/libs-base/releases/download/base-${pkgver//./_}/${_realname}-${pkgver}.tar.gz"
        # Use absolute paths in ./configure
        # https://github.com/gnustep/libs-base/pull/371, in review
        "https://github.com/gnustep/libs-base/commit/2039840e0e1f013f2d73deb6e083b88987cfa6af.patch")
sha256sums=('00b5bc4179045b581f9f9dc3751b800c07a5d204682e3e0eddd8b5e5dee51faa'
            '7f8d0d061abd24f10073f0a7ff0338b1907281aa2036745d8b2072732edf69dd')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -p1 -i "${srcdir}"/2039840e0e1f013f2d73deb6e083b88987cfa6af.patch
}

build() {
  rsync --recursive --times --links "${srcdir}/${_realname}-${pkgver}"/* "${srcdir}/build-${MSYSTEM}"
  cd "${srcdir}/build-${MSYSTEM}"

  CC="$MINGW_PREFIX/bin/clang" \
  CXX="$MINGW_PREFIX/bin/clang++" \
  ./configure \
    --prefix="${MINGW_PREFIX}"

  CC="$MINGW_PREFIX/bin/clang" \
  CXX="$MINGW_PREFIX/bin/clang++" \
  make
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  make install DESTDIR="${pkgdir}"

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
