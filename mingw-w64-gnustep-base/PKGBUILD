# Maintainer: Frederik Carlier <frederik.carlier@keysight.com>

_realname=gnustep-base
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.29.0
pkgrel=1
pkgdesc="GNUstep Base library (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64')
url='https://gnustep.github.org/'
license=('spdx:LGPL-2.1-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-clang"
             "${MINGW_PACKAGE_PREFIX}-gnustep-make"
             rsync)
depends=("${MINGW_PACKAGE_PREFIX}-libobjc2"
         "${MINGW_PACKAGE_PREFIX}-libffi"
         "${MINGW_PACKAGE_PREFIX}-icu"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-libxslt"
         "${MINGW_PACKAGE_PREFIX}-gnutls"
)
source=("https://github.com/gnustep/libs-base/releases/download/base-${pkgver//./_}/${_realname}-${pkgver}.tar.gz"
        # Fix GSXML compatibility with libxml2 v2.11.0
        # https://github.com/gnustep/libs-base/pull/295, merged
        "https://github.com/gnustep/libs-base/commit/37913d006d96a6bdcb963f4ca4889888dcce6094.patch"
        # Use absolute paths in ./configure
        # https://github.com/gnustep/libs-base/pull/371, in review
        "https://github.com/gnustep/libs-base/commit/2039840e0e1f013f2d73deb6e083b88987cfa6af.patch"
        # MinGW: Add dllimport/dllexport attributes when compiling with clang
        # https://github.com/gnustep/libs-base/pull/363, merged
        "https://github.com/gnustep/libs-base/commit/9041539ea25274cf92b7a3489ed5b03762d4af3f.patch")
sha256sums=('fa58eda665c3e0b9c420dc32bb3d51247a407c944d82e5eed1afe8a2b943ef37'
            'a7a3bad7bda7e63599677294f0ee0b2273b680168b6fd9b4b4c5618ba8a184d5'
            '2a325471df00494a2ff7b87a954e7ecee2e45c31296636326880a78078fa249f'
            'ceb7cfc82ff3801e82e12b791f18c04c42ba3ab9ee0cc79ee453c157bf89d8d0')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -p1 -i ${srcdir}/37913d006d96a6bdcb963f4ca4889888dcce6094.patch
  patch -p1 -i ${srcdir}/2039840e0e1f013f2d73deb6e083b88987cfa6af.patch
  patch -p1 -i ${srcdir}/9041539ea25274cf92b7a3489ed5b03762d4af3f.patch
}

build() {
  rsync --recursive --times --links "${srcdir}/${_realname}-${pkgver}"/* "${srcdir}/build-${MSYSTEM}"
  cd "${srcdir}/build-${MSYSTEM}"

  LDFLAGS="-fuse-ld=lld -lstdc++ -lgcc_s" \
  CC="$MINGW_PREFIX/bin/clang" \
  CXX="$MINGW_PREFIX/bin/clang++" \
  ./configure \
    --prefix="${MINGW_PREFIX}" \
    --build="${MINGW_CHOST}" \
    --host="${MINGW_CHOST}" \
    --target="${MINGW_CHOST}"
  
  CC="$MINGW_PREFIX/bin/clang" \
  CXX="$MINGW_PREFIX/bin/clang++" \
  make
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  make install DESTDIR="${pkgdir}"

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
