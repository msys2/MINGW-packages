# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=yasm
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3.0
pkgrel=7
pkgdesc="A rewrite of NASM to allow for multiple syntax supported (NASM, TASM, GAS, etc.) (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://www.tortall.net/projects/yasm/"
msys2_repository_url="https://github.com/yasm/yasm"
msys2_references=(
  "cpe: cpe:/a:tortall:yasm"
  "cpe: cpe:/a:yasm_project:yasm"
)
license=('spdx:BSD-2-Clause AND BSD-3-Clause AND (Artistic-1.0-Perl OR LGPL-2.0-or-later OR GPL-2.0-or-later)')
depends=("${MINGW_PACKAGE_PREFIX}-gettext-runtime")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-gettext-tools"
             "${MINGW_PACKAGE_PREFIX}-python")
source=(https://www.tortall.net/projects/yasm/releases/${_realname}-${pkgver}.tar.gz
        https://github.com/yasm/yasm/pull/287.patch)
sha256sums=('3dce6601b495f5b3d45b59f7d2492a340ee7e84b5beca17e48f862502bd5603f'
            '8e0d7a184d906564e6dd26b8f383c326ce580156e410f2d0ccfcfc4290d5e1fd')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  # Fix build with GCC 15.x
  patch -p1 -i "${srcdir}"/287.patch
}

build() {
  mkdir -p "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX}

  make
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}

  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/COPYING \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/Artistic.txt \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/Artistic.txt
  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/BSD.txt \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/BSD.txt
  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/GNU_GPL-2.0 \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/GNU_GPL-2.0
  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/GNU_LGPL-2.0 \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/GNU_LGPL-2.0
}
