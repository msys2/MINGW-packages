# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=yasm
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3.0
pkgrel=1
pkgdesc="A rewrite of NASM to allow for multiple syntax supported (NASM, TASM, GAS, etc.) (mingw-w64)"
arch=('any')
license=('BSD' 'GPL2' 'LGPL2.1' 'PerlArtistic')
url="http://www.tortall.net/projects/yasm/"
depends=("${MINGW_PACKAGE_PREFIX}-gettext")
options=('strip' '!libtool' 'staticlibs')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
source=(http://www.tortall.net/projects/yasm/releases/${_realname}-${pkgver}.tar.gz)
sha1sums=('b7574e9f0826bedef975d64d3825f75fbaeef55e')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
}

build() {
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  ${srcdir}/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST}

  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/BSD.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/BSD.txt"
}
