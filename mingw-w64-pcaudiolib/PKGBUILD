# Maintainer: Michael Rans <rans@email.com>

_realname=pcaudiolib
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3
pkgrel=1
pkgdesc="A portable cross-platform audio library (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/espeak-ng/pcaudiolib"
msys2_repository_url='https://github.com/espeak-ng/pcaudiolib'
license=("spdx:GPL-3.0-or-later")
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools")
source=("https://github.com/espeak-ng/pcaudiolib/releases/download/${pkgver}/pcaudiolib-${pkgver}.tar.gz"
        "001-fix-build-on-mingw.patch")
sha256sums=('e8bd15f460ea171ccd0769ea432e188532a7fb27fa73ec2d526088a082abaaad'
            'eb4a76ffd3ffd2d409aee1abf25bcb05f28cb8ba04da91ef71bdb2ae0559a9f1')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  patch -p1 -i ${srcdir}/001-fix-build-on-mingw.patch

  if [[ ${CC} == clang ]]; then
    sed -i "s|-nostdlib||g" m4/libtool.m4
  fi

  ./autogen.sh
}

build() {
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  export lt_cv_deplibs_check_method='pass_all'

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX}

  make
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/COPYING \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
