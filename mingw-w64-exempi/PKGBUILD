
_realname=exempi
_commit=edeaff6131c5264b8c070c03f4fc6db7264b5b0f
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.6.5.r9.gedeaff6
pkgrel=1
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
pkgdesc="Library to parse XMP metadata (mingw-w64)"
url="https://libopenraw.freedesktop.org/exempi"
msys2_repository_url="https://gitlab.freedesktop.org/libopenraw/exempi"
msys2_references=(
  "archlinux: exempi"
  "cpe: cpe:2.3:a:exempi_project:exempi"
)
license=("spdx:BSD-3-Clause")
depends=("${MINGW_PACKAGE_PREFIX}-expat"
         "${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-winpthreads"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("git"
             "${MINGW_PACKAGE_PREFIX}-boost"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-ninja")
source=("${_realname}"::"git+https://gitlab.freedesktop.org/libopenraw/$_realname.git#commit=$_commit"
        001-win32-mingw.patch
        002-win32-host.patch)
sha256sums=('224e864924aa4a0076e7f4aed0efc275313235c4f2deeee214a15aabf9722603'
            '6f59cf5eb300814e0a403606f2047b2f9b2863565d51e9237b9074b3f7d49b0e'
            'a271d34042b6dff7c8cabbd9388da5364b67c4268f0a4024de528ccf413e55fb')

pkgver() {
  cd "${srcdir}/${_realname}"

  git describe --long --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd ${_realname}

  apply_patch_with_msg \
    001-win32-mingw.patch \
    002-win32-host.patch
}

build() {
  mkdir -p build-${MSYSTEM}
  cd build-${MSYSTEM}

  LDFLAGS+=" -lpthread" \
  MSYS2_ARG_CONV_EXCL="--prefix=" \
   ${MINGW_PREFIX}/bin/meson setup \
     --prefix=${MINGW_PREFIX} \
     --wrap-mode=nodownload \
     --auto-features=enabled \
     --buildtype=plain \
     "../${_realname}"

  ${MINGW_PREFIX}/bin/ninja
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" ninja install

  install -Dm644 "${srcdir}/${_realname}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
