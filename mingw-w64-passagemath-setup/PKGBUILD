# Maintainer: Dirk Stolle

_realname=passagemath-setup
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=10.6.37
pkgrel=2
pkgdesc="passagemath: build system of the Sage library (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/passagemath/passagemath'
msys2_references=(
  'purl: pkg:pypi/passagemath-setup'
)
license=('spdx:GPL-2.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools")
optdepends=("${MINGW_PACKAGE_PREFIX}-python-jinja: for autogen feature")
options=('!strip')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname/-/_}-${pkgver}.tar.gz"
        "0001-dont-generate-init.py.patch")
sha256sums=('c5e6d603c69b4352ffa0aff5c55024cc3a1d6335e4b475950be778c915023cca'
            '5515fea938e3409fa3556828b63011d0a5bb47807bc4453876a28806d6747af9')

prepare() {
  cd "${_realname/-/_}-${pkgver}"

  # See <https://github.com/passagemath/passagemath/pull/1816>.
  patch -Nbp2 -i "${srcdir}/0001-dont-generate-init.py.patch"
}


build() {
  cp -r "${_realname/-/_}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl
}
