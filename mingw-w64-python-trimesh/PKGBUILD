# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>

_realname=trimesh
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=4.6.2
pkgrel=1
pkgdesc='Import, export, process, analyze and view triangular meshes. (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://trimsh.org/"
msys2_repository_url="https://github.com/mikedh/trimesh"
msys2_references=(
  'purl: pkg:pypi/trimesh'
)
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-numpy")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools")
optdepends=("${MINGW_PACKAGE_PREFIX}-python-networkx: graph operations"
            "${MINGW_PACKAGE_PREFIX}-python-scipy: convex hulls"
            "${MINGW_PACKAGE_PREFIX}-python-pyglet: preview windows")
options=('!strip')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('cfb9968b59626c26612ea5cadac055d97d119247f460d75b33f4f781177855d6')

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.md "${pkgdir}"${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE.md
}
