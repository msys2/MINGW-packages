# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=xalan-c

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.11
pkgrel=2
pkgdesc="An XSLT processing library (mingw-w64)"
arch=('any')
url="http://xalan.apache.org/xalan-c"
license=('APACHE')
makedepends=("sed" "${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-xerces-c=3.2.1" "${MINGW_PACKAGE_PREFIX}-tools")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" "${MINGW_PACKAGE_PREFIX}-xerces-c=3.2.1")
options=('!libtool' 'strip')
source=(http://apache.mirror.iphh.net/xalan/xalan-c/sources/xalan_c-$pkgver-src.tar.gz
    mingw-w64-fix.patch)
md5sums=('9227d3e7ab375da3c643934b33a585b8'
         '8be81685b5b6a0dda95a14d54ff888f4')

prepare(){
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/mingw-w64-fix.patch
}

build() {
  export XERCESCROOT=${MINGW_PREFIX}
  if [ "${MINGW_CHOST}" = "i686-w64-mingw32" ]; then
    export BITSTOBUILD=32
  else
    export BITSTOBUILD=64
  fi
  CXXFLAGS+=" -DNDEBUG -mthreads"
  CPPFLAGS+=""
  CFLAGS+=" -DNDEBUG -mthreads"
  LDFLAGS+=" -mthreads"
  LIBS+=" -mthreads"

  export XALANCROOT="${srcdir}"/${_realname}-${pkgver}/c
  export ICUROOT=${MINGW_PREFIX}
  export XALAN_LOCALE_SYSTEM=inmem
  export XALAN_LOCALE=en_US

  cd "${srcdir}"/${_realname}-${pkgver}/c
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}
  make -j1
}

package() {
  export XERCESCROOT=${MINGW_PREFIX}
  if [ "${MINGW_CHOST}" = "i686-w64-mingw32" ]; then
    export BITSTOBUILD=32
  else
    export BITSTOBUILD=64
  fi
  CXXFLAGS+=" -DNDEBUG -mthreads"
  CPPFLAGS+=""
  CFLAGS+=" -DNDEBUG -mthreads"
  LDFLAGS+=" -mthreads"
  LIBS+=" -mthreads"

  export XALANCROOT="${srcdir}"/${_realname}-${pkgver}/c
  export ICUROOT=${MINGW_PREFIX}
  export XALAN_LOCALE_SYSTEM=inmem
  export XALAN_LOCALE=en_US

  cd "${srcdir}"/${_realname}-${pkgver}/c
  make DESTDIR="$pkgdir" install
  mv "${pkgdir}${MINGW_PREFIX}"/lib/*.dll "${pkgdir}${MINGW_PREFIX}"/bin/
  find "${pkgdir}${MINGW_PREFIX}" -name '*.bat' -o -name '*.def' -o -name '*.exp' -o -name '*.111' | xargs -rtl1 rm
  pushd "${pkgdir}${MINGW_PREFIX}"/bin > /dev/null
  for ff in *.dll; do
    local nn=$(basename ${ff})
    nn=${nn%.dll}
    ${MINGW_PREFIX}/bin/gendef ${ff}
    ${MINGW_PREFIX}/bin/dlltool -D ${ff} -d ${nn}.def -l ../lib/lib${nn}.dll.a
    rm ${nn}.def
  done
  popd > /dev/null
}
