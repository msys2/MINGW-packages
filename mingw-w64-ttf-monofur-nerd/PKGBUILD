# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_font=monofur
_realname=ttf-${_font}-nerd
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgdesc="Patched font Monofur from nerd fonts library (mingw-w64)"
pkgver=3.0.2
pkgrel=1
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://github.com/ryanoasis/nerd-fonts'
_urlbase="${url}/releases/download/v${pkgver}"
license=('custom:Monofur Free License')
options=('!debug' '!strip')
groups=("${MINGW_PACKAGE_PREFIX}-nerd-fonts")
source=("${_font}-${pkgver}.tar.xz::${_urlbase}/Monofur.tar.xz")
sha256sums=('dcfea26b9b84528d62d01b81ae5eca51ea282b18d61ca41fbfdd23fb496c89fc')
noextract=("${_font}-${pkgver}.tar.xz")

prepare() {
  rm -rf ${_font}
  mkdir ${_font}
  bsdtar xf ${_font}-${pkgver}.tar.xz -C ${_font}
}

package() {
  install -Dm644 monofur/*.ttf -t "${pkgdir}${MINGW_PREFIX}/share/fonts/TTF"
  install -Dm644 monofur/LICENSE.txt -t "${pkgdir}${MINGW_PREFIX}/share/licenses/ttf-monofur-nerd/"
}
