# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=openturns
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.18
pkgrel=1
pkgdesc="Uncertainty treatment library (mingw-w64)"
license=('LGPL')
arch=(any)
mingw_arch=('mingw64' 'mingw32' 'ucrt64' 'clang64' 'clang32')
url="http://www.openturns.org/"
depends=("${MINGW_PACKAGE_PREFIX}-ceres-solver"
         "${MINGW_PACKAGE_PREFIX}-hdf5"
         "${MINGW_PACKAGE_PREFIX}-intel-tbb"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread-git"
         # "${MINGW_PACKAGE_PREFIX}-mpc" requires shared mpc
         # "${MINGW_PACKAGE_PREFIX}-mpfr" requires shared mpfr
         "${MINGW_PACKAGE_PREFIX}-muparser"
         "${MINGW_PACKAGE_PREFIX}-nlopt"
         $([[ ${MINGW_PACKAGE_PREFIX} != *-clang-* ]] && echo \
           "${MINGW_PACKAGE_PREFIX}-openblas" || echo \
           "${MINGW_PACKAGE_PREFIX}-f2cblaslapack" "${MINGW_PACKAGE_PREFIX}-openmp")
         "${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-dill"
         "${MINGW_PACKAGE_PREFIX}-python-psutil")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-boost"
             "${MINGW_PACKAGE_PREFIX}-swig")
source=("https://github.com/openturns/openturns/archive/v${pkgver}.tar.gz"
        "001-fix-default-site-packages.patch"
        "002-search-for-TBB-CONFIG-first.patch"
        "003-find-hdf5-c-component.patch")
sha256sums=('1840d3fd8b38fd5967b1fa04e49d8f760c2c497400430e97623595ca48754ae0'
            'd62063cb3ac4857b3882ead29727df8414e47de0b8be7d915db5b1a7a6aea339'
            'f85e501561b03ede1d325fc3736a28c91025e809988fc4b0a1b85a25e7c676c8'
            '57f3d405feea656a9a6ad6075205c655054a7cbe4c9034c0ea5bd95e7ea3bf62')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/001-fix-default-site-packages.patch
  # Partially backport https://github.com/openturns/openturns/commit/bb031b79
  patch -p1 -i "${srcdir}"/002-search-for-TBB-CONFIG-first.patch
  patch -p1 -i "${srcdir}"/003-find-hdf5-c-component.patch
}

build() {
  [[ -d "${srcdir}"/build-${MSYSTEM} ]] && rm -rf "${srcdir}"/build-${MSYSTEM}
  mkdir -p "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

  local -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=;-DOPENTURNS_DOC_PATH=" \
  ${MINGW_PREFIX}/bin/cmake.exe -Wno-dev \
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DOPENTURNS_DOC_PATH=${MINGW_PREFIX}/share/licenses/${_realname} \
    -DPYTHON_EXECUTABLE=${MINGW_PREFIX}/bin/python \
    -DUSE_MPFR=OFF \
    -DUSE_MPC=OFF \
    -DUSE_SPHINX=OFF \
    ${extra_config[@]} \
    ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}
  DESTDIR="$pkgdir" ${MINGW_PREFIX}/bin/cmake --install .
}
