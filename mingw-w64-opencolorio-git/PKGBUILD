# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=opencolorio
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=815.15e96c1f
pkgrel=1
pkgdesc="A color management framework for visual effects and animation (mingw-w64)"
arch=('any')
url="http://opencolorio.org"
license=('BSD')
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
depends=("${MINGW_PACKAGE_PREFIX}-boost"
         "${MINGW_PACKAGE_PREFIX}-glew"
         "${MINGW_PACKAGE_PREFIX}-lcms2"
         "${MINGW_PACKAGE_PREFIX}-python3"
         "${MINGW_PACKAGE_PREFIX}-tinyxml"
         "${MINGW_PACKAGE_PREFIX}-yaml-cpp")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "git")
options=('strip')
source=("${_realname}"::"git+https://github.com/imageworks/OpenColorIO.git"
        #${_realname}-${pkgver}.tar.gz::https://github.com/imageworks/OpenColorIO/tarball/v${pkgver}.tar.gz
        mingw-w64.patch
        fix-redefinitions.patch
        msvc-flags.patch
        declspec-typo.patch
        mingw-no-dupenv_s.patch
        detect-mingw-python.patch)
sha256sums=('SKIP'
            '071378804d6f54a59c988d6d749b3b5e1e12a64bc1ed1558856c7b40a39782bc'
            '802b66e1e52c5b4341afbdcc31211917b1223fb2db111c49603bc415c29d4b91'
            '010780ed9be44560684459690eca690de53a70bf822eb70b4e76b3f3df4991ae'
            'e05535790881bdd719084b1da131f2c0a7d858ab6a22c2e9e061eaf451383ea1'
            '3251ea540eae1fb0648248f0c60179765956003a1c4adbf6c88e7476171a8702'
            '330a81ce83c32bfffe214b3404c5001585b9113a0e58572ea09bb67960a80ced')

pkgver() {
  cd "${srcdir}/${_realname}"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${_realname}
  patch -p1 -i ${srcdir}/mingw-w64.patch
  patch -p1 -i ${srcdir}/fix-redefinitions.patch
  patch -p1 -i ${srcdir}/msvc-flags.patch
  patch -p1 -i ${srcdir}/declspec-typo.patch
  patch -p1 -i ${srcdir}/mingw-no-dupenv_s.patch
  patch -p1 -i ${srcdir}/detect-mingw-python.patch
}

build() {
  [[ -d "build-${MINGW_CHOST}" ]] && rm -rf "build-${MINGW_CHOST}"
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  
  local _py3ver=$(${MINGW_PREFIX}/bin/python3 -c "import sys;sys.stdout.write('.'.join(map(str, sys.version_info[:2])))")

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DGLEW_INCLUDES=${MINGW_PREFIX}/include \
    -DGLEW_LIBRARIES=${MINGW_PREFIX}/lib/libglew32.dll.a \
    -DUSE_EXTERNAL_TINYXML=ON \
    -DUSE_EXTERNAL_YAML=ON \
    -DUSE_EXTERNAL_LCMS=ON \
    -DOCIO_USE_BOOST_PTR=ON \
    -DOCIO_BUILD_PYGLUE=ON \
    -DOCIO_PYGLUE_LINK=ON \
    -DPYTHON_VERSION=${_py3ver} \
    -DPYTHON=${MINGW_PREFIX}/bin/python3.exe \
    -DPYTHON_EXECUTABLE=${MINGW_PREFIX}/bin/python3.exe \
    -DOCIO_BUILD_TRUELIGHT=OFF \
    ../${_realname}

  make
}

package() {
  cd build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install
  install -Dm644 ${srcdir}/${_realname}/LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
