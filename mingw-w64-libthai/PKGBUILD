# Maintainer: Andrew Sun <adsun701@gmail.com>

_realname=libthai
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}" "${MINGW_PACKAGE_PREFIX}-${_realname}-docs")
pkgver=0.1.29
pkgrel=2
pkgdesc="Thai language support routines (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://linux.thai.net/projects/libthai"
license=('LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-libdatrie")
makedepends=("autoconf-archive" "${MINGW_PACKAGE_PREFIX}-autotools" "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-doxygen")
source=("https://linux.thai.net/pub/thailinux/software/libthai/${_realname}-${pkgver}.tar.xz"
        'relocatable.patch')
sha256sums=('fc80cc7dcb50e11302b417cebd24f2d30a8b987292e77e003267b9100d0f4bcd'
            'f258c748e37a11ce20d307a9a27cf68de9543d79b7e60ebd31c7cf4398b5f6d9')

prepare() {
  cd "${srcdir}/libthai-${pkgver}"
  patch -Np1 -i "${srcdir}"/relocatable.patch
  autoreconf -fiv
}

build() {
  [[ -d "${srcdir}"/build-${MSYSTEM} ]] && rm -rf "${srcdir}"/build-${MSYSTEM}
  mkdir -p "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared

  make
  make install DESTDIR="${srcdir}/build-${MSYSTEM}/dest"
}

package_libthai() {
  optdepends=("${MINGW_PACKAGE_PREFIX}-${_realname}-docs: Documentation for ${_realname}")

  cd "${srcdir}"/build-${MSYSTEM}
  mkdir -p "${pkgdir}${MINGW_PREFIX}"
   mkdir -p "${pkgdir}${MINGW_PREFIX}/share/${_realnamne}"
  cp -r ${srcdir}/build-${MSYSTEM}/dest${MINGW_PREFIX}/lib "${pkgdir}${MINGW_PREFIX}"
  cp -r ${srcdir}/build-${MSYSTEM}/dest${MINGW_PREFIX}/share/${_reakbane}/* "${pkgdir}${MINGW_PREFIX}/share/${_realnamne}"
  cp -r ${srcdir}/build-${MSYSTEM}/dest${MINGW_PREFIX}/include "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}

package_libthai-docs() {
pkgdesc="Documentation for libthai (mingw64)"

  cd ${srcdir}/build-${MSYSTEM}
  mkdir -p ${pkgdir}${MINGW_PREFIX}

  cp -r ${srcdir}/build-${MSYSTEM}/dest${MINGW_PREFIX}/share \
    ${pkgdir}${MINGW_PREFIX}

  # thbrk.tri is in the main package.
  rm -rf ${pkgdir}${MINGW_PREFIX}/share/${_realnamne}
}


# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
