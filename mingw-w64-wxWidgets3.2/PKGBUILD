####
#
# Based on packages found at these URLs
#     https://archlinux.org/packages/extra/x86_64/wxgtk-common/
#     https://github.com/msys2/MINGW-packages/tree/master/mingw-w64-wxWidgets
#
# Maintainer: Tim Stahlhut <stahta01@gmail.com>
#
####

_realname=wxWidgets
_libname=wxmsw
_wx_basever=3.2
# Example _wx_buildver value is "-rc"
_wx_buildver=

pkgbase=mingw-w64-${_realname}3.2
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}-meta${_wx_basever}"
         "${MINGW_PACKAGE_PREFIX}-wxconfig${_wx_basever}"
         "${MINGW_PACKAGE_PREFIX}-wxrc${_wx_basever}"
         "${MINGW_PACKAGE_PREFIX}-locale${_wx_basever}"
         "${MINGW_PACKAGE_PREFIX}-bakefile${_wx_basever}"
         "${MINGW_PACKAGE_PREFIX}-msw_cb_headers${_wx_basever}"
         "${MINGW_PACKAGE_PREFIX}-${_libname}${_wx_basever}")
pkgver=${_wx_basever}.0
pkgrel=1
pkgdesc="A C++ library that lets developers create applications for Windows, Linux and UNIX (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://wxwidgets.org/"
license=('custom:wxWindows')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-autotools"
  "patch"
  "${MINGW_PACKAGE_PREFIX}-libpng"
  "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
  "${MINGW_PACKAGE_PREFIX}-libtiff"
  "${MINGW_PACKAGE_PREFIX}-cc"
)
options=('strip' 'staticlibs' 'buildflags' '!debug')
#options=('!strip' 'staticlibs' 'buildflags' '!debug') # Best options for debugging CodeLite crashes
source=(
  https://github.com/wxWidgets/wxWidgets/releases/download/v${pkgver}${_wx_buildver}/${_realname}-${pkgver}${_wx_buildver}.tar.bz2

  # This patch is a MSys2 run-time patch
  001-wxWidgets-3.0.2-relocate-prefix-in-bin-wx-config.patch

  # the wxTeam rejected this patch
  005-wxWidgets-3.1.3-Remove-WX_LIBS_STATIC-from-m4.patch

  # This is needed to build clang samples in check step
  010-wxWidgets-3.1.6-Redo-manifest-filename-logic.patch
)
sha256sums=('356e9b55f1ae3d58ae1fed61478e9b754d46b820913e3bfbc971c50377c1903a'
            '2b3b183a6a76ad539abc49a41033aa923c13b395c0e55189ba962068781c7135'
            '4a4828f0c9cdc638cffde6a30b5dfb14283719acc9e89e19de8ec2d5a80a5aec'
            'b30100e03f350e14060923781c3c7979735cc81996a13012bef980a23eb3d20b')
prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}${_wx_buildver}

  # Fix MSys2 Run-Time wx-config bug.
  patch -p1 -i "${srcdir}"/001-wxWidgets-3.0.2-relocate-prefix-in-bin-wx-config.patch

  # This patch is not really needed; but, WX_LIBS_STATIC does not work correctly under MSys2
  # Removed it to see if anything breaks or if anything is fixed.
  patch -p1 -i "${srcdir}"/005-wxWidgets-3.1.3-Remove-WX_LIBS_STATIC-from-m4.patch

  patch -p1 -i "${srcdir}"/010-wxWidgets-3.1.6-Redo-manifest-filename-logic.patch
}

build() {
  ####
  # Configure options added to support other software:
  #   --enable-graphics_ctx     codelite
  #   --enable-graphics-d2d     codeblocks
  #
  # Configure options added to check for build issues
  #   --disable-precomp-headers
  #
  # Configure options added to avoid possible future issues
  #   --with-cxx=17
  #   --enable-std_string
  #
  # Configure options added to avoid warnings:
  #   --with-regex=builtin
  #
  # Configure options known to cause build errors:
  #   --disable-regkey                                compile error
  #
  # Configure options believed to reduce code size or build time:
  #   --without-opengl
  #   --without-subdirs
  #   --disable-webview
  #   --disable-mediactrl
  ####

  if check_option "buildflags" "y"; then
  # Remove the -O and -ggdb options to avoid configuration warnings
  # from the normal settings found in /etc/makepkg_mingw??.conf

    CXXFLAGS=${CXXFLAGS/-Og }
    CXXFLAGS=${CXXFLAGS/-O2 }
    CXXFLAGS=${CXXFLAGS/-ggdb }

    CFLAGS=${CFLAGS/-Og }
    CFLAGS=${CFLAGS/-O2 }
    CFLAGS=${CFLAGS/-ggdb }
  fi

  local -a _debug_options=()

  if check_option "debug" "y"; then
    _debug_options+=("--enable-debug=yes")
    _debug_options+=("--enable-debug_gdb")
  else
    _debug_options+=("--enable-optimise")
  fi

  #echo "_debug_options := ${_debug_options[@]}"

  [[ -d "${srcdir}"/build-${MSYSTEM}-shared ]] && rm -rf "${srcdir}"/build-${MSYSTEM}-shared
  mkdir -p "${srcdir}"/build-${MSYSTEM}-shared && cd "${srcdir}"/build-${MSYSTEM}-shared

  ../${_realname}-${pkgver}${_wx_buildver}/configure \
    --prefix=${MINGW_PREFIX} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    "${_debug_options[@]}" \
    --enable-shared \
    --enable-std_string \
    --enable-iff \
    --disable-permissive \
    --enable-unicode \
    --enable-graphics_ctx \
    --enable-graphics-d2d \
    --enable-accessibility \
    --disable-monolithic \
    --disable-precomp-headers \
    --with-msw \
    --with-cxx=17 \
    --with-opengl \
    --with-libpng=sys \
    --with-libjpeg=sys \
    --with-libtiff=sys \
    --with-regex=builtin

  make #VERBOSE=1


  [[ -d "${srcdir}"/build-${MSYSTEM}-static ]] && rm -rf "${srcdir}"/build-${MSYSTEM}-static
  mkdir -p "${srcdir}"/build-${MSYSTEM}-static && cd "${srcdir}"/build-${MSYSTEM}-static

  ../${_realname}-${pkgver}${_wx_buildver}/configure \
    --prefix=${MINGW_PREFIX} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    "${_debug_options[@]}" \
    --disable-shared \
    --enable-std_string \
    --enable-iff \
    --disable-permissive \
    --enable-unicode \
    --enable-graphics_ctx \
    --enable-graphics-d2d \
    --enable-accessibility \
    --disable-monolithic \
    --disable-precomp-headers \
    --with-msw \
    --with-cxx=17 \
    --with-opengl \
    --with-libpng=sys \
    --with-libjpeg=sys \
    --with-libtiff=sys \
    --with-regex=builtin

  make #VERBOSE=1 -j1
}

check() {
#  cd "${srcdir}/build-${MSYSTEM}-shared/tests"    && make -k --jobs=1 || true

  cd "${srcdir}/build-${MSYSTEM}-shared/samples"  && make -k --jobs=1 || true
}

package_wxmsw3.2() {
  pkgdesc="A C++ library that lets developers create applications for Windows, Linux and UNIX (mingw-w64)"
  provides=("${MINGW_PACKAGE_PREFIX}-wxWidgets${_wx_basever}")
  conflicts=("${MINGW_PACKAGE_PREFIX}-wxWidgets${_wx_basever}")
  replaces=("${MINGW_PACKAGE_PREFIX}-${_libname}${_wx_basever}-git")
  depends=(
    "${MINGW_PACKAGE_PREFIX}-gcc-libs"
    "${MINGW_PACKAGE_PREFIX}-expat"
    "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
    "${MINGW_PACKAGE_PREFIX}-libpng"
    "${MINGW_PACKAGE_PREFIX}-libtiff"
    "${MINGW_PACKAGE_PREFIX}-xz"
    "${MINGW_PACKAGE_PREFIX}-zlib"
  )

  cd "${srcdir}"/build-${MSYSTEM}-static
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}"/build-${MSYSTEM}-shared
  make DESTDIR="${pkgdir}" install

  # Replace real full path with psuedo full path;
  # this seems to be needed sometime, but not always.
  local MINGW_PREFIX_WIN=$(cygpath -am ${MINGW_PREFIX})
  sed -s "s|-L${MINGW_PREFIX_WIN}/lib|-L${MINGW_PREFIX}/lib|g" -i "${pkgdir}${MINGW_PREFIX}/bin/wx-config"
  sed -s "s|-L${MINGW_PREFIX_WIN}/lib|-L${MINGW_PREFIX}/lib|g" -i "${pkgdir}${MINGW_PREFIX}/lib/wx/config/msw-unicode-${_wx_basever}"
  sed -s "s|-L${MINGW_PREFIX_WIN}/lib|-L${MINGW_PREFIX}/lib|g" -i "${pkgdir}${MINGW_PREFIX}/lib/wx/config/msw-unicode-static-${_wx_basever}"

  # create wx-config copy with version file suffix
  cp ${pkgdir}${MINGW_PREFIX}/bin/wx-config{,-${_wx_basever}}

  # create wxwin.m4 copy with version file suffix
  cp ${pkgdir}${MINGW_PREFIX}/share/aclocal/wxwin.m4 ${pkgdir}${MINGW_PREFIX}/share/aclocal/wxwin${_wx_basever}.m4

  # Remove conflicting files; see wxconfig package
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/wx-config

  # Remove conflicting files; that were copied to new names.
  rm -f ${pkgdir}${MINGW_PREFIX}/share/aclocal/wxwin.m4

  # Remove conflicting files; that do NOT need to be renamed.
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/wxrc.exe

  # Remove conflicting files; that I have no idea how to rename
  rm -fr ${pkgdir}${MINGW_PREFIX}/share/bakefile
  rm -fr ${pkgdir}${MINGW_PREFIX}/share/locale

  # License files
  cd "${srcdir}"/${_realname}-${pkgver}${_wx_buildver}/docs
  install -Dm644 preamble.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}${_wx_basever}/preamble.txt"
  install -Dm644 licence.txt  "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}${_wx_basever}/licence.txt"
  install -Dm644 licendoc.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}${_wx_basever}/licendoc.txt"
  install -Dm644 lgpl.txt     "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}${_wx_basever}/lgpl.txt"
  install -Dm644 gpl.txt      "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}${_wx_basever}/gpl.txt"
  install -Dm644 xserver.txt  "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}${_wx_basever}/xserver.txt"
}

package_wxWidgets-meta3.2() {
  pkgdesc="Meta package for wxWidgets library (mingw-w64)"
  conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}"
             "${MINGW_PACKAGE_PREFIX}-${_realname}3.0"
             "${MINGW_PACKAGE_PREFIX}-${_realname}-meta3.0")
  depends=("${MINGW_PACKAGE_PREFIX}-${_libname}${_wx_basever}"
           "${MINGW_PACKAGE_PREFIX}-locale${_wx_basever}"
           "${MINGW_PACKAGE_PREFIX}-wxrc${_wx_basever}"
           "${MINGW_PACKAGE_PREFIX}-wxconfig${_wx_basever}")
}

package_wxconfig3.2() {
  pkgdesc="config script for wxWidgets (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-${_libname}${_wx_basever}")
  provides=("${MINGW_PACKAGE_PREFIX}-wxconfig")
  conflicts=("${MINGW_PACKAGE_PREFIX}-wxconfig"
             "${MINGW_PACKAGE_PREFIX}-${_realname}"
             "${MINGW_PACKAGE_PREFIX}-${_realname}-meta3.0")

  cd "${srcdir}"/build-${MSYSTEM}-static
  make DESTDIR="${pkgdir}" install-wxconfig
  mv ${pkgdir}${MINGW_PREFIX}/bin/wx-config{,-static}

  cd "${srcdir}"/build-${MSYSTEM}-shared
  make DESTDIR="${pkgdir}" install-wxconfig
  # Remove conflicting file(s)
  rm -f ${pkgdir}${MINGW_PREFIX}/lib/wx/config/msw-unicode-${_wx_basever}
  rm -f ${pkgdir}${MINGW_PREFIX}/lib/wx/config/msw-unicode-static-${_wx_basever}
}

package_wxrc3.2() {
  pkgdesc="wxrc for wxWidgets (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-${_libname}${_wx_basever}")
  provides=("${MINGW_PACKAGE_PREFIX}-wxrc")
  conflicts=("${MINGW_PACKAGE_PREFIX}-wxrc"
             "${MINGW_PACKAGE_PREFIX}-${_realname}"
             "${MINGW_PACKAGE_PREFIX}-${_realname}-meta3.0")

  cd "${srcdir}"/build-${MSYSTEM}-shared
  make DESTDIR="${pkgdir}" install-wxrc
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/wxrc-${_wx_basever}.exe
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/wxrc-${_wx_basever}
}

package_locale3.2() {
  pkgdesc="locale for wxWidgets (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-${_libname}${_wx_basever}")
  provides=("${MINGW_PACKAGE_PREFIX}-locale")
  conflicts=("${MINGW_PACKAGE_PREFIX}-locale"
             "${MINGW_PACKAGE_PREFIX}-${_realname}"
             "${MINGW_PACKAGE_PREFIX}-${_realname}-meta3.0")

  cd "${srcdir}"/build-${MSYSTEM}-shared
  make DESTDIR="${pkgdir}" install
  rm -fr ${pkgdir}${MINGW_PREFIX}/{bin,include,lib}
  rm -fr ${pkgdir}${MINGW_PREFIX}/share/{aclocal,bakefile,licenses}
}

package_bakefile3.2() {
  pkgdesc="bakefile and wxwin.m4 for wxWidgets (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-${_libname}${_wx_basever}")
  provides=("${MINGW_PACKAGE_PREFIX}-bakefile")
  conflicts=("${MINGW_PACKAGE_PREFIX}-bakefile"
             "${MINGW_PACKAGE_PREFIX}-${_realname}"
             "${MINGW_PACKAGE_PREFIX}-${_realname}-meta3.0")

  cd "${srcdir}"/build-${MSYSTEM}-shared
  make DESTDIR="${pkgdir}" install
  rm -fr ${pkgdir}${MINGW_PREFIX}/{bin,include,lib}
  rm -fr ${pkgdir}${MINGW_PREFIX}/share/{licenses,locale}
}

package_msw_cb_headers3.2() {
  pkgdesc="private wxWidgets MSW headers needed by Code::Blocks (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-${_libname}${_wx_basever}")

  mkdir -p ${pkgdir}${MINGW_PREFIX}/include/wx-${_wx_basever}/wx/msw/private
  cd ${pkgdir}${MINGW_PREFIX}/include/wx-${_wx_basever}/wx/msw/private
  cp ${srcdir}/${_realname}-${pkgver}${_wx_buildver}/include/wx/msw/private/comptr.h ./
  cp ${srcdir}/${_realname}-${pkgver}${_wx_buildver}/include/wx/msw/private/graphicsd2d.h ./
  cp ${srcdir}/${_realname}-${pkgver}${_wx_buildver}/include/wx/msw/private/keyboard.h ./
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
