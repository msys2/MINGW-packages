# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=moar
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.31.5
pkgrel=1
pkgdesc='Terminal pager program (mingw-w64)'
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://github.com/walles/moar'
license=('spdx:BSD-2-Clause')
makedepends=("${MINGW_PACKAGE_PREFIX}-go" "${MINGW_PACKAGE_PREFIX}-cc")
options=('!strip')
source=("${_realname}-${pkgver}.tar.gz::https://github.com/walles/moar/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('2c03100e24f88163d808d52b7a5763c0fe731b29c46fd44a103a089c5429eb72')

build() {
    export GOOS=windows
    export GOROOT=${MINGW_PREFIX}/lib/go
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GO_LDFLAGS="-s -w"
    export GOFLAGS="-trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external"
    case "${CARCH}" in
      x86_64)
        GOFLAGS+=" -buildmode=pie"
        ;;
    esac

    cd "${_realname}-${pkgver}"
    go build -ldflags="-s -w -X main.versionString=${pkgver}"
}

check(){
    cd "${_realname}-${pkgver}"
    go test ./... || warning "Tests failed"
}

package() {
    cd "${_realname}-${pkgver}"
    install -Dm755 "moar.exe" "${pkgdir}${MINGW_PREFIX}/bin/moar.exe"
    install -Dm644 "moar.1" "${pkgdir}${MINGW_PREFIX}/share/man/man1/moar.1"
    install -Dm644 "LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
