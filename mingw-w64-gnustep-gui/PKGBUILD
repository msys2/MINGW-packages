# Maintainer: Frederik Carlier <frederik.carlier@keysight.com>

_realname=gnustep-gui
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.30.0
pkgrel=5
pkgdesc="GNUstep GUI Library (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64')
url='https://gnustep.github.io/'
license=('spdx:LGPL-2.1-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-clang"
             "${MINGW_PACKAGE_PREFIX}-lld"
             "${MINGW_PACKAGE_PREFIX}-gnustep-make"
             rsync)
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gnustep-base"
         "${MINGW_PACKAGE_PREFIX}-giflib"
         "${MINGW_PACKAGE_PREFIX}-icu"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libtiff")
source=("https://github.com/gnustep/libs-gui/releases/download/gui-${pkgver//./_}/${_realname}-${pkgver}.tar.gz"
        # GifQuantizeBuffer is considered private API in libgif 5.2 and later
        # https://github.com/gnustep/libs-gui/pull/207, merged
        "https://github.com/gnustep/libs-gui/commit/0c5e062e46e9801746ffbbfcaab2a3cbd6d253af.patch"
        # Make PACKAGE_SCOPE @public on MinGW
        # https://github.com/gnustep/libs-gui/pull/242/, in review
        "https://github.com/gnustep/libs-gui/commit/0b88dbbe321ff00298296a83d24296151a1a9b04.patch")
sha256sums=('469dcaa54ed05b2520a704c30c0761a75b3ade8428e2e64645fb7b38a15c3cc3'
            '5a7087b6cbd9524e228943799d352f5dcf3f9a442a27aab9f16f0e2fc75cf710'
            '164dd40e987f69b4effdd8b9b42a3ad2e0bf01c420c2a76bdf137f9351df92bd')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -p1 -i ${srcdir}/0c5e062e46e9801746ffbbfcaab2a3cbd6d253af.patch
  patch -p1 -i ${srcdir}/0b88dbbe321ff00298296a83d24296151a1a9b04.patch
}

build() {
  rsync --recursive --times --links "${srcdir}/${_realname}-${pkgver}"/* "${srcdir}/build-${MSYSTEM}"
  cd "${srcdir}/build-${MSYSTEM}"

  case ${MSYSTEM} in
    MINGW*|UCRT*)
      export LDFLAGS="-fuse-ld=lld -lstdc++ -lgcc_s"
      ;;
    CLANG*)
      export LDFLAGS="-lc++"
      ;;
  esac

  CC="$MINGW_PREFIX/bin/clang" \
  CXX="$MINGW_PREFIX/bin/clang++" \
  ./configure \
    --prefix="${MINGW_PREFIX}"
  
  CC="$MINGW_PREFIX/bin/clang" \
  CXX="$MINGW_PREFIX/bin/clang++" \
  make
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  make install DESTDIR="${pkgdir}"

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING.LIB" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING.LIB"
}
