# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_variant=-${KF5_VARIANT:-shared}
source "$(dirname ${BASH_SOURCE[0]})"/../mingw-w64-PKGBUILD-common/kde-frameworks5
_realname=lokalize
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=22.08.0
pkgrel=1
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=('GPL' 'LGPL' 'FDL')
url="https://apps.kde.org/lokalize/"
pkgdesc="Computer-Aided Translation System (mingw-w64)"
groups=("${MINGW_PACKAGE_PREFIX}-kdesdk")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-extra-cmake-modules"
             #"${MINGW_PACKAGE_PREFIX}-kdoctools-qt5${_namesuff}"
             "${MINGW_PACKAGE_PREFIX}-icoutils")
depends=("${MINGW_PACKAGE_PREFIX}-ki18n-qt5${_namesuff}"
         "${MINGW_PACKAGE_PREFIX}-ktextwidgets-qt5${_namesuff}"
         "${MINGW_PACKAGE_PREFIX}-kio-qt5${_namesuff}"
         "${MINGW_PACKAGE_PREFIX}-kxmlgui-qt5${_namesuff}"
         "${MINGW_PACKAGE_PREFIX}-knotifications-qt5${_namesuff}"
         "${MINGW_PACKAGE_PREFIX}-kconfig-qt5${_namesuff}"
         "${MINGW_PACKAGE_PREFIX}-kcoreaddons-qt5${_namesuff}"
         "${MINGW_PACKAGE_PREFIX}-kdbusaddons-qt5${_namesuff}"
         "${MINGW_PACKAGE_PREFIX}-kcrash-qt5${_namesuff}"
         "${MINGW_PACKAGE_PREFIX}-sonnet-qt5${_namesuff}"
         "${MINGW_PACKAGE_PREFIX}-hunspell"
         "${MINGW_PACKAGE_PREFIX}-hicolor-icon-theme")
source=("https://download.kde.org/stable/release-service/${pkgver}/src/${_realname}-${pkgver}.tar.xz"
        '0001-lokalize-22.04.3-disable-doc.patch'
        '0002-lokalize-22.04.3-fix-cast.patch')
sha256sums=('3d0162df098618e87114ee70156353b746f4a76338a766a121e283286229e56a'
            '5bdb3d89857b634b280c3cb648954d041391bcc7320002d4c310331265552b77'
            '239bf43c0f4113ee66d624186f339880f46fae0226a6675a21f2c3aec5e44af3')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  patch -p1 -i ${srcdir}/0001-lokalize-22.04.3-disable-doc.patch
  patch -p1 -i ${srcdir}/0002-lokalize-22.04.3-fix-cast.patch
}

build() {
  [[ -d "${srcdir}/build-${MSYSTEM}${_variant}" ]] && rm -rf "${srcdir}/build-${MSYSTEM}${_variant}"
  mkdir -p "${srcdir}/build-${MSYSTEM}${_variant}" && cd "${srcdir}/build-${MSYSTEM}${_variant}"

  local -a extra_config

  if [ "${_variant}" = "-static" ]; then
    extra_config+=( -DBUILD_SHARED_LIBS=NO )
  fi

  _kde_f5_build_env
  ${MINGW_PREFIX}/bin/cmake \
    -G"Ninja" \
    -DCMAKE_BUILD_TYPE=$(_kde_f5_CMAKE_BUILD_TYPE) \
    "${_kde_f5_KDE_INSTALL_DIRS[@]}" \
    -DBUILD_QCH=OFF \
    -DBUILD_TESTING=OFF \
    -DECM_DIR=${MINGW_PREFIX}/share/ECM \
    "${extra_config[@]}" \
    ../${_realname}-${pkgver}

  cmake --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}${_variant}"

  DESTDIR="${pkgdir}" cmake --install .
}
