# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_variant=-${KF5_VARIANT:-shared}
source "$(dirname ${BASH_SOURCE[0]})"/../mingw-w64-PKGBUILD-common/kde-frameworks5
_realname=lokalize
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=24.05.2
pkgrel=1
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'archlinux: lokalize'
)
msys2_repository_url="https://invent.kde.org/sdk/lokalize/"
url="https://apps.kde.org/lokalize/"
license=('GPL' 'LGPL' 'FDL')
pkgdesc="Computer-Aided Translation System (mingw-w64)"
groups=("${MINGW_PACKAGE_PREFIX}-kde-applications"
        "${MINGW_PACKAGE_PREFIX}-kdesdk")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-extra-cmake-modules"
             "${MINGW_PACKAGE_PREFIX}-kdoctools-qt5${_namesuff}"
             "${MINGW_PACKAGE_PREFIX}-icoutils")
depends=("${MINGW_PACKAGE_PREFIX}-ki18n-qt5${_namesuff}"
         "${MINGW_PACKAGE_PREFIX}-ktextwidgets-qt5${_namesuff}"
         "${MINGW_PACKAGE_PREFIX}-kio-qt5${_namesuff}"
         "${MINGW_PACKAGE_PREFIX}-kxmlgui-qt5${_namesuff}"
         "${MINGW_PACKAGE_PREFIX}-knotifications-qt5${_namesuff}"
         "${MINGW_PACKAGE_PREFIX}-kconfig-qt5${_namesuff}"
         "${MINGW_PACKAGE_PREFIX}-kcoreaddons-qt5${_namesuff}"
         "${MINGW_PACKAGE_PREFIX}-kdbusaddons-qt5${_namesuff}"
         "${MINGW_PACKAGE_PREFIX}-kcrash-qt5${_namesuff}"
         "${MINGW_PACKAGE_PREFIX}-sonnet-qt5${_namesuff}"
         "${MINGW_PACKAGE_PREFIX}-hunspell"
         "${MINGW_PACKAGE_PREFIX}-hicolor-icon-theme")
source=("https://download.kde.org/stable/release-service/${pkgver}/src/${_realname}-${pkgver}.tar.xz"{,.sig}
        '0002-lokalize-22.04.3-fix-cast.patch')
sha256sums=('baaed8e3b913a4ce5726f66e8f3113a7480dfee08dd7bfd7fb4709f1879a0a70'
            'SKIP'
            '239bf43c0f4113ee66d624186f339880f46fae0226a6675a21f2c3aec5e44af3')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  patch -p1 -i ${srcdir}/0002-lokalize-22.04.3-fix-cast.patch
}

build() {
  [[ -d "${srcdir}/build-${MSYSTEM}${_variant}" ]] && rm -rf "${srcdir}/build-${MSYSTEM}${_variant}"
  mkdir -p "${srcdir}/build-${MSYSTEM}${_variant}" && cd "${srcdir}/build-${MSYSTEM}${_variant}"

  local -a extra_config

  if [ "${_variant}" = "-static" ]; then
    extra_config+=( -DBUILD_SHARED_LIBS=NO )
  fi

  _kde_f5_build_env
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"Ninja" \
    -DCMAKE_BUILD_TYPE=$(_kde_f5_CMAKE_BUILD_TYPE) \
    "${_kde_f5_KDE_INSTALL_DIRS[@]}" \
    -DBUILD_QCH=OFF \
    -DBUILD_TESTING=OFF \
    -DECM_DIR=${MINGW_PREFIX}/share/ECM \
    "${extra_config[@]}" \
    ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake.exe --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}${_variant}"

  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install .
}
