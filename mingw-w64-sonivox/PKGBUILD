# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=sonivox
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=4.0.0
pkgrel=1
pkgdesc="Fork of the AOSP 'platform_external_sonivox' to use outside of Android (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/EmbeddedSynth/sonivox"
license=('spdx:Apache-2.0')
depends=(
  "${MINGW_PACKAGE_PREFIX}-cc-libs"
  "${MINGW_PACKAGE_PREFIX}-zlib"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-ninja"
  "${MINGW_PACKAGE_PREFIX}-gtest"
)
source=(
  "${_realname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz"
  "0001-dont-install-deps.patch"
  "https://www.ronimusic.com/sf2/Airfont_340.dls"  # soundfont required for testing...
)
sha256sums=('bd07e384041df010576b2bc1626625465f93ac6ae2e96ae792d0994b816880b1'
            'a906cfab0da79eac1dc490ca66020fd12e1238f1b9391c10455e2f59092c3918'
            'beb3e39e3c9fc51ef4dff36fdd8db0361471a91d244c3ee78af90f6d3c783b04')

prepare() {
  # Symlink the soundfont required for unit tests into place:
  # https://github.com/pedrolcl/sonivox/issues/87
  ln -s Airfont_340.dls soundfont.dls

  cd "${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-dont-install-deps.patch"
}

build() {
  # This ensures the soundfont used in unit tests is found and not downloaded during build.
  export TEMP="${srcdir}"
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DCMAKE_DLL_NAME_WITH_SOVERSION=ON \
      -S "${_realname}-${pkgver}" \
      -B build-${MSYSTEM} \
      -W no-dev

  cmake --build "build-${MSYSTEM}" --verbose
}

check() {
  ctest --test-dir "build-${MSYSTEM}" --output-on-failure
}

package() {
  DESTDIR="${pkgdir}" cmake --install "build-${MSYSTEM}"
}
