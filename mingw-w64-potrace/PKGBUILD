# Maintainer: Yusuke Sasaki <y_sasaki@nuem.nagoya-u.ac.jp>

_realname=potrace
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.15
pkgrel=2
pkgdesc="Tool for tracing a bitmap, which means, transforming a bitmap into a smooth, scalable image (mingw-w64)"
arch=('any')
url='https://potrace.sourceforge.io/'
license=('GPL2')
source=("https://potrace.sourceforge.io/download/${pkgver}/${_realname}-${pkgver}.tar.gz"
        001-reaarange-compiler-detect.patch)
sha256sums=('a9b33904ace328340c850a01458199e0064e03ccaaa731bc869a842b1b8d529d'
            '2903ae3c291468b9e30f20bc0ab9f261d0c6922ead8fc641b486290bfa33c187')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-reaarange-compiler-detect.patch
  autoreconf -fiv
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --mandir=${MINGW_PREFIX}/share/man \
    --enable-shared \
    --enable-static \
    --with-libpotrace
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
