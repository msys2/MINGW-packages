# Maintainer: @naveen521kk on Github, Naveen M K <naveen521kk@gmail.com>

_perlname=Math-Int64
_realname="${_perlname,,}"
pkgbase=mingw-w64-perl-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-perl-${_realname}")
pkgver=0.57
pkgrel=3
pkgdesc="Math::Int64 - Manipulate 64 bits integers in Perl (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://metacpan.org/dist/Math-Int64"
msys2_references=(
  'purl: pkg:cpan/SALVA/Math-Int64'
)
license=('spdx:Artistic-1.0-Perl OR GPL-1.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-perl")
groups=("${MINGW_PACKAGE_PREFIX}-perl-modules")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-make")
options=('!emptydirs')
source=("https://www.cpan.org/authors/id/S/SA/SALVA/${_perlname}-${pkgver}.tar.gz"
        "patchmakefile.py")
sha256sums=('12c60111c1cfceb257ac2ccc5b2e1721879efa09a1b0a73e8836afca107a7d75'
            '9b3a852cfb38c05aaea3a6dff4625b038c1d889ed99bb292d8b73e4fb83ff676')

build() {
  cd "$srcdir/${_perlname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL
  export pkgdir=$pkgdir
  python $srcdir/patchmakefile.py
  mingw32-make
}

package() {
  cd "$srcdir/${_perlname}-${pkgver}"
  mingw32-make DESTDIR="${pkgdir}/" install
}

