# Maintainer: @naveen521kk on Github, Naveen M K <naveen521kk@gmail.com>

_perlname=Math-Int64
_realname="${_perlname,,}"
pkgbase=mingw-w64-perl-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-perl-${_realname}")
pkgver=0.54
pkgrel=3
pkgdesc="Math::Int64 - Manipulate 64 bits integers in Perl (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=('GPL' 'PerlArtistic')
url="https://metacpan.org/release/Math-Int64"
depends=("${MINGW_PACKAGE_PREFIX}-perl")
groups=("${MINGW_PACKAGE_PREFIX}-perl-modules")
options=('!emptydirs')
makedepends=("${MINGW_PACKAGE_PREFIX}-python" "${MINGW_PACKAGE_PREFIX}-cc")
source=(
  "https://www.cpan.org/authors/id/S/SA/SALVA/${_perlname}-${pkgver}.tar.gz"
  "patchmakefile.py"
)
sha256sums=('dcfc51e698437ea6b9cefe0276215c56cdb6a7f85e3e24a2b6b4189f1960d351'
            '9b3a852cfb38c05aaea3a6dff4625b038c1d889ed99bb292d8b73e4fb83ff676')

build() {
  cd "$srcdir/${_perlname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL
  export pkgdir=$pkgdir
  python $srcdir/patchmakefile.py
  mingw32-make
}

package() {
  cd "$srcdir/${_perlname}-${pkgver}"
  mingw32-make DESTDIR="${pkgdir}/" install
}

