# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=cantarell-fonts
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.311
pkgrel=1
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
pkgdesc="Humanist sans serif font (mingw-w64)"
makedepends=("${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-appstream-glib"
             "${MINGW_PACKAGE_PREFIX}-gettext-tools"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-cc")
license=(custom:SIL)
url="https://www.gnome.org"
msys2_repository_url="https://gitlab.gnome.org/GNOME/cantarell-fonts"
source=("cantarell-fonts-0.311.tar.xz::https://gitlab.gnome.org/GNOME/cantarell-fonts/-/package_files/1276/download")
sha256sums=('706778e6f9468d534d7afe05e4f6e1586110395ef37d24df259746736fd6b702')

build() {
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  cp -r "../${_realname}-$pkgver/prebuilt" \
    "${srcdir}"/build-${MSYSTEM}

  MSYS2_ARG_CONV_EXCL="--prefix=;-Dfontsdir=" \
  ${MINGW_PREFIX}/bin/meson \
    --prefix=${MINGW_PREFIX} \
    --buildtype=plain \
    -Duseprebuilt=true \
    -Dbuildappstream=true \
    -Dfontsdir=${MINGW_PREFIX}/share/fonts/cantarell \
    "../${_realname}-${pkgver}"

  meson compile
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" meson install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
