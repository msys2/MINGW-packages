# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=cantarell-fonts
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.310
pkgrel=1
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
pkgdesc="Humanist sans serif font (mingw-w64)"
makedepends=("${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-appstream-glib"
             "${MINGW_PACKAGE_PREFIX}-gettext-tools"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-cc")
license=(custom:SIL)
url="https://www.gnome.org"
source=("cantarell-fonts-0.310.tar.xz::https://gitlab.gnome.org/GNOME/cantarell-fonts/-/package_files/1260/download")
sha256sums=('754d7003394b566acb5417505532aa84db942160e4f38bc73de3e2fa2ca58ce0')

prepare() {
  cd "${srcdir}"/${_realname}-$pkgver
}

build() {
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  cp -r "../${_realname}-$pkgver/prebuilt" \
    "${srcdir}"/build-${MSYSTEM}

  MSYS2_ARG_CONV_EXCL="--prefix=;-Dfontsdir=" \
  ${MINGW_PREFIX}/bin/meson \
    --prefix=${MINGW_PREFIX} \
    --buildtype=plain \
    -Duseprebuilt=true \
    -Dbuildappstream=true \
    -Dfontsdir=${MINGW_PREFIX}/share/fonts/cantarell \
    "../${_realname}-${pkgver}"

  meson compile
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" meson install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
