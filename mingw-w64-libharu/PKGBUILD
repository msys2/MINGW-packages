# Maintainer: RÃ©mi BERTHO <remi.bertho@openmailbox.org>

_realname=libharu
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.3.0
pkgrel=3
pkgdesc="C library for generating PDF documents (mingw-w64)"
url="https://github.com/libharu/libharu"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=('custom:ZLIB')
depends=("${MINGW_PACKAGE_PREFIX}-libpng")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/libharu/libharu/archive/RELEASE_${pkgver//./_}.tar.gz"
        "001-win_various_install_files.patch"
        "002-win_lib.patch"
        "003-fix-cmake-minor-version.patch"
        "${url}/pull/157.patch")
sha256sums=('8f9e68cc5d5f7d53d1bc61a1ed876add1faf4f91070dbc360d8b259f46d9a4d2'
            '2f5b85a98d650909e221d8dd57ed6f0eff7eca2695ddfd5fbd8727a798c5e9bf'
            'ff416a8a2f991652169c330b3a374e66fbe85f62a7b41a3519e4a9b22a51fe02'
            '1c7de8708e7060d10fb57ae666dd60a2cbcf58de45d4ebff52311da4e6ce5eaf'
            '702f8e8443b6e306c91c45b4aa9f8ecc0abe29e72d1313db0a793a0fb72d420a')

prepare() {
  cd "${srcdir}/${_realname}-RELEASE_${pkgver//./_}"
  patch -p1 -i "${srcdir}/001-win_various_install_files.patch"
  patch -p1 -i "${srcdir}/002-win_lib.patch"
  patch -p1 -i "${srcdir}/003-fix-cmake-minor-version.patch"
  patch -p1 -i "${srcdir}/157.patch"
}

build() {
  [[ -d ${srcdir}/build-${MSYSTEM} ]] && rm -rf ${srcdir}/build-${MSYSTEM}
  mkdir -p ${srcdir}/build-${MSYSTEM} && cd ${srcdir}/build-${MSYSTEM}

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    @{extra_config[@]} \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_STATIC_LIBS=ON \
    ../${_realname}-RELEASE_${pkgver//./_}

  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  cd build-${MSYSTEM}
  DESTDIR=${pkgdir} ${MINGW_PREFIX}/bin/cmake --install .
}
