# Maintainer: RÃ©mi BERTHO <remi.bertho@openmailbox.org>

_realname=libharu
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.4.0
pkgrel=2
pkgdesc="C library for generating PDF documents (mingw-w64)"
url="https://github.com/libharu/libharu"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=('custom:ZLIB')
depends=("${MINGW_PACKAGE_PREFIX}-libpng")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/libharu/libharu/archive/v${pkgver}.tar.gz"
        001-fix-installation-destination.patch
        002-rename-haru-to-hpdf.patch
        003-fix-header-version.patch
        004-add-compile-definitions.patch)
sha256sums=('d1c38c0492257c61fb60c85238d500c05184fd8e9e68fecba9cf304ff2d8726d'
            'b9dbd3ddfbf24eb1bc6d8a009b5dfcc6ab4344d20019f7d18f9bd6954c1feccd'
            'bcf2801ca0ce0b8f0236cc072730cb564fc62bf7627690d5ef0bfd61316a7f08'
            'bb1bfff1eca0b97f4feefd6b928b93fba0ac88ca54b5ec1f7fd064d92319bea4'
            '89bebe5ce1ecad776ba4442e8c9f19c867fcee5ff656df589236cafe3a0414ca')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  apply_patch_with_msg \
    001-fix-installation-destination.patch \
    002-rename-haru-to-hpdf.patch \
    003-fix-header-version.patch \
    004-add-compile-definitions.patch
}

build() {
  [[ -d ${srcdir}/build-${MSYSTEM} ]] && rm -rf ${srcdir}/build-${MSYSTEM}
  mkdir -p ${srcdir}/build-${MSYSTEM} && cd ${srcdir}/build-${MSYSTEM}

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    ${extra_config[@]} \
    ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  cd build-${MSYSTEM}
  DESTDIR=${pkgdir} ${MINGW_PREFIX}/bin/cmake --install .
}
