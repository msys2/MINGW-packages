# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=freerdp
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.8.0
pkgrel=1
pkgdesc="Free implementation of the Remote Desktop Protocol (RDP) (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url="https://www.freerdp.com/"
msys2_repository_url="https://github.com/FreeRDP/FreeRDP"
msys2_references=(
  'archlinux: freerdp'
  "cpe: cpe:/a:freerdp:freerdp"
)
license=('spdx:Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-cairo"
         "${MINGW_PACKAGE_PREFIX}-cjson"
         "${MINGW_PACKAGE_PREFIX}-ffmpeg"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-libusb"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-SDL2_ttf"
         "${MINGW_PACKAGE_PREFIX}-uriparser")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-icu"
             "${MINGW_PACKAGE_PREFIX}-iconv"
             "${MINGW_PACKAGE_PREFIX}-docbook-xsl")
source=("https://github.com/FreeRDP/FreeRDP/archive/${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('e313934a77a0bcca3af803455dd9ea1aa2f657c598e3397325aa48e6effd450d')

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  if [[ ${MINGW_PACKAGE_PREFIX} != *-clang-* ]]; then
    _extra_config+=("-DCMAKE_INTERPROCEDURAL_OPTIMIZATION=OFF")
  fi

  CFLAGS+=" -Wno-incompatible-pointer-types"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -Wno-dev \
    -G "Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_INSTALL_LIBDIR='lib' \
    "${_extra_config[@]}" \
    -DPROXY_PLUGINDIR='${MINGW_PREFIX}/lib/freerdp2/server/proxy/plugins' \
    -DWITH_AAD=ON \
    -DWITH_CAIRO=ON \
    -DWITH_CHANNELS=ON \
    -DWITH_CLIENT_CHANNELS=ON \
    -DWITH_DSP_FFMPEG=ON \
    -DWITH_FFMPEG=ON \
    -DWITH_ICU=ON \
    -DWITH_JPEG=ON \
    -DWITH_MANPAGES=ON \
    -DWITH_MBEDTLS=ON \
    -DWITH_SERVER=ON \
    -DWITH_SERVER_CHANNELS=ON \
    -DWITH_SWSCALE=ON \
    -DWITH_ZLIB=ON \
    -DWITH_WEBVIEW=OFF \
    -DWITH_WINPR_TOOLS=ON \
    -DCHANNEL_URBDRC_CLIENT=ON \
    -DUSE_UNWIND=OFF \
    -DBUILD_TESTING=OFF \
    -DDOCBOOKXSL_DIR=${MINGW_PREFIX}/share/xml/docbook/xsl-stylesheets-nons-1.79.2 \
    "../FreeRDP-${pkgver}"

  ${MINGW_PREFIX}/bin/cmake.exe --build ./
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install .
  install -vDm0644 "${srcdir}/FreeRDP-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
