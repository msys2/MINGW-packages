# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>

_pyname=google_cloud_core
_realname=google-cloud-core
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=2.4.3
pkgrel=1
pkgdesc='Google Cloud API client core library (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://pypi.org/project/google-cloud-core/"
msys2_repository_url='https://github.com/googleapis/python-cloud-core'
msys2_documentation_url='https://cloud.google.com/python/docs/reference/google-cloud-core/latest'
msys2_references=(
  'aur: python-google-cloud-core'
  'purl: pkg:pypi/google-cloud-core'
)
license=('spdx:Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-python-google-auth"
         "${MINGW_PACKAGE_PREFIX}-python-googleapis-common-protos"
         "${MINGW_PACKAGE_PREFIX}-python-google-api-core"
         "${MINGW_PACKAGE_PREFIX}-python-pytz"
         "${MINGW_PACKAGE_PREFIX}-python-requests")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools")
source=("https://pypi.org/packages/source/${_pyname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha256sums=('1fab62d7102844b278fe6dead3af32408b1df3eb06f5c7e8634cbd40edc4da53')

build() {
  cp -r "${_pyname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
