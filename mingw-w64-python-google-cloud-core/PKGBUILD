# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>

_realname=google-cloud-core
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=2.4.1
pkgrel=2
pkgdesc='Google Cloud API client core library (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://pypi.org/project/google-cloud-core/"
msys2_repository_url='https://github.com/googleapis/python-cloud-core'
msys2_documentation_url='https://cloud.google.com/python/docs/reference/google-cloud-core/latest'
msys2_references=(
  'aur: python-google-cloud-core'
  'purl: pkg:pypi/google-cloud-core'
)
license=('spdx:Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-python-google-auth"
         "${MINGW_PACKAGE_PREFIX}-python-googleapis-common-protos"
         "${MINGW_PACKAGE_PREFIX}-python-google-api-core"
         "${MINGW_PACKAGE_PREFIX}-python-pytz"
         "${MINGW_PACKAGE_PREFIX}-python-requests")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools")
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('9b7749272a812bde58fff28868d0c5e2f585b82f37e09a1f6ed2d4d10f134073')

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
