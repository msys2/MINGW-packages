_realname=lal-refactor
pkgbase=mingw-w64-${_realname}-git
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}-git
pkgver=24.0.r42.f1284f5
pkgrel=1

pkgdesc="Source code refactoring tools for the Ada programming language, leveraging the power of Libadalang (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")

url="https://github.com/AdaCore/lal-refactor"
license="spdx:Apache-2.0 WITH LLVM-exception"
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-gcc-ada"
  "${MINGW_PACKAGE_PREFIX}-gprbuild"
  "git"
)
depends=(
  "${MINGW_PACKAGE_PREFIX}-adasat"
  "${MINGW_PACKAGE_PREFIX}-libvss"
  "${MINGW_PACKAGE_PREFIX}-libadalang"
  "${MINGW_PACKAGE_PREFIX}-libadalang-tools"
)
_branch=24.0
source=("${_realname}-git::git+https://github.com/AdaCore/lal-refactor.git#branch=${_branch}")
sha256sums=('SKIP')

pkgver() {
  cd ${_realname}-git
  printf "%s.r%s.%s" "${_branch}" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
  cd ${srcdir}/${_realname}-git

  export BUILD_MODE=prod
  export LIBRARY_TYPE=relocatable
  make lib
  make bin
}

package() {
  cd ${srcdir}/${_realname}-git

  make install PREFIX=${pkgdir}${MINGW_PREFIX}

  # Copy License File
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" \
    "${srcdir}/${_realname}-git/LICENSE.txt"
}
