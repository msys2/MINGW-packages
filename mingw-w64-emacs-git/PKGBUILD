# Maintainer: Haroogan <Haroogan@gmail.com>

_name='emacs'
pkgname="${MINGW_PACKAGE_PREFIX}-${_name}-git"
pkgver=r118176.83087df
pkgrel=1
pkgdesc=\
"The extensible, customizable, self-documenting, real-time display editor."
url="http://www.gnu.org/software/${_name}/"
license=(
  'GPL3'
)
arch=(
  'any'
)
conflicts=(
  "${MINGW_PACKAGE_PREFIX}-${_name}"
)
depends=(
  "${MINGW_PACKAGE_PREFIX}-ctags"
  "${MINGW_PACKAGE_PREFIX}-dbus"
  "${MINGW_PACKAGE_PREFIX}-gcc-libs"
  "${MINGW_PACKAGE_PREFIX}-giflib"
  "${MINGW_PACKAGE_PREFIX}-gnutls"
  "${MINGW_PACKAGE_PREFIX}-imagemagick"
  "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
  "${MINGW_PACKAGE_PREFIX}-libpng"
  "${MINGW_PACKAGE_PREFIX}-librsvg"
  "${MINGW_PACKAGE_PREFIX}-libtiff"
  "${MINGW_PACKAGE_PREFIX}-libxml2"
  "${MINGW_PACKAGE_PREFIX}-xpm-nox"
  "${MINGW_PACKAGE_PREFIX}-zlib"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-gcc"
  "${MINGW_PACKAGE_PREFIX}-pkg-config"
  "git"
)
options=(
  'strip'
)
source=(
  "${_name}"::"git://git.savannah.gnu.org/${_name}.git"
  'image.c.diff'
  'lread.c.diff'
)
sha256sums=(
  'SKIP'
  '4571d45ec26fd556e73a70bb0ab0a2a8fa1efc5e3b3c5b472ab68bb7dc9bf52c'
  'b9db1b7d939301d0fedf52db6ac055f7265ee5bc0c3c757e394700ca39577b7f'
)

pkgver() {
  cd "${_name}"
  printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${_name}"
  patch --binary --forward -p0 < "${srcdir}/image.c.diff"
  patch --binary --forward -p0 < "${srcdir}/lread.c.diff"
  ./autogen.sh
}

build() {
  mkdir -p ".build/${MINGW_CHOST}"
  cd       ".build/${MINGW_CHOST}"

  local with_wide_int='no'

  if test "${CARCH}" == 'x86_64'; then
    with_wide_int='yes'
  fi

  # NOTE:
  # Do not add `-funroll-loops' to `CFLAGS'!
  PKG_CONFIG_PATH="${MINGW_PREFIX}/lib/pkgconfig"                              \
  "${srcdir}/${_name}/configure"                                               \
    CPPFLAGS="-DNDEBUG -isystem ${MINGW_PREFIX}/include"                       \
      CFLAGS="-pipe -O3 -fomit-frame-pointer"                                  \
     LDFLAGS="-s -Wl,-s"                                                       \
    --prefix="${MINGW_PREFIX}"                                                 \
    --build="${MINGW_CHOST}"                                                   \
    --with-wide-int="${with_wide_int}"                                         \
    --with-sound="yes"                                                         \
    --with-file-notification="yes"                                             \
    --without-gpm                                                              \
    --without-gconf                                                            \
    --without-gsettings                                                        \
    --without-selinux

  make
}

# check() {
  # cd ".build/${MINGW_CHOST}"
  # make check
# }

package() {
  cd ".build/${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}${MINGW_PREFIX}/bin/ctags.exe"
  rm -f "${pkgdir}${MINGW_PREFIX}/share/man/man1/ctags.1.gz"
}
