# Maintainer: Frederic Wang <fred.wang@free.fr>

_realname=idna
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}" "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=2.8
pkgrel=1
pkgdesc='Internationalized Domain Names in Applications (mingw-w64)'
url='https://github.com/kjd/idna'
license=('BSD-like')
arch=('any')
makedepends=("${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools")
_dtoken="d8/82/28a51052215014efc07feac7330ed758702fc0581347098a81699b5281cb"
source=("https://pypi.io/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('c357b3f628cf53ae2c4c05627ecc484553142ca23264e593d327bcde5e9c3407')

prepare() {
  cd ${srcdir}

  for pver in {2,3}; do
    rm -rf python${pver}-build-${CARCH}
    cp -r ${_realname}-${pkgver} python${pver}-build-${CARCH}
  done
}

build() {  
  for pver in {2,3}; do  
    msg "Python ${pver} build for ${CARCH}"  
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py build
  done  
}

check() {  
  for pver in {2,3}; do  
    msg "Python ${pver} test for ${CARCH}"  
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py test
  done  
}

package_python3-idna() {
  cd "${srcdir}/python${pver}-build-${CARCH}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}"  --optimize=1 --skip-build

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE.rst" "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}

package_python2-idna() {
  cd "${srcdir}/python${pver}-build-${CARCH}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}"  --optimize=1 --skip-build

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE.rst" "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE"
}

package_mingw-w64-i686-python2-idna() {
  package_python2-idna
}

package_mingw-w64-i686-python3-idna() {
  package_python3-idna
}

package_mingw-w64-x86_64-python2-idna() {
  package_python2-idna
}

package_mingw-w64-x86_64-python3-idna() {
  package_python3-idna
}
