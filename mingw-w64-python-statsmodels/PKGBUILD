# Contributor: Runar Tenfjord < runar dot tenfjord at gmail dot com >

_realname=statsmodels
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-$_realname"
         "${MINGW_PACKAGE_PREFIX}-python3-$_realname")
pkgver=0.10.1
pkgrel=2
pkgdesc="Statistical computations and models for use with SciPy (mingw-w64)"
arch=('any')
url="https://statsmodels.sourceforge.io/"
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-python2-scipy"
             "${MINGW_PACKAGE_PREFIX}-python3-scipy"
             "${MINGW_PACKAGE_PREFIX}-python2-pandas"
             "${MINGW_PACKAGE_PREFIX}-python3-pandas"
             "${MINGW_PACKAGE_PREFIX}-python2-patsy"
             "${MINGW_PACKAGE_PREFIX}-python3-patsy"
             "${MINGW_PACKAGE_PREFIX}-cython"
             "${MINGW_PACKAGE_PREFIX}-cython2"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-cython"
             "${MINGW_PACKAGE_PREFIX}-cython2"
             "${MINGW_PACKAGE_PREFIX}-gcc")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/${_realname}/${_realname}/archive/v${pkgver}.tar.gz"
        001-dont-include-extra-files.patch)
sha256sums=('269ec50719c47f946cf3872b8abfcb36e15d5614e3bb81c4c39e74cce673d5b8'
            'd6ca9de4dbf5a8a578b582ddab7b0931ed037470b04b5a3c0ed8056ace94e257')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-dont-include-extra-files.patch
  cd ${srcdir}
  
  cp -a ${_realname}-${pkgver} ${_realname}-py2-${pkgver}
  cp -a ${_realname}-${pkgver} ${_realname}-py3-${pkgver}

  cd ${_realname}-py2-${pkgver}
  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
}

build() {
  # build for python2
  cd ${srcdir}/${_realname}-py2-${pkgver}

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py build

  # build for python3
  cd ${srcdir}/${_realname}-py3-${pkgver}

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py build
}

package_python2-statsmodels() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2-scipy"
           "${MINGW_PACKAGE_PREFIX}-python2-pandas"
           "${MINGW_PACKAGE_PREFIX}-python2-patsy")
  optdepends=("${MINGW_PACKAGE_PREFIX}-python2-nose: needed for integrated test suite"
              "${MINGW_PACKAGE_PREFIX}-python2-matplotlib: needed for plotting functions")

  cd ${_realname}-py2-${pkgver}
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --skip-build \
    --prefix=${MINGW_PREFIX} --root=${pkgdir} --optimize=1

  install -Dm644 LICENSE.txt \
    "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE.txt"
}

package_python3-statsmodels() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3-scipy"
           "${MINGW_PACKAGE_PREFIX}-python3-pandas"
           "${MINGW_PACKAGE_PREFIX}-python3-patsy")
  optdepends=("${MINGW_PACKAGE_PREFIX}-python3-nose: needed for integrated test suite"
              "${MINGW_PACKAGE_PREFIX}-python3-matplotlib: needed for plotting functions")

  cd ${_realname}-py3-${pkgver}
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --skip-build \
    --prefix=${MINGW_PREFIX} --root=${pkgdir} --optimize=1

  install -Dm644 LICENSE.txt \
    "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE.txt"
}

package_mingw-w64-i686-python2-statsmodels() {
  package_python2-statsmodels
}

package_mingw-w64-i686-python3-statsmodels() {
  package_python3-statsmodels
}

package_mingw-w64-x86_64-python2-statsmodels() {
  package_python2-statsmodels
}

package_mingw-w64-x86_64-python3-statsmodels() {
  package_python3-statsmodels
}
