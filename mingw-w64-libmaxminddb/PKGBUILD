# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libmaxminddb
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.0.4
pkgrel=1
pkgdesc="C library for reading MaxMind DB files, including the GeoIP2 databases from MaxMind (mingw-w64)"
arch=('any')
url="https://dev.maxmind.com/geoip/"
license=('Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" "${MINGW_PACKAGE_PREFIX}-geoip2-database")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-libtool"
             "autoconf"
             "git"
             "patch")
options=('!emptydirs')
source=(${_realname}-${pkgver}.tar.gz::https://github.com/maxmind/${_realname}/archive/${pkgver}.tar.gz
        "MaxMind-DB"::git+https://github.com/maxmind/MaxMind-DB.git
        libtap::git+https://github.com/zorgnax/libtap.git
        mingw-no-undefined.patch)
sha256sums=('39ccc2c296bcc989c86513967a8cc9cb7e4efe0153ef61f07a3d7d1e931e3d02'
            'SKIP'
            'SKIP'
            'f3246d235bd6dfcb6fd8e89fa63de95b0384283fce12a0ebef56e1b2fe9bd248')

prepare() {
  cp -rf ${srcdir}/libtap ${srcdir}/${_realname}-${pkgver}/t/
  cp -rf ${srcdir}/MaxMind-DB ${srcdir}/${_realname}-${pkgver}/t/
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/mingw-no-undefined.patch
  autoreconf -fiv
}

build() {
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
