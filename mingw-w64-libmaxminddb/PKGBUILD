# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libmaxminddb
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.2.0
pkgrel=1
pkgdesc="C library for reading MaxMind DB files, including the GeoIP2 databases from MaxMind (mingw-w64)"
arch=('any')
url="https://dev.maxmind.com/geoip/"
license=('Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" "${MINGW_PACKAGE_PREFIX}-geoip2-database")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-libtool"
             "autoconf"
             "git"
             "patch")
options=('!emptydirs')
source=(${_realname}-${pkgver}.tar.gz::https://github.com/maxmind/${_realname}/archive/${pkgver}.tar.gz
        "MaxMind-DB"::git+https://github.com/maxmind/MaxMind-DB.git
        libtap::git+https://github.com/zorgnax/libtap.git
        0001-libmaxminddb-1.2.0-mingw-no-undefined.patch)
sha256sums=('4147241d4e3103bd843167ae5a0d5cf176632b6898cb1cca6223d2d58cc53ef5'
            'SKIP'
            'SKIP'
            'd8215ab43c8abea0017b64cd407e831816c527927fa943a4c817d0c07420f997')

prepare() {
  cp -rf ${srcdir}/libtap ${srcdir}/${_realname}-${pkgver}/t/
  cp -rf ${srcdir}/MaxMind-DB ${srcdir}/${_realname}-${pkgver}/t/
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-libmaxminddb-1.2.0-mingw-no-undefined.patch"
  ./bootstrap
}

build() {
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
