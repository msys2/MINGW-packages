# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=nest-asyncio
pkgbase=mingw-w64-python-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-${_realname}
pkgver=1.5.8
pkgrel=1
pkgdesc='Patch asyncio to allow nested event loops (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
license=('BSD')
url='https://github.com/erdewit/nest_asyncio'
depends=(
    ${MINGW_PACKAGE_PREFIX}-python)
makedepends=(
    ${MINGW_PACKAGE_PREFIX}-python-setuptools
    ${MINGW_PACKAGE_PREFIX}-python-setuptools-scm)
source=(https://github.com/erdewit/nest_asyncio/archive/v${pkgver}.tar.gz)
sha256sums=('98b103e427d1f768cceb05374cf9c63ca2094d49c0cb92240f230228edcf691d')

prepare() {
  rm -rf "python-build-${MSYSTEM}" | true
  cp -r "nest_asyncio-${pkgver}" "python-build-${MSYSTEM}"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
