# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=sphinx-lv2-theme
_pyname=sphinx_lv2_theme
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.4.6
pkgrel=3
pkgdesc="A minimal static theme for Sphinx (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://gitlab.com/lv2/sphinx_lv2_theme"
msys2_repository_url='https://gitlab.com/lv2/sphinx_lv2_theme'
msys2_references=(
  'archlinux: python-sphinx-lv2-theme'
  'purl: pkg:pypi/sphinx-lv2-theme'
)
license=("spdx:0BSD")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python-build"
  "${MINGW_PACKAGE_PREFIX}-python-installer"
  "${MINGW_PACKAGE_PREFIX}-python-setuptools"
)
depends=("${MINGW_PACKAGE_PREFIX}-python-sphinx")
source=("https://pypi.org/packages/source/${_pyname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha256sums=('74e82ea08fd0039be100416bd881937b2bf4ea27cd6dbe37733ee08bc43f3b96')

build() {
  cp -r "${_pyname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
