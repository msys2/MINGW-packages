# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=immutables
pkgbase=mingw-w64-python-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-${_realname}
pkgver=0.16
pkgrel=1
pkgdesc='An immutable mapping type for Python (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='https://github.com/MagicStack/immutables'
license=('Apache-2.0')
depends=(
    ${MINGW_PACKAGE_PREFIX}-python)
makedepends=(
    ${MINGW_PACKAGE_PREFIX}-python-setuptools
    "${MINGW_PACKAGE_PREFIX}-cc")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/MagicStack/${_realname}/archive/v${pkgver}.tar.gz")
sha256sums=('69413536a62cdbf0e5b3e5d8ab6d7996d19bfa47d19b390bb7cd3825b31eefa2')

prepare() {
  cd "${srcdir}"
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}"
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
