# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=immutables
pkgbase=mingw-w64-python-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-${_realname}
pkgver=0.19
pkgrel=1
pkgdesc='An immutable mapping type for Python (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64' 'clang32')
url='https://github.com/MagicStack/immutables'
license=('spdx:Apache-2.0')
depends=(
    ${MINGW_PACKAGE_PREFIX}-python)
makedepends=(
    ${MINGW_PACKAGE_PREFIX}-python-setuptools
    "${MINGW_PACKAGE_PREFIX}-cc")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/MagicStack/${_realname}/archive/v${pkgver}.tar.gz")
sha256sums=('2a3d6f217dad8d83e87dd6a2af33a62ace73217da48fad1f866c756a734a9f50')

prepare() {
  cd "${srcdir}"
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}"
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
