# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libgoom2
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2k4
pkgrel=2
pkgdesc="Shared library part of the Goom visualization plugin (mingw-w64)"
arch=('any')
url="http://www.fovea.cc/?page=details_produits&id=goom"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "flex")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
options=('staticlibs' 'strip')
source=("http://downloads.sourceforge.net/project/goom/goom2k4/0/goom-${pkgver}-0-src.tar.gz"
        001-no-undefined.patch
        002-fix-random.patch)
md5sums=('8100dd07e0c6784fdf079eeaa53a5c7f'
         'aafa277545e2e53c458370aae5627f67'
         'fc40ea7df842ec732a846e03320b6aa0')

prepare() {
  cd "${srcdir}/goom2k4-0"
  patch -p1 -i ${srcdir}/001-no-undefined.patch
  patch -p1 -i ${srcdir}/002-fix-random.patch

  aclocal -I m4
  libtoolize --copy --force
  automake --foreign --add-missing
  autoconf
}

build() {
  mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}
  ../goom2k4-0/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST} \
      --enable-shared \
      --enable-static \
      --without-xmms \
      --without-sdl

  make
}

check() {
  cd "${srcdir}/build-${CARCH}"
  make check
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install
}
