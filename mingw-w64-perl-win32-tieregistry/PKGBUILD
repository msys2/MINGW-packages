# Maintainer: @naveen521kk on Github, Naveen M K <naveen521kk@gmail.com>

_perlname=Win32-TieRegistry
_realname="${_perlname,,}"
pkgbase=mingw-w64-perl-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-perl-${_realname}")
pkgver=0.30
pkgrel=6
pkgdesc="Manipulate the Win32 Registry (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://metacpan.org/release/Win32-TieRegistry"
msys2_references=(
  'purl: pkg:cpan/CHORNY/Win32-TieRegistry'
)
msys2_repository_url="https://github.com/chorny/Win32-TieRegistry"
license=('perl_5')
groups=("${MINGW_PACKAGE_PREFIX}-perl-modules")
depends=(
  "${MINGW_PACKAGE_PREFIX}-perl"
  "${MINGW_PACKAGE_PREFIX}-perl-win32-winerror"
  "${MINGW_PACKAGE_PREFIX}-perl-win32api-registry"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-make"
  "${MINGW_PACKAGE_PREFIX}-python"
)
options=('!emptydirs')
source=(
  "https://cpan.metacpan.org/authors/id/C/CH/CHORNY/${_perlname}-${pkgver}.tar.gz"
  "patchmakefile.py"
)
sha512sums=('22d91033624d835ce9e15fd2e5dc022ef6202644e92718ef8b6bb06f42a8741fae07fb498c900486cd418086a7aec03ae56d731639b822b6c9458fcfcf65db60'
            'd7ec5ba4a3d75f674f4028e8a53bc4177ab71a379bc63affebf910e1d5a0e491b6642318218b55ddc4d90e0125d61e4b97626782af57d44522bcb5543fb21cb2')

build() {
  cd "$srcdir/${_perlname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL
  export pkgdir=$pkgdir
  python $srcdir/patchmakefile.py
  mingw32-make
}

package() {
  cd "$srcdir/${_perlname}-${pkgver}"
  mingw32-make DESTDIR="${pkgdir}/" install
}
