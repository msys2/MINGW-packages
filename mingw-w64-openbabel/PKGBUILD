# Maintainer: Jeroen Ooms <jeroen@berkeley.edu>

_realname=openbabel
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.4.1
pkgrel=1
pkgdesc="Chemical toolbox (mingw-w64)"
arch=('any')
license=("custom")
url="https://openbabel.org"
source=("https://github.com/openbabel/openbabel/archive/openbabel-2-4-1.tar.gz")
sha256sums=('594c7f8a83f3502381469d643f7b185882da1dd4bc2280c16502ef980af2a776')
depends=("${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-cairo")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")

options=('staticlibs' 'strip')

prepare() {
  cd "${srcdir}/openbabel-openbabel-2-4-1"
  sed -i 's/OB_MODULE_PATH/XXXXOB_MODULE_PATH/g' src/config.h.cmake
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_DISABLE_FIND_PACKAGE_wxWidgets=TRUE \
	-DCMAKE_DISABLE_FIND_PACKAGE_Eigen3=TRUE \
	-DCMAKE_DISABLE_FIND_PACKAGE_Eigen2=TRUE \
	-DLIBXML_INCLUDE_DIR="$(xml2-config --cflags)" \
	-DLIBXML_LIBRARY="$(xml2-config --libs)" \
    -DRUN_SWIG=OFF \
    -DBUILD_GUI=OFF \
    ../openbabel-openbabel-2-4-1

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
  mv ${pkgdir}${MINGW_PREFIX}/bin ${pkgdir}${MINGW_PREFIX}/lib
  mkdir -p ${pkgdir}${MINGW_PREFIX}/bin
  mv ${pkgdir}${MINGW_PREFIX}/lib/*.exe ${pkgdir}${MINGW_PREFIX}/bin
}
