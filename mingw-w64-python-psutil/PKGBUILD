# Maintainer: Manuel Naranjo <naranjo.manuel@gmail.com>

_pyname=psutil
_realname=${_pyname}
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}"
         "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=5.6.3
pkgrel=2
pkgdesc="A cross-platform process and system utilities module for Python (mingw-w64)"
arch=('any')
url="https://github.com/giampaolo/psutil"
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools")
options=('staticlibs' 'strip' '!debug')
source=(${_realname}-${pkgver}.tar.gz::"${url}/${_realname}/archive/release-${pkgver}.tar.gz"
        '0001-MSYS-Allow-detecting-when-running-on-MSYS.patch'
        '0002-msys-fixes-for-msys2.patch')
sha256sums=('55b86dc0a9fc4e258ae5d86d6edf317432a4e3dc45c7324b8a82838b07e74f4a'
            'd3a325e2a073f23e5a6e9ce15e371e0c113af07942deb57e05b7185bc2407d4a'
            'b5dadc659509fa07b3333eb235f8a43511bd01acbb16c36f840ff61ef0ede834')

prepare() {
  cd "${srcdir}"/${_realname}-release-${pkgver}
  patch -p1 -i "${srcdir}/0001-MSYS-Allow-detecting-when-running-on-MSYS.patch"
  patch -p1 -i "${srcdir}/0002-msys-fixes-for-msys2.patch"
  cd ${srcdir}

  for builddir in python{2,3}-build; do
    rm -rf ${builddir} | true
    cp -r "${_realname}-release-${pkgver}" "${builddir}"
  done
}

build() {
  for pver in {2,3}; do
    msg "Python ${pver} test for ${CARCH}"
    cd "${srcdir}/python${pver}-build"
    ${MINGW_PREFIX}/bin/python${pver} setup.py build
  done
}

check() {
  for pver in {2,3}; do
    msg "Python ${pver} test for ${CARCH}"
    cd "${srcdir}/python${pver}-build"
    ${MINGW_PREFIX}/bin/python${pver} setup.py check
  done
}

# Note that build() is sometimes skipped because it's done in
# the packages setup.py install for simplicity if you can do so.
package_python3-psutil() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3")

  cd "${srcdir}/python3-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/COPYING"
}

package_python2-psutil() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2")

  cd "${srcdir}/python2-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/COPYING"
}

package_mingw-w64-i686-python2-psutil() {
  package_python2-psutil
}

package_mingw-w64-i686-python3-psutil() {
  package_python3-psutil
}

package_mingw-w64-x86_64-python2-psutil() {
  package_python2-psutil
}

package_mingw-w64-x86_64-python3-psutil() {
  package_python3-psutil
}
