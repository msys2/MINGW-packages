# Maintainer: Manuel Naranjo <naranjo.manuel@gmail.com>

_pyname=psutil
_realname=${_pyname}
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}"
         "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=4.3.0
pkgrel=2
pkgdesc="A cross-platform process and system utilities module for Python (mingw-w64)"
arch=('any')
url="https://github.com/giampaolo/"
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools")
options=('staticlibs' 'strip' '!debug')
source=(
  ${_realname}-${pkgver}.zip::"${url}/${_realname}/archive/release-${pkgver}.zip"
  '0001-MSYS-Allow-detecting-when-running-on-MSYS.patch'
  '0002-msys-fixes-for-msys2.patch'
)
sha256sums=(
  'ee914ec129bf2c74e1e23d128fa203a580bfa9ace26ee4af18f4615e10b42ac4'
  '0248f917fdf89a8d40acad44c646bcb7002f4cd9d943b4a95416d0964e1c2d75'
  '68c394e580a5990cc2b5c5c84ff9d8d9c866efb946ba1ae492d1bf3c04f4f1fe'
)

prepare() {
  cd "$srcdir"/
  for builddir in python{2,3}-build; do
    rm -rf $builddir | true
    cp -r "${_realname}-release-${pkgver}" "${builddir}"
    pushd "${builddir}"
    patch -p1 -i "${srcdir}/0001-MSYS-Allow-detecting-when-running-on-MSYS.patch"
    patch -p1 -i "${srcdir}/0002-msys-fixes-for-msys2.patch"
    popd
  done
}

build() {
  for pver in {2,3}; do
    msg "Python ${pver} test for ${CARCH}"
    cd "${srcdir}/python${pver}-build"
    ${MINGW_PREFIX}/bin/python${pver} setup.py build
  done
}

check() {
  for pver in {2,3}; do
    msg "Python ${pver} test for ${CARCH}"
    cd "${srcdir}/python${pver}-build"
    ${MINGW_PREFIX}/bin/python${pver} setup.py check
  done
}

# Note that build() is sometimes skipped because it's done in
# the packages setup.py install for simplicity if you can do so.
package_python3-psutil() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3")

  cd "${srcdir}/python3-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/COPYING"
}

package_python2-psutil() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2")

  cd "${srcdir}/python2-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/COPYING"
}

package_mingw-w64-i686-python2-psutil() {
  package_python2-psutil
}

package_mingw-w64-i686-python3-psutil() {
  package_python3-psutil
}

package_mingw-w64-x86_64-python2-psutil() {
  package_python2-psutil
}

package_mingw-w64-x86_64-python3-psutil() {
  package_python3-psutil
}
