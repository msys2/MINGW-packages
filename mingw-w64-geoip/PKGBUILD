# Maintainer: Mateusz Miku≈Ça <mati865@gmail.com>

_realname=geoip
_pkgname=geoip-api-c
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.6.9
pkgrel=1
pkgdesc="GeoIP Legacy C API (mingw-w64)"
arch=('any')
url="https://www.maxmind.com/app/c"
license=('LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-geoip2-database"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=('autoconf'
             'libtool')
options=('!emptydirs')
source=(${_realname}-${pkgver}.tar.gz::https://github.com/maxmind/${_pkgname}/archive/v${pkgver}.tar.gz)
sha256sums=('05468adbb6bdbddc588ee6f0df990f47b1b24d278519c35d59f7a8058a2a9825')

prepare(){
  cd ${_pkgname}-${pkgver}
  autoreconf -vfi
}

build() {
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}
  ../${_pkgname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared
  make
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install DESTDIR="${pkgdir}"
}
