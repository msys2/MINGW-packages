_realname=wabt
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.0.36
pkgrel=1
pkgdesc="The WebAssembly Binary Toolkit is a suite of tools for WebAssembly (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/WebAssembly/wabt"
msys2_references=(
  'archlinux: wabt'
)
license=('APACHE-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-make"
  'git'
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-python-ply"
  "${MINGW_PACKAGE_PREFIX}-gtest"
)
source=("git+$url#tag=$pkgver"
        "git+https://github.com/WebAssembly/testsuite"
        "git+https://github.com/google/googletest"
        "git+https://github.com/dabeaz/ply")
validpgpkeys=('5DE3E0509C47EA3CF04A42D34AEE18F83AFDEB23')
sha512sums=('2c7a31932a9a30614f0ceedb136dfc87f4399d7f80b2a7d5a9dfcd19d5efa9aff3dd5fb469661d309ae32426b2232b5d0799b44d7301758d9660e635bcfe5d66'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd "${_realname}"

  git submodule init
  git config submodule."external/testsuite".url "${srcdir}/testsuite"
  git config submodule."external/googletest".url "${srcdir}/googletest"
  git config submodule."external/ply".url "${srcdir}/ply"
  git submodule update --recursive

  mkdir build
}

build() {
  cd "${_realname}/build"

  export CFLAGS+=" -ffat-lto-objects"
  cmake \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -G "MinGW Makefiles" \
    ..

  mingw32-make
}

check() {
  cd "${_realname}"
  mingw32-make test
}

package() {
  cd "${_realname}/build"

  mingw32-make DESTDIR="${pkgdir}" install
}
