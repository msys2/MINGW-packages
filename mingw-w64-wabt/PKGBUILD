_realname=wabt
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.0.36
pkgrel=2
pkgdesc="The WebAssembly Binary Toolkit is a suite of tools for WebAssembly (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url="https://github.com/WebAssembly/wabt"
msys2_references=(
  'archlinux: wabt'
)
license=('spdx:Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-openssl")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cc"
             'git'
             "${MINGW_PACKAGE_PREFIX}-python-ply"
             "${MINGW_PACKAGE_PREFIX}-gtest"
             "${MINGW_PACKAGE_PREFIX}-python")
source=("git+$url#tag=$pkgver"
        "0001-replace-arm64-with-aarch64.patch"
        "git+https://github.com/WebAssembly/testsuite"
        "git+https://github.com/dabeaz/ply")
validpgpkeys=('5DE3E0509C47EA3CF04A42D34AEE18F83AFDEB23')
sha512sums=('2c7a31932a9a30614f0ceedb136dfc87f4399d7f80b2a7d5a9dfcd19d5efa9aff3dd5fb469661d309ae32426b2232b5d0799b44d7301758d9660e635bcfe5d66'
            'd9f829924ef172dbe0b090e4c9596c947439f6b1726fd596c687bfd4ad67543382ae2c1fda942bd7c45ac29fb2f463462879b0dcde5b25fdc5af58528c50f23c'
            'SKIP'
            'SKIP')

prepare() {
  cd "${srcdir}/${_realname}"

  patch -Np1 -i "${srcdir}/0001-replace-arm64-with-aarch64.patch"

  git submodule init
  git config submodule."external/testsuite".url "${srcdir}/testsuite"
  git config submodule."external/ply".url "${srcdir}/ply"
  git submodule update --recursive
}

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DWITH_WASI=ON \
      -DUSE_SYSTEM_GTEST=ON \
      -DPython3_EXECUTABLE=${MINGW_PREFIX}/bin/python.exe \
      -S "${_realname}" \
      -B "build-${MSYSTEM}"

  cmake --build "build-${MSYSTEM}"
}

check() {
  cmake --build "build-${MSYSTEM}" --target test
}

package() {
  DESTDIR="${pkgdir}" cmake --install "build-${MSYSTEM}"

  install -Dm644 "${srcdir}/${_realname}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
