# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=Pillow
pkgbase=mingw-w64-python-pillow
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-pillow" "${MINGW_PACKAGE_PREFIX}-python3-pillow")
pkgver=4.3.0
pkgrel=1
arch=('any')
license=('custom')
url="https://github.com/python-pillow/Pillow/"
depends=("${MINGW_PACKAGE_PREFIX}-freetype"
         "${MINGW_PACKAGE_PREFIX}-lcms2"
         "${MINGW_PACKAGE_PREFIX}-libjpeg"
         "${MINGW_PACKAGE_PREFIX}-libraqm"
         "${MINGW_PACKAGE_PREFIX}-libtiff"
         "${MINGW_PACKAGE_PREFIX}-libwebp"
         "${MINGW_PACKAGE_PREFIX}-openjpeg2"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-python2-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-tk")
options=('staticlibs')
source=(${_realname}-${pkgver}.tar.gz::https://github.com/python-pillow/Pillow/archive/${pkgver}.tar.gz
        001-freeze-support.patch
        002-shared-openjpeg.patch)
sha256sums=('a7bd05a7a0d260c17e60fffa2d9f23ba977e01e98c65b7953163de90e0e102d3'
            '6865da61534a45645fff71e139226bbce00c636a5f82ef824d13ad5a22efaac5'
            '0c989d518dd2c1f6126d60f5302a5170165d0cadff6923e5b9537ca3a8168df8')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-freeze-support.patch
  patch -p1 -i ${srcdir}/002-shared-openjpeg.patch
  cd ${srcdir}
  cp -r "${_realname}-${pkgver}" "python2-build-${CARCH}"
  cp -r "${_realname}-${pkgver}" "python3-build-${CARCH}"
}

package_python3-pillow() {
  pkgdesc="Python Imaging Library (PIL) fork Python3 version (mingw-w64)"
  depends+=("${MINGW_PACKAGE_PREFIX}-python3"
            "${MINGW_PACKAGE_PREFIX}-python3-olefile")
  provides=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
  replaces=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
  conflicts=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
  optdepends=("${MINGW_PACKAGE_PREFIX}-tk: for the ImageTK module")

  local _mingw_prefix=$(cygpath -am ${MINGW_PREFIX})
  local _py3basever=$(${MINGW_PREFIX}/bin/python3 -c "import sys;sys.stdout.write('.'.join(map(str, sys.version_info[:2])))")
  
  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX#\/} --root="${pkgdir}/" --optimize=0

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
  install -dm755 "${pkgdir}${MINGW_PREFIX}/include/python${_py3basever}m/"
  install -m644 -t "${pkgdir}${MINGW_PREFIX}/include/python${_py3basever}m/" libImaging/*.h

  # clean up bins
  cd "${pkgdir}${MINGW_PREFIX}/bin"
  for f in *.py; do
    sed -e "s|${_mingw_prefix}/bin/|/usr/bin/env |g" -i ${f}
    mv "$f" "${f%.py}"
  done
}

package_python2-pillow() {
  pkgdesc="Python Imaging Library (PIL) fork. Python2 version (mingw-w64)"
  depends+=("${MINGW_PACKAGE_PREFIX}-python2"
            "${MINGW_PACKAGE_PREFIX}-python2-olefile")
  provides=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}")
  replaces=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}")
  conflicts=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}")
  optdepends=("${MINGW_PACKAGE_PREFIX}-tk: for the ImageTK module")
  
  local _mingw_prefix=$(cygpath -am ${MINGW_PREFIX})
  local _py2basever=$(${MINGW_PREFIX}/bin/python2 -c "import sys;sys.stdout.write('.'.join(map(str, sys.version_info[:2])))")

  cd "${srcdir}/python2-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX#\/} --root="${pkgdir}/" --optimize=0

  sed -i 's|/usr/local/bin/python$|/usr/bin/env python2|' \
    "${pkgdir}${MINGW_PREFIX}/lib/python${_py2basever}/site-packages/PIL/OleFileIO.py"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE"

  install -dm755 "${pkgdir}${MINGW_PREFIX}/include/python${_py2basever}/"
  install -m644 -t "${pkgdir}${MINGW_PREFIX}/include/python${_py2basever}/" libImaging/*.h

  # clean up bins
  cd "${pkgdir}${MINGW_PREFIX}/bin"
  for f in *.py; do
    sed -e "s|${_mingw_prefix}/bin/|/usr/bin/env |g" -i ${f}
    mv "${f}" "${f%.py}2"
  done
}

package_mingw-w64-i686-python2-pillow() {
  package_python2-pillow
}

package_mingw-w64-i686-python3-pillow() {
  package_python3-pillow
}

package_mingw-w64-x86_64-python2-pillow() {
  package_python2-pillow
}

package_mingw-w64-x86_64-python3-pillow() {
  package_python3-pillow
}
