# Maintainer: Oscar Fuentes <ofv@wanadoo.es>

_realname=tklib
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
_hash="ee5498840f"
# Increase every time we refresh the package with a new upstream hash:
_releasecounter=3
_basever=0.7.0
pkgver="$_basever+$_releasecounter.$_hash"
_dir="Tk_Library_Source_Code-$_hash"
pkgrel=1
pkgdesc="A companion to Tcllib, for Tk related packages (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
url='https://core.tcl.tk/tklib/home'
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-tk"
         "${MINGW_PACKAGE_PREFIX}-tcllib")
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools")
source=("https://core.tcl-lang.org/tklib/tarball/$_hash/$_dir.tar.gz")
sha256sums=('05b87db9ad2173e19176887941ab2852978f94a1885f86ef64e862876e986bfa')

build() {
  cd "${srcdir}"/${_dir}
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}
  make all
}

package() {
  cd "${srcdir}"/${_dir}
  make DESTDIR="${pkgdir}" install
  mkdir -p "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/
  cp license.terms "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
