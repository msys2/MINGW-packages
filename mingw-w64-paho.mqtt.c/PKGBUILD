# Maintainer: Yang Zheng <yz757865041@gmail.com>
# Contributors: Marcin Sielski <marcin.sielski(at)gmail.com>

_realname=paho.mqtt.c
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.3.15
pkgrel=1
pkgdesc="Eclipse Paho MQTT C client (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://eclipse-paho.github.io/paho.mqtt.c/MQTTClient/html/'
msys2_repository_url='https://github.com/eclipse-paho/paho.mqtt.c'
msys2_references=(
  'aur: paho-mqtt-c'
)
license=('spdx:EPL-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-openssl")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
source=("https://github.com/eclipse-paho/paho.mqtt.c/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('60ce2cfdc146fcb81c621cb8b45874d2eb1d4693105d048f60e31b8f3468be90')
noextract=("${_realname}-${pkgver}.tar.gz")

prepare() {
  echo "Extracting ${_realname}-${pkgver}.tar.gz ..."
  bsdtar -xzf ${_realname}-${pkgver}.tar.gz || true
}

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  CFLAGS+=" -D_WINDOWS"
  CXXFLAGS+=" -D_WINDOWS"
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -G'Ninja' \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      "${extra_config[@]}" \
      -DPAHO_BUILD_STATIC=ON \
      -DPAHO_WITH_SSL=ON \
      -DPAHO_BUILD_DOCUMENTATION=OFF \
      -DPAHO_BUILD_SAMPLES=ON \
      -S ${_realname}-${pkgver} \
      -B build-${MSYSTEM}

  ${MINGW_PREFIX}/bin/cmake.exe --build build-${MSYSTEM}
}


package() {
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install build-${MSYSTEM}
  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/LICENSE \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
