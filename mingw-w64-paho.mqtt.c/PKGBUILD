# Maintainer: Marcin Sielski <marcin.sielski(at)gmail.com>

_realname=paho.mqtt.c
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.1.0
pkgrel=1
pkgdesc="Eclipse Paho MQTT C client (mingw-w64)"
arch=('any')
url='https://eclipse.org/paho/clients/c/'
license=('EPL/EDL')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake")
options=('strip' 'staticlibs')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/eclipse/${_realname}/archive/v${pkgver}.tar.gz"
        "0001-CMakeLists.patch")
sha256sums=('970b9621efc24251441d386274cd0a90cf6ad52b64031f3c8cb53fdd5526f5f3'
            '4163cf0db590d39f5b86aa54a0e5331b7e7e374e796748d6df78c8be30ceaf83')

prepare() {
  cd "$srcdir"/${_realname}-${pkgver}
  patch -p2 -i ${srcdir}/0001-CMakeLists.patch
}

build() {
  cd "$srcdir"/${_realname}-${pkgver}
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -G'MSYS Makefiles' \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      "${extra_config[@]}" \
      -DBUILD_{SHARED,STATIC}_LIBS=ON \
      ../${_realname}-${pkgver}

  make
}

check() {
  cd "${srcdir}"/build-${CARCH}
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install DESTDIR="${pkgdir}"
}
