# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=tolua
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=5.2.0
pkgrel=2
pkgdesc="A tool that greatly simplifies the integration of C/C++ code with Lua. (mingw-w64)" 
arch=('any')
url="http://www.tecgraf.puc-rio.br/~celes/tolua/" 
license=('MIT')
depends=("${_mingw_suff}-lua")
makedepends=("${_mingw_suff}-gcc")
options=('staticlibs' 'strip' 'emptydirs')
source=(http://www.tecgraf.puc-rio.br/~celes/tolua/${_realname}-${pkgver}.tar.gz)
md5sums=('c5b4084f3a828d35646e08282050afbd')

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  sed -i "s|LUA=/usr/local|LUA=${MINGW_PREFIX}|g" config
  make -j1 \
    AR="ar rcu" \
    RANLIB="ranlib" \
    STRIP="strip" \
    CC="${MINGW_CHOST}-gcc"
}

package(){
  cd ${srcdir}/${_realname}-${pkgver}
  mkdir -p ${pkgdir}${MINGW_PREFIX}/{bin,lib,include}
  cp bin/*.exe ${pkgdir}${MINGW_PREFIX}/bin/
  cp lib/*.a ${pkgdir}${MINGW_PREFIX}/lib/
  cp include/*.h ${pkgdir}${MINGW_PREFIX}/include/
}
