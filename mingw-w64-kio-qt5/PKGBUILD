# Maintainer (MSYS2): Ray Donnelly <mingw.android@gmail.com>
# Contributor (MSYS2): Mateusz Miku≈Ça <mati865@gmail.com>

_variant=-${KF5_VARIANT:-shared}
source "$(dirname ${BASH_SOURCE[0]})"/../mingw-w64-PKGBUILD-common/kde-frameworks5
_kde_f5_init_package "${_variant}" "kio"
pkgver=5.26.0
pkgrel=1
arch=('any')
pkgdesc="Network transparent access to files and data (mingw-w64-qt5${_namesuff})"
license=('LGPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-extra-cmake-modules")
depends=()
_kde_f5_add_depends "${_variant}" "${MINGW_PACKAGE_PREFIX}-qt5${_namesuff}"
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${_basename}-${pkgver}.tar.xz"
        "disable-GSSAPI-detection.patch"
        'D2546.diff')
sha256sums=('40d546074af313be97f64b644daad46fc9c287dbcf4bc41ca9fb5ff6c02976cf'
            '61b1db61204325b7f11f0e8963731fe1644593ff0d1601e52778028e8e5d5573'
            '627e1be0e0d1cb53ea0a02b9d3c061c1f25a65cd9328d1255d365c26d90853da')

prepare() {
  mkdir -p build-${CARCH}${_variant}
  
  cd ${_basename}-${pkgver}
  # Disable GSSAPI until we have MinGW-w64 krb5-config
  patch -p1 -i ${srcdir}/disable-GSSAPI-detection.patch
  patch -p0 -i ${srcdir}/D2546.diff

  # repalce old QStandardPaths functions with our new *Msys functions
  find . -type f -name "*.cpp*" -print0 | xargs -0 sed -i -e "s/QStandardPaths::locateAll(/QStandardPaths::locateAllMsys(/g" \
                                                          -e "s/QStandardPaths::locate(/QStandardPaths::locateMsys(/g" \
                                                          -e "s/QStandardPaths::standardLocations(/QStandardPaths::standardLocationsMsys(/g" \
                                                          -e "s/QStandardPaths::writableLocation(/QStandardPaths::writableLocationMsys(/g"
}

build() {
  local -a extra_config
  cd build-${CARCH}${_variant}
  if [ "${_variant}" = "-static" ]; then
    extra_config+=( -DBUILD_SHARED_LIBS=NO )
    QT5_PREFIX=${MINGW_PREFIX}/qt5-static
    export PATH=${QT5_PREFIX}/bin:"$PATH"
  else
    QT5_PREFIX=${MINGW_PREFIX}
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake ../${_basename}-${pkgver} \
      -DCMAKE_BUILD_TYPE=$(_kde_f5_CMAKE_BUILD_TYPE) \
      -DCMAKE_INSTALL_PREFIX=${QT5_PREFIX} \
      -DKDE_INSTALL_LIBDIR=lib \
      -DBUILD_TESTING=OFF \
      -DECM_DIR=${MINGW_PREFIX}/share/ECM \
      "${extra_config[@]}" \
      -G'MSYS Makefiles'
  make
}

package() {
  cd build-${CARCH}${_variant}
  make DESTDIR="${pkgdir}" install
}
