# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=jxrlib
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.1
pkgrel=1
pkgdesc="Open source implementation of jpegxr (mingw-w64)"
arch=('any')
url="https://jxrlib.codeplex.com/"
license=('BSD')
validpgpkeys=('gpg_KEY')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-iconv")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
options=('strip' 'staticlibs')
source=(#jxrlib_${pkgver//./_}.tar.gz::http://jxrlib.codeplex.com/downloads/get/685250
        http://jxrlib.codeplex.com/downloads/get/685250#/jxrlib_${pkgver//./_}.tar.gz
        "CMakeLists.txt"
        "jxrlib_warnings.patch"
        "jxrlib_mingw.patch")
md5sums=('c0a6a4529cb687d127cf52b52f5ec848'
         '31c0666ddd914e0683a458c3b6aa3b0e'
         '8ee3a08fbba3c74ea394e91d562c8be8'
         'e72528f9e35f5c52a3287b4fd31a0e44')

prepare() {
  cd "${srcdir}"/${_realname}
  
  # Sanitize charset and line endings
  for file in `find . -type f -name '*.c' -or -name '*.h' -or -name '*.txt'`; do
    ${MINGW_PREFIX}/bin/iconv -f ISO-8859-15 -t UTF-8 ${file} > ${file}.new && \
        sed -i 's|\r||g' ${file}.new && \
        touch -r ${file} ${file}.new && mv ${file}.new ${file}
  done
  
  # Remove file which already exists as part of the mingw headers
  rm -f common/include/guiddef.h
  
  cp -f ${srcdir}/CMakeLists.txt .
  
  patch -p1 -i ${srcdir}/jxrlib_warnings.patch
  patch -p1 -i ${srcdir}/jxrlib_mingw.patch
}

build() {
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}
 
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -G'MSYS Makefiles' \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      "${extra_config[@]}" \
      ../${_realname}

  make
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make DESTDIR=${pkgdir} install
}
