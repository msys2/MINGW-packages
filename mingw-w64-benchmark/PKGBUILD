# Maintainer: Andrew Sun <adsun701@gmail.com>

_realname=benchmark
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.8.5
pkgrel=1
pkgdesc="A microbenchmark support library, by Google (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/google/benchmark"
license=('spdx:Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
checkdepends=("${MINGW_PACKAGE_PREFIX}-gtest")
source=("https://github.com/google/benchmark/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "001-fix-pkgconfig-files.patch")
sha256sums=('d26789a2b46d8808a48a4556ee58ccc7c497fcd4c0af9b90197674a81e04798a'
            'e88f53dc504e7e0a4eeacd474f0d5f63d5a14f5dca1fc45f817a07548ba3233f')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/001-fix-pkgconfig-files.patch
}

build() {
  # Shared Build
  mkdir -p "${srcdir}"/build-${MSYSTEM}-shared && cd "${srcdir}"/build-${MSYSTEM}-shared

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=ON \
    -DBENCHMARK_ENABLE_WERROR=OFF \
    -DBENCHMARK_USE_BUNDLED_GTEST=OFF \
    -DBENCHMARK_ENABLE_TESTING=OFF \
    ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake --build .

  # Static Build
  mkdir -p "${srcdir}"/build-${MSYSTEM}-static && cd "${srcdir}"/build-${MSYSTEM}-static

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=OFF \
    -DBENCHMARK_ENABLE_WERROR=OFF \
    -DBENCHMARK_USE_BUNDLED_GTEST=OFF \
    -DBENCHMARK_ENABLE_TESTING=OFF \
    ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake --build .
}

check() {
  # Shared Check
  cd "${srcdir}"/build-${MSYSTEM}-shared
  ${MINGW_PREFIX}/bin/cmake \
    -DBENCHMARK_ENABLE_TESTING=ON \
    ../${_realname}-${pkgver}
  ${MINGW_PREFIX}/bin/cmake --build .

  PATH=$PWD/src:$PATH ${MINGW_PREFIX}/bin/ctest.exe ./ || warning "Tests failed"

  # Static Check
  cd "${srcdir}"/build-${MSYSTEM}-static
  ${MINGW_PREFIX}/bin/cmake \
    -DBENCHMARK_ENABLE_TESTING=ON \
    ../${_realname}-${pkgver}
  ${MINGW_PREFIX}/bin/cmake --build .

  ${MINGW_PREFIX}/bin/ctest.exe ./ || warning "Tests failed"
}

package() {
  # Static Install
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install "${srcdir}"/build-${MSYSTEM}-static

  # Shared Install
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install "${srcdir}"/build-${MSYSTEM}-shared

  # License
  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/LICENSE \
    -t "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}
}
