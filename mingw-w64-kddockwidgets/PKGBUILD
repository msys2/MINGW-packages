
_realname=kddockwidgets
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.4.0
pkgrel=2
pkgdesc='Dock Widgets Library by KDAB for Qt6 (mingw-w64)'
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://www.kdab.com/software-technologies/developer-tools/kddockwidgets'
license=('spdx:GPL-2.0-only AND GPL-3.0-only')
msys2_repository_url='https://github.com/KDAB/KDDockWidgets'
msys2_references=(
  'archlinux: kddockwidgets'
)
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-fmt"
         "${MINGW_PACKAGE_PREFIX}-qt6-base"
         "${MINGW_PACKAGE_PREFIX}-qt6-declarative"
         "${MINGW_PACKAGE_PREFIX}-spdlog")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-nlohmann-json"
             "${MINGW_PACKAGE_PREFIX}-qt6-tools")
source=("https://github.com/KDAB/KDDockWidgets/releases/download/v$pkgver/kddockwidgets-$pkgver.tar.gz")
sha256sums=('51dbf24fe72e43dd7cb9a289d3cab47112010f1a2ed69b6fc8ac0dff31991ed2')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      ../${_realname}-${pkgver}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .
  
  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/LICENSES/GPL-2.0-only.txt \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/GPL-2.0-only.txt
  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/LICENSES/GPL-3.0-only.txt \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/GPL-3.0-only.txt
}
