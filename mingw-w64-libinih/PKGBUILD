# Maintainer: Miloš Komarčević <miloskomarcevic@aim.com>

_realname=libinih
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=57
pkgrel=3
pkgdesc="A simple .INI file parser written in C (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
msys2_references=(
  'archlinux: libinih'
  'cygwin: inih'
)
url='https://github.com/benhoyt/inih'
license=('spdx:BSD-3-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-python")
source=("https://github.com/benhoyt/inih/archive/refs/tags/r${pkgver}.tar.gz"
        "https://patch-diff.githubusercontent.com/raw/benhoyt/inih/pull/157.patch")
sha256sums=('f03f98ca35c3adb56b2358573c8d3eda319ccd5287243d691e724b7eafa970b3'
            '289c3c3ab5b30387410e42d2061a4ab5aa96e134045be280ad34f6303a8407f8')

prepare() {
  cd "${srcdir}"/inih-r${pkgver}

  # https://github.com/benhoyt/inih/pull/157
  patch -Np1 -i "${srcdir}"/157.patch
}

build() {
  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/meson.exe setup \
      --prefix="${MINGW_PREFIX}" \
      --wrap-mode=nodownload \
      --auto-features=enabled \
      --buildtype=plain \
      -Ddefault_library=static \
      -Ddistro_install=true \
      -Dwith_INIReader=true \
      "build-${MSYSTEM}-static" \
      "inih-r${pkgver}"

  ${MINGW_PREFIX}/bin/meson.exe compile -C "build-${MSYSTEM}-static"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/meson.exe setup \
      --prefix="${MINGW_PREFIX}" \
      --wrap-mode=nodownload \
      --auto-features=enabled \
      --buildtype=plain \
      -Ddefault_library=shared \
      -Ddistro_install=true \
      -Dwith_INIReader=true \
      "build-${MSYSTEM}-shared" \
      "inih-r${pkgver}"

  ${MINGW_PREFIX}/bin/meson.exe compile -C "build-${MSYSTEM}-shared"
}

package() {
  ${MINGW_PREFIX}/bin/meson.exe install --destdir "${pkgdir}" -C "build-${MSYSTEM}-static"
  ${MINGW_PREFIX}/bin/meson.exe install --destdir "${pkgdir}" -C "build-${MSYSTEM}-shared"

  # https://github.com/benhoyt/inih/issues/162
  for _f in "${pkgdir}${MINGW_PREFIX}"/lib/pkgconfig/*.pc; do
    echo "Cflags.private: -UINI_SHARED_LIB" >> ${_f}
  done

  install -Dm644 "${srcdir}/inih-r${pkgver}/LICENSE.txt" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
