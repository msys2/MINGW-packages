# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libunistring
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.9.6
pkgrel=1
pkgdesc="Library for manipulating Unicode strings and C strings. (mingw-w64)"
arch=('any')
url="https://www.gnu.org/software/libunistring"
license=("GPL3")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-libiconv")
options=('staticlibs' 'strip')
source=("https://ftp.gnu.org/gnu/libunistring/${_realname}-${pkgver}.tar.gz")
sha256sums=('9625eec2507f4789ebb6fc48ebda98be0e0168979a2f68aa8b680bf8eeabbd47')

build() {
  #export lt_cv_deplibs_check_method='pass_all'
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../libunistring-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-threads=win32

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
