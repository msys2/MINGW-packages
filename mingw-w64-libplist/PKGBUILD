# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=libplist
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.0.0
pkgrel=3
pkgdesc='A library to handle Apple Property List format in binary or XML (mingw-w64)'
arch=('any')
url='https://www.libimobiledevice.org/'
license=('GPL2+')
source=("https://www.libimobiledevice.org/downloads/${_realname}-${pkgver}.tar.bz2"
        001-cython-module.patch
        002-dont-include-winmain-in-static.patch
        003-fix-cython-decode.patch)
depends=("${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-cython")
options=('staticlibs')
makedepends=("${MINGW_PACKAGE_PREFIX}-python3")
sha256sums=('3a7e9694c2d9a85174ba1fa92417cfabaea7f6d19631e544948dc7e17e82f602'
            '42e7c4d3fa91b5b5d206faf51e95690f74b751b1896056bb32a616e17563ee93'
            '65f4603a85f63567fac917dfb6e2d974078973ed815652817bedea80e4467016'
            '05332e049e84c21894589309864ab764bde586e41b5c1e38d8099c9ff5309a18')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-cython-module.patch
  patch -p1 -i ${srcdir}/002-dont-include-winmain-in-static.patch
  patch -p1 -i ${srcdir}/003-fix-cython-decode.patch

  autoreconf -fiv
}

build() {
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}

  PYTHON=${MINGW_PREFIX}/bin/python3 \
  ../${_realname}-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST} \
      --enable-static \
      --enable-shared

  make
}

check() {
  cd "${srcdir}"/build-${CARCH}
  # || true because of FAIL: bigarraycmp .. File does not exists
  make check || true
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install DESTDIR="${pkgdir}"

  rm -f ${pkgdir}${MINGW_PREFIX}/lib/python*/site-packages/*.a
}
