# Maintainer: @naveen521kk on Github, Naveen M K <naveen521kk@gmail.com>

_perlname=Perl-Tidy
_realname="${_perlname,,}"
pkgbase=mingw-w64-perl-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-perl-${_realname}")
pkgver=20220217
pkgrel=1
pkgdesc="indent and reformat perl scripts (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://metacpan.org/release/${_perlname}"
groups=("${MINGW_PACKAGE_PREFIX}-perl-modules")
provides=(
  "${MINGW_PACKAGE_PREFIX}-perl-perl-tidy-debugger"
  "${MINGW_PACKAGE_PREFIX}-perl-perl-tidy-devnull"
  "${MINGW_PACKAGE_PREFIX}-perl-perl-tidy-diagnostics"
  "${MINGW_PACKAGE_PREFIX}-perl-perl-tidy-filewriter"
  "${MINGW_PACKAGE_PREFIX}-perl-perl-tidy-formatter"
  "${MINGW_PACKAGE_PREFIX}-perl-perl-tidy-htmlwriter"
  "${MINGW_PACKAGE_PREFIX}-perl-perl-tidy-ioscalar"
  "${MINGW_PACKAGE_PREFIX}-perl-perl-tidy-ioscalararray"
  "${MINGW_PACKAGE_PREFIX}-perl-perl-tidy-indentationitem"
  "${MINGW_PACKAGE_PREFIX}-perl-perl-tidy-linebuffer"
  "${MINGW_PACKAGE_PREFIX}-perl-perl-tidy-linesink"
  "${MINGW_PACKAGE_PREFIX}-perl-perl-tidy-linesource"
  "${MINGW_PACKAGE_PREFIX}-perl-perl-tidy-logger"
  "${MINGW_PACKAGE_PREFIX}-perl-perl-tidy-tokenizer"
  "${MINGW_PACKAGE_PREFIX}-perl-perl-tidy-verticalaligner"
  "${MINGW_PACKAGE_PREFIX}-perl-perl-tidy-verticalaligner-alignment"
  "${MINGW_PACKAGE_PREFIX}-perl-perl-tidy-verticalaligner-line"
)
depends=(
  "${MINGW_PACKAGE_PREFIX}-perl"
)
options=('!emptydirs')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-cc"
  $([[ ${MINGW_PACKAGE_PREFIX} != *-clang-* ]] || \
    echo "${MINGW_PACKAGE_PREFIX}-gcc-compat")
  "${MINGW_PACKAGE_PREFIX}-perl-extutils-makemaker"
)
license=('gpl_2')
source=(
  'https://cpan.metacpan.org/authors/id/S/SH/SHANCOCK/Perl-Tidy-20220217.tar.gz'
  'patchmakefile.py'
)
sha256sums=('bd8bc63043c8bc94aa04811b29f93af794d8871c793c8bd36015dcbdd8a51e83'
            '00fe5ced42aa635714909a8eba1347a6c132011a9bce68e9c75b27ebd64628f2')

build() {
  cd "$srcdir/${_perlname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL
  export pkgdir=$pkgdir
  python $srcdir/patchmakefile.py
  mingw32-make
}

package() {
  cd "$srcdir/${_perlname}-${pkgver}"
  mingw32-make DESTDIR="${pkgdir}/" install
}
