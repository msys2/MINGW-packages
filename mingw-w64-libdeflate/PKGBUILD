# Maintainer: Miloš Komarčević <miloskomarcevic@aim.com>

_realname=libdeflate
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.14
pkgrel=1
pkgdesc="Heavily optimized library for DEFLATE/zlib/gzip compression and decompression (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://github.com/ebiggers/libdeflate'
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc")
source=(${_realname}-${pkgver}.tar.gz::"${url}/archive/v${pkgver}.tar.gz"
        "001-libdeflate-makefile-mingw-fix.patch"
        "002-pkg-config.patch")
sha256sums=('89e7df898c37c3427b0f39aadcf733731321a278771d20fc553f92da8d4808ac'
            'a5648a9e8d5d64b1a9301ad5f555052c8a8f2967a1a50a422f99fbd4f6a5dc4f'
            'e0a093985eb05f3b8eb725249e8fc4ec9645cdad8eaafd4a3ce1a0d7a1ef45c5')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/001-libdeflate-makefile-mingw-fix.patch
  patch -p1 -i "${srcdir}"/002-pkg-config.patch
}

build() {
  [[ -d "${srcdir}"/build-${MSYSTEM} ]] && rm -rf "${srcdir}"/build-${MSYSTEM}
  cp -a "${srcdir}"/${_realname}-${pkgver} "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

  make PREFIX=${MINGW_PREFIX}
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}
  make install PREFIX=${MINGW_PREFIX} DESTDIR="${pkgdir}"

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/COPYING "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
