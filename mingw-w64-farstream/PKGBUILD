# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=farstream
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.2.7
pkgrel=2
pkgdesc="Farstream (formerly Farsight) - Audio/Video Communications Framework (mingw-w64)"
arch=('any')
url="https://www.freedesktop.org/wiki/Software/Farstream"
license=('LGPL')
options=('staticlibs' 'strip')
depends=("${MINGW_PACKAGE_PREFIX}-gst-plugins-base" "${MINGW_PACKAGE_PREFIX}-libnice")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "gtk-doc")
source=(https://freedesktop.org/software/${_realname}/releases/${_realname}/${_realname}-${pkgver}.tar.gz{,.asc}
        0001-farstream-0.2.4.patch
        0002-give-cc-to-gir-scanner.all.patch)
sha256sums=('dad0ed43cf94e71df558c23825abe57db59d1ee8d970ffaec5e2dbc2e7acbf7d'
            'SKIP'
            '3d85a6c236ce5af088e98c5afc95e5969cac27050cffcc4b1963ccfb004dbbfe'
            'd6001b52ababf3358a4ddfd710dffc1c75e79e662c618de80efacb5806314544')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-farstream-0.2.4.patch"
  patch -p1 -i "${srcdir}/0002-give-cc-to-gir-scanner.all.patch"

  WANT_AUTOMAKE=latest autoreconf --force --install
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --enable-static \
    --enable-introspection

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  rm -f ${pkgdir}${MINGW_PREFIX}/lib/gstreamer*/*.a
  rm -f ${pkgdir}${MINGW_PREFIX}/lib/farstream*/*.a
}
