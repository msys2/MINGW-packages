# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=speex
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.2rc2
pkgrel=2
pkgdesc="A free codec for free speech (mingw-w64)"
arch=('any')
license=('BSD')
url="http://www.speex.org/"
depends=("${MINGW_PACKAGE_PREFIX}-libogg" "${MINGW_PACKAGE_PREFIX}-speexdsp")
options=('strip' '!libtool' 'staticlibs')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")
source=(http://downloads.us.xiph.org/releases/${_realname}/${_realname}-${pkgver}.tar.gz
        01-mingw-winmm.patch)
md5sums=('6ae7db3bab01e1d4b86bacfa8ca33e81'
         '8edb0120f01d93e8202a97ebb1f5fc24')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/01-mingw-winmm.patch
  
  autoreconf -fiv
}

build() {
  [[ -d "build-${MINGW_CHOST}" ]] && rm -rf "build-${MINGW_CHOST}"
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --enable-static

  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
