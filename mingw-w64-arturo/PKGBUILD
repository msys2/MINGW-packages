# Maintainer: Arturo Programming Language <https://github.com/arturo-lang>

_realname=arturo
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=0.10.0
pkgrel=2
pkgdesc="Simple, expressive & portable programming language for efficient scripting. (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64')
url="https://arturo-lang.io/"
msys2_repository_url="https://github.com/arturo-lang/arturo"
license=("spdx:MIT")
depends=(
  "${MINGW_PACKAGE_PREFIX}-cc-libs"
  "${MINGW_PACKAGE_PREFIX}-gmp"
  "${MINGW_PACKAGE_PREFIX}-libwinpthread"
  "${MINGW_PACKAGE_PREFIX}-mpfr"
  "${MINGW_PACKAGE_PREFIX}-openssl"
  "${MINGW_PACKAGE_PREFIX}-pcre"
  "${MINGW_PACKAGE_PREFIX}-sqlite3"
  "${MINGW_PACKAGE_PREFIX}-webview"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-nim"
  "${MINGW_PACKAGE_PREFIX}-pkgconf"
)
source=("https://github.com/arturo-lang/arturo/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "001-no-static-linking.patch")
sha256sums=('408646496895753608ad9dc6ddfbfa25921c03c4c7356f2832a9a63f4a7dc351'
            '51af1d69911ff49af86e97f578aa4f1a651f04c359623f71c07b8387f70ed515')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/001-no-static-linking.patch
}

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("--release")
  else
    extra_config+=("--debug")
  fi

  cd "${srcdir}/${_realname}-${pkgver}"
  # Using dev flag to build webview DLLs
  nim build.nims "${extra_config[@]}" -l --dev
}

package() {
  install -d "${pkgdir}"${MINGW_PREFIX}/bin
  cp "${srcdir}"/${_realname}-${pkgver}/bin/*.exe "${pkgdir}"${MINGW_PREFIX}/bin/

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/LICENSE \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
