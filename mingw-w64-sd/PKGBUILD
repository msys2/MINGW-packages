# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=sd
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.1.0
pkgrel=1
pkgdesc="Intuitive find & replace CLI (sed alternative) (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://github.com/chmln/sd'
msys2_repository_url='https://github.com/chmln/sd'
msys2_references=(
  'archlinux: sd'
  'purl: pkg:cargo/sd'
)
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
options=('!strip') # it's done at build stage
source=("https://github.com/chmln/sd/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('defdce484f8c92f265e1282490572575028967c2c55d356111d1e49a3ea9a88e')

prepare() {
  cd "${_realname}-${pkgver}"

  cargo fetch --locked --target "${RUST_CHOST}"
}

build() {
  cd "${_realname}-${pkgver}"

  cargo build --release --frozen -p sd-cli
}

check() {
  cd "${_realname}-${pkgver}"

  cargo test --release --frozen -p sd-cli
}

package() {
  cd "${_realname}-${pkgver}"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path sd-cli \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
  install -Dm644 gen/sd.1 "${pkgdir}${MINGW_PREFIX}/share/man/man1/sd.1"
  install -Dm644 gen/completions/sd.bash "${pkgdir}${MINGW_PREFIX}/share/bash-completion/completions/sd"
  install -Dm644 gen/completions/_sd "${pkgdir}${MINGW_PREFIX}/share/zsh/site-functions/_sd"
  install -Dm644 gen/completions/sd.fish "${pkgdir}${MINGW_PREFIX}/share/fish/vendor_completions.d/sd.fish"
}
