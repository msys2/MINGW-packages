# Maintainer: Francis Giraldeau <francis.giraldeau@nrc-cnrc.gc.ca>

_realname=cgns
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.5.1
pkgrel=1
pkgdesc="CFD General Notation System library and tools (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://cgns.github.io"
msys2_repository_url="https://github.com/CGNS/CGNS"
license=('spdx:Zlib')
depends=($([[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]] || echo "${MINGW_PACKAGE_PREFIX}-gcc-libgfortran")
         "${MINGW_PACKAGE_PREFIX}-hdf5")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-fc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
source=(https://github.com/CGNS/CGNS/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz)
sha256sums=('ae63b0098764803dd42b7b2a6487cbfb3c0ae7b22eb01a2570dbce49316ad279')

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  if [[ ${MSYSTEM} == CLANG64 ]]; then
    extra_config+=("-DCGNS_ENABLE_FORTRAN=OFF")
  else
    extra_config+=("-DCGNS_ENABLE_FORTRAN=ON")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -GNinja \
    ${extra_config[@]} \
    -DCGNS_ENABLE_HDF5=ON \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -S ${_realname}-${pkgver} \
    -B build-${MSYSTEM}

  ${MINGW_PREFIX}/bin/cmake.exe --build build-${MSYSTEM}
}

package() {
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install build-${MSYSTEM}

  # license
  install -D -m644 "${srcdir}"/${_realname}-${pkgver}/license.txt \
  "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
