# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=tidy
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=5.6.0
pkgrel=1
pkgdesc="A tool to tidy down your HTML code to a clean style (mingw-w64)"
arch=(any)
url="http://www.html-tidy.org/"
license=(custom)
#depends=(glibc)
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake" "${MINGW_PACKAGE_PREFIX}-libxslt")
conflicts=("${MINGW_PACKAGE_PREFIX}-tidyhtml")
provides=("${MINGW_PACKAGE_PREFIX}-tidyhtml")
replaces=("${MINGW_PACKAGE_PREFIX}-tidyhtml")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/htacg/tidy-html5/archive/$pkgver.tar.gz")
sha256sums=('08a63bba3d9e7618d1570b4ecd6a7daa83c8e18a41c82455b6308bc11fe34958')

build() {
  [[ -d "$srcdir}/build-${CARCH}" ]] && rm -rf "$srcdir/build-${CARCH}"
  mkdir -p "$srcdir/build-${CARCH}" && cd "$srcdir/build-${CARCH}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
    -G'MSYS Makefiles' \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    "${extra_config[@]}" \
    -DBUILD_{SHARED,STATIC}_LIBS=ON \
    -DBUILD_TAB2SPACE=ON \
    ../tidy-html5-$pkgver
  make
}

package() {
  cd ${srcdir}/build-${CARCH}
  make DESTDIR="$pkgdir" install

# Compatibility symlinks until everything is ported
  ln -s tidybuffio.h "$pkgdir"/${MINGW_PREFIX}/include/buffio.h
  ln -s tidyplatform.h "$pkgdir"/${MINGW_PREFIX}/include/platform.h

  install -Dm755 tab2space.exe $pkgdir/${MINGW_PREFIX}/bin
  install -Dm644 "$srcdir"/${_realname}-html5-$pkgver/README/LICENSE.md \
    "$pkgdir${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
