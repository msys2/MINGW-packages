# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=djview
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.10.6
pkgrel=2
pkgdesc='Portable DjVu viewer (mingw-w64)'
url='https://djvu.sourceforge.io/djview4.html'
license=('GPL')
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
depends=("${MINGW_PACKAGE_PREFIX}-djvulibre"
         "${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-qt5-base"
         "${MINGW_PACKAGE_PREFIX}-libtiff")
makedepends=("${MINGW_PACKAGE_PREFIX}-qt5-tools" "${MINGW_PACKAGE_PREFIX}-autotools" "${MINGW_PACKAGE_PREFIX}-cc")
source=("https://downloads.sourceforge.net/djvu/${_realname}-${pkgver}.tar.gz")
sha256sums=('8446f3cd692238421a342f12baa365528445637bffb96899f319fe762fda7c21')
conflicts=("${MINGW_PACKAGE_PREFIX}-djview4")
provides=("${MINGW_PACKAGE_PREFIX}-djview4")
replaces=("${MINGW_PACKAGE_PREFIX}-djview4")

build() {
  [[ -d ${srcdir}/build-${MSYSTEM} ]] && rm -rf ${srcdir}/build-${MSYSTEM}
  cp -rf ${_realname}-${pkgver} build-${MSYSTEM} && cd ${srcdir}/build-${MSYSTEM}

  export QMAKE=${MINGW_PREFIX}/bin/qmake.exe
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared

  make
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
  cp src/release/djview.exe "${pkgdir}${MINGW_PREFIX}"/bin/djview4.exe
  ln -s "${pkgdir}${MINGW_PREFIX}"/bin/djview4.exe "${pkgdir}${MINGW_PREFIX}"/bin/djview.exe
}
