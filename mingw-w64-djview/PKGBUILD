# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=djview
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.12.3
pkgrel=1
pkgdesc='Portable DjVu viewer (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://djvu.sourceforge.io/djview4.html'
msys2_repository_url="https://sourceforge.net/p/djvu/djview-git/ci/master/tree/"
license=('spdx:GPL-2.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-djvulibre"
         "${MINGW_PACKAGE_PREFIX}-libtiff"
         "${MINGW_PACKAGE_PREFIX}-qt6-5compat"
         "${MINGW_PACKAGE_PREFIX}-qt6-base")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-qt6-tools")
source=("https://downloads.sourceforge.net/djvu/${_realname}-${pkgver}.tar.gz")
sha256sums=('17bfb9731ab8070e01235381f08e57b1231ffb5f49eb031873ebfa189cdaf47d')

prepare() {
  cd ${_realname}-${pkgver}
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  [[ -d "${srcdir}"/build-${MSYSTEM} ]] && rm -rf "${srcdir}"/build-${MSYSTEM}
  cp -rf ${_realname}-${pkgver} build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

  export QMAKE=${MINGW_PREFIX}/bin/qmake6.exe
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --enable-static \
    --enable-shared

  make LRELEASE=lrelease-qt6
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  make DESTDIR="${pkgdir}" install LRELEASE=lrelease-qt6

  cp src/release/djview.exe "${pkgdir}${MINGW_PREFIX}"/bin/djview4.exe
  ln -s "${pkgdir}${MINGW_PREFIX}"/bin/djview4.exe "${pkgdir}${MINGW_PREFIX}"/bin/djview.exe

  install -Dm644 COPYING "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
