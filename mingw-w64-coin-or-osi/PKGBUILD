# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=coin-or-osi
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.108.9
pkgrel=1
pkgdesc="COIN-OR Open Solver Interface. (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64')
url='https://github.com/coin-or/Osi'
msys2_references=(
  'archlinux: coin-or-osi'
)
license=('spdx:EPL-2.0')
groups=("${MINGW_PACKAGE_PREFIX}-coin-or")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-coin-or-coinutils")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-fc")
source=("${url}/archive/releases/${pkgver}/${_realname}-${pkgver}.tar.gz"
        001-fix-build-on-mingw.patch
        002-fix-build-with-clang.patch)
sha256sums=('8b09802960d7d4fd9579b3e4320bfb36e7f8dca5e5094bf1f5edf1b7003f5562'
            'b7d725a85ac94ef923d0ca08e1e7ec0ebb737cfd5351ccecc51e1e1bf056c9be'
            '4bee4a81171ab9f375f389bd1063124844f478cf7e182ce305db572b76f154bc')

prepare() {
  cd Osi-releases-${pkgver}
  patch -p1 -i ${srcdir}/001-fix-build-on-mingw.patch

  if [[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]]; then
    patch -p1 -i ${srcdir}/002-fix-build-with-clang.patch
  fi
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  export lt_cv_deplibs_check_method='pass_all'

  if [[ ${MINGW_PACKAGE_PREFIX} != *-clang-* ]]; then
    export F77=${MINGW_PREFIX}/bin/gfortran.exe
  else
    export F77=${MINGW_PREFIX}/bin/flang.exe
  fi

  CXXFLAGS+=" -Wno-register" \
  ../"Osi-releases-${pkgver}"/configure \
    --prefix="${MINGW_PREFIX}" \
    --enable-shared \
    --disable-static \
    --enable-dependency-linking

  make
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  make check
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  make install DESTDIR="${pkgdir}"

  install -Dm644 "${srcdir}/Osi-releases-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
