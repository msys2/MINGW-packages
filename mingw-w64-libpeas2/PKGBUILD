# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=libpeas
_versionedname=libpeas2
pkgbase=mingw-w64-${_versionedname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_versionedname}"
pkgver=2.0.1
pkgrel=1
pkgdesc="A GObject-based plugins engine (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://gitlab.gnome.org/GNOME/libpeas"
msys2_references=(
  'archlinux: libpeas-2'
)
license=("spdx:LGPL-2.1-or-later")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-glade"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             "${MINGW_PACKAGE_PREFIX}-python-gobject"
             "${MINGW_PACKAGE_PREFIX}-gi-docgen"
             "${MINGW_PACKAGE_PREFIX}-vala")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gobject-introspection-runtime"
        )
optdepends=(
  "${MINGW_PACKAGE_PREFIX}-python-gobject: Python loader"
)
source=(https://download.gnome.org/sources/${_realname}/${pkgver:0:3}/${_realname}-${pkgver}.tar.xz
        fix-build.patch)
sha256sums=('9ddc1d51f38663da4df52163051b7b2cea3a242cfaee9f5a7e140f0784c8aa77'
            'cd331d2abd26783775587cf018fce8c728a2affae291ff67f1c6963ddd8d453b')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  patch -p1 -i ${srcdir}/fix-build.patch
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
  ${MINGW_PREFIX}/bin/meson.exe setup \
    --prefix="${MINGW_PREFIX}" \
    --wrap-mode=nodownload \
    --auto-features=enabled \
    --buildtype=plain \
    -Dgtk_doc=true \
    -Dgjs=false \
    -Dlua51=false \
    -Dvapi=true \
    ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/meson.exe compile
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/meson.exe install

  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/${_versionedname}2/COPYING"
}
