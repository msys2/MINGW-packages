# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libmicrodns
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.0.10
pkgrel=1
pkgdesc="Minimal mDNS resolver library (mingw-w64)"
arch=('any')
url="https://github.com/videolabs/libmicrodns"
license=("LGPL2.1")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-libtasn1")
options=('strip' 'staticlibs')
source=(${_realname}-${pkgver}.tar.gz::https://github.com/videolabs/libmicrodns/archive/${pkgver}.tar.gz)
sha256sums=('623e3077bd940816215d9707c217dc8f957d3dee9b7fd3ea7aaa4de0cd881833')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  autoreconf -fis
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-static \
    --enable-shared

  make
}

package() {
  cd build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install

  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
