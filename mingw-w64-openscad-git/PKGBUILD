# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=openscad
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=r7764.ac1d89f23
pkgrel=1
pkgdesc="The programmers solid 3D CAD modeller (mingw-w64)"
arch=('any')
license=('GPL2')
url='http://openscad.org/'
depends=("${MINGW_PACKAGE_PREFIX}-qt5"
	"${MINGW_PACKAGE_PREFIX}-boost"
	"${MINGW_PACKAGE_PREFIX}-cgal"
	"${MINGW_PACKAGE_PREFIX}-opencsg"
	"${MINGW_PACKAGE_PREFIX}-glew"
	"${MINGW_PACKAGE_PREFIX}-qscintilla"
	"${MINGW_PACKAGE_PREFIX}-shared-mime-info"
	"${MINGW_PACKAGE_PREFIX}-libzip"
	"${MINGW_PACKAGE_PREFIX}-double-conversion")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
	"${MINGW_PACKAGE_PREFIX}-eigen3"
	"${MINGW_PACKAGE_PREFIX}-gdb"
	"${MINGW_PACKAGE_PREFIX}-imagemagick"
	"${MINGW_PACKAGE_PREFIX}-pkg-config"
	"git")
options=(!staticlibs !buildflags strip)
source=("${_realname}"::"git+https://github.com/${_realname}/${_realname}.git")
sha256sums=('SKIP')

_basedir=${_realname}
_prefix=${MINGW_PREFIX}

pkgver() {
	cd "${srcdir}/${_realname}"
	( set -o pipefail
		git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
		printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

prepare() {
	cd "${srcdir}/${_basedir}"
	LANG=en	git submodule init
	LANG=en	git submodule update
	rm -f libraries/MCAD/*.py
	rm -rf libraries/MCAD/.git/
	rm -f libraries/MCAD/.gitignore
}

build() {
	cd "${srcdir}/${_basedir}"

	MSYS2_ARG_CONV_EXCL="PREFIX=;BOOSTDIR=;CGALDIR=;EIGENDIR=;GLEWDIR=;OPENCSGDIR=" \
	BOOSTDIR=${_prefix} \
	CGALDIR=${_prefix} \
	EIGENDIR=${_prefix} \
	GLEWDIR=${_prefix} \
	OPENCSGDIR=${_prefix} \
	${_prefix}/bin/qmake PREFIX="${_prefix}" "CONFIG+=release experimental" "CONFIG-=debug_and_release debug_and_release_target"

	make

	convert "icons/${_realname}.png" -resize 128x128\> "icons/${_realname}.png"
}

package() {
	cd ${srcdir}/${_realname}
	make INSTALL_ROOT="${pkgdir}" install
	install -Dm644 "icons/${_realname}.xml" "${pkgdir}${_prefix}/share/mime/packages/${_realname}.xml"
}
