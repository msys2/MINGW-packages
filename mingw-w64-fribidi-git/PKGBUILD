# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>
#Note that this is a temporary package until the next version is 
#released.  I created it because 1.0.1 release did not compile but
#some libraries such libraqm can use the new API.

_realname=fribidi
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=279.565f83a
pkgrel=1
pkgdesc="A Free Implementation of the Unicode Bidirectional Algorithm (mingw-w64)"
arch=('any')
license=('LGPL')
url="https://fribidi.org/"
depends=("${MINGW_PACKAGE_PREFIX}-glib2")
options=('strip' '!libtool' 'staticlibs')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
#https://github.com/fribidi/fribidi/archive/v1.0.1.tar.gz
source=("${_realname}"::"git+https://github.com/fribidi/fribidi.git#branch=master"
        "0001-No-need-to-specialcase-export-symbols-for-OS_WIN32.patch"
        "0003-fix-eols-in-tests-on.mingw.patch")
noextract=("${_realname}-${pkgver}.tar.gz")
sha256sums=('SKIP'
            '5b267f489574f2cb7f52b9cfbb362320c964cab427db9cf86c9cc7c042c98be0'
            '51b6729ea94b3dd3a8f8418733a39cde7a17888de6a407905509d8c968eff6cd')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

pkgver() {
  cd "${srcdir}"/${_realname}
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${srcdir}/${_realname}
  apply_patch_with_msg \
    0001-No-need-to-specialcase-export-symbols-for-OS_WIN32.patch \
    0003-fix-eols-in-tests-on.mingw.patch
  autoreconf -fiv
  rm README
  ln README.md README
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  ../${_realname}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-static \
    --with-glib --disable-docs

  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
}
