# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_pyname=Pygments
_realname=pygments
# this is a separate package to break the pygments <-> sphinx dependency cycle
pkgbase=mingw-w64-python-${_realname}-docs
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}-docs")
pkgver=2.17.2
pkgrel=3
pkgdesc="Python syntax highlighter (mingw-w64) (documentation)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
msys2_references=(
  'pypi: Pygments'
  "cpe: cpe:/a:pygments:pygments"
)
url="https://pygments.org/"
msys2_repository_url="https://github.com/pygments/pygments"
license=('spdx:BSD-2-Clause')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python-sphinx"
  "${MINGW_PACKAGE_PREFIX}-python-wcag-contrast-ratio"
)
source=("https://pypi.org/packages/source/P/Pygments/${_realname}-${pkgver}.tar.gz")
sha256sums=('da46cec9fd2de5be3a8a784f434e4c4ab670b4ff54d605c4c2717e9d49c4c367')

prepare() {
  rm -rf python-build-${MSYSTEM} | true
  cp -r ${_pyname}-${pkgver} python-build-${MSYSTEM}
}

build () {
  cd "${srcdir}/python-build-${MSYSTEM}"
  make -C doc html
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  mkdir -p "${pkgdir}${MINGW_PREFIX}/share/doc"
  cp -rT doc/_build/html "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}"
}
