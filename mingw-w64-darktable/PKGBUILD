# Maintainer: Peter Budai <peterbud@hotmail.com>

_realname=darktable
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.4.0
pkgrel=1
pkgdesc="darktable is an open source photography workflow application and raw developer (mingw-w64)"
arch=('any')
url='https://www.darktable.org'
license=('GPL3')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake")
depends=("${MINGW_PACKAGE_PREFIX}-exiv2"
  "${MINGW_PACKAGE_PREFIX}-lcms2"
  "${MINGW_PACKAGE_PREFIX}-lensfun"
  "${MINGW_PACKAGE_PREFIX}-dbus-glib"
  "${MINGW_PACKAGE_PREFIX}-openexr"
  "${MINGW_PACKAGE_PREFIX}-sqlite3"
  "${MINGW_PACKAGE_PREFIX}-libxslt"
  "${MINGW_PACKAGE_PREFIX}-libsoup"
  "${MINGW_PACKAGE_PREFIX}-libwebp"
  "${MINGW_PACKAGE_PREFIX}-libsecret"
  "${MINGW_PACKAGE_PREFIX}-lua"
  "${MINGW_PACKAGE_PREFIX}-graphicsmagick"
  "${MINGW_PACKAGE_PREFIX}-openjpeg2"
  "${MINGW_PACKAGE_PREFIX}-gtk3"
  "${MINGW_PACKAGE_PREFIX}-pugixml"
  "${MINGW_PACKAGE_PREFIX}-libexif"
  "${MINGW_PACKAGE_PREFIX}-osm-gps-map"
  "${MINGW_PACKAGE_PREFIX}-libgphoto2"
  "${MINGW_PACKAGE_PREFIX}-flickcurl"
  "${MINGW_PACKAGE_PREFIX}-drmingw"
)
options=('!strip')
source=(${_realname}-${pkgver}.tar.xz::"https://github.com/darktable-org/${_realname}/releases/download/release-${pkgver}/${_realname}-${pkgver}.tar.xz"
  "01-darktable-fix-empty-set.patch")
sha256sums=('9d37388aee79d5ada71062bbac3cda612a61d1a781f6320b784b27308f3a1878'
            'b80e86515384321f798bea36b64b344acb098d87dd6d80196299e72b4f5b5496')

prepare() {
  cd "$srcdir"/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/01-darktable-fix-empty-set.patch
}

build() {
  cd "$srcdir"/${_realname}-${pkgver}
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -G'MSYS Makefiles' \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      "${extra_config[@]}" \
      ../${_realname}-${pkgver}

  make
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install DESTDIR="${pkgdir}"

  # Remove dependencies which are by default added for WIN32 builds
  # but not needed under MSYS2/MINGW due to the defined dependencies
  cd "${pkgdir}${MINGW_PREFIX}"
  find bin -maxdepth 1 -type f -not -wholename 'bin/*darktable*' -prune -exec rm -rf {} +
  find lib -mindepth 1 -type d -not -wholename 'lib/darktable*' -prune -exec rm -rf {} +
  rm -rf share/glib-2.0
  rm -rf share/lensfun
  rm -rf share/icons
}
