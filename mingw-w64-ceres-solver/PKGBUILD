# Maintainer: Diego Sogari <diego.sogari@falker.com.br>

_realname=ceres-solver
pkgbase=mingw-w64-${_realname}
pkgname=(
	"${MINGW_PACKAGE_PREFIX}-${_realname}"
	"${MINGW_PACKAGE_PREFIX}-${_realname}-lgpl"
)
pkgver=2.0.0
pkgrel=2
pkgdesc="A large scale non-linear optimization library (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64')
url="http://ceres-solver.org/"
license=('New BSD license')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake")
depends=("${MINGW_PACKAGE_PREFIX}-eigen3"
         "${MINGW_PACKAGE_PREFIX}-glog"
         "${MINGW_PACKAGE_PREFIX}-suitesparse")
checkdepends=("${MINGW_PACKAGE_PREFIX}-gflags")
source=("${url}${_realname}-${pkgver}.tar.gz"
        "${_realname}-1.13.0-CMakefiles.patch"
        "${_realname}-1.13.0-fix-testcode-failed.patch")
sha256sums=('10298a1d75ca884aa0507d1abb0e0f04800a92871cd400d4c361b56a777a7603'
            '608c8e1ec137d391a4f4367365400214054f44657a17c8231280d731210732c9'
            '8a4a67ddceef26924b10cf3576d47aadf2d789c8e88fa5f18b7ec7bd60ed3561')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Np1 -i "../${_realname}-1.13.0-CMakefiles.patch"
  patch -Np1 -i "../${_realname}-1.13.0-fix-testcode-failed.patch"
}

_build_variant() {
  TAG="$1"         # "" or "-lgpl"
  SUITESPARSE="$2" # ON or OFF (passed to cmake)
  rm -rf "${srcdir}/build-${MINGW_CHOST}${TAG}"
  mkdir "${srcdir}/build-${MINGW_CHOST}${TAG}"
  cd "${srcdir}/build-${MINGW_CHOST}${TAG}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DBUILD_SHARED_LIBS=YES \
    -DSUITESPARSE=${SUITESPARSE} \
    "../${_realname}-${pkgver}"

  make
}

build() {
   echo "Build base variant (with SuiteSparse)"
   _build_variant "" "ON"
   echo "Build LGPL variant (without SuiteSparse)"
   _build_variant "-lgpl" "OFF"
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make test
  cd "${srcdir}/build-${MINGW_CHOST}-lgpl"
  make test
}

#package() {
#  cd "${srcdir}/build-${MINGW_CHOST}"
#  make DESTDIR="${pkgdir}" install
#}


package_ceres-solver() {
  conflicts=("ceres-solver-lgpl")
  pkgdesc="A large scale non-linear optimization library (mingw-w64); built with SuiteSparse (GPL)"
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}

package_ceres-solver-lgpl(){
  conflicts=("ceres-solver")
  provides=("ceres-solver")
  pkgdesc="A large scale non-linear optimization library (mingw-w64); built without SuiteSparse (LGPL)"
  cd "${srcdir}/build-${MINGW_CHOST}-lgpl"
  make DESTDIR="${pkgdir}" install
}


# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
