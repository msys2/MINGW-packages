# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=poppler
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=0.24.5
pkgrel=3
pkgdesc="PDF rendering library based on xpdf 3.0 (mingw-w64)"
arch=('any')
url="http://poppler.freedesktop.org"
license=("GPL")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config" "${_mingw_suff}-glib2" "${_mingw_suff}-qt5")
depends=(
		"${_mingw_suff}-icu"
		"${_mingw_suff}-libtiff"
		"${_mingw_suff}-curl"
		"${_mingw_suff}-openjpeg"
		"${_mingw_suff}-libpng"
		"${_mingw_suff}-freetype"
		"${_mingw_suff}-libjpeg"
		"${_mingw_suff}-cairo"
		"${_mingw_suff}-zlib"
		"${_mingw_suff}-poppler-data")
optdepends=("${_mingw_suff}-glib2: libpoppler-glib"
			"${_mingw_suff}-qt5: libpoppler-qt5"
)
options=('strip' 'staticlibs')
source=("http://poppler.freedesktop.org/poppler-${pkgver}.tar.xz"
		"poppler_configureac-qt5.patch"
		give-cc-to-gir-scanner.mingw.patch)
md5sums=('334f2ac95cc8039f2a57fe63e4a81c69'
         '6ce60b7290a345da8e8d61dbb6cc63c1'
         '727c1b414987f6ee2ba841d9db57a967')

prepare() {
	cd "$srcdir/poppler-$pkgver"
	patch -p1 -i "$srcdir/poppler_configureac-qt5.patch"
	patch -p1 -i "$srcdir/give-cc-to-gir-scanner.mingw.patch"
	sed -i -e '/AC_PATH_XTRA/d' configure.ac
	sed -i "s:AM_CONFIG_HEADER:AC_CONFIG_HEADERS:" configure.ac
	autoreconf -fi
}

build() {
	mkdir -p "${srcdir}/build-${MINGW_CHOST}"
	cd "${srcdir}/build-${MINGW_CHOST}"
	"${srcdir}"/${_realname}-${pkgver}/configure \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--enable-xpdf-headers \
		--enable-zlib \
		--enable-libcurl \
		--disable-gtk-test \
		--enable-utils \
		--disable-gtk-doc-html \
		--with-font-configuration=win32
	make
}

package() {
	cd "${srcdir}/build-${MINGW_CHOST}"
	make DESTDIR="$pkgdir" install
	find "${pkgdir}${MINGW_CHOST}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
}
