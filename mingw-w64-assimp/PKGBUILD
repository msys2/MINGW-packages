# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=assimp
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=5.3.1
pkgrel=2
pkgdesc="Portable Open Source library to import various well-known 3D model formats in an uniform manner (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://www.assimp.org/"
license=('spdx:BSD-3-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-minizip"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-pkgconf")
source=(https://github.com/assimp/assimp/archive/v${pkgver}.tar.gz
        001-static-lib-fixes.patch
        002-fix-pc-file.patch
        003-fix-malformed-header-guard.patch)
sha256sums=('a07666be71afe1ad4bc008c2336b7c688aca391271188eb9108d0c6db1be53f1'
            '4a6b93c0438f97b0bf76708cf6ee73c6140bd34416407eebd9c313c31e491f27'
            '94b7f66ae6f86b0d2e08e8d1a63283466ee28365c6e2ec85aea7afe15f4727ff'
            'a31bb376c34a8043b138b1ba28201ba1e57049a79b3b47dc155a18d3620d9dc1')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 < ${srcdir}/001-static-lib-fixes.patch
  patch -p1 < ${srcdir}/002-fix-pc-file.patch
  patch -p1 < ${srcdir}/003-fix-malformed-header-guard.patch
}

build() {
  mkdir -p ${srcdir}/build-${MSYSTEM}-shared && cd ${srcdir}/build-${MSYSTEM}-shared

  echo ":: Building shared library..."
  # RelWithDebInfo build type is required to match with qt6-3d (#11959)
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DASSIMP_WARNINGS_AS_ERRORS=OFF \
    -DASSIMP_BUILD_ASSIMP_TOOLS=ON \
    -DASSIMP_BUILD_ASSIMP_VIEW=$([[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]] && \
                                  echo "OFF" || echo "ON") \
    -DASSIMP_BUILD_TESTS=OFF \
    -DASSIMP_BUILD_ZLIB=OFF \
    -DASSIMP_IGNORE_GIT_HASH=ON \
    ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake.exe --build .

  mkdir -p ${srcdir}/build-${MSYSTEM}-static && cd ${srcdir}/build-${MSYSTEM}-static

  echo ":: Building static library..."
  # RelWithDebInfo build type is required to match with qt6-3d (#11959)
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DASSIMP_WARNINGS_AS_ERRORS=OFF \
    -DASSIMP_BUILD_ASSIMP_TOOLS=OFF \
    -DASSIMP_BUILD_TESTS=OFF \
    -DASSIMP_BUILD_ZLIB=OFF \
    -DASSIMP_IGNORE_GIT_HASH=ON \
    -DBUILD_SHARED_LIBS=OFF \
    ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake.exe --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}-shared"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install .

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"

  # Fix cmake files
  local PREFIX_WIN=$(cygpath -am ${MINGW_PREFIX})
  find "${pkgdir}${MINGW_PREFIX}/lib/cmake" -type f -name '*.cmake' \
      -exec sed -i -e "s|${PREFIX_WIN}|\$\{_IMPORT_PREFIX\}|g" {} \;

  # Install static library
  cd "${srcdir}/build-${MSYSTEM}-static"
  install -Dm755 lib/libassimp.a "${pkgdir}${MINGW_PREFIX}/lib/libassimp.a"
}
