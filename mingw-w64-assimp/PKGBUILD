# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=assimp
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=5.2.3
pkgrel=1
pkgdesc="Portable Open Source library to import various well-known 3D model formats in an uniform manner (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-minizip"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")
url="https://www.assimp.org/"
source=(${_realname}-${pkgver}.tar.gz::https://github.com/assimp/assimp/archive/v${pkgver}.tar.gz
        001-fix-assimp_cmd-rc-file.patch
        002-llvm-rc-workaround.patch
        003-disable-werror.patch
        004-soversion-with-clang.patch)
options=('staticlibs')
sha256sums=('b20fc41af171f6d8f1f45d4621f18e6934ab7264e71c37cd72fd9832509af2a8'
            '9f12e8760b24e45fa5d308d6d80a65eebe7f499c283e8b54f0d8bb3dab95c5ae'
            '3bdb6483fda22db9126991380a01220785e2e1d3ac1be7e4694096e5f69bcdfd'
            '35f08302caedff9822f7399ef10521b8128d291bb07ffdf8195201005ca387b0'
            '6bd125c59bcf663c0f23d19796ec99a6af01392309936aeb67e9e2169512bc5d')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-fix-assimp_cmd-rc-file.patch
  patch -p1 -i ${srcdir}/003-disable-werror.patch

  if [[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]]; then
    patch -p1 -i "${srcdir}/002-llvm-rc-workaround.patch"
    patch -p1 -i "${srcdir}/004-soversion-with-clang.patch"
  fi
}

build() {
  [[ -d ${srcdir}/build-${MSYSTEM} ]] && rm -rf ${srcdir}/build-${MSYSTEM}
  mkdir -p ${srcdir}/build-${MSYSTEM} && cd ${srcdir}/build-${MSYSTEM}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DASSIMP_BUILD_ASSIMP_TOOLS=ON \
    -DASSIMP_BUILD_ASSIMP_VIEW=$([[ ${MINGW_PACKAGE_PREFIX} == *-clang-aarch64* ]] && \
                                  echo "OFF" || echo "ON") \
    -DASSIMP_BUILD_TESTS=OFF \
    $([[ ${MINGW_PACKAGE_PREFIX} == *-clang-aarch64* ]] || \
      echo "-DDirectX_DXERR_LIBRARY=${MINGW_PREFIX}/lib/libdxerr9.a") \
    ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake.exe --build ./
}

package() {
  cd "${srcdir}//build-${MSYSTEM}"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install .

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
  # Fix cmake files
  local PREFIX_WIN=$(cygpath -am ${MINGW_PREFIX})
  find "${pkgdir}${MINGW_PREFIX}/lib/cmake" -type f -name '*.cmake' \
      -exec sed -i -e "s|${PREFIX_WIN}|\$\{_IMPORT_PREFIX\}|g" {} \;
}
