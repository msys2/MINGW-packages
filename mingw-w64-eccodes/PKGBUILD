# Contributor: Jeongbin Park <pjb7687@gmail.com>

_realname=eccodes
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.33.0
pkgrel=1
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
pkgdesc="ECMWF's GRIB and BUFR encoding/decoding library (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-netcdf"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-jasper"
         "${MINGW_PACKAGE_PREFIX}-openjpeg2"
         "${MINGW_PACKAGE_PREFIX}-libaec"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         $([[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]] || echo "${MINGW_PACKAGE_PREFIX}-gcc-libgfortran"))
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-python-numpy"
             $([[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]] || echo "${MINGW_PACKAGE_PREFIX}-gcc-fortran"))
license=('spdx:Apache-2.0')
url="https://confluence.ecmwf.int/display/ECC/ecCodes+Home"
source=(https://confluence.ecmwf.int/download/attachments/45757960/${_realname}-${pkgver}-Source.tar.gz
        fix-mingw.patch)
noextract=(${_realname}-${pkgver}-Source.tar.gz)
sha256sums=('bdcec8ce63654ec6803400c507f01220a9aa403a45fa6b5bdff7fdcc44fd7daf'
            '34fb5c05384f9aa73825d7500f71470b9f954a1136e0d116a2d54012eec05d3f')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

prepare() {
  echo "Extracting ${_realname}-${pkgver}-Source.tar.gz ..."
  tar xf "${srcdir}"/${_realname}-${pkgver}-Source.tar.gz || true

  cd "${srcdir}"/${_realname}-${pkgver}-Source

  apply_patch_with_msg \
    fix-mingw.patch
}

build() {
  mkdir -p "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  if [[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]]; then
    _extra_config+=("-DENABLE_FORTRAN=OFF")
  else
    _extra_config+=("-DENABLE_FORTRAN=ON")
 fi

  # CMake options copied from:
  #   https://github.com/ecmwf/eccodes/blob/master/.appveyor.yml

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DCMAKE_INSTALL_DATADIR="${MINGW_PREFIX}/share" \
      -DCMAKE_INSTALL_DATAROOTDIR="${MINGW_PREFIX}/share/${_realname}/definitions" \
      "${_extra_config[@]}" \
      -DENABLE_AEC=ON \
      -DENABLE_JPG=ON \
      -DENABLE_JPG_LIBJASPER=ON \
      -DENABLE_JPG_LIBOPENJPEG=ON \
      -DENABLE_NETCDF=ON \
      -DENABLE_PNG=ON \
      -DIEEE_LE=1 \
      -DENABLE_MEMFS=OFF \
      -DENABLE_EXAMPLES=OFF \
      -DENABLE_TESTS=OFF \
      -DPYTHON_EXECUTABLE=${MINGW_PREFIX}/bin/python \
      ../${_realname}-${pkgver}-Source

  "${MINGW_PREFIX}"/bin/cmake --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake --install .

  # Remove full path reference
  local _PREFIX_WIN="$(cygpath -wm ${MINGW_PREFIX})"
  for pcfile in "${pkgdir}${MINGW_PREFIX}"/lib/pkgconfig/*.pc; do
    sed -e "s|${_PREFIX_WIN}|\$\{prefix\}|g" -i "${pcfile}"
    sed -e "s|.dll||g" -i "${pcfile}"
  done

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}-Source/NOTICE \
    "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/NOTICE
  install -Dm644 "${srcdir}"/${_realname}-${pkgver}-Source/LICENSE \
    "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSE
}
