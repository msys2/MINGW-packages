# Maintainer: luau-project <luau.project@gmail.com>

_realname=luasocket
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-lua-${_realname}"
         "${MINGW_PACKAGE_PREFIX}-lua51-${_realname}"
         "${MINGW_PACKAGE_PREFIX}-lua53-${_realname}")
pkgver=3.1.0
pkgrel=1
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/lunarmodules/luasocket'
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-lua"
             "${MINGW_PACKAGE_PREFIX}-lua51"
             "${MINGW_PACKAGE_PREFIX}-lua53"
             "${MINGW_PACKAGE_PREFIX}-pkgconf")
source=("${url}/archive/refs/tags/v${pkgver}.tar.gz"
        "makefile-for-msys2.patch::${url}/pull/435.patch")
sha256sums=('bf033aeb9e62bcaa8d007df68c119c966418e8c9ef7e4f2d7e96bddeca9cca6e'
            '95ef2686ab86f22e7960fd089a6b9b3ded51276ad8c18ac618dfec30f2e7e140')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -Np1 -i "${srcdir}/makefile-for-msys2.patch"
  
  # current Lua
  _options=("PLAT=msys2${MSYSTEM,,}"
            "LUAV=$(pkgconf --variable=V lua)"
            "DEBUG=DEBUG"
            "SOCKET_V=${pkgver}"
            "MYCFLAGS=${CFLAGS}"
            "MYLDFLAGS=${LDFLAGS}")
  
  # Lua 5.1
  cp -r "${srcdir}/${_realname}-${pkgver}" "${srcdir}/${_realname}-${pkgver}-51"

  _options51=("PLAT=msys2${MSYSTEM,,}"
              "LUAV=5.1"
              "DEBUG=DEBUG"
              "SOCKET_V=${pkgver}"
              "MYCFLAGS=${CFLAGS}"
              "MYLDFLAGS=${LDFLAGS}")

  # Lua 5.3
  cp -r "${srcdir}/${_realname}-${pkgver}" "${srcdir}/${_realname}-${pkgver}-53"

  _options53=("PLAT=msys2${MSYSTEM,,}"
              "LUAV=5.3"
              "DEBUG=DEBUG"
              "SOCKET_V=${pkgver}"
              "MYCFLAGS=${CFLAGS}"
              "MYLDFLAGS=${LDFLAGS}")
}

build() {
  # current Lua
  make -C "${srcdir}/${_realname}-${pkgver}/src" "${_options[@]}" all
  
  # Lua 5.1
  make -C "${srcdir}/${_realname}-${pkgver}-51/src" "${_options51[@]}" all

  # Lua 5.3
  make -C "${srcdir}/${_realname}-${pkgver}-53/src" "${_options53[@]}" all
}

package_lua-luasocket() {
  depends=("${MINGW_PACKAGE_PREFIX}-lua")
  pkgdesc="Network support for the Lua language (mingw-w64)"
  
  make -C "${srcdir}/${_realname}-${pkgver}/src" DESTDIR="${pkgdir}" "${_options[@]}" install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/lua-${_realname}/LICENSE"
}

package_lua51-luasocket() {
  depends=("${MINGW_PACKAGE_PREFIX}-lua51")
  pkgdesc="Network support for the Lua 5.1 language (mingw-w64)"

  make -C "${srcdir}/${_realname}-${pkgver}-51/src" DESTDIR="${pkgdir}" "${_options51[@]}" install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}-51/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/lua51-${_realname}/LICENSE"
}

package_lua53-luasocket() {
  depends=("${MINGW_PACKAGE_PREFIX}-lua53")
  pkgdesc="Network support for the Lua 5.3 language (mingw-w64)"

  make -C "${srcdir}/${_realname}-${pkgver}-53/src" DESTDIR="${pkgdir}" "${_options53[@]}" install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}-53/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/lua53-${_realname}/LICENSE"
}

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
