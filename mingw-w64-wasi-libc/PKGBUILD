_realname=wasi-libc
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0+531+ec0effd7
_commit=ec0effd769df5f05b647216578bcf5d3b5449725 # tags/wasi-sdk-30
pkgrel=1
epoch=1
pkgdesc="WASI libc implementation for WebAssembly (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url="https://github.com/WebAssembly/wasi-libc"
msys2_repository_url='https://github.com/WebAssembly/wasi-libc'
msys2_references=(
  'archlinux: wasi-libc'
)
license=('spdx:Apache-2.0 WITH LLVM-exception AND Apache-2.0 AND MIT')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-clang"
  "${MINGW_PACKAGE_PREFIX}-llvm"
  "${MINGW_PACKAGE_PREFIX}-wasm-component-ld"
  "${MINGW_PACKAGE_PREFIX}-wasm-pkg-tools"
  "${MINGW_PACKAGE_PREFIX}-wasm-tools"
  "${MINGW_PACKAGE_PREFIX}-wit-bindgen"
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-ninja"
  'git'
)
options=('!buildflags' '!strip')
source=("git+${url}.git#commit=${_commit}")
sha256sums=('114e76159d9d63085df3cea6510a5b90f4796357d52a64063e805ef9f0ae1f21')

pkgver() {
  cd "${_realname}"

  printf "0+%s+%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  local targets=(
    wasm32-wasi
    wasm32-wasip1
    wasm32-wasip1-threads
    wasm32-wasip2
  ) cmake_options=(
    -Wno-dev
    -S ${_realname}
    -B build-${MSYSTEM}
    -G Ninja
    -D BUILD_SHARED=off
    -D CMAKE_C_COMPILER=clang
    -D CMAKE_AR=llvm-ar
    -D CMAKE_NM=llvm-nm
    -D CMAKE_RANLIB=llvm-ranlib
  )
  if check_option "debug" "n"; then
    cmake_options+=("-DCMAKE_BUILD_TYPE=Release")
  else
    cmake_options+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  for target in "${targets[@]}"; do
    cmake "${cmake_options[@]}" -D TARGET_TRIPLE="${target}"
    cmake --build build-${MSYSTEM}
  done
}

package() {
  install -dm755 "${pkgdir}${MINGW_PREFIX}"/share
  cp -dr --preserve=mode,timestamp build-${MSYSTEM}/sysroot "${pkgdir}${MINGW_PREFIX}"/share/wasi-sysroot
  install -Dm644 ${_realname}/LICENSE* -t "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}
}
