# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gstreamer
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.4.5
pkgrel=1
pkgdesc="GStreamer Multimedia Framework (mingw-w64)"
arch=('any')
url="http://gstreamer.freedesktop.org/"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-pkg-config"
            "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
            "intltool"
            "gtk-doc"
            "flex"
            "bison"
            )
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
        "${MINGW_PACKAGE_PREFIX}-libxml2"
        "${MINGW_PACKAGE_PREFIX}-glib2"
        "${MINGW_PACKAGE_PREFIX}-gettext"
        "${MINGW_PACKAGE_PREFIX}-gmp"
        "${MINGW_PACKAGE_PREFIX}-gsl")
options=(!libtool strip staticlibs)
source=("${url}/src/gstreamer/gstreamer-${pkgver}.tar.xz"
        'give-cc-to-gir-scanner.mingw.patch'
        '0004-fix-X-detection.mingw.patch')
md5sums=('88a9289c64a4950ebb4f544980234289'
         '59215d83b4c39aac03848cc9b4eb3f25'
         'eb0a27e74755e4ee1c5259c8c4ab40e8')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/give-cc-to-gir-scanner.mingw.patch
  patch -p0 -i ${srcdir}/0004-fix-X-detection.mingw.patch
  autoreconf -fi
}

build() {
  local -a extra_config
  if check_option "debug" "y"; then
    extra_config+=( --enable-debug=yes )
  else
    extra_config+=( --enable-debug=no )
  fi

  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  cp -r "${srcdir}/${_realname}-${pkgver}" "${srcdir}/build-${MINGW_CHOST}"
  #mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  export PKG_CONFIG_EXPAND_PATHS=1
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-static \
    --enable-shared \
    --enable-introspection \
    --disable-poisoning \
    --disable-gtk-doc \
    --enable-docbook \
    --enable-check \
    --disable-silent-rules \
    "${extra_config[@]}"
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="$pkgdir" install
  find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
  rm -f ${pkgdir}${MINGW_PREFIX}/lib/gstreamer*/*.a
}
