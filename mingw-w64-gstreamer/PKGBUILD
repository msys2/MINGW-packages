# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gstreamer
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.2.4
pkgrel=1
pkgdesc="GStreamer Multimedia Framework (mingw-w64)"
arch=('any')
url="http://gstreamer.freedesktop.org/"
license=("LGPL")
makedepends=("${_mingw_suff}-gcc"
		"${_mingw_suff}-pkg-config"
		"${_mingw_suff}-gobject-introspection"
		"intltool"
		"gtk-doc"
		"flex"
		"bison")
depends=(
		"${_mingw_suff}-gcc-libs"
		"${_mingw_suff}-libxml2"
		"${_mingw_suff}-glib2"
		"${_mingw_suff}-gettext")
options=(!libtool strip staticlibs)
source=("${url}/src/gstreamer/gstreamer-${pkgver}.tar.xz"
		give-cc-to-gir-scanner.mingw.patch)
md5sums=('74d8f674187a5f0ddf38da594c3998c3'
         '59215d83b4c39aac03848cc9b4eb3f25')

prepare() {
	cd ${srcdir}/${_realname}-${pkgver}
	patch -p1 -i ${srcdir}/give-cc-to-gir-scanner.mingw.patch

	autoreconf -fi
}

build() {
	mkdir -p "${srcdir}/build-${MINGW_CHOST}"
	[[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
	cp -r "${srcdir}/${_realname}-${pkgver}" "${srcdir}/build-${MINGW_CHOST}"
	cd "${srcdir}/build-${MINGW_CHOST}"
	./configure \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--disable-static \
		--enable-shared \
		--enable-introspection \
		--enable-silent-rules \
		--disable-poisoning \
		--disable-gtk-doc \
		--enable-docbook \
		--enable-check \
		--enable-debug
	make
}

package() {
	cd "${srcdir}/build-${MINGW_CHOST}"
	make -j1 DESTDIR="$pkgdir" install
	find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
	rm -f ${pkgdir}${MINGW_PREFIX}/lib/gstreamer*/*.a
}
