# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=directx-shader-compiler
_pkgname=DirectXShaderCompiler
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=1.7.2212.1
pkgrel=1
pkgdesc='A compiler for HLSL to DXIL (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='https://github.com/microsoft/DirectXShaderCompiler'
license=('spdx:NCSA' 'spdx:MS-PL' 'spdx:LLVM-exceptions')
makedepends=("${MINGW_PACKAGE_PREFIX}-clang"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-python"
             "git")
#depends=("${MINGW_PACKAGE_PREFIX}-dxil-redist")
source=("https://github.com/microsoft/DirectXShaderCompiler/archive/refs/tags/v${pkgver}.tar.gz"
        "001-cmake-mingw-support.patch"
        "002-headers-update.patch"
        "003-lib-sources-fixes.patch"
        "004-tools-sources-fixes.patch"
        "005-avoid-nominmax-redefines.patch")
sha256sums=('f345a555aa02d16cf2ac6e6e72bf340f20626849ece6ba3700ae17a1ca6fc6b7'
            '864aefb277012fdfd3d4a0dc99fe108c7cbf54a5ed488b9edffa0aeae6102b87'
            'd41841ca2e55b5b9c1a633f2d187d129714a5c3f58bf908216e0de7aa9d57340'
            '3f0a0de21ae4b541ae00e6496ab94bed0ca3fa125d357b650ac5bb10b84f5ec0'
            '2f04d177a5ddfea40a5243520ee37febbeeb1644b7da793733e8064f1dbcabcc'
            '160bf8658e8b6ae63dd213fe5c68b78c561c14d070a1ad7893283c37f516e66f')

prepare() {
  cd ${_pkgname}-${pkgver}

  patch -p1 -i ../001-cmake-mingw-support.patch
  patch -p1 -i ../002-headers-update.patch
  patch -p1 -i ../003-lib-sources-fixes.patch
  patch -p1 -i ../004-tools-sources-fixes.patch
  patch -p1 -i ../005-avoid-nominmax-redefines.patch
}

build() {
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -Wno-dev \
    -GNinja \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_C_COMPILER=clang \
    -DCMAKE_CXX_COMPILER=clang++ \
    -C ../${_pkgname}-${pkgver}/cmake/caches/PredefinedParams.cmake \
    ../${_pkgname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  cd build-${MSYSTEM}

  install -Dm755 "bin/dxc.exe" "${pkgdir}${MINGW_PREFIX}/bin/dxc.exe"
  install -Dm755 "bin/dxcompiler.dll" "${pkgdir}${MINGW_PREFIX}/bin/dxcompiler.dll"

  install -Dm755 "lib/libdxcompiler.dll.a" "${pkgdir}${MINGW_PREFIX}/lib/libdxcompiler.dll.a"

  install -Dm644 "${srcdir}/${_pkgname}-${pkgver}/include/dxc/dxcapi.h" "${pkgdir}${MINGW_PREFIX}/include/dxcapi.h"
  install -Dm644 "${srcdir}/${_pkgname}-${pkgver}/include/dxc/dxcerrors.h" "${pkgdir}${MINGW_PREFIX}/include/dxcerrors.h"
  install -Dm644 "${srcdir}/${_pkgname}-${pkgver}/include/dxc/dxcisense.h" "${pkgdir}${MINGW_PREFIX}/include/dxcisense.h"

  install -Dm644 "${srcdir}/${_pkgname}-${pkgver}/LICENSE.TXT" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.TXT"
}
