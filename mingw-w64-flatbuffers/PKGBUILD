# Maintainer: Martell Malone <martellmalone@gmail.com>

_realname=flatbuffers
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.1.0
pkgrel=1
pkgdesc='Memory Efficient Serialization Library (mingw-w64)'
arch=('any')
url='https://google.github.io/flatbuffers/'
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-libsystre")
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/google/${_realname}/archive/v${pkgver}.tar.gz"
         "0001-Add-a-libflatbuffers-for-other-projects-to-use.patch")
sha256sums=('6ac776d86e1c9ac84497c51aeac5ddc79c9596166abd937dea073e1cc574a673'
            '84823326981c764fe5c554286e4e7396ee9366e859a6eadede8573d200195342')
options=('strip' '!libtool' 'staticlibs')

prepare() {
  cd $srcdir/${_realname}-${pkgver}
  patch -p1 < $srcdir/0001-Add-a-libflatbuffers-for-other-projects-to-use.patch
}

build() {
  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    ../${_realname}-${pkgver}

  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make install DESTDIR="${pkgdir}"
}
