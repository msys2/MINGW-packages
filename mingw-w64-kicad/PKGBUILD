# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=kicad
pkgbase=mingw-w64-${_realname}
_sub=("footprints" "symbols" "templates" "packages3D")
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}"
	"${MINGW_PACKAGE_PREFIX}-${_realname}-meta"
	"${MINGW_PACKAGE_PREFIX}-${_realname}-footprints"
	"${MINGW_PACKAGE_PREFIX}-${_realname}-symbols"
	"${MINGW_PACKAGE_PREFIX}-${_realname}-templates"
	"${MINGW_PACKAGE_PREFIX}-${_realname}-packages3D")
pkgver=5.1.0
pkgrel=2
pkgdesc="Software for the creation of electronic schematic diagrams and printed circuit board artwork (mingw-w64)"
arch=("any")
url="http://www.kicad-pcb.org"
license=("GPLv3+")
depends=("${MINGW_PACKAGE_PREFIX}-boost"
	"${MINGW_PACKAGE_PREFIX}-curl"
	"${MINGW_PACKAGE_PREFIX}-glew"
	"${MINGW_PACKAGE_PREFIX}-glm"
	"${MINGW_PACKAGE_PREFIX}-ngspice"
	"${MINGW_PACKAGE_PREFIX}-python2"
	"${MINGW_PACKAGE_PREFIX}-wxPython"
	"${MINGW_PACKAGE_PREFIX}-wxWidgets"
	"${MINGW_PACKAGE_PREFIX}-openssl"
	"${MINGW_PACKAGE_PREFIX}-freeglut"
	"${MINGW_PACKAGE_PREFIX}-zlib")
 
makedepends=("git"
	"${MINGW_PACKAGE_PREFIX}-cmake"
	"${MINGW_PACKAGE_PREFIX}-gcc"
	"${MINGW_PACKAGE_PREFIX}-doxygen"
	"${MINGW_PACKAGE_PREFIX}-pkg-config"
	"${MINGW_PACKAGE_PREFIX}-swig"
	"${MINGW_PACKAGE_PREFIX}-cairo"
	"${MINGW_PACKAGE_PREFIX}-oce")
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/KiCad/kicad-source-mirror/archive/${pkgver}.tar.gz"
	"${_realname}-i18n-${pkgver}.tar.gz"::"https://github.com/KiCad/kicad-i18n/archive/${pkgver}.tar.gz"
	"${_realname}-footprints-${pkgver}.tar.gz"::"https://github.com/KiCad/kicad-footprints/archive/${pkgver}.tar.gz"
	"${_realname}-symbols-${pkgver}.tar.gz"::"https://github.com/KiCad/kicad-symbols/archive/${pkgver}.tar.gz"
	"${_realname}-templates-${pkgver}.tar.gz"::"https://github.com/KiCad/kicad-templates/archive/${pkgver}.tar.gz"
	"${_realname}-packages3D-${pkgver}.tar.gz"::"https://github.com/KiCad/kicad-packages3D/archive/${pkgver}.tar.gz")
sha256sums=('64b28735a666f060da5d49ac73bd653d3db5a54dd40e3c13d8fa34ffa6c1e682'
            'bb022b905c52e9be87e5ef52020194001ddf7a89d773b202f2b940a6e86392d7'
            '965cb3902df557a32ecf4101c37af3e43291f2485f42a23392797da837523e23'
            '7ca872eac9fb4117b50bcbbade59c96bd4ebad4d765baac1b95e46d13c25cb71'
            '36c4ac1ea78333cd92dc82b28f57066a9888801dc7d803d822e22b5b2a924516'
            '5ea2a0eb8cbe73bf069c96b8c823ef2af4c430e87287c7052aa32f24a9d2c7e6')

prepare() {
  cd "${srcdir}"
  
  [[ -d build-${MINGW_CHOST} ]] && rm -r build-${MINGW_CHOST}
  [[ -d build-i18n ]] && rm -r build-i18n
  for _i in ${_sub[@]}; do
    [[ -d build-${_i} ]] && rm -r build-${_i}
  done
}

build_kicad() {
  cd "${srcdir}"
  # Configure and build KiCad.
  mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=;DCMAKE_PREFIX_PATH=;DEFAULT_INSTALL_PATH=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_PREFIX_PATH=${MINGW_PREFIX} \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DDEFAULT_INSTALL_PATH=${MINGW_PREFIX} \
    -DOPENSSL_ROOT_DIR=${MINGW_PREFIX} \
    -DKICAD_SCRIPTING=ON \
    -DKICAD_SCRIPTING_MODULES=ON \
    -DKICAD_SCRIPTING_WXPYTHON=ON \
    -DPYTHON_EXECUTABLE=${MINGW_PREFIX}/bin/python2.exe \
    ../${_realname}-source-mirror-${pkgver}

  make

  cd "${srcdir}"
  # Configure the translationn installation build.
  mkdir -p build-i18n && cd build-i18n
  
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G "MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    ../${_realname}-i18n-${pkgver}
}

build_sub() {
  cd "${srcdir}"
  mkdir -p build-${1} && cd build-${1}
  
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G "MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    ../${_realname}-${1}-${pkgver}
}

_doc=("ca" "de" "en" "es" "fr" "id" "it" "ja" "nl" "pl" "ru" "zh")
for _d in ${_doc[@]}; do _doc_pkg+=("\"${MINGW_PACKAGE_PREFIX}-${_realname}-doc-${_d}: Documentation for KiCad (mingw-w64)\""); done
for _s in ${_sub[@]}; do _sub_pkg+=("${MINGW_PACKAGE_PREFIX}-${_realname}-${_s}"); done

eval "package_${MINGW_PACKAGE_PREFIX}-${_realname}() {

  optdepends+=("${_doc_pkg[@]}" "\${_sub_pkg[@]}")
  conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
  replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
  
  build_kicad
  
  cd "\"\${srcdir}\"/build-${MINGW_CHOST}"
  make DESTDIR="\"\${pkgdir}\"" install

  cd "\"\${srcdir}\"/build-i18n"
  make DESTDIR="\"\${pkgdir}\"" install
}"

eval "package_${MINGW_PACKAGE_PREFIX}-${_realname}-meta() {
  depends=("${MINGW_PACKAGE_PREFIX}-${_realname}"
           "${_sub_pkg[@]}")
}"

for _s in ${_sub[@]}; do
  eval "package_${MINGW_PACKAGE_PREFIX}-${_realname}-${_s}() {
    options=('!strip')
    conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}-${_s}-git")
    replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}-${_s}-git")
    build_sub ${_s}
    cd "\"\${srcdir}\"/build-${_s}"
    make DESTDIR="\"\${pkgdir}\"" install
  }"
done
