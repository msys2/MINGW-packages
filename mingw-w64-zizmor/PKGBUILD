# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=zizmor
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.16.0
pkgrel=1
pkgdesc="A static analysis tool for GitHub Actions (mingw-w64)"
url='https://github.com/zizmorcore/zizmor'
msys2_repository_url='https://github.com/zizmorcore/zizmor'
msys2_changelog_url='https://docs.zizmor.sh/release-notes/'
msys2_references=(
  'anitya: 378497'
  'archlinux: zizmor'
  'purl: pkg:cargo/zizmor'
)
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
license=('spdx:MIT')
depends=(
  "${MINGW_PACKAGE_PREFIX}-cc-libs"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-rust"
)
source=("https://github.com/zizmorcore/${_realname}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('8c0f9ef81c6d5b12d2c85a5ede787c8588ed40100f8e1826251934f18f5e0755')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${_realname}-${pkgver}"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${_realname}-${pkgver}"
  cargo build --frozen --release
}

check() {
  cd "${_realname}-${pkgver}"
  cargo test --frozen
}

package() {
  cd "${_realname}-${pkgver}"
  install -Dm 755 -t "${pkgdir}${MINGW_PREFIX}/bin" \
    target/release/zizmor.exe

  install -Dm 644 LICENSE -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
}
