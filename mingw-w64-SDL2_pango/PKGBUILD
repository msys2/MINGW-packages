# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=SDL2_pango
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.1.5
pkgrel=2
pkgdesc="SDL2 port of SDL_Pango (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/markuskimius/SDL2_Pango'
msys2_repository_url='https://github.com/markuskimius/SDL2_Pango'
license=('spdx:LGPL-2.1-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-SDL2"
         "${MINGW_PACKAGE_PREFIX}-freetype"
         "${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-pango")
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-gettext-tools"
             "${MINGW_PACKAGE_PREFIX}-pkgconf")
source=("https://github.com/markuskimius/SDL2_Pango/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "001-include-freetype-in-pc.patch")
sha256sums=('3b3fe6008445feb1fca61b17b9d950d688e49dcca60dbbf8667c0f980ddfc563'
            '5f76d83d3fc8baa3b903999ca3b5df184b0bcc682f46b6e6f1f0c89a04dc65f0')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -Np1 -i "${srcdir}/001-include-freetype-in-pc.patch"

  autoreconf -fiv
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  export lt_cv_deplibs_check_method='pass_all'
  ../"${_realname}-${pkgver}"/configure \
    --prefix="${MINGW_PREFIX}" \
    --enable-shared \
    --enable-static

  make
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make install DESTDIR="${pkgdir}"

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
