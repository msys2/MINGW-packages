# Maintainer: Johannes Schindelin <johannes.schindelin@gmx.de>

_realname=msmtp
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.8.27
pkgrel=1
pkgdesc="An SMTP client (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url="https://marlam.de/msmtp/"
msys2_repository_url="https://github.com/marlam/msmtp"
msys2_references=(
  'archlinux: msmtp'
  "cpe: cpe:2.3:a:marlam:msmtp"
  "cpe: cpe:2.3:a:martin_lambers:msmtp"
)
license=('spdx:GPL-3.0-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-gettext-tools"
             "${MINGW_PACKAGE_PREFIX}-pkgconf")
depends=("${MINGW_PACKAGE_PREFIX}-gettext-runtime"
         "${MINGW_PACKAGE_PREFIX}-gnutls"
         "${MINGW_PACKAGE_PREFIX}-gsasl"
         "${MINGW_PACKAGE_PREFIX}-libsecret"
         "${MINGW_PACKAGE_PREFIX}-libidn2"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread")
source=(https://marlam.de/msmtp/releases/${_realname}-${pkgver}.tar.xz{,.sig}
        https://github.com/marlam/msmtp/commit/09f413a6d053b160cd62441c2e4f07ca78352241.patch
        001-config-file-location.patch
        002-net-socket-fix.patch)
sha256sums=('94030580a63a747faa0a3b9b1b264ae355aad33a4d94b832bfeb5b21633c965e'
            'SKIP'
            '67030d935ebbc420cdc96dafe50bedf9633079d488a13381fff2fedf059dfc0c'
            '490aa84627c8696ccbcca7257ca7d74cdc22e96953aca654d938bf325f1e3408'
            '605c24bc891a6c1da68500067d4055ef8ddc41b8880489a677572026defedbc2')
validpgpkeys=('2F61B4828BBA779AECB3F32703A2A4AB1E32FD34') # Martin Lambers <marlam@marlam.de>

prepare () {
  cd ${_realname}-${pkgver}
  patch -p1 < ../001-config-file-location.patch
  patch -p1 < ../002-net-socket-fix.patch

  # backport NLS fixes from upstream
  patch -p1 < ../09f413a6d053b160cd62441c2e4f07ca78352241.patch

  autoreconf -fi
}

build() {
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  LIBS=" -lpthread -lshlwapi" \
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --with-libgsasl

  make
}

package() {
  cd build-${MSYSTEM}
  make DESTDIR="${pkgdir}" install

  install -Dm0644 ${srcdir}/${_realname}-${pkgver}/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
