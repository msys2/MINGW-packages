# Maintainer: Johannes Schindelin <johannes.schindelin@gmx.de>

_realname=msmtp
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.8.22
pkgrel=1
pkgdesc="An SMTP client (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=('spdx:GPL-3.0-or-later')
url="https://msmtp.sourceforge.io/"
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools" "${MINGW_PACKAGE_PREFIX}-cc")
depends=("${MINGW_PACKAGE_PREFIX}-gettext"
         "${MINGW_PACKAGE_PREFIX}-gnutls"
         "${MINGW_PACKAGE_PREFIX}-gsasl"
         "${MINGW_PACKAGE_PREFIX}-libffi"
         "${MINGW_PACKAGE_PREFIX}-libidn"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread")
options=('strip')
source=(https://marlam.de/msmtp/releases/${_realname}-${pkgver}.tar.xz{,.sig}
        '0001-include-winsock2.patch')
sha256sums=('1b04206286a5b82622335e4eb09e17074368b7288e53d134543cbbc6b79ea3e7'
            'SKIP'
            '3729fc8d9a11370324561a76cafe09035391e48c9a55876d1aaa5077944ebf8d')
validpgpkeys=('2F61B4828BBA779AECB3F32703A2A4AB1E32FD34') # Martin Lambers <marlam@marlam.de>

prepare () {
  cd ${srcdir}/${_realname}-${pkgver}

  patch -p1 -i "${srcdir}/0001-include-winsock2.patch"
  #autoreconf -fiv
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  LDFLAGS+=" -lpthread" \
  ../${_realname}-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX}

  sed -i 's/-R\/mingw..\/lib//' src/Makefile
  touch ${srcdir}/${_realname}-${pkgver}/src/*.c
  make
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
  install -Dm0644 ${srcdir}/${_realname}-${pkgver}/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
