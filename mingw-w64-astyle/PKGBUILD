# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=astyle
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.05.1
pkgrel=2
pkgdesc='A free, fast and small automatic formatter for C, C++, C#, and Java source code (mingw-w64)'
arch=('any')
license=('LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
source=("https://sourceforge.net/projects/${_realname}/files/${_realname}/${_realname}%202.05.1/${_realname}_${pkgver}_linux.tar.gz"
        "mingw-w64-fix-install.patch")
sha256sums=('fbdfc6f1966a972d19a215927266c76d4183eee235ed1e2bd7ec551c2a270eac'
            '6ab64dbc8608d54460e6948e7da8203ab8aa55035e7e34f91a3178ec2bdccca0')

prepare() {
  cd "${srcdir}"/${_realname}
  patch -p1 -i "${srcdir}"/mingw-w64-fix-install.patch
}

build() {
  [ -d "${srcdir}"/build-${CARCH} ] && rm -rf "${srcdir}"/build-${CARCH}
  mv "${srcdir}"/${_realname} "${srcdir}"/build-${CARCH}
  cd "${srcdir}"/build-${CARCH}/build/gcc
  make
}

package() {
  cd "${srcdir}"/build-${CARCH}/build/gcc
  make install prefix=${pkgdir}${MINGW_PREFIX}
}
