# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=astyle
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.0.1
pkgrel=1
pkgdesc='A free, fast and small automatic formatter for C, C++, C#, and Java source code (mingw-w64)'
arch=('any')
license=('LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
source=("https://sourceforge.net/projects/${_realname}/files/${_realname}/${_realname}%20${pkgver}/${_realname}_${pkgver}_linux.tar.gz"
        "mingw-w64-fix-install.patch")
sha256sums=('6c3ab029e0e4a75e2e603d449014374aa8269218fdd03a4aaa46ab743b1912fd'
            '5723492b717a588cfec0aa094f2754587e5b28effff2e0b717d59b0f8641e78a')

prepare() {
  cd "${srcdir}"/${_realname}
  patch -p1 -i "${srcdir}"/mingw-w64-fix-install.patch
}

build() {
  [ -d "${srcdir}"/build-${CARCH} ] && rm -rf "${srcdir}"/build-${CARCH}
  mv "${srcdir}"/${_realname} "${srcdir}"/build-${CARCH}
  cd "${srcdir}"/build-${CARCH}/build/gcc
  make
}

package() {
  cd "${srcdir}"/build-${CARCH}/build/gcc
  make install prefix=${pkgdir}${MINGW_PREFIX}
}
