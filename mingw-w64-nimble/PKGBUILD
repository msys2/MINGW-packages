# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=nimble
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.9.0
pkgrel=1
pkgdesc='Package manager for the Nim programming language (mingw-w64)'
arch=('any')
url='https://github.com/nim-lang/nimble'
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-nim")
source=("${_realname}-${pkgver}.tar.gz"::https://github.com/nim-lang/nimble/archive/v${pkgver}.tar.gz)
sha256sums=('d02cc0b1787fa30e6d88a1ee7aa067c1555b7763d2716742d1d10132df550456')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  nim c -d:release src/nimble.nim
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  install -Dm 755 "src/nimble.exe" -t "${pkgdir}${MINGW_PREFIX}/bin/"
  install -Dm 644  "license.txt" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
