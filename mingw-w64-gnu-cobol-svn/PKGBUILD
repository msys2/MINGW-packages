# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: JÃ¼rgen Pfeifer <juergen@familiepfeifer.de>

_realname=gnu-cobol
pkgbase=mingw-w64-${_realname}-svn
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}-svn
_base_ver=2.0
pkgver=2.0.r1454
pkgrel=1
pkgdesc="GNU Cobol is a free, modern COBOL compiler (mingw-w64)"
arch=('any')
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
url="https://sourceforge.net/projects/open-cobol/"
options=('strip')
license=('GPL' 'LGPL')
makedepends=("subversion"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-libiconv")
depends=("${MINGW_PACKAGE_PREFIX}-db"
         "${MINGW_PACKAGE_PREFIX}-gmp"
         "${MINGW_PACKAGE_PREFIX}-ncurses")
source=("${_realname}::svn+https://svn.code.sf.net/p/open-cobol/code/branches/gnu-cobol-2.0"
        001-fix-typo.patch)
sha256sums=('SKIP'
            '791061da46371ed5465d8bd35b74cc695c113c8a10116772ac8cbd7c2911e05e')

pkgver() {
  cd "${srcdir}/${_realname}"
  local ver="$(svnversion)"
  printf "%s.r%s" "${_base_ver}" "${ver//[[:alpha:]]}"
}

prepare() {
  cd ${srcdir}/${_realname}
  patch -p1 -i ${srcdir}/001-fix-typo.patch
  autoreconf -fi
}

build() {
  export lt_cv_deplibs_check_method='pass_all' # libncurses
  [[ -d ${srcdir}/build-${CARCH} ]] && rm -rf ${srcdir}/build-${CARCH}
  cp -rf ${_realname} build-${CARCH}
  # mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}
  cd ${srcdir}/build-${CARCH}
  ./configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --infodir=${MINGW_PREFIX}/share/info \
    --mandir=${MINGW_PREFIX}/share/man \
    --enable-shared \
    --enable-static \
    --disable-rpath

  make -j1
}

package() {
  cd ${srcdir}/build-${CARCH}
  make DESTDIR="${pkgdir}" install

  mkdir -p ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}
  cp -pf ${srcdir}/${_realname}/COPYING* \
    ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}
}
