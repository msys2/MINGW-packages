# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: JÃ¼rgen Pfeifer <juergen@familiepfeifer.de>

_realname=gnu-cobol
_mingw_suff=mingw-w64-${CARCH}
pkgname=${_mingw_suff}-${_realname}-svn
pkgrel=1
pkgver=r241
pkgdesc="GNU Cobol is a free, modern COBOL compiler"
arch=('any')
provides=("${_mingw_suff}-${_realname}")
conflicts=("${_mingw_suff}-${_realname}")
url="http://sourceforge.net/projects/open-cobol/"
options=('strip')
license=('GPL' 'LGPL')
groups=("${_mingw_suff}")
makedepends=("subversion"
		"${_mingw_suff}-gcc"
		"${_mingw_suff}-libiconv")
depends=("${_mingw_suff}-db" 
		"${_mingw_suff}-gmp" 
		"${_mingw_suff}-ncurses")
source=("$_realname::svn+http://svn.code.sf.net/p/open-cobol/code/trunk"
		fix-including-headers.patch
		fix-building-cob-config.patch)
md5sums=('SKIP'
         'c567c1529b3793395d2d928dfa1b6403'
         'ee0dfbba2e0c5f6b7a681f86e93a7e74')

pkgver() {
	cd "$srcdir/$_realname"
	local ver="$(svnversion)"
	printf "r%s" "${ver//[[:alpha:]]}"
}

prepare() {
	cd ${srcdir}/${_realname}
	patch -p1 -i ${srcdir}/fix-including-headers.patch
	patch -p1 -i ${srcdir}/fix-building-cob-config.patch
	cd ${_realname}
	autoreconf -fi
}

build() {
	export lt_cv_deplibs_check_method='pass_all' # libncurses
	[[ -d ${srcdir}/build-${CARCH} ]] && rm -rf ${srcdir}/build-${CARCH}
	mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}
	../$_realname/$_realname/configure \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--target=${MINGW_CHOST} \
		--prefix=${MINGW_PREFIX} \
		--infodir=${MINGW_PREFIX}/share/info \
		--mandir=${MINGW_PREFIX}/share/man \
		--enable-shared \
		--enable-static \
		--disable-rpath
	make
}

package() {
	cd ${srcdir}/build-${CARCH}
	make DESTDIR="$pkgdir" install
	
	mkdir -p ${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname  
	cp -pf ${srcdir}/${_realname}/${_realname}/COPYING* \
		${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname
}
