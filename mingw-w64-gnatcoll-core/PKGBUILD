_realname=gnatcoll-core
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.0
pkgrel=1

pkgdesc="The GNAT Components Collection - Core packages (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64')
replaces=("${MINGW_PACKAGE_PREFIX}-gnatcoll")
conflicts=("${MINGW_PACKAGE_PREFIX}-gnatcoll")
url="https://github.com/AdaCore/gnatcoll-core"
msys2_references=(
  'aur: gnatcoll-core'
)
license=('spdx:GPL-3.0-or-later WITH GCC-exception-3.1')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc-ada"
             "${MINGW_PACKAGE_PREFIX}-gprbuild")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gcc-ada"
         "${MINGW_PACKAGE_PREFIX}-libgpr")
source=("https://github.com/AdaCore/gnatcoll-core/archive/v${pkgver}/gnatcoll-core.tar.gz")
sha256sums=('0b3ef5687d9d7be115d90143a3b9da2c30779ea8bfe0a6226bedc7715d28fd8d')
options=('strip')

prepare() {
  echo "${pkgver}" > "${srcdir}/gnatcoll-core-${pkgver}/version_information"
}

build() {
  cd "${srcdir}/gnatcoll-core-${pkgver}"

  make BUILD=PROD PROCESSORS="$(nproc)" ENABLE_SHARED=yes
}

package() {

  cd "${srcdir}/gnatcoll-core-${pkgver}"

  make install BUILD=PROD PROCESSORS="$(nproc)" ENABLE_SHARED=yes prefix="${pkgdir}${MINGW_PREFIX}"

  # Copy License Files
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" \
    "${srcdir}/gnatcoll-core-${pkgver}"/COPYING*
}
