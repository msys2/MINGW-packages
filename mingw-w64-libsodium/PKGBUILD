# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libsodium
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.0.16
pkgrel=1
pkgdesc="P(ortable|ackageable) NaCl-based crypto library (mingw-w64)"
arch=("any")
url="https://github.com/jedisct1/libsodium"
license=('custom:ISC')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-minisign")
source=("https://download.libsodium.org/libsodium/releases/${_realname}-${pkgver}.tar.gz"{,.minisig})
validpgpkeys=('54A2B8892CC3D6A597B92B6C210627AABA709FE1') #Frank Denis (Jedi/Sector One) <pgp@pureftpd.org>
sha256sums=('eeadc7e1e1bcef09680fb4837d448fbdf57224978f865ac1c16745868fbd0533'
            '0a5ec3d5a44c07b33ad337c4690827459be5e5500dee612101a6a0b333051849')
_validminisignkey='RWQf6LRCGA9i53mlYecO4IzT51TGPpvWucNSCh1CBM0QTaLn73Y7GFO3'

prepare() {
  minisign -Vm ${_realname}-$pkgver.tar.gz -P $_validminisignkey
}

build() {
  rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --enable-static

  make
}

check() {
  cd build-${MINGW_CHOST}
  make -k check
}

package() {
  cd build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
