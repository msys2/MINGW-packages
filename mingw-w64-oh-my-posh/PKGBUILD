# Maintainer: wszqkzqk <wszqkzqk@qq.com>
_realname=oh-my-posh
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=15.3.0
pkgrel=1
pkgdesc="A prompt theme engine for any shell. (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://github.com/JanDeDobbeleer/oh-my-posh"
license=('MIT')
makedepends=(   "${MINGW_PACKAGE_PREFIX}-go"
                "${MINGW_PACKAGE_PREFIX}-cc")
sha256sums=('6672bf32fd86ca0b19cb62af4ed3bbd9bed523ef5c55a63db44665ffa00d4066')
source=("$_realname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
build() {
    export GOOS=windows
    export GOROOT=${MINGW_PREFIX}/lib/go
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

    cd "$_realname-$pkgver/src"
    go build
}
package() {
    cd "$_realname-$pkgver/src"
    install -Dm 755 ./src.exe "${pkgdir}${MINGW_PREFIX}/bin/oh-my-posh.exe"
    install -Dm 644 "../COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
    install -d "${pkgdir}${MINGW_PREFIX}/share/oh-my-posh/themes"
    install -m 644 ../themes/* -t "${pkgdir}${MINGW_PREFIX}/share/oh-my-posh/themes"
}
