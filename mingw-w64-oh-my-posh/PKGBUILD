# Maintainer: wszqkzqk <wszqkzqk@qq.com>
_realname=oh-my-posh
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=12.13.2
pkgrel=1
pkgdesc="A prompt theme engine for any shell. (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://github.com/JanDeDobbeleer/oh-my-posh"
license=('MIT')
makedepends=(   "${MINGW_PACKAGE_PREFIX}-go"
                "${MINGW_PACKAGE_PREFIX}-cc")
sha256sums=('71048f82b166d88ac88ce3ee8450dce67698d548df460f9333073c20f4832954')
source=("$_realname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
build() {
    export GOOS=windows
    export GOROOT=${MINGW_PREFIX}/lib/go
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

    cd "$_realname-$pkgver/src"
    go build
}
package() {
    cd "$_realname-$pkgver/src"
    install -Dm 755 ./oh-my-posh -t "${pkgdir}${MINGW_PREFIX}/bin/"
    install -Dm 644 "../COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
    install -d "${pkgdir}${MINGW_PREFIX}/share/oh-my-posh/themes"
    install -m 644 ../themes/* -t "${pkgdir}${MINGW_PREFIX}/share/oh-my-posh/themes"
}
