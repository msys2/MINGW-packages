# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=fribidi
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.0.12
pkgrel=2
pkgdesc="A Free Implementation of the Unicode Bidirectional Algorithm (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
license=('LGPL')
url="https://github.com/fribidi/fribidi/"
depends=()
options=('strip' '!libtool' 'staticlibs')
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-cc")
source=(${_realname}-${pkgver}.tar.gz::"https://github.com/fribidi/fribidi/archive/v${pkgver}.tar.gz"
        fribidi-add-cflags-private.patch
        fribidi-disable-doc.patch)
sha256sums=('2e9e859876571f03567ac91e5ed3b5308791f31cda083408c2b60fa1fe00a39d'
            '9af5b2c25387f9146b56a79b7cb8ab4c138fe40ad69a4283c069b008b786575b'
            '3adfbdee6766245b5cb6632805af571069dd26b2a2a238e9fb4e98982d087e32')
            
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  apply_patch_with_msg \
   fribidi-add-cflags-private.patch \
   fribidi-disable-doc.patch
   
  autoreconf -fi
  
}

build() {
  mkdir -p build-${MSYSTEM}-static
  cd build-${MSYSTEM}-static

  MSYS2_ARG_CONV_EXCL="--prefix=" \
  ../${_realname}-${pkgver}/configure \
            --prefix=${MINGW_PREFIX} \
            --build=${MINGW_CHOST} \
            --host=${MINGW_CHOST} \
            --target=${MINGW_CHOST} \
            --enable-static=yes \
            --enable-shared=no \
            --enable-debug=no
 
  make

  cd ..
  mkdir -p build-${MSYSTEM}-shared
  cd build-${MSYSTEM}-shared

  MSYS2_ARG_CONV_EXCL="--prefix=" \
  ../${_realname}-${pkgver}/configure \
            --prefix=${MINGW_PREFIX} \
            --build=${MINGW_CHOST} \
            --host=${MINGW_CHOST} \
            --target=${MINGW_CHOST} \
            --enable-static=no \
            --enable-shared=yes \
            --enable-debug=no
            
  make
}

check() {
  cd build-${MSYSTEM}-shared

  make check
}

package() {
  cd ${srcdir}/build-${MSYSTEM}-static
  make DESTDIR="${pkgdir}/" install

  cd ${srcdir}/build-${MSYSTEM}-shared
  make DESTDIR="${pkgdir}/" install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
