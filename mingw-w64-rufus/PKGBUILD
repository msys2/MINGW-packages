# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=rufus
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.5
pkgrel=2
pkgdesc="The Reliable USB Formatting Utility (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64')
url='https://rufus.ie'
msys2_repository_url='https://github.com/pbatard/rufus'
license=('spdx:GPL-3.0-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc")
source=("${msys2_repository_url}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "ucrt-build.patch")
sha256sums=('4e8135f1d6be9f85cc93b49ac4bb70082315884b207f36c771cb320c24473564'
            'e690cf87049d289f8cc65fa277917a7847e4972d6609e8107c28068d990fd527')

prepare() {
  cd "${_realname}-${pkgver}"

  if [ $MSYSTEM = UCRT64 ]; then
    unix2dos "${srcdir}/ucrt-build.patch"
    patch -Np1 --binary -i "${srcdir}/ucrt-build.patch"
  fi
}

build() {
  mkdir -p "build-${MSYSTEM}" && cd "build-${MSYSTEM}"

  ../"${_realname}-${pkgver}"/configure \
    --prefix="${MINGW_PREFIX}" \
    --enable-silent-rules \
    --disable-debug

  make
}

package() {
  cd "build-${MSYSTEM}"

  install -Dm755 src/rufus.exe "${pkgdir}${MINGW_PREFIX}/bin/rufus.exe"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE.txt" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.txt"
}
