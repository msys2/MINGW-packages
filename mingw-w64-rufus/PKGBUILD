# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=rufus
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.13
pkgrel=1
pkgdesc="The Reliable USB Formatting Utility (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64')
url='https://rufus.ie'
msys2_repository_url='https://github.com/pbatard/rufus'
license=('spdx:GPL-3.0-or-later')
msys2_references=(
  'anitya: 378415'
  'cpe: cpe:2.3:a:rufus_project:rufus'
)
msys2_ignore_vulnerabilities=(
  "CVE-2026-23988" # fixed in 4.12
)
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc")
source=("${msys2_repository_url}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('fbb5cf8b3ab8cf6e76aa0268c049f3cdf6d7d7abcc9dd0de3aee57bc6affe490')

build() {
  mkdir -p "build-${MSYSTEM}" && cd "build-${MSYSTEM}"

  ../"${_realname}-${pkgver}"/configure \
    --prefix="${MINGW_PREFIX}" \
    --disable-debug

  make
}

package() {
  cd "build-${MSYSTEM}"

  install -Dm755 src/rufus.exe "${pkgdir}${MINGW_PREFIX}/bin/rufus.exe"
  install -Dm644 "../${_realname}-${pkgver}/LICENSE.txt" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.txt"
}
