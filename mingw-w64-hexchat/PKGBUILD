# Maintainer Zach Bacon <zachbacon@budgiemedia.rocks>

_realname=hexchat
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.14.1
pkgrel=1
pkgdesc="A popular and easy to use graphical IRC (chat) client (mingw-w64)"
arch=('any')
url='https://hexchat.github.io/'
license=('GPL')
depends=("${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2"
         "${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-gtk2"
         "${MINGW_PACKAGE_PREFIX}-libproxy"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-pango")
makedepends=("intltool"
             "${MINGW_PACKAGE_PREFIX}-iso-codes"
             "${MINGW_PACKAGE_PREFIX}-lua51"
             "${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-python3")
optdepends=("${MINGW_PACKAGE_PREFIX}-enchant: Spell check"
            "${MINGW_PACKAGE_PREFIX}-iso-codes: Display language names instead of codes"
            "${MINGW_PACKAGE_PREFIX}-lua51: Lua plugin"
            "${MINGW_PACKAGE_PREFIX}-python3: Python plugin")
source=("https://dl.hexchat.net/hexchat/hexchat-${pkgver}.tar.xz"{,.asc}
        "appstream.patch")
sha256sums=('b032e4bcebe2229f87047439979a1246ddcbf599e7e538baa3f2abfac9a003a2'
            'SKIP'
            '38e5b3373f32e8379e50d9bb35a8c02cf54756e677ee0a9878ab421bc2ef398f')
validpgpkeys=('108BF2212A051F4A72B18448B3C7CE210DE76DFC') # Patrick Griffis

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/appstream.patch
}
build() {
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}
  meson ${srcdir}/${_realname}-${pkgver} \
    -Dwith-lua='lua5.1' \
    -Dwith-libcanberra='false' \
    -Dwith-libnotify='false' \
    -Dwith-dbus='false' \
    -Dwith-perl='false' \
    -Dwith-upd='false' \
    -Dwith-text='true'
  ninja
}

package() {
  cd "${srcdir}"/build-${CARCH}

 DESTDIR="${pkgdir}${MINGW_PREFIX}" ninja install
}
