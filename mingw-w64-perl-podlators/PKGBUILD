# Maintainer: @naveen521kk on Github, Naveen M K <naveen521kk@gmail.com>

_perlname=podlators
_realname="${_perlname,,}"
pkgbase=mingw-w64-perl-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-perl-${_realname}")
pkgver=4.14
pkgrel=1
pkgdesc="Convert POD data to various other formats (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://metacpan.org/release/${_perlname}"
groups=("${MINGW_PACKAGE_PREFIX}-perl-modules")
provides=(
  "${MINGW_PACKAGE_PREFIX}-perl-pod-man"
  "${MINGW_PACKAGE_PREFIX}-perl-pod-parselink"
  "${MINGW_PACKAGE_PREFIX}-perl-pod-text"
  "${MINGW_PACKAGE_PREFIX}-perl-pod-text-color"
  "${MINGW_PACKAGE_PREFIX}-perl-pod-text-overstrike"
  "${MINGW_PACKAGE_PREFIX}-perl-pod-text-termcap"
)
depends=(
  "${MINGW_PACKAGE_PREFIX}-perl"
  "${MINGW_PACKAGE_PREFIX}-perl-pod-simple"
)
options=('!emptydirs')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-cc"
  $([[ ${MINGW_PACKAGE_PREFIX} != *-clang-* ]] || \
    echo "${MINGW_PACKAGE_PREFIX}-gcc-compat")
  "${MINGW_PACKAGE_PREFIX}-perl-extutils-makemaker"
)
license=('perl_5')
source=(
  'https://cpan.metacpan.org/authors/id/R/RR/RRA/podlators-4.14.tar.gz'
  'patchmakefile.py'
)
sha256sums=('7af1c41de34b2e4dbff700a29d7387549c2b6cf16142214450c924707ddb0f82'
            '00fe5ced42aa635714909a8eba1347a6c132011a9bce68e9c75b27ebd64628f2')

build() {
  cd "$srcdir/${_perlname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL
  export pkgdir=$pkgdir
  python $srcdir/patchmakefile.py
  mingw32-make
}

package() {
  cd "$srcdir/${_perlname}-${pkgver}"
  mingw32-make DESTDIR="${pkgdir}/" install
}
