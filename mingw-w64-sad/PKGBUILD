# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=sad
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.4.31
pkgrel=1
pkgdesc="Space Age seD (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64')
url='https://github.com/ms-jpq/sad'
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust" "${MINGW_PACKAGE_PREFIX}-python")
optdepends=("${MINGW_PACKAGE_PREFIX}-fzf: for tui usage"
            "${MINGW_PACKAGE_PREFIX}-delta: for diff colourizer")
source=("${url}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('c717e54798e21356340271e32e43de5b05ba064ae64edf639fede27b1ed15ceb')

prepare() {
  cd "${_realname}-${pkgver}"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${_realname}-${pkgver}"

  cargo build --release --frozen
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  cargo test --release --frozen
}

package() {
  cd "${_realname}-${pkgver}"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
