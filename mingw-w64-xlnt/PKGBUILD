# Maintainer: Steve Jemens <sjemens@hotmail.com>

_realname=xlnt
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.5.0
pkgrel=4
pkgdesc="User-friendly xlsx library for C++14 (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/tfussell/xlnt"
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "unzip")
source=("${_realname}-${pkgver}.zip"::"https://github.com/tfussell/xlnt/archive/v${pkgver}.zip"
        001-include-limits.patch
        https://github.com/tfussell/xlnt/pull/746.patch)
noextract=("${_realname}-${pkgver}.zip") # bsdtar fails extracting the symlinks
sha256sums=('27db56b53745bf40e94edc531eb54ef67a75a40c9acc47609f0d8c8950b46d4c'
            '2b1029f172bb5f80db7f408be6f68a3b63d11188fccd1f6dd19cad0060f0bd62'
            'a90685ce004f545efab6433aec4ad98e70844200009ced719adcb02bf72c27f3')

prepare() {
  unzip -oq "${_realname}-${pkgver}.zip" -d "${srcdir}"

  # Causes error in clang, not so important
  cd "${srcdir}/${_realname}-${pkgver}"
  rm "third-party/libstudxml/version"

  patch -p1 -i "${srcdir}"/001-include-limits.patch
  patch -p1 -i "${srcdir}"/746.patch
}

build() {
  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  echo "Building static library"
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G "Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_PREFIX_PATH=${MINGW_PREFIX} \
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
    "${_extra_config[@]}" \
    -DSTATIC=ON \
    -DTESTS=OFF \
    -S ${_realname}-${pkgver} \
    -B build-${MSYSTEM}-static

  ${MINGW_PREFIX}/bin/cmake.exe --build build-${MSYSTEM}-static

  echo "Building shared library"
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G "Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_PREFIX_PATH=${MINGW_PREFIX} \
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
    "${_extra_config[@]}" \
    -DSTATIC=OFF \
    -DTESTS=OFF \
    -S ${_realname}-${pkgver} \
    -B build-${MSYSTEM}-shared

  ${MINGW_PREFIX}/bin/cmake.exe --build build-${MSYSTEM}-shared
}

package() {
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install build-${MSYSTEM}-static

  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install build-${MSYSTEM}-shared

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/LICENSE.md \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
