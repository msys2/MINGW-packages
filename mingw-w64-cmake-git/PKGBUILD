# Maintainer: Martell Malone <martell malone at g mail dot com>

_realname=cmake
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}-git"
pkgver=r23121.078e9e2
pkgrel=1
pkgdesc="A cross-platform open-source make system (mingw-w64)."
arch=('any')
url="http://www.cmake.org/"
license=("MIT")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config")
provides=("${_mingw_suff}-${_realname}")
conflicts=("${_mingw_suff}-${_realname}")
depends=("${_mingw_suff}-winpthreads"
		"${_mingw_suff}-curl"
		#"${_mingw_suff}-expat"
		"${_mingw_suff}-libarchive"
		"${_mingw_suff}-ncurses"
		"${_mingw_suff}-pkg-config"
		"${_mingw_suff}-zlib")
options=('staticlibs' 'strip')
source=("${_realname}"::"git+http://cmake.org/cmake.git"
		'disable-response-files-for-msys.patch')
md5sums=('SKIP'
         '9cb83b715a68f03f9db8c2eebc127f80')

pkgver() {
  cd "$srcdir/$_realname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd ${_realname}
	#patch -Np1 -i ${srcdir}/findfreetype.patch
	patch -Np1 -i ${srcdir}/disable-response-files-for-msys.patch
}

build() {
	mkdir -p ${pkgdir}${MINGW_PREFIX}
	pushd ${MINGW_PREFIX} > /dev/null
	export PREFIX_LIB=`pwd -W`
	popd > /dev/null
	
    mkdir -p "${srcdir}/${_realname}-build-${CARCH}"
    cd "${srcdir}/${_realname}-build-${CARCH}"
	
    MSYSTEM=MINGW "${srcdir}"/${_realname}/configure \
      --prefix=${PREFIX_LIB} \
      --system-libs \
	  --no-system-expat \
      --no-qt-gui \
	  --parallel=3 \
	  --mandir=share \
	  --docdir=share/doc/cmake
    
	plain "Start building..."
	make
}

package() {
	mkdir -p "${pkgdir}${MINGW_PREFIX}"
	pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
	export PREFIX_WIN=`pwd -W`
	popd > /dev/null

    cd "${srcdir}/${_realname}-build-${CARCH}"
	./bin/cmake.exe -DCMAKE_INSTALL_PREFIX:PATH=${PREFIX_WIN} -P cmake_install.cmake
}
