# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=pywinpty
pkgbase=mingw-w64-python-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-${_realname}
pkgver=1.1.6
pkgrel=1
pkgdesc='Pseudoterminals for Windows in Python (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64')
license=('MIT')
url='https://github.com/spyder-ide/pywinpty'
depends=(
    ${MINGW_PACKAGE_PREFIX}-python
    ${MINGW_PACKAGE_PREFIX}-winpty)
makedepends=(
    ${MINGW_PACKAGE_PREFIX}-python-maturin
    ${MINGW_PACKAGE_PREFIX}-rust)
source=(https://github.com/spyder-ide/${_realname}/archive/v${pkgver}.tar.gz
        pywinpty-1.1.6-mingw-fix.patch)
sha256sums=('8ae7d1974fae42430c63a733fb10bc3c1b002a13fbe6302d96c361ed4b8fc3a9'
            'a56ad38cb18bcefe8d6bf27769b4121fc737286965cf1fa7d7b796cf48e82fed')

prepare() {
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}"

  # Copy winpty lib to a different name to avoid conflict when linking the pyd
  cp -p ${MINGW_PREFIX}/lib/libwinpty.dll.a python-build-${MSYSTEM}/libsyswinpty.dll.a

  cd "python-build-${MSYSTEM}"
  patch -p1 -i ${srcdir}/pywinpty-1.1.6-mingw-fix.patch
}

build() {
  export OSTYPE="${CARCH}-pc-windows-gnu"

  msg "Python build for ${MSYSTEM}"
  ${MINGW_PREFIX}/bin/maturin build --no-sdist \
    --target ${OSTYPE} --out python-build-${MSYSTEM} \
    --interpreter ${MINGW_PREFIX}/bin/python.exe \
    --cargo-extra-args="--release" \
    --rustc-extra-args="--verbose" \
    --manifest-path python-build-${MSYSTEM}/Cargo.toml
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  local _py3basever=$(${MINGW_PREFIX}/bin/python -c "import sys;sys.stdout.write('.'.join(map(str, sys.version_info[:2])))")

  mkdir -p ${pkgdir}${MINGW_PREFIX}/lib/python${_py3basever}/site-config
  bsdtar xzf ${_realname}-${pkgver}-*.whl -C ${pkgdir}${MINGW_PREFIX}/lib/python${_py3basever}/site-config

  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE.txt"
}
