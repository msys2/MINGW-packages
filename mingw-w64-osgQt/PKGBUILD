# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=osgQt
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}" "${MINGW_PACKAGE_PREFIX}-${_realname}-debug")
pkgver=3.5.5
pkgrel=1
pkgdesc="OpenSceneGraph Qt5 Modules (mingw-w64)"
arch=('any')
license=('LGPL')
url="https://github.com/openscenegraph/osgQt"
depends=("${MINGW_PACKAGE_PREFIX}-qt5")
makedepends=("make"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-OpenSceneGraph=${pkgver}"
             "${MINGW_PACKAGE_PREFIX}-OpenSceneGraph-debug=${pkgver}")
options=(!strip staticlibs !buildflags)
source=(${_realname}-${pkgver}.tar.gz::https://github.com/openscenegraph/osgQt/archive/${pkgver}.tar.gz
        0001-remove-missing-macros.patch)
sha256sums=('3e20af0496615077c2182e676fdee2a141a4216bf2f8af151197161aa3b88d1f'
            '998070d0cfffd88b3ac80835e71dd2e9739397ed2af0bf4678207cccb4a08123')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-remove-missing-macros.patch
}
build() {
  plain "No build step"
}

package_prog() {
  local _buildtype=${1}
  local _builddir=${_buildtype}-${MINGW_CHOST}

  if [[ "${_buildtype}" == "Release" ]]; then
    depends+=("${MINGW_PACKAGE_PREFIX}-OpenSceneGraph")
  else
    depends+=("${MINGW_PACKAGE_PREFIX}-OpenSceneGraph-debug")
  fi

  [[ -d ${srcdir}/${_builddir} ]] && rm -rf ${srcdir}/${_builddir}
  mkdir -p ${srcdir}/${_builddir} && cd ${srcdir}/${_builddir}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=${_buildtype} \
    -DBUILD_OSG_EXAMPLES=ON \
    ../${_realname}-${pkgver}

  make DESTDIR=${pkgdir} -j1 install

  if [[ "${_buildtype}" == "Debug" ]]; then
    rm -rf ${pkgdir}${MINGW_PREFIX}/include
    rm -rf ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig

  fi
}

package_mingw-w64-i686-osgQt() {
  package_prog Release
}

package_mingw-w64-i686-osgQt-debug() {
  package_prog Debug
}

package_mingw-w64-x86_64-osgQt() {
  package_prog Release
}

package_mingw-w64-x86_64-osgQt-debug() {
  package_prog Debug
}
