# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=osgQt
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}" "${MINGW_PACKAGE_PREFIX}-${_realname}-debug")
pkgver=3.5.7
pkgrel=4
pkgdesc="OpenSceneGraph Qt5 Modules (mingw-w64)"
arch=('any')
license=('LGPL')
url="https://github.com/openscenegraph/osgQt"
depends=("${MINGW_PACKAGE_PREFIX}-qt5")
makedepends=("make"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-OpenSceneGraph"
             "${MINGW_PACKAGE_PREFIX}-OpenSceneGraph-debug")
options=(!strip staticlibs !buildflags)
source=(${_realname}-${pkgver}.tar.gz::https://github.com/openscenegraph/osgQt/archive/${pkgver}.tar.gz
        001-qt5-render-issue.patch
        002-osg-compatibility-old-version.patch)
sha256sums=('dcc4436590639168e6470fe9c5343c82bca738d3296ebee014f40f2dc029afa1'
            'c0b2483583f0d1b8c8f828e953dfad8a5ae47453df86714a1caf091e5ba3f3d1'
            'c253e66e5153075e117c70e6014dee085ec7c6289b61505609b6912612a4c35b')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-qt5-render-issue.patch
  patch -p1 -i ${srcdir}/002-osg-compatibility-old-version.patch
}
build() {
  plain "No build step"
}

package_prog() {
  local _buildtype=${1}
  local _builddir=${_buildtype}-${MINGW_CHOST}

  if [[ "${_buildtype}" == "Release" ]]; then
    depends+=("${MINGW_PACKAGE_PREFIX}-OpenSceneGraph")
  else
    depends+=("${MINGW_PACKAGE_PREFIX}-OpenSceneGraph-debug")
  fi

  [[ -d ${srcdir}/${_builddir} ]] && rm -rf ${srcdir}/${_builddir}
  mkdir -p ${srcdir}/${_builddir} && cd ${srcdir}/${_builddir}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=${_buildtype} \
    -DBUILD_OSG_EXAMPLES=ON \
    ../${_realname}-${pkgver}

  make DESTDIR=${pkgdir} -j1 install

  if [[ "${_buildtype}" == "Debug" ]]; then
    rm -rf ${pkgdir}${MINGW_PREFIX}/include
    rm -rf ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig
  fi
}

package_mingw-w64-i686-osgQt() {
  package_prog Release
}

package_mingw-w64-i686-osgQt-debug() {
  package_prog Debug
}

package_mingw-w64-x86_64-osgQt() {
  package_prog Release
}

package_mingw-w64-x86_64-osgQt-debug() {
  package_prog Debug
}
