# Contributor: Liu Pak Hin <wl@m-labs.hk>

if [[ $MINGW_PACKAGE_PREFIX == *-clang-* ]]; then
  _clangprefix=1
  _compiler=clang
else
  _compiler=clang
fi

_realname=lld
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
_version=15.0.4
_rc=""
_tag=llvmorg-${_version}${_rc}
pkgver=${_version}${_rc/-/}
pkgrel=1
pkgdesc="Linker tools for LLVM"
arch=(any)
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://llvm.org/"
license=("custom:Apache 2.0 with LLVM Exception")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             )
depends=("${MINGW_PACKAGE_PREFIX}-llvm")
options=('!debug' 'strip')
_url=https://github.com/llvm/llvm-project/releases/download/${_tag}
source=("${_url}/lld-${pkgver}.src.tar.xz"{,.sig}
        "${_url}/cmake-${pkgver}.src.tar.xz"{,.sig}
        "${_url}/libunwind-${pkgver}.src.tar.xz"{,.sig}) 
sha256sums=('189963d19c01fa26eff13fe945c501b0f7a8c2cb781a42b3c916531db7c4a2a9'
            'SKIP'
            '9df45bf3a0a46264d5007485592381bbaf50f034b4155290cb0d917539d8facf'
            'SKIP'
            'a424d9357a0428c2be0c9bce8691dffa74fa5d7d6d6ecb2f4297a6c58ccad070'
            'SKIP')
validpgpkeys=('B6C8F98282B944E3B0D5C2530FC3042E345AD05D'  # Hans Wennborg, Google.
              '474E22316ABF4785A88C6E8EA2C794A986419D8A'  # Tom Stellard
              'D574BD5D1D0E98895E3BF90044F2485E45D59042') # Tobias Hiet

prepare() {
  cd "${srcdir}/cmake-${pkgver}.src"
  cp "Modules/GNUInstallPackageDir.cmake" "${srcdir}/lld-${pkgver}.src/cmake/Modules"
  cp "Modules/ExtendPath.cmake" "${srcdir}/lld-${pkgver}.src/cmake/Modules"
  cp "Modules/FindPrefixFromConfig.cmake" "${srcdir}/lld-${pkgver}.src/cmake/Modules"

  cd "${srcdir}/libunwind-${pkgver}.src"
  cp -R "include/mach-o" "${srcdir}/lld-${pkgver}.src/MachO/Arch/mach-o"
  cp -R "include/mach-o" "${srcdir}/lld-${pkgver}.src/MachO/mach-o"
}

build() {
  cd "${srcdir}"

  [[ -d build-${MSYSTEM} ]] && rm -rf build-${MSYSTEM}
  mkdir build-${MSYSTEM} && cd build-${MSYSTEM}

  local -a extra_config

  if check_option "debug" "y"; then
    extra_config+=(-DCMAKE_BUILD_TYPE=Debug)
    VERBOSE="VERBOSE=1"
  else
    extra_config+=(-DCMAKE_BUILD_TYPE=Release)
  fi
  
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_SYSTEM_IGNORE_PATH=/usr/lib \
    "${extra_config[@]}" \
    ../lld-${pkgver}.src

  ${MINGW_PREFIX}/bin/cmake.exe --build .
}

check() {
  cd "${srcdir}"/build-${MSYSTEM}
  make check-lld || true
}

package() {
  DESTDIR=${pkgdir} ${MINGW_PREFIX}/bin/cmake --install build-${MSYSTEM}
}
