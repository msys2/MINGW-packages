# Maintainer: Carlos Antunes <cmantunes@gmail.com>
# Maintainer: fauxpark <fauxpark@gmail.com>
# Contributed by: nanodude

_realname=binutils
_all_targets="arm-none-eabi riscv64-unknown-elf"

pkgbase=mingw-w64-embedded-${_realname}
pkgname=($(for _target in ${_all_targets}; do
               echo "${MINGW_PACKAGE_PREFIX}-${_target}-${_realname}"
           done))
pkgver=2.35
pkgrel=2
arch=('any')
license=('GPL')
url='https://www.gnu.org/software/binutils/binutils.html'
options=('staticlibs')
source=("https://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.xz")
sha256sums=('1b11659fb49e20e18db460d44485f09442c8c56d5df165de9461eb09c8302f85')

build_common() {
    _target=$1

    echo "Building" ${_target}

    mkdir -p ${srcdir}/binutils-${pkgver}/binutils-build-${MINGW_CHOST}-${_target}
    cd ${srcdir}/binutils-${pkgver}/binutils-build-${MINGW_CHOST}-${_target}

    ../configure \
        --build=${MINGW_CHOST} \
        --prefix=${MINGW_PREFIX} \
        --target=${_target} \
        --enable-multilib \
        --enable-interwork \
        --with-gnu-as \
        --with-gnu-ld \
        --disable-nls \
        --enable-ld=default \
        --enable-gold \
        --enable-plugins \
        --enable-deterministic-archives
    make
}

build() {
    for _target in ${_all_targets}; do
        build_common ${_target}
    done
}

package_common() {
    _target=$1

    groups=("${MINGW_PACKAGE_PREFIX}-${_target}-toolchain")

    cd ${srcdir}/binutils-${pkgver}/binutils-build-${MINGW_CHOST}-${_target}

    make DESTDIR="${pkgdir}" install

    cd ${pkgdir}${MINGW_PREFIX}

    for info in as bfd binutils gprof ld; do
        mv share/info/${info}.info share/info/${_target}-${info}.info
    done
}

package_mingw-w64-x86_64-arm-none-eabi-binutils() {
    pkgdesc='GNU Tools for ARM Embedded Processors - Binutils (mingw-w64)'
    package_common 'arm-none-eabi'
}
package_mingw-w64-i686-arm-none-eabi-binutils() {
    package_mingw-w64-x86_64-arm-none-eabi-binutils
}

package_mingw-w64-x86_64-riscv64-unknown-elf-binutils() {
    pkgdesc='GNU Tools for RISC-V Embedded Processors - Binutils (mingw-w64)'
    package_common 'riscv64-unknown-elf'
}
package_mingw-w64-i686-riscv64-unknown-elf-binutils() {
    package_mingw-w64-x86_64-riscv64-unknown-elf-binutils
}
