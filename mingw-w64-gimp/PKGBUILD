# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=gimp
_mingw_suff=mingw-w64-${CARCH}
pkgname=("${_mingw_suff}-${_realname}")
pkgver=2.8.10
pkgrel=6
pkgdesc="GNU Image Manipulation Program"
arch=('any')
groups=("${_mingw_suff}")
url="http://www.gimp.org/"
license=('GPL' 'LGPL')
depends=("${_mingw_suff}-python2-pygtk"
		"${_mingw_suff}-lcms2"
		"${_mingw_suff}-xpm-nox"
		"${_mingw_suff}-libwmf"
		"${_mingw_suff}-librsvg"
		"${_mingw_suff}-libmng"
		"${_mingw_suff}-dbus-glib"
		"${_mingw_suff}-libexif"
		"${_mingw_suff}-gegl"
		"${_mingw_suff}-jasper"
		"${_mingw_suff}-hicolor-icon-theme"
		"${_mingw_suff}-babl"
		"${_mingw_suff}-openexr"
		"${_mingw_suff}-webkitgtk2"
		"${_mingw_suff}-poppler"
		"${_mingw_suff}-curl"
		"${_mingw_suff}-ghostscript"
		)
makedepends=('intltool'
		"${_mingw_suff}-iso-codes"
		"${_mingw_suff}-curl"
		"${_mingw_suff}-webkitgtk2"
		"${_mingw_suff}-poppler"
		"${_mingw_suff}-ghostscript")
options=('strip' 'makeflags')
install=gimp-${CARCH}.install
source=(ftp://ftp.gimp.org/pub/gimp/v${pkgver%.*}/${_realname}-${pkgver}.tar.bz2 
        uri-backend-libcurl.patch
		patch_CVE197.patch
		patch_CVE1913.patch
		freetype_location.patch
		gegl-0.3-fix.patch
		find-directx-fix.patch
		python-m4.patch
		mingw-add-ModuleFileName-dir-to-PATH.patch)
md5sums=('84c964aab7044489af69f7319bb59b47'
         'e894f4b2ffa92c71448fdd350e9b78c6'
         '901643efc82fae57af466d8a0a8991ff'
         'dcc2acc9f9962a7c83ffb4243c08c469'
         'ca0f32cb329ddad8079f8e2c0fc75b29'
         'd1e19159164cac7b39578a6b0bb02cbb'
         '0df0c9c5f55012023894ed8b959c2fa9'
         '34aba5cb94d368627bf17eb5aedee556'
         '0d5c523e91fbfbab43c2f12447ccfebc')

prepare() {
	cd "${srcdir}/${_realname}-${pkgver}"
	#patch -p1 -i ${srcdir}/uri-backend-libcurl.patch
	patch -p1 -i ${srcdir}/patch_CVE1913.patch
	patch -p1 -i ${srcdir}/patch_CVE197.patch
	patch -p1 -i ${srcdir}/freetype_location.patch
	patch -p1 -i ${srcdir}/gegl-0.3-fix.patch
	patch -p1 -i ${srcdir}/find-directx-fix.patch
	patch -p1 -i ${srcdir}/python-m4.patch
	patch -p1 -i ${srcdir}/mingw-add-ModuleFileName-dir-to-PATH.patch
	
	rm -fr autom4te.cache
	libtoolize -c
	aclocal -I m4macros
	automake -a -c
	autoheader
	autoconf

	#autoreconf -fi -I m4macros
}

build() {
	cd "${srcdir}/${_realname}-${pkgver}"

	CPPFLAGS+=" -I${MINGW_PREFIX}/include/noX"
	CFLAGS+=" -D__USE_MINGW_ANSI_STDIO=1 -I${MINGW_PREFIX}/include/noX"
	CXXFLAGS+=" -D__USE_MINGW_ANSI_STDIO=1 -I${MINGW_PREFIX}/include/noX"
	PYTHON=${MINGW_PREFIX}/bin/python2 \
	./configure --prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--target=${MINGW_CHOST} \
		--sysconfdir=${MINGW_PREFIX}/etc \
		--enable-mp \
		--enable-gimp-console \
		--enable-python \
		--with-gif-compression=lzw \
		--with-libcurl \
		--without-aa \
		--with-lcms=lcms2 \
		--with-pdbgen \
		--with-directx-sdk=${MINGW_PREFIX}/${MINGW_CHOST} \
		--without-x
	make
}

package() {
	cd "${srcdir}/${_realname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	ln -s gimptool-2.0 "${pkgdir}${MINGW_PREFIX}/bin/gimptool.exe"
	cp ${pkgdir}${MINGW_PREFIX}/lib/gimp/2.0/{plug-ins,python}/pyconsole.py
	sed -e "s|${MINGW_PREFIX}/bin/python2|python2w.exe|g" -i ${pkgdir}${MINGW_PREFIX}/lib/gimp/2.0/interpreters/pygimp.interp
	rm -f "${pkgdir}${MINGW_PREFIX}/lib/*.def"
	rm -f "${pkgdir}${MINGW_PREFIX}/lib/gimp/2.0/modules/*.dll.a"
}
