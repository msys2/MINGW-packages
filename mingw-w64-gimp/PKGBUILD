# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=gimp
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.9.2
pkgrel=5
pkgdesc="GNU Image Manipulation Program (mingw-w64)"
arch=('any')
url="https://www.gimp.org/"
license=('GPL' 'LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-babl"
         "${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-dbus-glib"
         "${MINGW_PACKAGE_PREFIX}-gegl"
         "${MINGW_PACKAGE_PREFIX}-gexiv2"
         "${MINGW_PACKAGE_PREFIX}-ghostscript"
         "${MINGW_PACKAGE_PREFIX}-hicolor-icon-theme"
         "${MINGW_PACKAGE_PREFIX}-jasper"
         "${MINGW_PACKAGE_PREFIX}-lcms2"
         "${MINGW_PACKAGE_PREFIX}-libexif"
         "${MINGW_PACKAGE_PREFIX}-libmng"
         "${MINGW_PACKAGE_PREFIX}-librsvg"
         "${MINGW_PACKAGE_PREFIX}-libwmf"
         "${MINGW_PACKAGE_PREFIX}-openexr"
         "${MINGW_PACKAGE_PREFIX}-poppler"
         "${MINGW_PACKAGE_PREFIX}-python2-pygtk"
         "${MINGW_PACKAGE_PREFIX}-python2-gobject"
         "${MINGW_PACKAGE_PREFIX}-webkitgtk2"
         "${MINGW_PACKAGE_PREFIX}-xpm-nox"
        )
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-iso-codes"
             "intltool")
options=('strip' 'makeflags')
install=gimp-${CARCH}.install
source=(https://download.gimp.org/pub/gimp/v${pkgver%.*}/${_realname}-${pkgver}.tar.bz2
        find-directx-fix.patch
        python-m4.patch
        mingw-add-ModuleFileName-dir-to-PATH.patch
        replace-pygtk-codegen.patch
        fix_GEGL_FTBFS.patch)
sha256sums=('85bed436b868b2e45ffab48d1bf5743006932770e8cca3322e345aefc000e29e'
            'f7f9badc547f1a0317b95e012530db8df63b7c7038c9d9ff8d0b510c86f43439'
            '212d16c241773d4a0f861dc30585f22984330a5aae8ca16ba17a89c7cf87c3ba'
            '682656d1f7ec70bfd3dd10eba3f8db8abb75c80e44933f8c09c730cea5aeb549'
            'fb15dbeefded7d0df697290288024a8ac500469ec122536c2ac24df1a07d8966'
            '6b03d842f9ed7fb17ecc11a79246bb316e2c97e6fdf3ca2b86f364435fda20e5')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/find-directx-fix.patch
  patch -p1 -i ${srcdir}/python-m4.patch
  patch -p1 -i ${srcdir}/mingw-add-ModuleFileName-dir-to-PATH.patch
  patch -p1 -i ${srcdir}/replace-pygtk-codegen.patch
  patch -p1 -i ${srcdir}/fix_GEGL_FTBFS.patch

  rm -fr autom4te.cache
  libtoolize -c
  aclocal -I m4macros
  automake -a -c
  autoheader
  autoconf

  #autoreconf -fi -I m4macros
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  cp -rf ${_realname}-${pkgver} build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  PYTHON=${MINGW_PREFIX}/bin/python2 \
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --sysconfdir=${MINGW_PREFIX}/etc \
    --enable-mp \
    --enable-gimp-console \
    --enable-python \
    --without-aa \
    --with-pdbgen \
    --with-directx-sdk=${MINGW_PREFIX}/${MINGW_CHOST} \
    --without-x

  # V=1 because of a race condition (a generated 'bin2c' type
  # file ending up truncated once). This might avoid it.
  make V=1
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  ln -s gimptool-2.0 "${pkgdir}${MINGW_PREFIX}/bin/gimptool.exe"
  cp ${pkgdir}${MINGW_PREFIX}/lib/gimp/2.0/{plug-ins,python}/pyconsole.py
  sed -e "s|${MINGW_PREFIX}/bin/python2|python2w.exe|g" -i ${pkgdir}${MINGW_PREFIX}/lib/gimp/2.0/interpreters/pygimp.interp
  rm -f "${pkgdir}${MINGW_PREFIX}/lib/*.def"
  rm -f "${pkgdir}${MINGW_PREFIX}/lib/gimp/2.0/modules/*.dll.a"

  cp ${pkgdir}${MINGW_PREFIX}/bin/gimp-${pkgver%.*}.exe ${pkgdir}${MINGW_PREFIX}/bin/gimp.exe
}
