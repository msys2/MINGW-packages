# Maintainer: Ruben Agin <phabrics@phabrics.com>

_realname=tme
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.12rc12
pkgrel=1
pkgdesc="The Machine Emulator, or tme, provides a general-purpose framework for computer emulation. (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
license=('GPL3')
url="https://phabrics.github.io/tme.html"
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-pkgconf")
depends=("${MINGW_PACKAGE_PREFIX}-gtk4"
         "${MINGW_PACKAGE_PREFIX}-SDL2"
         "${MINGW_PACKAGE_PREFIX}-libvncserver"
         "${MINGW_PACKAGE_PREFIX}-libltdl")
source=("https://github.com/phabrics/nme/releases/download/${pkgver}/${_realname}-${pkgver}.tar.xz")
sha256sums=('cd67faeef202578794df9280eb6f19b4361fa10e0de9bf082d5ed181321c1f14')
options=('libtool')

build() {
    mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

    ../"${_realname}-${pkgver}"/configure \
      --prefix="${MINGW_PREFIX}" \
      --build="${MINGW_CHOST}" \
      --host="${MINGW_CHOST}" \
      --disable-ltdl-install \
      --disable-lto \
      CPPFLAGS=-Wno-int-conversion 

    make tmesh_LDFLAGS= 
}

package() {
    cd "${srcdir}/build-${MSYSTEM}"
    make install DESTDIR="${pkgdir}" tmesh_LDFLAGS= 
    install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
