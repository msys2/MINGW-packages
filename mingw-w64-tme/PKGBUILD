# Maintainer: Ruben Agin <phabrics@phabrics.com>

_realname=tme
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.12rc9
pkgrel=1
pkgdesc="The Machine Emulator, or ${_realname}, provides a general-purpose framework for computer emulation. (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=('GPL3')
url="https://osdn.net/users/phabrics/pf/${_realname}/wiki/FrontPage"
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
	     "bison"
	     "texinfo")
depends=("${MINGW_PACKAGE_PREFIX}-gtk3"
	 "${MINGW_PACKAGE_PREFIX}-libvncserver"
	 "${MINGW_PACKAGE_PREFIX}-libltdl")
source=("https://osdn.net/users/phabrics/pf/${_realname}/dl/${_realname}-${pkgver}.tar.xz")
sha256sums=('28499e869f09e1f3c8ca76ed97f29bd647db3cd015e09c660dfe30193bed7763')

build()
{
    mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

    if [[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]]; then
        CFLAGS+=" -Wno-int-conversion"
    fi

    ../"${_realname}-${pkgver}"/configure \
      --prefix="${MINGW_PREFIX}" \
      --build="${MINGW_CHOST}" \
      --host="${MINGW_CHOST}" \
      --target="${MINGW_CHOST}" \
      --enable-hosts="gtk rfb openvpn"

    make
}

package()
{
    cd "${srcdir}/build-${MSYSTEM}"
    make install DESTDIR="${pkgdir}"
    install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
