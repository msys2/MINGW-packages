# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>

_realname=gsutil
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=5.27
pkgrel=3
pkgdesc='A command line tool for interacting with cloud storage services. (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://cloud.google.com/storage/docs/gsutil"
msys2_repository_url="https://github.com/GoogleCloudPlatform/gsutil"
msys2_references=(
  'purl: pkg:pypi/gsutil'
)
license=('spdx:Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-argcomplete"
         "${MINGW_PACKAGE_PREFIX}-python-crcmod"
         "${MINGW_PACKAGE_PREFIX}-python-fasteners"
         "${MINGW_PACKAGE_PREFIX}-python-google-auth"
         "${MINGW_PACKAGE_PREFIX}-python-httplib2"
         "${MINGW_PACKAGE_PREFIX}-python-monotonic"
         "${MINGW_PACKAGE_PREFIX}-python-pyopenssl"
         "${MINGW_PACKAGE_PREFIX}-python-six"
         "${MINGW_PACKAGE_PREFIX}-python-google-apitools"
         "${MINGW_PACKAGE_PREFIX}-python-gcs-oauth2-boto-plugin"
         "${MINGW_PACKAGE_PREFIX}-python-google-reauth"
         "${MINGW_PACKAGE_PREFIX}-python-retry-decorator")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-monotonic>=1.4")
options=('!strip')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('681a2d844acdf05fac989da6dd406944ae11cb27a4cf3c9edef74d2585ab5f05')

prepare() {
  cd "$srcdir"/${_realname}-${pkgver}
  sed -e "s|httplib2==0.20.4|httplib2>=0.20.4|g" -i setup.py

  cd "$srcdir"
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}"
}

build() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m pytest || warning "Tests failed"
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
