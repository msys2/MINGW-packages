# Maintainer: Ryuta Suzuki <oroppas@gmail.com>

_realname=openpyxl
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=3.0.0
pkgrel=1
pkgdesc="A python library to read/write Excel 2007 xlsx/xlsm file (mingw-w64)"
url="https://openpyxl.readthedocs.io/"
arch=('any')
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python3-jdcal"
         "${MINGW_PACKAGE_PREFIX}-python3-et-xmlfile"
         "${MINGW_PACKAGE_PREFIX}-python3-defusedxml"
         "${MINGW_PACKAGE_PREFIX}-python3-pandas"
         "${MINGW_PACKAGE_PREFIX}-python3-pillow")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-pip"
             "${MINGW_PACKAGE_PREFIX}-python3-pytest")
_pkgtag='5eaaf27880d0'
source=(openpyxl-${pkgver}.tar.gz::"https://bitbucket.org/openpyxl/openpyxl/get/${pkgver}.tar.gz"
        "LICENSE")
sha256sums=('1883a3c28830fb753392dc260a3f495b40fe78364cc274da902d66f18e70ad92'
            '717016f755510c08d8752a7d2cf0a355624d380b68b88e9d92ea153ea896daaa')

prepare() {
  rm -rf python3-build-${CARCH}
  cp -r "openpyxl-openpyxl-${_pkgtag}" "python3-build-${CARCH}"
}

build() {
  msg "Python 3 build for ${CARCH}"  
  cd "${srcdir}/python3-build-${CARCH}"
  ${MINGW_PREFIX}/bin/python3 setup.py build
}

package() {
  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 ${srcdir}/LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE
}
