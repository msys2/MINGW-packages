# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Lars Kanis <lars@greiz-reinsdorf.de>

_realname=freetds
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.4.23
pkgrel=1
pkgdesc="A free implementation of Sybase's DB-Library, CT-Library and ODBC libraries (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://www.freetds.org/'
msys2_repository_url='https://github.com/FreeTDS/freetds/'
msys2_changelog_url='https://www.freetds.org/news.html'
msys2_documentation_url='https://www.freetds.org/docs.html'
msys2_issue_tracker_url='https://github.com/FreeTDS/freetds/issues/'
msys2_references=(
  'archlinux: freetds'
  "cpe: cpe:/a:freetds:freetds"
)
license=('spdx:LGPL-2.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-libiconv")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-pkgconf")
source=(https://www.freetds.org/files/stable/freetds-${pkgver}.tar.gz
        0001-Don-t-use-MSYS2-file-libws2_32.patch)
sha256sums=('9016d00dfcf0b9dc256b0d857d46506db2f84bb6a38bfa80ce2ea6cf6e6367fe'
            '8232a51f6bf8cfe51624effce087cc707d410b6a6e3e066a9eb08ef4298cdb5f')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/0001-Don-t-use-MSYS2-file-libws2_32.patch
  # autoreconf to get updated libtool files with clang support
  autoreconf -fiv
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  ../freetds-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --includedir=${MINGW_PREFIX}/include/${_realname} \
    --enable-static \
    --enable-shared \
    --with-openssl \
    --enable-sspi

  make
}

package () {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
}
