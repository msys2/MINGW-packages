# Maintainer: Dirk Stolle

_realname=ppl
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.2
pkgrel=1
pkgdesc="Parma Polyhedra Library - convex polyhedra + numerical abstractions (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://bugseng.com/products/ppl'
msys2_documentation_url='https://support.bugseng.com/ppl/'
msys2_repository_url='https://github.com/BUGSENG/PPL'
msys2_references=(
  'anitya: 17287'
  'archlinux: ppl'
  'gentoo: dev-libs/ppl'
)
license=('spdx:GPL-3.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-glpk"
         "${MINGW_PACKAGE_PREFIX}-gmp")
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc")
# Documentation, including HTML and PDF variants, is ca. 1520 files, while the whole package with
# documentation is ca. 1560 files. Therefore, documentation is disabled. Maybe it should be split
# into a separate docs package.
options=(!docs)
source=("https://support.bugseng.com/ppl/download/ftp/releases/${pkgver}/ppl-${pkgver}.tar.gz"
        "0001-make-test-immune-from-LTO.patch"
        "0002-replace-auto_ptr-with-unique_ptr.patch"
        "0003-replace-mem_fun-with-mem_fn.patch"
)
sha256sums=('6bc36dd4a87abc429d8f9c00c53e334e5041a9b0857cfc00dbad6ef14294aac8'
            '1703c76fac3375652b0f7aaa64c825eb9cf7aac6264ddc045e1ddbee8f96cd39'
            'f45c05aa77e1958e6b32fdbc12f9d284eeea0af4b3e97a12d4d93192b301663f'
            '9f7b36310fec3180fd33c7c9f8259b9d96dba6d174bea455b220d16e6e4f74dd')

prepare() {
  cd "${_realname}-${pkgver}"

  # Fix configure check broken by LTO (Fedora).
  # See <https://github.com/BUGSENG/PPL/commit/d7cd42677f9c59590127badb4c0972df5a65f6bc>.
  patch -Np1 -i "${srcdir}/0001-make-test-immune-from-LTO.patch"

  # Fix detection of C++11 features (Fedora)
  sed -i 's,== 201103L,>= 201103L,g' m4/ac_check_cxx11.m4

  # Code still uses std::auto_ptr in some places, but std::auto_ptr has been
  # officially removed from C++ with C++17. GCC still seems to handle it, but
  # Clang does not, so replace it with std::unique_ptr which is the recommended
  # replacement for std::auto_ptr in newer C++ standards.
  patch -Np1 -i "${srcdir}/0002-replace-auto_ptr-with-unique_ptr.patch"

  # Code still uses std::mem_fun_ref, but that has been removed in C++17 and
  # Clang cannot handle it anymore. So it's replaced with std::mem_fn.
  if [[ ${MSYSTEM} == CLANG* ]]
  then
    patch -Np1 -i "${srcdir}/0003-replace-mem_fun-with-mem_fn.patch"
  fi
}

build() {
  mkdir -p "build-${MSYSTEM}" && cd "build-${MSYSTEM}"

  ../"${_realname}-${pkgver}"/configure \
    --prefix="${MINGW_PREFIX}" \
    --build="${MINGW_CHOST}" \
    --host="${MINGW_CHOST}" \
    --target="${MINGW_CHOST}" \
    --enable-interfaces="c,cxx" \
    --enable-static \
    --enable-shared

  # Note: ppl has a few more interfaces, e.g. for Java and for several Prolog variants. However,
  # these are not used so we do not build them.

  make
}

check() {
  cd "build-${MSYSTEM}"

  make check || echo "Some tests failed."
}

package() {
  cd "build-${MSYSTEM}"

  make install DESTDIR="${pkgdir}"

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
