# Maintainer: Martell Malone <martellmalone@gmail.com>

_realname=mcpp
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.7.2
pkgrel=3
pkgdesc="Matsui's CPP implementation precisely conformed to standards (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64')
license=('custom')
url="https://mcpp.sourceforge.io/"
msys2_references=(
  "cpe: cpe:/a:mcpp_project:mcpp"
)
makedepends=("${MINGW_PACKAGE_PREFIX}-cc" "${MINGW_PACKAGE_PREFIX}-autotools")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" "${MINGW_PACKAGE_PREFIX}-libiconv")
source=("https://downloads.sourceforge.net/mcpp/${_realname}-${pkgver}.tar.gz")
sha256sums=('3b9b4421888519876c4fc68ade324a3bbd81ceeb7092ecdbbc2055099fcb8864')

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-mcpplib

  make
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
}
