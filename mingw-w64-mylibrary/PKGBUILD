# Maintainer: Yury Bobylev <bobilev_yury@mail.ru>

_realname=mylibrary
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=4.0
pkgrel=1
pkgdesc="Home librarian (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url='https://github.com/ProfessorNavigator/mylibrary'
license=('spdx:GPL-3.0-only')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
			 "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-make"
             "${MINGW_PACKAGE_PREFIX}-cc"
			 "${MINGW_PACKAGE_PREFIX}-pkg-config"
			 "${MINGW_PACKAGE_PREFIX}-gettext"
			 "${MINGW_PACKAGE_PREFIX}-doxygen"
			 "${MINGW_PACKAGE_PREFIX}-texlive-latex-recommended"
			 "${MINGW_PACKAGE_PREFIX}-texlive-latex-extra"
			 "${MINGW_PACKAGE_PREFIX}-texlive-plain-generic"
			 "${MINGW_PACKAGE_PREFIX}-texlive-fonts-recommended"
			 "${MINGW_PACKAGE_PREFIX}-texlive-font-utils")
depends=("${MINGW_PACKAGE_PREFIX}-gtkmm-4.0"
		 "${MINGW_PACKAGE_PREFIX}-libgcrypt"
		 "${MINGW_PACKAGE_PREFIX}-djvulibre"
		 "${MINGW_PACKAGE_PREFIX}-poppler"
		 "${MINGW_PACKAGE_PREFIX}-libarchive"
		 "${MINGW_PACKAGE_PREFIX}-icu")
source=("https://github.com/ProfessorNavigator/${_realname}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('b880ab9dae3288fe829fa3176e3b28d45ea0f06059a678a77170936386a78987')

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DBUILD_SHARED_LIBS=ON \
      -S "${_realname}-${pkgver}" \
      -B "build-${MSYSTEM}" \
	  -DUSE_OPENMP=OFF \
	  -DUSE_PLUGINS=ON \
	  -DCREATE_HTML_DOCS_MLBOOKPROC=ON \
	  -DCREATE_PDF_DOCS_MLBOOKPROC=ON \
	  -DCREATE_HTML_DOCS_PLUGINIFC=ON \
	  -DCREATE_HTML_DOCS_PLUGINIFC=ON 

  cmake --build "build-${MSYSTEM}"
}

package() {
  DESTDIR="${pkgdir}" cmake --install "build-${MSYSTEM}"

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
