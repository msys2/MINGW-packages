# Maintainer: Taozuhong <taozuhong@hotmail.com>

_realname=odpi
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=5.6.4
pkgrel=1
pkgdesc="Oracle Database Programming Interface for Drivers and Applications (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://oracle.github.io/odpi/"
msys2_repository_url="https://github.com/oracle/odpi"
msys2_references=(
  'archlinux: odpi'
)
license=('spdx:Apache-2.0, UPL-1.0')
makedepends=(
  "base-devel"
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-dlfcn")
source=("https://github.com/oracle/${_realname}/archive/refs/tags/v${pkgver}.tar.gz"
        "0001-Makefile.patch"
        "odpi.pc")
sha256sums=('d8ba665d9deb0f0c601e38de477d3808e7d426438b9e37345bef53f0e75687ea'
            '9925deb4efa4855174ffdeaa8d09b2cf4bccc843c36d84cf279afe0d7db41fb5'
            'b3a390fcc25ce7feca0b224228ca41dc8de39a6e0f0d6ca32a438f3955bb1750')

prepare() {
  cd "${srcdir}"
  sed -i "s,@VERSION@,${pkgver}," "${srcdir}/odpi.pc"
  sed -i "s,@PREFIX@,${MINGW_PREFIX}," "${srcdir}/odpi.pc"

  cd "${srcdir}/odpi-${pkgver}"
  patch -p1 -i "${srcdir}/0001-Makefile.patch"
}

build() {
  cd "${srcdir}/odpi-${pkgver}"

  make
}

package() {
  cd "${srcdir}/odpi-${pkgver}"

  PREFIX=${pkgdir}${MINGW_PREFIX} make install
  install -Dm644 "${srcdir}/odpi.pc" "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/odpi.pc"
  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/LICENSE.txt \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.txt
}
