# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=freeglut
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
_pre=
_ver=3.0.0
pkgver=${_ver}${_pre}
pkgrel=3
pkgdesc="Freeglut allows the user to create and manage windows containing OpenGL contexts (mingw32-w64)"
arch=('any')
url="http://freeglut.sourceforge.net/"
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('strip' '!libtool' 'staticlibs')
source=("http://downloads.sourceforge.net/freeglut/${_realname}-${_ver}${_pre}.tar.gz"
        "0001-Emit-the-correct-LIBNAME-to-.pc-files.patch")
md5sums=('90c3ca4dd9d51cf32276bc5344ec9754'
         '60f8ec454e4fcd6e9fe1b5f2109d9903')
noextract=(${_realname}-${_ver}${_pre}.tar.gz)

prepare() {
  # Clean up old sources so re-patching doesn't fail.
  [[ -d ${srcdir}/${_realname}-${_ver}${_pre} ]] && rm -rf ${srcdir}/${_realname}-${_ver}${_pre}
  tar -xzvf ${_realname}-${_ver}${_pre}.tar.gz -C ${srcdir}
  # Upstreaming at https://github.com/dcnieho/FreeGLUT/pull/41
  cd ${_realname}-${_ver}${_pre}
  patch -p3 -i "${srcdir}"/0001-Emit-the-correct-LIBNAME-to-.pc-files.patch
}

build() {
  rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    ../${_realname}-${_ver}${_pre}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install
}
