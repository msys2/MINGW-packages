# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=udsoncan
pkgbase=mingw-w64-python3-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=1.6
pkgrel=1
pkgdesc="Implementation of the Unified Diagnostic Services (UDS) protocol defined by ISO-14229 written in Python 3 (mingw-w64)"
arch=('any')
url='https://github.com/pylessard/python-udsoncan'
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python3")
makedepends=("${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools")
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/pylessard/python-udsoncan/archive/v${pkgver}.tar.gz")
sha512sums=('764ab7b70e00dbe24d4c4102f16466c5853bebc3f2e7113b78a19abe3f86f9c5de9c94dd4d38fb7ab36b624aa4c674035dd6f1f1d0ead7dbd0392a211c08e581')

prepare() {
  cp -rf python-${_realname}-${pkgver} python3-build-${CARCH}
}

build() {
  msg "Python 3 build for ${CARCH}"  
  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py build
}

package() {
  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE.txt"
}
