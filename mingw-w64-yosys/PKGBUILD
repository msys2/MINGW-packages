_realname=yosys
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.9.r10702.0ccc7229c
pkgrel=1
pkgdesc="A framework for RTL synthesis tools (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64')
license=("ISC")
url="http://www.clifford.at/yosys/"
groups=("${MINGW_PACKAGE_PREFIX}-eda")
depends=(
  "${MINGW_PACKAGE_PREFIX}-ghdl"
  "${MINGW_PACKAGE_PREFIX}-python"
)
checkdepends=("${MINGW_PACKAGE_PREFIX}-iverilog")
makedepends=(
  'flex'
  'git'
  "${MINGW_PACKAGE_PREFIX}-ghdl"
  "${MINGW_PACKAGE_PREFIX}-python"
)

_commit='0ccc7229'
source=(
  "${_realname}::git://github.com/YosysHQ/${_realname}.git#commit=${_commit}"
  "ghdl-yosys-plugin::git://github.com/ghdl/ghdl-yosys-plugin.git#commit=98f4594b"
)
sha256sums=(
  'SKIP'
  'SKIP'
)

pkgver() {
  cd "${_realname}"
  printf "0.9.r%s.%s" "$(git rev-list --count "${_commit}")" "$(git rev-parse --short "${_commit}")"
}

build() {
  cd "${srcdir}/${_realname}"

  if [ "$CARCH" = "i686" ]; then
    make config-msys2-32
  fi

  if [ "$CARCH" = "x86_64" ]; then
    make config-msys2-64
  fi

  mv "${srcdir}/ghdl-yosys-plugin"/src frontends/ghdl
  echo "ENABLE_GHDL=1" >> Makefile.conf
  echo "GHDL_PREFIX=${MINGW_PREFIX}" >> Makefile.conf

  make \
    GIT_REV="${_commit}" \
    PRETTY=0
}

check() {
  cd "${srcdir}/${_realname}"
  make test
}

package() {
  cd "${srcdir}/${_realname}"
  make DESTDIR="${pkgdir}" install
}
