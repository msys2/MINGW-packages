

_realname=yosys
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.51
pkgrel=1
pkgdesc="A framework for RTL synthesis tools (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64')
url="https://yosyshq.net/yosys"
msys2_repository_url="https://github.com/YosysHQ/yosys"
msys2_references=(
  'archlinux: yosys'
)
license=('spdx:ISC')
groups=("${MINGW_PACKAGE_PREFIX}-eda")
depends=(
  "${MINGW_PACKAGE_PREFIX}-cc-libs"
  "${MINGW_PACKAGE_PREFIX}-ghdl"
  "${MINGW_PACKAGE_PREFIX}-libwinpthread"
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-readline"
  "${MINGW_PACKAGE_PREFIX}-tcl"
  "${MINGW_PACKAGE_PREFIX}-zlib"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-pkgconf"
)
checkdepends=("${MINGW_PACKAGE_PREFIX}-iverilog")
_ghdl_plugin_commit="a8c3018956833a1f551ebf2d597b357624f5fc2e"
source=(
  "${_realname}-${pkgver}.tar.gz::https://github.com/YosysHQ/yosys/releases/download/v${pkgver}/yosys.tar.gz"
  "https://github.com/ghdl/ghdl-yosys-plugin/archive/${_ghdl_plugin_commit}/ghdl-yosys-plugin-${_ghdl_plugin_commit}.tar.gz"
)
sha256sums=('72abcf81925fc7232cba25d190a48d7be5079569ec2ba233bac2bb116c383eea'
            'a0e4bfa37164a8a34d34b4e805d78f08f8ea4cd29906dd0359e2443250df52d3')
noextract=(${_realname}-${pkgver}.tar.gz)

prepare() {
  mkdir -p ${_realname}-${pkgver}
  tar -xzf ${_realname}-${pkgver}.tar.gz -C ${_realname}-${pkgver}

  #cp -r "${srcdir}/ghdl-yosys-plugin-${_ghdl_plugin_commit}"/src ${_realname}-${pkgver}/frontends/ghdl
}

build() {
  [[ -d build-${MSYSTEM} ]] && rm -rf build-${MSYSTEM}
  cp -r ${_realname}-${pkgver} build-${MSYSTEM} && cd build-${MSYSTEM}

  make config-msys2-64

  make \
    PRETTY=0 \
    ENABLE_GHDL=1 \
    GHDL_PREFIX=${MINGW_PREFIX}
}

check() {
  cd build-${MSYSTEM}
  make test
}

package() {
  cd build-${MSYSTEM}

  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/COPYING \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
