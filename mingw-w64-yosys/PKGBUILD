
_realname=yosys
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.30
pkgrel=1
pkgdesc="A framework for RTL synthesis tools (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64')
license=('spdx:ISC')
url="http://www.clifford.at/yosys/"
groups=("${MINGW_PACKAGE_PREFIX}-eda")
depends=("${MINGW_PACKAGE_PREFIX}-ghdl"
         "${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-click")
checkdepends=("${MINGW_PACKAGE_PREFIX}-iverilog")
makedepends=("git"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-ghdl"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-python-pip")

_commit='f7a8284'
_plugin_commit='5b64ccf'
source=("https://github.com/YosysHQ/yosys/archive/refs/tags/${_realname}-${pkgver}.tar.gz"
        "ghdl-yosys-plugin::git+https://github.com/ghdl/ghdl-yosys-plugin.git#commit=${_plugin_commit}"
        "001-fix-tcl-flags.patch")
sha256sums=('1b29c9ed3d396046b67c48f0900a5f2156c6136f2e0651671d05ee26369f147d'
            'SKIP'
            'f4126af28f3d0e717e954dc96e3266cedadb0e2638628a5190d54c044fa22e0e')

prepare() {
  cd "${srcdir}/yosys-${_realname}-${pkgver}"

  patch -p1 -i "${srcdir}/001-fix-tcl-flags.patch"
}

build() {
  cd "${srcdir}/yosys-${_realname}-${pkgver}"

  if [ "$MSYSTEM_CARCH" = "i686" ]; then
    make config-msys2-32
  fi

  if [ "$MSYSTEM_CARCH" = "x86_64" ]; then
    make config-msys2-64
  fi

  mv "${srcdir}/ghdl-yosys-plugin/src" frontends/ghdl
  local _tclver=$(echo 'set a "[info tclversion]" ; puts $a ; exit' | $MINGW_PREFIX/bin/tclsh)  
  echo "ENABLE_GHDL=1" >> Makefile.conf
  echo "GHDL_PREFIX=${MINGW_PREFIX}" >> Makefile.conf
  echo "TCL_VERSION=tcl${_tclver}" >> Makefile.conf
  echo "PREFIX=${MINGW_PREFIX}" >> Makefile.conf

  make \
    GIT_REV="${_commit}" \
    PRETTY=0
}

check() {
  cd "${srcdir}/yosys-${_realname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/yosys-${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -vDm 644 ${srcdir}/yosys-${_realname}-${pkgver}/COPYING -t "${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname/"
}
