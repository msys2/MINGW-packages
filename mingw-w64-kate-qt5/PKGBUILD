# Maintainer: Mateusz Miku≈Ça <mati865@gmail.com>

_realname=kate
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=16.04.3
pkgrel=1
pkgdesc="Kate is a multi-document, multi-view text editor for KDE (mingw-w64)"
url='https://kate-editor.org/'
arch=('any')
license=('GPL')
depends=("${MINGW_PACKAGE_PREFIX}-kio-qt5")
makedepends=("${MINGW_PACKAGE_PREFIX}-extra-cmake-modules" "${MINGW_PACKAGE_PREFIX}-python3")
source=("http://download.kde.org/stable/applications/${pkgver}/src/${_realname}-${pkgver}.tar.xz")
sha256sums=('8df0040326469361d1f163616810c9c1a89dad675a81144dc3b5d3ae3f3f15a9')

build() {
  [[ -d "${srcdir}/build-${CARCH}" ]] && rm -rf "${srcdir}/build-${CARCH}"
  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"
  
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
	  -G'MSYS Makefiles' \
	  -DCMAKE_BUILD_TYPE=Release \
	  -DBUILD_TESTING=OFF \
	  -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
	  -DCMAKE_INSTALL_LIBDIR=lib \
	  ../${_realname}-${pkgver}
  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install
}
