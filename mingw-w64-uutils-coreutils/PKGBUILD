# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>
# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=coreutils
pkgbase=mingw-w64-uutils-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-uutils-${_realname}")
_commit=8b6613782b15607d4f302bddf55a57f84db4a9ad # supporting make LN= for hardlink
pkgver=0.2.2.g$_commit
pkgrel=1
pkgdesc="Cross-platform Rust rewrite of the GNU coreutils (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://github.com/uutils/coreutils'
msys2_references=(
  'archlinux: uutils-coreutils'
  'purl: pkg:cargo/coreutils'
)
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-oniguruma" "${MINGW_PACKAGE_PREFIX}-cc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust" "${MINGW_PACKAGE_PREFIX}-pkgconf")
source=("${url}/archive/${_commit}.tar.gz")
#source=("${url}/archive/${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('469cd4c306fe12c1d72f7858f3e68ee21899f54947e62a5a4bffe17f22a3d221 ')

prepare() {
  #cd "${_realname}-${pkgver}"
  cd "${_realname}-${_commit}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

export RUSTONIG_DYNAMIC_LIBONIG=1
export RUSTFLAGS="${RUSTFLAGS/+crt-static/-crt-static}"

# spliting build() sometimes cause building twice at make build
package() {
  #cd "${_realname}-${pkgver}"
  cd "${_realname}-${_commit}"

  make install \
    DESTDIR="${pkgdir}" \
    PREFIX="${MINGW_PREFIX}" \
    SKIP_UTILS='stty runcon chcon' \
    PROG_PREFIX=uu- \
    PROFILE=release \
    MULTICALL=y \
    LN="ln -vf"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/uutils-${_realname}/LICENSE"
}
