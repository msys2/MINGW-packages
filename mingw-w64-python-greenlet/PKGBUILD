# Maintainer: Andrew Sun <adsun701@gmail.com>

_realname=greenlet
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=3.2.4
pkgrel=1
pkgdesc="Lightweight in-process concurrent programming (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://greenlet.readthedocs.io"
msys2_repository_url="https://github.com/python-greenlet/greenlet/"
msys2_changelog_url="https://github.com/python-greenlet/greenlet/blob/master/CHANGES.rst"
msys2_references=(
  'anitya: 6870'
  'archlinux: python-greenlet'
  'gentoo: dev-python/greenlet'
  'purl: pkg:pypi/greenlet'
)
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools")
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('0dca0d95ff849f9a364385f36ab49f50065d76964944638be9691e1832e9f86d')

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm0644 LICENSE.PSF "${pkgdir}"${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE.PSF
}
