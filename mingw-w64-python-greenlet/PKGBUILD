# Maintainer: Andrew Sun <adsun701@gmail.com>

_realname=greenlet
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}"  "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=0.4.13
pkgrel=1
pkgdesc="Lightweight in-process concurrent programming (mingw-w64)"
arch=('any')
url="https://pypi.python.org/pypi/greenlet"
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools")
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/g/greenlet/${_realname}-${pkgver}.tar.gz")
sha256sums=('0fef83d43bf87a5196c91e73cb9772f945a4caaff91242766c5916d1dd1381e4')

prepare() {  
  cd "${srcdir}"
  for pver in {2,3}; do 
    rm -rf python${pver}-build-${CARCH} | true
    cp -r "${_realname}-${pkgver}" "python${pver}-build-${CARCH}"
  done  
}  
  
build() {
  cd "${srcdir}"
  for pver in {2,3}; do  
    msg "Python ${pver} build for ${CARCH}"  
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py build
  done  
}

package_python3-greenlet() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3")

  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
  
  install -Dm0644 LICENSE.PSF "${pkgdir}"${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE.PSF
}

package_python2-greenlet() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2")

  cd "${srcdir}/python2-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
  
  install -Dm0644 LICENSE.PSF "${pkgdir}"${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE.PSF
}

package_mingw-w64-i686-python2-greenlet() {
  package_python2-greenlet
}

package_mingw-w64-i686-python3-greenlet() {
  package_python3-greenlet
}

package_mingw-w64-x86_64-python2-greenlet() {
  package_python2-greenlet
}

package_mingw-w64-x86_64-python3-greenlet() {
  package_python3-greenlet
}
