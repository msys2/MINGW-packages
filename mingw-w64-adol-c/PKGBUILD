# Maintainer: Rafal Brzegowy <rafal.brzegowy@yahoo.com>

_realname=adol-c
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.7.2
pkgrel=6
pkgdesc='Automatic Differentiation of Algorithms written in C/C++ (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
depends=("${MINGW_PACKAGE_PREFIX}-boost"
         "${MINGW_PACKAGE_PREFIX}-omp")
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc")
license=('EPL')
url='https://github.com/coin-or/ADOL-C'
source=(https://github.com/coin-or/ADOL-C/archive/releases/${pkgver}.tar.gz)
sha256sums=('701E0856BAAE91B98397960D5E0A87A549988DE9D4002D0E9A56FA08F5455F6E')

prepare() {
  cd ${_realname}-releases-${pkgver}
  autoreconf -fi
}

build() {
  [[ -d "${srcdir}/build-${MSYSTEM}" ]] && rm -rf "${srcdir}/build-${MSYSTEM}"
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  local -a extra_config

  extra_config+=(--with-openmp-flag="-fopenmp")
  if [[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]]; then
    LDFLAGS+=" -lomp"
  fi

  ../${_realname}-releases-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --enable-static \
    --with-gnu-ld \
    --libdir=${MINGW_PREFIX}/lib \
    --with-boost=${MINGW_PREFIX} \
    "${extra_config[@]}"

  make
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make install DESTDIR="${pkgdir}"
  install -Dm644 ${srcdir}/${_realname}-releases-${pkgver}/LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}

# vim: ts=2 sw=2 et:
