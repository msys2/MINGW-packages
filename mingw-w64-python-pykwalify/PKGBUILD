# Maintainer: Joel Holdsworth <jholdsworth@nvidia.com>

_realname=pykwalify
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.8.0
pkgrel=3
pkgdesc="Python YAML/JSON schema validation library (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'aur: python-pykwalify'
  'gentoo: dev-python/pykwalify'
  'purl: pkg:pypi/pykwalify'
)
license=('spdx:MIT')
msys2_repository_url='https://github.com/grokzen/pykwalify/'
msys2_documentation_url='https://pykwalify.readthedocs.io/en/master/'
msys2_issue_tracker_url='https://github.com/grokzen/pykwalify/issues/'
url='https://github.com/grokzen/pykwalify'
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-dateutil"
         "${MINGW_PACKAGE_PREFIX}-python-docopt"
         "${MINGW_PACKAGE_PREFIX}-python-ruamel-yaml")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools")
options=('!strip')
source=("https://github.com/Grokzen/${_realname}/releases/download/${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('796b2ad3ed4cb99b88308b533fb2f559c30fa6efb4fa9fda11347f483d245884')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl
}
