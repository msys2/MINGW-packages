
_realname=elmerfem
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
pkgver=9.0.r4035.g82bb0e9
pkgrel=1
_commit=82bb0e9cc379f074037184d91676edd78a76d61a
pkgdesc="Finite element software for multiphysical problems (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://www.elmerfem.org/'
msys2_repository_url="https://github.com/ElmerCSC/elmerfem"
msys2_references=(
  'aur: elmerfem'
)
license=('spdx:GPL-2.0-or-later')
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}")
depends=("${MINGW_PACKAGE_PREFIX}-adios2"
         "${MINGW_PACKAGE_PREFIX}-anari-sdk"
         "${MINGW_PACKAGE_PREFIX}-boost"
         "${MINGW_PACKAGE_PREFIX}-cli11"
         "${MINGW_PACKAGE_PREFIX}-cgns"
         "${MINGW_PACKAGE_PREFIX}-eigen3"
         "${MINGW_PACKAGE_PREFIX}-fast_float"
         "${MINGW_PACKAGE_PREFIX}-ffmpeg"
         $([[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]] || echo "${MINGW_PACKAGE_PREFIX}-gcc-libgfortran")         
         "${MINGW_PACKAGE_PREFIX}-gl2ps"
         "${MINGW_PACKAGE_PREFIX}-liblas"
         "${MINGW_PACKAGE_PREFIX}-libmariadbclient"
         "${MINGW_PACKAGE_PREFIX}-netcdf-fortran"
         "${MINGW_PACKAGE_PREFIX}-opencascade"
         "${MINGW_PACKAGE_PREFIX}-openslide"
         "${MINGW_PACKAGE_PREFIX}-openvdb"
         "${MINGW_PACKAGE_PREFIX}-omp"
         "${MINGW_PACKAGE_PREFIX}-openblas"
         $([[ ${CARCH} == aarch64 ]] || echo "${MINGW_PACKAGE_PREFIX}-parmetis")
         "${MINGW_PACKAGE_PREFIX}-pdal"
         "${MINGW_PACKAGE_PREFIX}-qt6-declarative"
         "${MINGW_PACKAGE_PREFIX}-qwt-qt6"
         "${MINGW_PACKAGE_PREFIX}-scnlib"
         "${MINGW_PACKAGE_PREFIX}-suitesparse"
         "${MINGW_PACKAGE_PREFIX}-utf8cpp"
         "${MINGW_PACKAGE_PREFIX}-vtk"
         )
makedepends=(git
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-fc"             
             $([[ ${CARCH} == aarch64 ]] || echo "${MINGW_PACKAGE_PREFIX}-msmpi"))
source=("${_realname}"::"git+https://github.com/ElmerCSC/elmerfem.git#commit=${_commit}")
sha256sums=('971bad89b9b5070fdff8dcd29abf24967431080570f00d46678d82abac86477a')

_apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -p1 -i "${srcdir}/${_patch}"
  done
}

pkgver() {
  cd "${srcdir}/${_realname}"
  git describe --long --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g' | sed 's/release\.//g'
}

prepare() {
  cd ${_realname}

  # _apply_patch_with_msg \
  #   002-fix-build-with-newer-qt5.patch \
  #   003-Add-local-variable-i.patch \
  #   004-Fix-the-evaluation-of-tensor-components.patch \
  #   005-add-i-to-local-scope.patch \
  #   006-fix-some-compile-errors-in-DCRComplexSolve.patch \
  #   007-flang-logical-xor.patch \
  #   008-fix-openmp.patch
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  if [[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]]; then
    export FFLAGS="-I${MINGW_PREFIX}/include"
  fi

  if [[ ${CARCH} == aarch64 ]]; then
    _extra_config+=("-DWITH_MPI=OFF")
    _extra_config+=("-DWITH_ElmerIce=OFF")
  else
    _extra_config+=("-DWITH_MPI=ON")
    _extra_config+=("-DWITH_ElmerIce=ON")
  fi

  CFLAGS+=" -Wno-implicit-function-declaration -Wno-old-style-definition" \
  CXXFLAGS+=" -Wno-deprecated-declarations" \
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -Wno-dev \
      -G"Ninja" \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${_extra_config[@]}" \
      -DCPACK_BUNDLE_EXTRA_WINDOWS_DLLS=OFF \
      -DBUILD_SHARED_LIBS=ON \
      -DBLA_VENDOR="OpenBLAS" \
      -DHAVE_QP=OFF \
      -DWITH_ELMERGUI=ON \
      -DWITH_LUA=ON \
      -DWITH_MATC=OFF \
      -DWITH_Mumps=OFF \
      -DWITH_OCC=ON \
      -DWITH_OpenMP=ON \
      -DWITH_PARAVIEW=ON \
      -DWITH_QT6=ON \
      -DWITH_VTK=ON \
      -DWITH_Zoltan=OFF \
      -DCREATE_PKGCONFIG_FILE=ON \
      ../${_realname}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  "${MINGW_PREFIX}"/bin/ctest.exe -LE slow || true
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .

  WIN_PREFIX=$(cygpath -wm ${MINGW_PREFIX})
  for _f in elmerf90 elmerld; do
    sed -e "s|${WIN_PREFIX}|${MINGW_PREFIX}|g" -i "${pkgdir}${MINGW_PREFIX}"/bin/${_f}
  done

  rm "${pkgdir}${MINGW_PREFIX}"/bin/libarpack*
  rm "${pkgdir}${MINGW_PREFIX}"/bin/*.bat

  install -Dm644 "${srcdir}/${_realname}/license_texts/LICENSES" -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
  install -Dm644 "${srcdir}/${_realname}/license_texts/LICENSES_GPL.txt" -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
  install -Dm644 "${srcdir}/${_realname}/license_texts/GPL-2.txt" -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
  install -Dm644 "${srcdir}/${_realname}/license_texts/LGPL-2.1.txt" -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
}
