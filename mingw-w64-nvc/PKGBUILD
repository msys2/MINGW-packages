# Maintainer: Adam Barnes <ambarnes@gmail.com>

_realname=nvc
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
_version="1.7.0.r289"
_commit="134941af"
pkgver="${_version}.g${_commit}"
pkgrel=1
pkgdesc="VHDL compiler and simulator (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url='https://www.nickg.me.uk/nvc/'
license=('GPL3')
groups=("${MINGW_PACKAGE_PREFIX}-eda")
depends=(
  "${MINGW_PACKAGE_PREFIX}-llvm" # required by NVC for compiling
  "${MINGW_PACKAGE_PREFIX}-cc"   # required by NVC for linking
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-autotools"
  "git"
  "${MINGW_PACKAGE_PREFIX}-ncurses"
  "${MINGW_PACKAGE_PREFIX}-libffi"
  "${MINGW_PACKAGE_PREFIX}-check"
  "${MINGW_PACKAGE_PREFIX}-pkg-config"
)
source=("${_realname}-${pkgver}::git+https://github.com/nickg/nvc.git#commit=${_commit}")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_realname}-${pkgver}"
  git describe --long | sed 's/^r\(.*\)/\1/; s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ./autogen.sh
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  ../"${_realname}-${pkgver}"/configure \
    --prefix="${MINGW_PREFIX}" \
    --build="${MINGW_CHOST}" \
    --host="${MINGW_CHOST}" \
    --target="${MINGW_CHOST}" \
    --enable-static \
    --enable-shared \
    --program-prefix=

  make
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  make check
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  make install DESTDIR="${pkgdir}"
}
