# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=plfit
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.9.4
pkgrel=1
pkgdesc='Fitting power-law distributions to empirical data (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
msys2_references=(
  'archlinux: plfit'
)
url='https://github.com/ntamas/plfit/'
license=('spdx:GPL-2.0-or-later')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-ninja"
)
source=("${_realname}-${pkgver}.tar.gz::https://github.com/ntamas/plfit/archive/refs/tags/${pkgver}.tar.gz"
        https://github.com/ntamas/plfit/commit/56dff070af373a331241e0d474e8d2d168d031ad.patch)
sha256sums=('308fa174235f44dd4b32fefd5dce06bae20dfb96046d413c27faea7b1e9427d6'
            '47d50a20717a66eddb469d97af0e2235b984a3d18f1866fe3245662436259a56')

prepare() {
  cd "${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/56dff070af373a331241e0d474e8d2d168d031ad.patch"
}

build() {
  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${_extra_config[@]}" \
      -DBUILD_SHARED_LIBS=OFF \
      -S "${_realname}-${pkgver}" \
      -B "build-${MSYSTEM}-static"

  "${MINGW_PREFIX}"/bin/cmake.exe --build "build-${MSYSTEM}-static"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DCMAKE_DLL_NAME_WITH_SOVERSION=ON \
      "${_extra_config[@]}" \
      -DBUILD_SHARED_LIBS=ON \
      -S "${_realname}-${pkgver}" \
      -B "build-${MSYSTEM}-shared"

  "${MINGW_PREFIX}"/bin/cmake.exe --build "build-${MSYSTEM}-shared"
}

check() {
  "${MINGW_PREFIX}"/bin/ctest.exe --test-dir "build-${MSYSTEM}-static" || warning "Tests failed"
  "${MINGW_PREFIX}"/bin/ctest.exe --test-dir "build-${MSYSTEM}-shared" || warning "Tests failed"
}

package() {
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install "build-${MSYSTEM}-static"
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install "build-${MSYSTEM}-shared"

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
