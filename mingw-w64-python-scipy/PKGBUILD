# Contributor: Runar Tenfjord < runar dot tenfjord at gmail dot com >

_realname=scipy
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-$_realname" "${MINGW_PACKAGE_PREFIX}-python3-$_realname")
pkgver=0.14.0
pkgrel=2
pkgdesc="SciPy is open-source software for mathematics, science, and engineering. (mingw-w64)"
arch=('any')
url="http://www.scipy.org/"
license=('BSD')
makedepends=(
            "${MINGW_PACKAGE_PREFIX}-gcc-fortran"
            "${MINGW_PACKAGE_PREFIX}-openblas"
            "${MINGW_PACKAGE_PREFIX}-python2-numpy"
            "${MINGW_PACKAGE_PREFIX}-python3-numpy"
            "${MINGW_PACKAGE_PREFIX}-python2-setuptools"
            "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
)
source=(
    "https://pypi.python.org/packages/source/s/scipy/scipy-${pkgver}.tar.gz"
    fix-qhull.patch
)
md5sums=('d7c7f4ccf8b07b08d6fe49d5cd51f85d'
         '62e14262492e7fbdc3fb65d106619ce3')


prepare() {
  cd ${_realname}-$pkgver
  patch -Np1 -i ${srcdir}/fix-qhull.patch
  cd ..

  cp -a $_realname-$pkgver $_realname-py2-$pkgver
  cp -a $_realname-$pkgver $_realname-py3-$pkgver

  cd $_realname-py2-$pkgver
  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
    -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
    -i $(find . -name '*.py')
}

build() {
  export LDFLAGS="-Wall -shared"

  # build for python2
  cd ${srcdir}/$_realname-py2-$pkgver
  local result="fail"
  while [ "$result" = "fail" ]; do
    MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python2 setup.py config_fc --fcompiler=gnu95 build && result="success"
  done

  # build for python3
  cd ${srcdir}/$_realname-py3-$pkgver
  result="fail"
  while [ "$result" = "fail" ]; do
    MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py config_fc --fcompiler=gnu95 build && result="success"
  done
}

package_python2-scipy() {
  depends=(
        "${MINGW_PACKAGE_PREFIX}-openblas"
        "${MINGW_PACKAGE_PREFIX}-python2-numpy"
  )
  optdepends=("${MINGW_PACKAGE_PREFIX}-python2-nose: testsuite")

  cd $_realname-py2-$pkgver
  export LDFLAGS="-Wall -shared"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py config_fc --fcompiler=gnu95 install \
  --prefix=${MINGW_PREFIX} --root=${pkgdir} --optimize=1

  install -Dm644 LICENSE.txt \
    "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE"
}

package_python3-scipy() {
  depends=(
        "${MINGW_PACKAGE_PREFIX}-openblas"
        "${MINGW_PACKAGE_PREFIX}-python3-numpy"
  )
  optdepends=("${MINGW_PACKAGE_PREFIX}-python3-nose: testsuite")

  cd $_realname-py3-$pkgver
  export LDFLAGS="-Wall -shared"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py config_fc --fcompiler=gnu95 install \
  --prefix=${MINGW_PREFIX} --root=${pkgdir} --optimize=1

  install -Dm644 LICENSE.txt \
    "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}

package_mingw-w64-i686-python2-scipy() {
  package_python2-scipy
}

package_mingw-w64-i686-python3-scipy() {
  package_python3-scipy
}

package_mingw-w64-x86_64-python2-scipy() {
  package_python2-scipy
}

package_mingw-w64-x86_64-python3-scipy() {
  package_python3-scipy
}
