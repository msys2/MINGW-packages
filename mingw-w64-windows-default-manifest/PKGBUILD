# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=windows-default-manifest
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=6.4
pkgrel=5
pkgdesc='Default Windows application manifest (mingw-w64)'
url='https://cygwin.com/'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "git")
_GIT_TAG="release-6_4"
source=("git+https://sourceware.org/git/cygwin-apps/${_realname}.git#tag=${_GIT_TAG}"
        "001-drop-windows-vista.patch"
        "002-support-long-path.patch")
sha256sums=('SKIP'
            'c5925e14ed810cde6e010c5b21f3221551a50e931c90ae489776994ed6f0f941'
            '9e881bb8df185c40b4fb1de2ca428f79e105b110b102416ed9d027ca9edc375f')

prepare() {
  cd "${srcdir}"/${_realname}
  patch -p1 -i "${srcdir}"/001-drop-windows-vista.patch
  patch -p1 -i "${srcdir}"/002-support-long-path.patch
}

build() {
  [[ -d "${srcdir}"/build-${MSYSTEM} ]] && rm -rf "${srcdir}"/build-${MSYSTEM}
  cp -rf "${srcdir}"/${_realname} "${srcdir}"/build-${MSYSTEM}
  cd "${srcdir}"/build-${MSYSTEM}

  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}

  make
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}
  make DESTDIR=${pkgdir} install
}
