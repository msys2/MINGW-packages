# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=cp2k
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2026.1
pkgrel=1
pkgdesc="A quantum chemistry and solid state physics software package (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64')
url='https://www.cp2k.org'
msys2_repository_url="https://github.com/cp2k/cp2k"
license=('spdx:GPL-2.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-dbcsr"
         #"${MINGW_PACKAGE_PREFIX}-dftd4"
         "${MINGW_PACKAGE_PREFIX}-fftw"
         $([[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]] || echo "${MINGW_PACKAGE_PREFIX}-gcc-libgfortran")
         "${MINGW_PACKAGE_PREFIX}-hdf5"
         "${MINGW_PACKAGE_PREFIX}-libint2"
         "${MINGW_PACKAGE_PREFIX}-libxc"
         $([[ ${CARCH} == aarch64 ]] || echo "${MINGW_PACKAGE_PREFIX}-libxsmm")
         "${MINGW_PACKAGE_PREFIX}-mctc-lib"
         "${MINGW_PACKAGE_PREFIX}-omp"
         "${MINGW_PACKAGE_PREFIX}-openblas"
         #"${MINGW_PACKAGE_PREFIX}-opencl-icd"
         "${MINGW_PACKAGE_PREFIX}-spglib"
         "${MINGW_PACKAGE_PREFIX}-tblite")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-fc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-python")
             #"${MINGW_PACKAGE_PREFIX}-opencl-headers")
source=("https://github.com/cp2k/cp2k/releases/download/v${pkgver}/${_realname}-${pkgver}.tar.bz2"
        "001-fix-build-on-mingw-w64.patch"
        "002-fix-conflict-with-cmake-find-lapack.patch"
        "003-make-tblite-independent-from-dftd4.patch"
        "004-Fix-build-without-MPI.patch")
sha256sums=('4364c74bcffaa474bc234e11686b09550e4d06932acf2147a341e4f7679dd88e'
            'c52ad5fe1c2e128ba1773cacf2d0783f0b95b33b82a131f460a9aa2568576e30'
            'fb29a3611a197849655031ac62a54fa1f97c147aae3c00251fb99f73789f8751'
            'd2e282f673f5697e2fabb6041a5f7540c1b4556a8da57430c4200a8ee3c1ebe5'
            '957fa2c9dba75fbafa78db90ab9312de04de398bd48c1365e68fc4b8d8fa6656')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

prepare() {
  cd ${_realname}-${pkgver}
  apply_patch_with_msg \
    001-fix-build-on-mingw-w64.patch \
    002-fix-conflict-with-cmake-find-lapack.patch \
    003-make-tblite-independent-from-dftd4.patch \
    004-Fix-build-without-MPI.patch

  mv cmake/modules/FindLapack.cmake cmake/modules/Findcp2kLapack.cmake
  mv cmake/modules/FindBlas.cmake cmake/modules/Findcp2kBlas.cmake
}

build() {
  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  if [[ ${CARCH} == x86_64 ]]; then
    _extra_config+=("-DCP2K_USE_LIBXSMM=ON") #"-DCP2K_USE_ACCEL=OPENCL")
  fi

  FFLAGS=" -D__MINGW -D__NO_STATM_ACCESS -D__NO_SOCKETS" \
  CFLAGS+=" -D__NO_SOCKETS" \
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${_extra_config[@]}" \
      -DBUILD_SHARED_LIBS=ON \
      -DCP2K_USE_FFTW3=ON \
      -DCP2K_USE_HDF5=ON \
      -DCP2K_USE_LIBINT2=ON \
      -DCP2K_USE_LIBXC=ON \
      -DCP2K_USE_SPGLIB=ON \
      -DCP2K_USE_TBLITE=ON \
      -DCP2K_USE_MPI=OFF \
      -DCP2K_BLAS_VENDOR=OpenBLAS \
      -DPython_EXECUTABLE=${MINGW_PREFIX}/bin/python \
      -Dcustom-lapack_FOUND=TRUE \
      -S "${_realname}-${pkgver}" \
      -B "build-${MSYSTEM}"

  "${MINGW_PREFIX}"/bin/cmake.exe --build "build-${MSYSTEM}"
}

package() {
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install "build-${MSYSTEM}"

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/LICENSE \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
