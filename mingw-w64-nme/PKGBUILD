# Maintainer: Ruben Agin <phabrics@phabrics.com>

_realname=nme
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.12
pkgrel=1
pkgdesc="A networked, modular, extensible computer emulator for Sun2/3 and SPARC machines. (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://phabrics.github.io/tme.html"
msys2_repository_url="https://github.com/phabrics/nme"
license=('spdx:BSD-4-Clause')
conflicts=("${MINGW_PACKAGE_PREFIX}-tme")
provides=("${MINGW_PACKAGE_PREFIX}-tme")
replaces=("${MINGW_PACKAGE_PREFIX}-tme")
depends=("${MINGW_PACKAGE_PREFIX}-gtk4"
         "${MINGW_PACKAGE_PREFIX}-libvncserver"
         "${MINGW_PACKAGE_PREFIX}-libltdl"
         "${MINGW_PACKAGE_PREFIX}-sdl3")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools")
source=("https://github.com/phabrics/nme/releases/download/${pkgver}/${_realname}-${pkgver}-libopenvpn-2.3.8.tar.xz")
sha256sums=('68ec7becba2e2cbc8487ba1402d7ac4ab889df3b8b4b7e8d2bb7b2c106c84341')
options=('libtool')

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  if [[ ${CARCH} == aarch64 ]]; then
    CPPFLAGS+=" -Dtme_misc_x86_cycles=tme_misc_def_cycles"
  fi
  
  ../"${_realname}-${pkgver}"/configure \
    --prefix="${MINGW_PREFIX}" \
    --enable-threads=sdl \
    --disable-ltdl-install

  make
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  make install DESTDIR="${pkgdir}"

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/COPYING \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
