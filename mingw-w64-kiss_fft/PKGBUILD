# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Sandro Cumerlato <sandro.cumerlato@gmail.com>

_realname=kiss_fft
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
_uglyver=1_3_0
pkgver=${_uglyver//_/.}
pkgrel=2
pkgdesc="A Fast Fourier Transform based up on the principle: Keep It Simple, Stupid (mingw-w64)"
url="http://kissfft.sourceforge.net/"
arch=('any')
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
source=("http://sourceforge.net/projects/kissfft/files/kissfft/v${_uglyver}/kiss_fft${_uglyver//_/}.tar.gz")
md5sums=('8497ac4c6eb423ed2f984516b0906d24')

prepare() {
  cd "kiss_fft${_uglyver//_/}"
  make clean
}

build() {
  cd "kiss_fft${_uglyver//_/}"
  gcc -shared ${CFLAGS} -ffast-math -fomit-frame-pointer -dA -fverbose-asm -o kiss_fft.dll -Wl,--out-implib,libkiss_fft.dll.a kiss_fft.c
}

package() {
  mkdir -p ${pkgdir}${MINGW_PREFIX}/{bin,lib,include}
  mkdir -p ${pkgdir}${MINGW_PREFIX}/include/${_realname}
  cd "${srcdir}/kiss_fft${_uglyver//_/}"
  install -m644 kiss_fft.dll "${pkgdir}${MINGW_PREFIX}/bin/"
  install -m644 libkiss_fft.dll.a "${pkgdir}${MINGW_PREFIX}/lib/"
  install -m644 kiss_fft.h "${pkgdir}${MINGW_PREFIX}/include/${_realname}"
}
