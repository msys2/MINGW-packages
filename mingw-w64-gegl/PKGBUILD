# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=gegl
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.4.34
pkgrel=4
pkgdesc="Generic Graphics Library (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://gegl.org/"
license=("GPL3" "LGPL3")
makedepends=("${MINGW_PACKAGE_PREFIX}-asciidoc"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             "${MINGW_PACKAGE_PREFIX}-gtk-doc"
             "${MINGW_PACKAGE_PREFIX}-luajit"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-ruby"
             "${MINGW_PACKAGE_PREFIX}-vala"
             "${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-openmp")
depends=("${MINGW_PACKAGE_PREFIX}-babl"
         "${MINGW_PACKAGE_PREFIX}-cairo"
         "${MINGW_PACKAGE_PREFIX}-exiv2"
         "${MINGW_PACKAGE_PREFIX}-ffmpeg"
         "${MINGW_PACKAGE_PREFIX}-gexiv2"
         "${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2"
         "${MINGW_PACKAGE_PREFIX}-gettext"
         "${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-jasper"
         "${MINGW_PACKAGE_PREFIX}-json-glib"
         "${MINGW_PACKAGE_PREFIX}-libjpeg"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libraw"
         "${MINGW_PACKAGE_PREFIX}-librsvg"
         "${MINGW_PACKAGE_PREFIX}-libspiro"
         "${MINGW_PACKAGE_PREFIX}-libwebp"
         "${MINGW_PACKAGE_PREFIX}-lcms"
         "${MINGW_PACKAGE_PREFIX}-lensfun"
         "${MINGW_PACKAGE_PREFIX}-maxflow"
         "${MINGW_PACKAGE_PREFIX}-openexr"
         "${MINGW_PACKAGE_PREFIX}-pango"
         "${MINGW_PACKAGE_PREFIX}-SDL2"
         "${MINGW_PACKAGE_PREFIX}-suitesparse")
#options=('!strip' 'debug')
noextract=("${_realname}-${pkgver}.tar.xz")
source=(https://download.gimp.org/pub/gegl/${pkgver%.*}/${_realname}-${pkgver}.tar.xz)
sha256sums=('ef63f0bca5b431c6119addd834ca7fbb507c900c4861c57b3667b6f4ccfcaaaa')

prepare() {
  cd "${srcdir}"
  tar -xf "${_realname}-${pkgver}.tar.xz" || true

# The tar utility cannot extract gegl_crop.lua and gegl_fill-path.lua from
# the archive because they are symblic links (to gegl_rectangle.lua and
# gegl_vector-stroke.lua, respectively.
#
# Here we just make copies to work around that.

  cd "${srcdir}/${_realname}-${pkgver}"
  cp bin/lua/gegl_rectangle.lua bin/lua/gegl_crop.lua
  cp bin/lua/gegl_vector-stroke.lua bin/lua/gegl_fill-path.lua
}

build() {
  [[ -d build-${MSYSTEM} ]] && rm -rf build-${MSYSTEM}
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

# FIXME: re-enable docs after this issue is fixed:
# https://gitlab.gnome.org/GNOME/gegl/-/issues/294

  MSYS2_ARG_CONV_EXCL="--prefix=" \
  meson \
    --prefix="${MINGW_PREFIX}" \
    --buildtype=plain \
    --auto-features=enabled \
    -Dlibv4l=disabled \
    -Dlibv4l2=disabled \
    -Dmrg=disabled \
    -Dpygobject=disabled \
    -Dsdl1=disabled \
    -Dintrospection=true \
    -Ddocs=false \
    ../${_realname}-${pkgver}

  ninja
}

package() {
  cd build-${MSYSTEM}
  DESTDIR="${pkgdir}" ninja install

  rm "${pkgdir}${MINGW_PREFIX}"/lib/gegl-*/*.dll.a
}
