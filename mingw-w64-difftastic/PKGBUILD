# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=difftastic
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-docs")
pkgver=0.67.0
pkgrel=1
pkgdesc="A structural diff that understands syntax (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://difftastic.wilfred.me.uk'
msys2_repository_url='https://github.com/Wilfred/difftastic'
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-mdbook"
             'git')
source=("git+${msys2_repository_url}#tag=${pkgver}"
        "remove-makedepends-jq.patch"
        "dont-use-jemalloc.patch")
sha256sums=('d7f053d430cf3e029e3cf8944ab02f11a9ef99b7cf2e8e7bf6cc85aebd7ff0fd'
            'f72984fbbdce964f71cf7f69ed02fcdde5a0701b7cbd498bd88375476c4b918c'
            '4de77c64e0ee3bdbcaa1476798c5223b4ab89fa6e7a6abfa235bccfcb4c83031')

prepare() {
  cd "${_realname}"

  # so tree-sitter grammars can be built
  if test true != "$(git config core.symlinks)"; then
    git config core.symlinks true &&
    MSYS='winsymlinks:nativestrict' git restore --source=HEAD :/
  fi

  patch -Np1 -i ../remove-makedepends-jq.patch
  patch -p1 -i ../dont-use-jemalloc.patch

  rm rust-toolchain.toml
  cargo fetch --locked --target "${RUST_CHOST}"
}

build() {
  cd "${_realname}"

  export RUSTFLAGS="${RUSTFLAGS/+crt-static/-crt-static}"
  cargo build --release --frozen

  # documentation
  cd manual
  sed -i "s/DFT_VERSION_HERE/${pkgver}/g" -i src/introduction.md
  mdbook build
}

check() {
  cd "${_realname}"

  export RUSTFLAGS="${RUSTFLAGS/+crt-static/-crt-static}"
  cargo test --release --frozen
}

package_difftastic() {
  cd "${_realname}"

  export RUSTFLAGS="${RUSTFLAGS/+crt-static/-crt-static}"
  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 difft.1 "${pkgdir}${MINGW_PREFIX}/share/man/man1/difft.1"
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}

package_difftastic-docs() {
  pkgdesc+=' (Documentation)'
  depends=()

  mkdir -p "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/manual"
  cp -vr "${_realname}"/manual/book "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/manual"
}

# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
