_realname=yubico-piv-tool
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.7.3
pkgrel=1
pkgdesc="Command line tool for the YubiKey PIV application (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://developers.yubico.com/yubico-piv-tool/'
msys2_repository_url='https://github.com/Yubico/yubico-piv-tool'
license=('BSD')
msys2_references=(
  'archlinux: yubico-piv-tool'
  'anitya: 6998'
  'gentoo: sys-auth/yubico-piv-tool'
)
depends=(
  "${MINGW_PACKAGE_PREFIX}-openssl"
)
optdepends=("${MINGW_PACKAGE_PREFIX}-p11-kit")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "gengetopt"
             "${MINGW_PACKAGE_PREFIX}-openssl"
#             "help2man"
             "${MINGW_PACKAGE_PREFIX}-check")
validpgpkeys=('1D7308B0055F5AEF36944A8F27A9C24D9588EA0F') # Aveen Ismail <aveen.ismail@yubico.com>
source=("https://developers.yubico.com/${_realname}/Releases/${_realname}-${pkgver}.tar.gz"{,.sig}
        "0001-Separate-WIN32-and-MSVC-to-allow-for-MSYS2-gcc-build.patch"
        "0002-Extra-define-for-mingw.patch"
        # "0002-Extra-define-and-cast-away-const-for-BCryptDeriveKey.patch"
        # "0003-perl-help2man.patch"
        # "0004-ecdh_c-pointer.patch"
        "ykcs11.module")
noextract=("${_realname}-${pkgver}.tar.gz")
sha256sums=('fcb25c42f54298ece8b20684fb3c581ed9195a162cbc55180a4161501be93181'
            'SKIP'
            '5e455650ac2047754acca0537d26782c1c1b858fdd895d12a2cd90524d2067e5'
            'cb6cffb9485b7f152546420ea4f85243878dbb59e63072ab0305f9481e3d0c65'
            '2b0b1e3ded93d3f88190e9f0ffef698ebc48ff1e5db85187759e487d6b87b1bd')

prepare() {
  bsdtar -xf "${_realname}-${pkgver}.tar.gz" --exclude README.adoc
  cd "${_realname}-${pkgver}"
  patch -Np1 -i "${srcdir}"/0001-Separate-WIN32-and-MSVC-to-allow-for-MSYS2-gcc-build.patch
  patch -Np1 -i "${srcdir}"/0002-Extra-define-for-mingw.patch
  # patch -Np2 -i "${srcdir}"/0003-perl-help2man.patch
  # patch -Np2 -i "${srcdir}"/0004-ecdh_c-pointer.patch
}

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  # -DHELP2MAN_LOCATION="perl /usr/bin/help2man" \
#      -DZLIB_LIB_DIR="${MINGW_PREFIX}/lib" \
# 
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -GNinja \
      -DCMAKE_C_FLAGS="-Wno-error=unknown-pragmas -Wno-error=shadow -Wno-incompatible-pointer-types" \
      -DGENERATE_MAN_PAGES=OFF \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -S "${_realname}-${pkgver}" \
      -B "build-${MSYSTEM}"

  cmake --build "build-${MSYSTEM}"
}

check() {
  cd "build-${MSYSTEM}"
  PATH="$PWD/lib:$PWD/ykcs11:$PATH" cmake --build . --target test
}

package() {
  DESTDIR="${pkgdir}" cmake --install "build-${MSYSTEM}"

  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
  # See https://aur.archlinux.org/packages/ykcs11-p11-kit-module
  # so that p11-kit, p11tool, etc can use libykcs11 out of the box
  # also https://github.com/Yubico/yubico-piv-tool/issues/92
  mkdir -p ${pkgdir}${MINGW_PREFIX}/share/p11-kit/modules
  install -Dm644 ${srcdir}/ykcs11.module ${pkgdir}${MINGW_PREFIX}/share/p11-kit/modules/ykcs11.module
}
