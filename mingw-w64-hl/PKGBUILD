# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=hl
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.36.0
pkgrel=1
pkgdesc="A fast and powerful log viewer and processor (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://github.com/pamburus/hl'
msys2_repository_url='https://github.com/pamburus/hl'
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-zstd")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-capnproto"
             "${MINGW_PACKAGE_PREFIX}-openssl"
             'git')
source=("git+${url}#tag=v${pkgver}"
        "zstd-sys.tar.gz::https://crates.io/api/v1/crates/zstd-sys/2.0.16+zstd.1.5.7/download"
        "zstd-sys-remove-statik.patch")
sha256sums=('8bc0e32dafa07556d3ec1f47f85102e0feade6ab9cf6bedafb96bc422b183c36'
            '91e19ebc2adc8f83e43039e79776e3fda8ca919132d68a1fed6a5faca2683748'
            '48f4900ceb02d3aaf9a1020f33d56629156e96759f456c0e7ca18bfcf910767b')

_env() {
  export OPENSSL_NO_VENDOR=1
  export ZSTD_SYS_USE_PKG_CONFIG=1
  export RUSTFLAGS="${RUSTFLAGS/+crt-static/-crt-static}"
}

prepare() {
  cd "${_realname}"

  rm rust-toolchain.toml

  patch -d ../zstd-sys-2.0.16+zstd.1.5.7 -i ../zstd-sys-remove-statik.patch
  cat >> Cargo.toml <<END

[patch.crates-io]
zstd-sys.path = "../zstd-sys-2.0.16+zstd.1.5.7"
END

  cargo update -p zstd-sys --config='net.git-fetch-with-cli=true'
  cargo fetch --locked --target "${RUST_CHOST}"
}

build() {
  cd "${_realname}"

  _env
  cargo build --release --frozen
}

check() {
  cd "${_realname}"

  _env
  cargo test --release --frozen
}

package() {
  cd "${_realname}"

  _env
  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
