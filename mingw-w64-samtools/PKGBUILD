# Contributor: Teemu NÃ¤tkinniemi ("teepean") <tnatkinn@gmail.com>

_realname=samtools
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.12
pkgrel=1
pkgdesc="tools for manipulating next-generation sequencing data"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
url="http://www.htslib.org/"
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-htslib>=1.12"
         "${MINGW_PACKAGE_PREFIX}-ncurses"
         "${MINGW_PACKAGE_PREFIX}-xz"
         "${MINGW_PACKAGE_PREFIX}-bzip2"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-libdeflate")
source=(https://github.com/samtools/samtools/releases/download/$pkgver/$_realname-$pkgver.tar.bz2)
sha256sums=('6da3770563b1c545ca8bdf78cf535e6d1753d6383983c7929245d5dba2902dcb')

prepare() {
  cd $srcdir/$_realname-$pkgver

  # prevent shipped htslib to be used
  rm -rf htslib-$pkgver || exit 1
}

build() {
  cd $srcdir/$_realname-$pkgver

  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --with-htslib=system

  make
}

package() {
  cd $srcdir/$_realname-$pkgver

  make DESTDIR=$pkgdir install

  install -d $pkgdir${MINGW_PREFIX}/include/bam
  install -Dm644 *.h $pkgdir${MINGW_PREFIX}/include/bam

  install -Dm644 LICENSE $pkgdir${MINGW_PREFIX}/share/licenses/$_realname/LICENSE
}
