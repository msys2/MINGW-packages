# Maintainer: Zoltan Gyarmati <zgyarmati@zgyarmati.de>

_realname=dwarfstack
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.2
pkgrel=1
pkgdesc="Provides stacktrace with source file/line information (mingw-w64)"
arch=('any')
mingw_arch=( 'mingw64' 'ucrt64' 'clang64')
url="https://github.com/ssbssa/dwarfstack"
license=('spdx:LGPL-2.1-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc")
source=("https://github.com/ssbssa/${_realname}/archive/${pkgver}/${_realname}-${pkgver}.tar.gz.tar.gz")
sha256sums=('1fca1d12756941c4c932b50f9abe56a3756f012a1e93deef553e141a78cc2709')

build() {
  cp -r "${srcdir}/${_realname}-${pkgver}" "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  make CC=${CC}
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make install PREFIX="${pkgdir}${MINGW_PREFIX}"
  install -D -m644 "${srcdir}/${_realname}-${pkgver}/LICENSE.txt" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
