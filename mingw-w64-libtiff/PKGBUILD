# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

_realname=libtiff
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.0.9
pkgrel=2
pkgdesc="Library for manipulation of TIFF images (mingw-w64)"
arch=('any')
url="http://www.simplesystems.org/libtiff/"
license=(MIT)
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
#         "${MINGW_PACKAGE_PREFIX}-jbigkit"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-xz"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('staticlibs' 'strip')
source=(http://download.osgeo.org/libtiff/tiff-${pkgver}.tar.gz
        CVE-2017-18013.patch
        CVE-2018-5784.patch
        CVE-2018-7456.patch
        CVE-2018-8905.patch
        CVE-2018-10963.patch)
sha256sums=('6e7bdeec2c310734e734d19aae3a71ebe37a4d842e0e23dbb1b8921c0026cfcd'
            'ea49366f085d324073bdbcc183db9b190e7ff4178649daf158e91ea1cfb12fef'
            'b93ff957acc1236481e9c5f33844334c62f3636d575f5f2cc418491349ebe7a5'
            '2e00f46633e205fb6ed04957f07c5b13e3cc527876076c3cbda80f3894220909'
            '8aed7c35dd2fb79af16a704a95cddff2852f85dbb019e9e9cdb54638b529935b'
            'f3c46de22050adad6da91498c549562af833d48e84028f36da5ec0387534e8af')

prepare() {
  cd tiff-$pkgver
# Security fixes
  patch -p1 -i ../CVE-2017-18013.patch
  patch -p1 -i ../CVE-2018-5784.patch
  patch -p1 -i ../CVE-2018-7456.patch
  patch -p1 -i ../CVE-2018-8905.patch
  patch -p1 -i ../CVE-2018-10963.patch
}

build() {
  export CFLAGS+=" -fno-strict-aliasing"
  export CXXFLAGS+=" -fno-strict-aliasing"

  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  ../tiff-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared \
    --enable-cxx \
    --disable-jbig \
    --without-x

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  cp ${srcdir}/tiff-${pkgver}/libtiff/{tiffiop,tif_dir}.h "${pkgdir}${MINGW_PREFIX}/include/"
  cp "libtiff/tif_config.h" "${pkgdir}${MINGW_PREFIX}/include/"

  # License
  # See https://fedoraproject.org/wiki/Licensing:MIT#Hylafax_Variant
  install -Dm644 "${srcdir}/tiff-${pkgver}/COPYRIGHT" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYRIGHT"
}
