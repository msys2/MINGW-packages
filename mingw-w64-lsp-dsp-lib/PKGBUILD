# Maintainer: Alexandros Theodotou <alex at zrythm dot org>

_realname=lsp-dsp-lib
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.0.1
pkgrel=1
pkgdesc='SIMD-optimized library for digital signal processing'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
options=('strip' 'staticlibs' '!makeflags')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc")
license=('LGPL3')
url="https://github.com/lsp-plugins/${_realname}"
source=("https://github.com/lsp-plugins/$_realname/releases/download/$pkgver/$_realname-src-$pkgver.tar.gz")
sha256sums=('78e3c8a3037ad8fd280c1c20f71f7df46e3582630aa2fdef10f984dc61f739a5')

build() {
  cd "${srcdir}/${_realname}"

  architecture=${CARCH}
  if [[ "${CARCH}" == "i386" ]]; then
    architecture=i586
  fi

  make config PREFIX=${MINGW_PREFIX} ARCHITECTURE=${architecture} X_CXX_TOOL=${CXX} X_CC_TOOL=${CC}
  make
}

package() {
  cd "${srcdir}/${_realname}"

  make DESTDIR=${pkgdir} install
  mkdir -p "${pkgdir}${MINGW_PREFIX}/bin"
  mv "${pkgdir}${MINGW_PREFIX}/lib/"*.dll "${pkgdir}${MINGW_PREFIX}/bin"

  for file in COPYING COPYING.LESSER; do
    install -Dm644 $file "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/$file"
  done
}
