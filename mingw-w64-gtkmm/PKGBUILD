# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gtkmm
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=2.24.4
pkgrel=2
pkgdesc="C++ bindings for gtk2 (mingw-w64)"
arch=('any')
url="http://www.gtkmm.org"
license=("LGPL")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config")
depends=("${_mingw_suff}-atkmm" "${_mingw_suff}-pangomm" "${_mingw_suff}-gtk2")
options=(staticlibs strip)
source=("http://ftp.gnome.org/pub/GNOME/sources/gtkmm/${pkgver%.*}/gtkmm-$pkgver.tar.xz")
md5sums=('b9ac60c90959a71095f07f84dd39961d')

build() {
	CPPFLAGS+=" -D_REENTRANT"
	mkdir -p "${srcdir}/build-${MINGW_CHOST}"
	cd "${srcdir}/build-${MINGW_CHOST}"
	../${_realname}-${pkgver}/configure \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--enable-shared \
		--enable-static \
		--disable-documentation
	make
}

package() {
	cd "${srcdir}/build-${MINGW_CHOST}"
	make DESTDIR="$pkgdir" install
	find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
}
