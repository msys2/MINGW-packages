# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=rxspencer
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=alpha3.8.g6
pkgrel=2
pkgdesc="Henry Spencer's BSD regular expression library modified to allow building as a shared library (mingw-w64)"
arch=('any')
url="https://garyhouston.github.io/regex/"
options=('strip' 'staticlibs')
license=('custom')
source=(${_realname}-${pkgver}.tar.gz::https://github.com/garyhouston/${_realname}/archive/${pkgver}.tar.gz)
noextract=(${_realname}-${pkgver}.tar.gz)
sha256sums=('2176def84f876b3abd845543e3b2f6bca8c5a2c7e8bd4e9034f179e67d754a4d')

prepare() {
  tar --exclude=ltmain.sh -zxf ${_realname}-${pkgver}.tar.gz

  cd "${srcdir}"/${_realname}-${pkgver}
  autoreconf -f -i
}

build() {
  [[ -d "${srcdir}"/build-${MINGW_CHOST} ]] && rm -rf "${srcdir}"/build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install

  mkdir ${pkgdir}/${MINGW_PREFIX}/bin
  mv ${pkgdir}${MINGW_PREFIX}/lib/librxspencer-0.dll ${pkgdir}/${MINGW_PREFIX}/bin

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYRIGHT" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYRIGHT"
}
