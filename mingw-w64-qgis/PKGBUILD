# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=qgis
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.36.2
pkgrel=4
pkgdesc='Geographic Information System (GIS) that supports vector, raster & database formats (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://qgis.org/'
msys2_repository_url="https://github.com/qgis/QGIS"
msys2_references=(
  'archlinux: qgis'
)
license=('spdx:GPL-2.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-draco"
         "${MINGW_PACKAGE_PREFIX}-gdal"
         "${MINGW_PACKAGE_PREFIX}-gsl"
         "${MINGW_PACKAGE_PREFIX}-exiv2"
         "${MINGW_PACKAGE_PREFIX}-hdf5"
         "${MINGW_PACKAGE_PREFIX}-libspatialite"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-libzip"
         "${MINGW_PACKAGE_PREFIX}-netcdf"
         "${MINGW_PACKAGE_PREFIX}-opencl-icd"
         "${MINGW_PACKAGE_PREFIX}-pdal"
         "${MINGW_PACKAGE_PREFIX}-proj"
         "${MINGW_PACKAGE_PREFIX}-protobuf"
         "${MINGW_PACKAGE_PREFIX}-python-gdal"
         "${MINGW_PACKAGE_PREFIX}-python-owslib"
         "${MINGW_PACKAGE_PREFIX}-python-pyqt5"
         "${MINGW_PACKAGE_PREFIX}-python-qscintilla-qt5"
         "${MINGW_PACKAGE_PREFIX}-qca-qt5"
         "${MINGW_PACKAGE_PREFIX}-qt5-3d"
         "${MINGW_PACKAGE_PREFIX}-qt5-base"
         "${MINGW_PACKAGE_PREFIX}-qt5-declarative"
         "${MINGW_PACKAGE_PREFIX}-qt5-gamepad"
         "${MINGW_PACKAGE_PREFIX}-qt5-location"
         "${MINGW_PACKAGE_PREFIX}-qt5-multimedia"
         "${MINGW_PACKAGE_PREFIX}-qt5-serialport"
         "${MINGW_PACKAGE_PREFIX}-qt5-svg"
         "${MINGW_PACKAGE_PREFIX}-qtkeychain-qt5"
         $([[ ${CARCH} == aarch64 ]] || echo "${MINGW_PACKAGE_PREFIX}-qtwebkit")
         "${MINGW_PACKAGE_PREFIX}-qscintilla-qt5"
         "${MINGW_PACKAGE_PREFIX}-qwt-qt5"
         "${MINGW_PACKAGE_PREFIX}-spatialindex")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-opencl-clhpp"
             "${MINGW_PACKAGE_PREFIX}-sip"
             "${MINGW_PACKAGE_PREFIX}-pyqt-builder"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-qt5-tools")
source=("https://qgis.org/downloads/${_realname}-$pkgver.tar.bz2"
        "001-fix-building-with-mingw-w64-clang.patch"
        "002-fix-settingstree-init-order-clang.patch"
        "003-fix-pyrcc-command.patch"
        "004-fix-customwidgets-install.patch"
        "005-fix-building-qgis-with-pdal.patch")
sha256sums=('8c886ab59094d7ea7fff5c299173b969509b88d896880d2c5d818c400cee69a4'
            'f805b861d4081299cb319e85ee8c4841942b6ad798e549be32280f65c04fcf6e'
            'e880eef26fa06d8fae291346f87735e140145c2d65558fce69e480e303c629e2'
            'c0f599996d90da55d1370f7144d545352d0075ca7876d4ade52c520b237ce318'
            '216dc67ef707f2e918afb7d190c9ea75c11065dea0d23d11b94cab5d8463bac3'
            'b582354d836a228e965cc18de9656493330dd1d44b1e9210a6466811eba59d51')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  if [[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]]; then
    apply_patch_with_msg \
      001-fix-building-with-mingw-w64-clang.patch \
      002-fix-settingstree-init-order-clang.patch
  fi
  apply_patch_with_msg \
    003-fix-pyrcc-command.patch \
    004-fix-customwidgets-install.patch
  # https://github.com/PDAL/wrench/pull/35
  # https://github.com/hobuinc/untwine/pull/162
  apply_patch_with_msg \
    005-fix-building-qgis-with-pdal.patch
}

build() {
  mkdir -p ${srcdir}/build-${MSYSTEM} && cd ${srcdir}/build-${MSYSTEM}

  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  if [[ ${CARCH} == aarch64 ]]; then
    _extra_config+=("-DWITH_QTWEBKIT=OFF")
  fi

  CXXFLAGS+=" -Wno-ignored-attributes"
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_CXX_STANDARD=17 \
    "${_extra_config[@]}" \
    -DPython_EXECUTABLE=${MINGW_PREFIX}/bin/python \
    -DQGIS_BIN_SUBDIR=bin \
    -DQGIS_DATA_SUBDIR=share \
    -DQGIS_LIBEXEC_SUBDIR=bin \
    -DQGIS_PLUGIN_SUBDIR=lib/qgis/plugins \
    -DQT_PLUGINS_DIR=share/qt5/plugins \
    -DWITH_3D=ON \
    -DWITH_BINDINGS=ON \
    -DWITH_CUSTOM_WIDGETS=ON \
    -DWITH_PDAL=ON \
    -DWITH_QWTPOLAR=ON \
    -DENABLE_TESTS=OFF \
    -DWITH_GRASS=OFF \
    -DHAS_KDE_QT5_PDF_TRANSFORM_FIX=TRUE \
    -DHAS_KDE_QT5_SMALL_CAPS_FIX=TRUE \
    -DHAS_KDE_QT5_FONT_STRETCH_FIX=TRUE \
    ../${_realname}-$pkgver

  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .

  install -Dm644 "${srcdir}"/${_realname}-$pkgver/COPYING "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
