# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=mman-win32
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.0.2
pkgrel=1
arch=('any')
pkgdesc="A light implementation of the mmap functions for MinGW (mingw-w64)"
url="https://github.com/mcgarrah/mman-win32"
depends=()
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('strip' 'staticlibs')
source=(https://github.com/mcgarrah/${_realname}/archive/$pkgver.tar.gz
        001-win.patch)
sha256sums=('90984842581fb554fd445bec9744e4a242798422203b7906673bd3b810fa02f6'
            'c755cacd29a350e52d87952899e5330dbb7bd98e01f77ee88a3bcd9653d3ce0d')

prepare() {
  cd ${_realname}-${pkgver}
  patch --binary -p1 -i ${srcdir}/001-win.patch
}

build() {
  mkdir -p build-${MINGW_CHOST}

  cd ${_realname}-${pkgver}
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --libdir=${MINGW_PREFIX}/lib \
    --incdir=${MINGW_PREFIX}/${MINGW_CHOST}/include/sys
  make
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR=$pkgdir install
}
