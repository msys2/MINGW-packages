_realname=templates_parser
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.0
pkgrel=1

pkgdesc="Ada tools to create templated text streams, such as dynamic HTML documents (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64')
msys2_references=(
  'aur: templates_parser'
  'gentoo: dev-ada/templates-parser'
)

url="https://github.com/AdaCore/templates-parser"
license=('spdx:GPL-3.0-or-later WITH GCC-exception-3.1')
makedepends=("${MINGW_PACKAGE_PREFIX}-gprbuild"
             "${MINGW_PACKAGE_PREFIX}-python-sphinx"
             "${MINGW_PACKAGE_PREFIX}-python-sphinx_rtd_theme"
             "which")
depends=("${MINGW_PACKAGE_PREFIX}-gnatcoll-gmp")
_repo_name=templates-parser
source=("https://github.com/AdaCore/${_repo_name}/archive/v${pkgver}/${_repo_name}-${pkgver}.tar.gz")
sha256sums=('28caf00cfd7039f91055c726c91c45483fefae20eafaef3a91cb026e4343ee97')

prepare() {
  cd "${srcdir}/${_repo_name}-${pkgver}"
  make DEFAULT_LIBRARY_TYPE=relocatable prefix="${pkgdir}${MINGW_PREFIX}" setup
}

build() {
  cd "${srcdir}/${_repo_name}-${pkgver}"
  make -j1 build DEFAULT_LIBRARY_TYPE=relocatable build-doc DOC_FORMATS=html
}

package() {
  cd "${srcdir}/${_repo_name}-${pkgver}"
  make -j1 install prefix="${pkgdir}${MINGW_PREFIX}" DOC_FORMATS=html
  # Copy License Files
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" \
    "${srcdir}/${_repo_name}-${pkgver}"/COPYING*
}
