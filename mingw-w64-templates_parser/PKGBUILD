_realname=templates_parser
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.r941.53cff2e
pkgrel=1
_branch=24.0

pkgdesc="Template parser package for Ada (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
msys2_references=(
  'aur: templates_parser'
)

url="https://github.com/AdaCore/templates-parser"
license=('GPL-3.0-only')
makedepends=("${MINGW_PACKAGE_PREFIX}-gprbuild"
             "${MINGW_PACKAGE_PREFIX}-python-sphinx"
             "${MINGW_PACKAGE_PREFIX}-python-sphinx_rtd_theme"
             "which")
depends=("${MINGW_PACKAGE_PREFIX}-gnatcoll" # I mean "${MINGW_PACKAGE_PREFIX}-gnatcoll-gmp"
        #  "${MINGW_PACKAGE_PREFIX}-langkit"
         )
source=("${_realname}-git::git+https://github.com/AdaCore/templates-parser#branch=${_branch}")
sha256sums=('SKIP')

pkgver() {
  cd ${_realname}-git
  printf "%s.r%s.%s" "$_branch" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

prepare() {
  cd "${srcdir}/${_realname}-git"
  make DEFAULT_LIBRARY_TYPE=relocatable prefix="${pkgdir}${MINGW_PREFIX}" setup
}

build() {
  cd "${srcdir}/${_realname}-git"
  make build DEFAULT_LIBRARY_TYPE=relocatable build-doc DOC_FORMATS=html
}

package() {
  cd "${srcdir}/${_realname}-git"
  make -j1 install prefix="${pkgdir}${MINGW_PREFIX}" DOC_FORMATS=html
  # Copy License Files
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" \
    "${srcdir}/${_realname}-git"/COPYING*
}
