# Maintainer: Dirk Stolle

_realname=iml
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.0.5
pkgrel=1
pkgdesc="Integer Matrix Library (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://cs.uwaterloo.ca/~astorjoh/iml.html'
msys2_references=(
  'anitya: 7660'
  'archlinux: iml'
  'gentoo: sci-libs/iml'
)
license=('spdx:GPL-2.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-cblas"
         "${MINGW_PACKAGE_PREFIX}-gmp")
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc")
source=("https://cs.uwaterloo.ca/~astorjoh/${_realname}-${pkgver}.tar.bz2")
sha256sums=('1dad666850895a5709b00b97422e2273f293cfadea7697a9f90b90953e847c2a')

prepare() {
  cd "${_realname}-${pkgver}"

  autoreconf -fiv
}

build() {
  mkdir -p "build-${MSYSTEM}" && cd "build-${MSYSTEM}"

  ../"${_realname}-${pkgver}"/configure \
    --prefix="${MINGW_PREFIX}" \
    --build="${MINGW_CHOST}" \
    --host="${MINGW_CHOST}" \
    --target="${MINGW_CHOST}" \
    --with-cblas=-lcblas \
    --with-gmp-include="${MINGW_PREFIX}/include" \
    --with-gmp-lib="${MINGW_PREFIX}/bin ${MINGW_PREFIX}/lib" \
    --enable-static \
    --enable-shared

  make
}

check() {
  cd "build-${MSYSTEM}"

  make check
}

package() {
  cd "build-${MSYSTEM}"

  make install DESTDIR="${pkgdir}"

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
