# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=OpenSceneGraph
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}" "${MINGW_PACKAGE_PREFIX}-${_realname}-debug")
_pkgver=3.5.1
pkgver=${_pkgver//-/}
pkgrel=1
arch=('any')
url="http://www.openscenegraph.org/"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-gcc-libs"
             "${MINGW_PACKAGE_PREFIX}-boost"
             "${MINGW_PACKAGE_PREFIX}-collada-dom-svn"
             "${MINGW_PACKAGE_PREFIX}-curl"
             "${MINGW_PACKAGE_PREFIX}-ffmpeg"
             "${MINGW_PACKAGE_PREFIX}-fltk"
             "${MINGW_PACKAGE_PREFIX}-freetype"
             "${MINGW_PACKAGE_PREFIX}-gdal"
             "${MINGW_PACKAGE_PREFIX}-giflib"
             "${MINGW_PACKAGE_PREFIX}-gstreamer"
             "${MINGW_PACKAGE_PREFIX}-gtk2"
             "${MINGW_PACKAGE_PREFIX}-gtkglext"
             "${MINGW_PACKAGE_PREFIX}-jasper"
             "${MINGW_PACKAGE_PREFIX}-libjpeg"
             "${MINGW_PACKAGE_PREFIX}-libpng"
             "${MINGW_PACKAGE_PREFIX}-libtiff"
             "${MINGW_PACKAGE_PREFIX}-libxml2"
             "${MINGW_PACKAGE_PREFIX}-lua"
             "${MINGW_PACKAGE_PREFIX}-SDL"
             "${MINGW_PACKAGE_PREFIX}-SDL2"
             "${MINGW_PACKAGE_PREFIX}-poppler"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-qt5"
             "${MINGW_PACKAGE_PREFIX}-wxWidgets"
             "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' 'strip')
source=(http://trac.openscenegraph.org/downloads/developer_releases/${_realname}-${_pkgver}.zip
        find-collada.patch
        find-ffmpeg.patch
        find-freetype.patch
        additional-gl-header.patch
        fix-cast-error.patch
        add-gstreamer-cflags.patch)
sha256sums=('c409e9095d77faab3db8fe2047d75e7ef348fd9f06ecd8b7629fceb8fe1d24e0'
            '24f64f975e776fc018f9c65a1bcae043dded1d4174342d863f9f205b013acc13'
            '19e8ad935f2a1bb7ded4da12da048333e5e2278f8000cb064fe5a540ef7b689f'
            'f872320611a96d424350326e63d1d43f07b691eb4d9f3cfeb8a2ee0fe0c9bbbe'
            '76eafa53bfc4bb9e0260bf1835622e46210b61cf244a0440beceed9969eeaca7'
            '7b54b25e6280095f82045179cb4cef3664415e734995d7ed0f90ff45ab8bb001'
            '56b1c31a5d673807a0bdfdf481bc32d9dc8fa22afa900c494d1f6d155ccc3a6e')

prepare() {
  cd "${srcdir}/${_realname}-${_pkgver}"
  patch -p1 -i ${srcdir}/find-collada.patch
  patch -p1 -i ${srcdir}/find-ffmpeg.patch
  patch -p1 -i ${srcdir}/find-freetype.patch
  patch -p1 -i ${srcdir}/additional-gl-header.patch
  patch -p1 -i ${srcdir}/fix-cast-error.patch
  patch -p1 -i ${srcdir}/add-gstreamer-cflags.patch
}

build() {
  export FREETYPE_DIR=${MINGW_PREFIX}

  for builddir in {Release,Debug}-${MINGW_CHOST}; do
    [[ -d ${builddir} ]] && rm -rf ${builddir}
    mkdir -p ${builddir}
    pushd ${builddir}
      [[ "${builddir%-${MINGW_CHOST}}" == "Release" ]] && {
        local _opts="-DBUILD_OSG_EXAMPLES:BOOL=ON"
      } || {
        local _opts="-DBUILD_OSG_EXAMPLES:BOOL=OFF -DBUILD_OSG_APPLICATIONS=OFF"
      }

      msg "Building ${builddir%-${MINGW_CHOST}} version..."

      MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
      COLLADA_DIR=${MINGW_PREFIX} \
      ${MINGW_PREFIX}/bin/cmake.exe \
        -G"MSYS Makefiles" \
        -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
        -DCMAKE_BUILD_TYPE=${builddir%-${MINGW_CHOST}} \
        ${_opts} \
        ../${_realname}-${_pkgver}

      make
    popd
  done
}

package_release() {
  depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
           "${MINGW_PACKAGE_PREFIX}-boost"
           "${MINGW_PACKAGE_PREFIX}-collada-dom-svn"
           "${MINGW_PACKAGE_PREFIX}-curl"
           "${MINGW_PACKAGE_PREFIX}-ffmpeg"
           "${MINGW_PACKAGE_PREFIX}-fltk"
           "${MINGW_PACKAGE_PREFIX}-freetype"
           "${MINGW_PACKAGE_PREFIX}-gdal"
           "${MINGW_PACKAGE_PREFIX}-giflib"
           "${MINGW_PACKAGE_PREFIX}-gstreamer"
           "${MINGW_PACKAGE_PREFIX}-gtk2"
           "${MINGW_PACKAGE_PREFIX}-gtkglext"
           "${MINGW_PACKAGE_PREFIX}-jasper"
           "${MINGW_PACKAGE_PREFIX}-libjpeg"
           "${MINGW_PACKAGE_PREFIX}-libpng"
           "${MINGW_PACKAGE_PREFIX}-libtiff"
           "${MINGW_PACKAGE_PREFIX}-libxml2"
           "${MINGW_PACKAGE_PREFIX}-lua"
           "${MINGW_PACKAGE_PREFIX}-SDL"
           "${MINGW_PACKAGE_PREFIX}-SDL2"
           "${MINGW_PACKAGE_PREFIX}-poppler"
           "${MINGW_PACKAGE_PREFIX}-python3"
           "${MINGW_PACKAGE_PREFIX}-qt5"
           "${MINGW_PACKAGE_PREFIX}-wxWidgets"
           "${MINGW_PACKAGE_PREFIX}-zlib")
  pkgdesc="Open source high performance 3D graphics toolkit (mingw-w64)"

  cd Release-${MINGW_CHOST}
  make DESTDIR=${pkgdir} -j1 install
}

package_debug() {
  depends=("${MINGW_PACKAGE_PREFIX}-${_realname}=$pkgver")
  options=('staticlibs' '!strip')
  pkgdesc="Open source high performance 3D graphics toolkit (debug) (mingw-w64)"

  cd Debug-${MINGW_CHOST}
  make DESTDIR=${pkgdir} -j1 install

  rm -rf ${pkgdir}${MINGW_PREFIX}/include
  rm -rf ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig
  rm -rf ${pkgdir}${MINGW_PREFIX}/share
}

package_mingw-w64-i686-OpenSceneGraph() {
  package_release
}

package_mingw-w64-i686-OpenSceneGraph-debug() {
  package_debug
}

package_mingw-w64-x86_64-OpenSceneGraph() {
  package_release
}

package_mingw-w64-x86_64-OpenSceneGraph-debug() {
  package_debug
}
