# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=OpenSceneGraph
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
_pkgver=3.2.1-rc2
pkgver=${_pkgver//-/}
pkgrel=1
pkgdesc="Open source high performance 3D graphics toolkit (mingw-w64)"
arch=('any')
url="http://www.openscenegraph.org/"
license=("LGPL")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config" "${_mingw_suff}-cmake" "subversion")
depends=("${_mingw_suff}-gcc-libs"
		"${_mingw_suff}-collada-dom-svn"
		"${_mingw_suff}-zlib"
		"${_mingw_suff}-boost"
		"${_mingw_suff}-curl"
		"${_mingw_suff}-lua"
		"${_mingw_suff}-freeglut"
		"${_mingw_suff}-libjpeg"
		"${_mingw_suff}-libpng"
		"${_mingw_suff}-libtiff"
		"${_mingw_suff}-libxml2"
		"${_mingw_suff}-SDL"
		"${_mingw_suff}-jasper"
		"${_mingw_suff}-qt5"
		"${_mingw_suff}-gtkglext"
		"${_mingw_suff}-giflib"
		#"${_mingw_suff}-wxwidgets"
		)
provides=("${_mingw_suff}-${_realname}")
conflicts=("${_mingw_suff}-${_realname}")
options=('staticlibs' 'strip')
source=(http://www.openscenegraph.org/downloads/developer_releases/${_realname}-${_pkgver}.zip
		osg-collada-dae-fixes.patch
		find-collada.patch
		find-freetype.patch
		fix-cast-issue.patch)
md5sums=('f1bcd7099c5e29b7a91dd072fb912d4b'
         '45d0668f56c5e4ca58002f38fd3eb4a0'
         '668def9c99f044db9a8215fa927d6ac2'
         '9ab69e1cd26826196fac5b3f01e6cb95'
         '7e41d50ddebee00123280b1188e8fcc3')

prepare() {
	cd "${srcdir}/${_realname}-${_pkgver}"
	patch -p1 -i ${srcdir}/osg-collada-dae-fixes.patch
	patch -p1 -i ${srcdir}/find-collada.patch
	patch -p1 -i ${srcdir}/find-freetype.patch
	patch -p1 -i ${srcdir}/fix-cast-issue.patch
}

build() {
	mkdir -p ${pkgdir}${MINGW_PREFIX}
	pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
	export PREFIX_WIN=`pwd -W`
	popd > /dev/null

	pushd ${MINGW_PREFIX} > /dev/null
	export PREFIX_DEPS=`pwd -W`
	popd > /dev/null
	
	[[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
	mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
	export FREETYPE_DIR=${PREFIX_DEPS}
	${MINGW_PREFIX}/bin/cmake.exe \
		-G"MSYS Makefiles" \
		-DCMAKE_INSTALL_PREFIX:PATH=${PREFIX_WIN} \
		-DBUILD_OSG_EXAMPLES:BOOL=ON \
		-DACTUAL_3DPARTY_DIR=${PREFIX_DEPS} \
		../${_realname}-${_pkgver}

	make
}

package() {
	cd "${srcdir}/build-${MINGW_CHOST}"
	make -j1 install

	mkdir -p ${pkgdir}${MINGW_PREFIX}/bin
		pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
	export PREFIX_WIN=`pwd -W`
	popd > /dev/null
	
	for ff in ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/*.pc; do
		sed -i "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" $ff
	done
}
