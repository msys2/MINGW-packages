# Based on: https://aur.archlinux.org/packages/adasat

_realname=adasat
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.r138.3ed4895
pkgrel=1
_branch=24.0

pkgdesc="Implementation of a DPLL-based SAT solver in Ada"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')

url="https://github.com/AdaCore/AdaSAT"
license=('spdx:Apache-2.0 WITH LLVM-exception')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc-ada"
             "${MINGW_PACKAGE_PREFIX}-gprbuild"
             "git")
depends=()
source=("${_realname}-git::git+https://github.com/AdaCore/AdaSAT.git#branch=${_branch}")
sha256sums=('SKIP')

pkgver() {
  cd "${_realname}-git"
  printf "%s.r%s.%s" "$_branch" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
  cd "${srcdir}/${_realname}-git"

  BUILD_MODE=prod \
  make all-libs
}

package() {
  cd "${srcdir}/${_realname}-git"

  BUILD_MODE=prod \
  INSTALL_DIR=${pkgdir}/${MINGW_PREFIX} \
  make install

  # Copy License Files
  install -Dm644 -t "${pkgdir}/${MINGW_PREFIX}/share/licenses/${_realname}" \
    ${srcdir}/${_realname}-git/LICENSE
}
