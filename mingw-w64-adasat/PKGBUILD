# Maintainer: Philipp Smirnov <psmirnov@airmail.cc>
# Based on: https://aur.archlinux.org/packages/adasat

_realname=adasat
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=r128.f948e22
pkgrel=1
_branch=master

pkgdesc="Implementation of a DPLL-based SAT solver in Ada"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')

url="https://github.com/AdaCore/AdaSAT"
license=('Apache-2.0')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc-ada"
             "${MINGW_PACKAGE_PREFIX}-gprbuild")
depends=()
source=("${_realname}::git+https://github.com/AdaCore/AdaSAT.git#branch=${_branch}")
sha256sums=('SKIP')

pkgver() {
  cd ${srcdir}/${_realname}
  printf "r%s.%s" \
      "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short HEAD)"
}

build() {
  cd ${srcdir}/${_realname}

  BUILD_MODE=prod \
  make all-libs
}

package() {
  cd ${srcdir}/${_realname}

  BUILD_MODE=prod \
  INSTALL_DIR=${pkgdir}/${MINGW_PREFIX} \
  make install

  # Copy License Files
  install -Dm644 -t "${pkgdir}/${MINGW_PREFIX}/share/licenses/${_realname}" \
    ${srcdir}/${_realname}/LICENSE
}
