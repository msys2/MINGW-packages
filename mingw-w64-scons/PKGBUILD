# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

_realname=scons
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.5.0
pkgrel=1
pkgdesc="Extensible Python-based build utility"
arch=('any')
groups=('base-devel')
url="http://scons.org"
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python2")
source=("https://downloads.sourceforge.net/sourceforge/$_realname/$_realname-$pkgver.tar.gz"
        scons-2.3.4-cyglink-msys2.patch
        scons-2.3.4-msys2-plat-mingw-env.patch
        mingw_w64.py
        scons-2.5.0-msys2-win32-spawn.patch
        scons-2.5.0-msys2-mingw-plat.patch
        scons-2.5.0-msys2-mingw-long-paths.patch)
sha256sums=('eb296b47f23c20aec7d87d35cfa386d3508e01d1caa3040ea6f5bbab2292ace9'
            '01b2bd24c39057cac5f3d21fa6d469de0c9e6e3f40c438ffc7f0f64d6e760571'
            'c024560dc93636d92614c17d7519080dd931f6c1471088616692a443d3cfc064'
            '984c55d0ca18ac594941bb6336fe994efe2f5bba20225e8cb1f5d42c80eaf651'
            '12b001e8570f353ac6893ae00cc0b52e6628e46d7ad6d2d8b00ec6a48e7a5a38'
            '43b8b938acfc3c7e80c678e4971d7b2b3b229925af796597024c6c011b933653'
            '7fd51215d332a104c5d21daaa07a8fd23367574dce61b32f6b0a462161c0e4fd')

prepare() {
  cd "$srcdir/$_realname-$pkgver"
  cp ../mingw_w64.py engine/SCons/Tool/

  # avoid errors when re-running makepkg
  rm -f engine/SCons/Platform/msys.py engine/SCons/Tool/msyslink.py
  patch -p1 -i ${srcdir}/scons-2.3.4-cyglink-msys2.patch
  patch -p0 -i ${srcdir}/scons-2.3.4-msys2-plat-mingw-env.patch
  patch -p0 -i ${srcdir}/scons-2.5.0-msys2-win32-spawn.patch
  patch -p0 -i ${srcdir}/scons-2.5.0-msys2-mingw-plat.patch
  patch -p0 -i ${srcdir}/scons-2.5.0-msys2-mingw-long-paths.patch
}

package() {
  cd "$srcdir/$_realname-$pkgver"

  /bin/python2 setup.py install \
      --standard-lib \
      --prefix=${MINGW_PREFIX} \
      --install-data=${MINGW_PREFIX}/share \
      --optimize=1 \
      --root="$pkgdir"

  # fix for python 2.7
  sed -i 's:^#!.*bin/env python:#!/usr/bin/env python2:' \
      "$pkgdir/${MINGW_PREFIX}"/bin/*

  install -Dm644 LICENSE.txt \
    "$pkgdir/${MINGW_PREFIX}/share/licenses/$pkgname/LICENSE"
}
