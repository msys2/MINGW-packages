# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_pyname=SCons
_realname=scons
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=4.6.0
pkgrel=1
pkgdesc="Extensible Python-based build utility (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
license=('spdx:MIT')
url="https://scons.org"
msys2_repository_url="https://github.com/SCons/scons"
msys2_references=(
  'pypi: SCons'
)
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python-wheel")
options=(!strip)
source=("https://pypi.org/packages/source/${_pyname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz"
        "0001-fix-mingw-path.patch"
        "0002-fix-missing-man-pages.patch")
sha256sums=('7db28958b188b800f803c287d0680cc3ac7c422ed0b1cf9895042c52567803ec'
            '4416b87da2d7e2879d6f2fd13796887b0a0de3dfffb9be3ec2f565f794a72048'
            '1914141147aec5bd5b2c4a87ffce932e0837d7951cfc3ef209f12c6a30a194c4')

prepare() {
  cd "${srcdir}/${_pyname}-${pkgver}"

  patch -Np1 -i "${srcdir}/0001-fix-mingw-path.patch"
  patch -Np1 -i "${srcdir}/0002-fix-missing-man-pages.patch"
}

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -D -m644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
