# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.su>

_realname=wasm-server-runner
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.0.1
pkgrel=7
pkgdesc="Cargo run for wasm programs (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://crates.io/crates/wasm-server-runner'
msys2_repository_url='https://github.com/jakobhellermann/wasm-server-runner'
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-nasm"
             "${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-rust-bindgen")
source=("${msys2_repository_url}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('fa4ac6e035cb0530a88851b3e929c3052f97242dba614de786b4b0b708a96041')

prepare() {
  cd "${_realname}-${pkgver}"

  cargo update -p rcgen --precise 0.14.7
  cargo update -p aws-lc-rs --precise 1.15.4
  cargo update -p aws-lc-sys --precise 0.37.0
  cargo update -p wasm-bindgen-cli-support --precise 0.2.114

  cargo fetch --locked --target "${RUST_CHOST}"
}

build() {
  cd "${_realname}-${pkgver}"

  cargo build --release --frozen
}

check() {
  cd "${_realname}-${pkgver}"

  cargo test --release --frozen
}

package() {
  cd "${_realname}-${pkgver}"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE.md "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.md"
}
