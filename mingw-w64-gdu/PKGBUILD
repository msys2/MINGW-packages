# Maintainer: Wu Zhenyu <wuzy01@qq.com>
_realname=gdu

pkgbase=mingw-w64-$_realname
pkgname=$MINGW_PACKAGE_PREFIX-$_realname
pkgver=5.13.2
pkgrel=1
license=('spdx:MIT')
pkgdesc="Fast disk usage analyzer (mingw-w64)"
makedepends=(
  "$MINGW_PACKAGE_PREFIX-go"
  "$MINGW_PACKAGE_PREFIX-cc"
  git)
arch=('any')
mingw_arch=(mingw32 mingw64 ucrt64 clang32 clang64)
url="https://github.com/dundee/gdu"
source=("$url/archive/v$pkgver.tar.gz")
sha256sums=('f4f237f6da470599f6393591282cfd67922a963325859a939ca40ba7e18024a8')

prepare() {
  cd ${_realname}-$pkgver
  mkdir -p dist/
}

build() {
  cd ${_realname}-$pkgver
  local _DATE=$(git log -1 --pretty=%ct) # get commit date to make it REPRODUCABLE
  local _BUILDINFO="-X 'github.com/dundee/gdu/v5/build.Version=${pkgver}' \
                    -X 'github.com/dundee/gdu/v5/build.User=${PACKAGER}' \
                    -X 'github.com/dundee/gdu/v5/build.Time=${_DATE}'"
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" ${_BUILDINFO}" \
    -o dist/gdu \
    github.com/dundee/gdu/v5/cmd/gdu
}

package() {
  cd ${_realname}-$pkgver
  install -Dm755 dist/$_realname "${pkgdir}$MINGW_PREFIX/bin/$_realname.exe"
  install -Dm644 gdu.1    "${pkgdir}$MINGW_PREFIX/share/man/man1/$_realname.1"
  install -Dm644 LICENSE.md  "${pkgdir}$MINGW_PREFIX/share/licenses/${_realname}/LICENSE"
}
