# Maintainer: Yuyi Wang <Strawberry_Str@hotmail.com>

_realname=git
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.39.2.windows.1
pkgrel=1
pkgdesc="The fast distributed version control system (mingw-w64)"
arch=('any')
url="https://git-for-windows.github.io/"
license=('GPL2')
provides=('git' 'git-core')


makedepends=("docbook-xsl"
             "xmlto"
             "${MINGW_PACKAGE_PREFIX}-asciidoc"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-tk")

depends=("${MINGW_PACKAGE_PREFIX}-ca-certificates"
         "${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-expat>=2.0"
         "${MINGW_PACKAGE_PREFIX}-gettext"
         "${MINGW_PACKAGE_PREFIX}-libiconv"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-pcre2"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "perl>=5.14.0"
         "perl-Authen-SASL"
         "perl-Error"
         "perl-libwww"
         "perl-MIME-tools"
         "perl-Net-SMTP-SSL"
         "perl-TermReadKey")

optdepends=("subversion: git-svn"
            "${MINGW_PACKAGE_PREFIX}-python: git-p4"
            "${MINGW_PACKAGE_PREFIX}-tk: gitk & git-gui")

replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}-doc-html"
          "${MINGW_PACKAGE_PREFIX}-${_realname}-doc-man")

source=("https://github.com/git-for-windows/git/archive/refs/tags/v$pkgver.tar.gz"
        "1-mingw.patch"
        "2-version.patch"
        "4-headless.patch"
        "6-interpr.patch"
        "7-arm64.patch")

sha256sums=('5661b4906803d9523f0d23714d687ea36b58ab41a366ace33c5df6706457b6b8'
            '94a2ee925a17130fd0c1debd5c72dff8f8f1dfd22612b456fdd45ae5d0e1b215'
            'c7fe26ea8d51918506d3adfa0a21aedc322f3c04e933cd31e0eb40a2f899d022'
            'd08164a391d63cdad3932818538bd20e465baa85b1a0d1a0bb383bb4963d94e4'
            'cc582f4526aa57c64436cca5d8ab583837213c03408a1cc32ea4795577f575ce'
            '2464016253e6f8f35d27036d7708c779b599350d8d062007ab52a6341df3fd77')

prepare() {
    cd "$srcdir/$_realname-$pkgver"
    patch -Np1 -i $srcdir/1-mingw.patch
    patch -Np1 -i $srcdir/2-version.patch
    patch -Np1 -i $srcdir/4-headless.patch
    patch -Np1 -i $srcdir/6-interpr.patch
    if test "CLANGARM64" == "$MSYSTEM"
    then
        patch -Np1 -i $srcdir/7-arm64.patch
    fi
    echo "v$pkgver-$pkgrel" > version
}

package () {
    # Git wants to decide itself whether to use ANSI stdio emulation or not
    CPPFLAGS="$(echo "$CPPFLAGS" |
        sed 's/-D__USE_MINGW_ANSI_STDIO\(=[0-9]*\)\?//')"

    # run cmake first, it will generate GIT-VERSION-FILE
    mkdir -p $srcdir/build-${MSYSTEM}
    cd $srcdir/build-${MSYSTEM}
    MSYS2_ARG_CONV_EXCL="-DFALLBACK_RUNTIME_PREFIX=" ${MINGW_PREFIX}/bin/cmake.exe $srcdir/$_realname-$pkgver/contrib/buildsystems \
        -GNinja \
        -DUSE_VCPKG=off \
        -DSKIP_DASHED_BUILT_INS=ON \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX="$pkgdir/$MINGW_PREFIX" \
        -DFALLBACK_RUNTIME_PREFIX="$MINGW_PREFIX"
    ${MINGW_PREFIX}/bin/cmake --install .

    cd "$srcdir/$_realname-$pkgver"

    # gitk
    make -C gitk-git prefix="$pkgdir/$MINGW_PREFIX" install

    # git-gui
    DESTDIR="$pkgdir" make -C git-gui gitexecdir="$MINGW_PREFIX/libexec/git-core" install

    # gitweb
    DESTDIR="$pkgdir" make prefix="$MINGW_PREFIX" gitwebdir="$MINGW_PREFIX/var/www/cgi-bin" install-gitweb

    # docs
    DESTDIR="$pkgdir" make prefix="$MINGW_PREFIX" install-html
    DESTDIR="$pkgdir" make prefix="$MINGW_PREFIX" install-man

    # subtree, for backwards-compatibility with MSys2's Git package
    DESTDIR="$pkgdir" make -C contrib/subtree prefix="$MINGW_PREFIX" install
    DESTDIR="$pkgdir" make -C contrib/subtree prefix="$MINGW_PREFIX" install-doc

    # contacts
    DESTDIR="$pkgdir" make -C contrib/contacts prefix="$MINGW_PREFIX" install
    DESTDIR="$pkgdir" make -C contrib/contacts prefix="$MINGW_PREFIX" install-doc

    # jump
    install contrib/git-jump/git-jump "$pkgdir/$MINGW_PREFIX/libexec/git-core/"

    # resurrect
    install contrib/git-resurrect.sh "$pkgdir/$MINGW_PREFIX/libexec/git-core/git-resurrect"

    # mw
    DESTDIR="$pkgdir" make -C contrib/mw-to-git prefix="$MINGW_PREFIX" perllibdir="$MINGW_PREFIX/share/perl5" install

    # git-credential-wincred.exe
    make -C contrib/credential/wincred prefix="$pkgdir/$MINGW_PREFIX" install

    # completions
    install -d "$pkgdir/$MINGW_PREFIX/share/git/completion/"
    install contrib/completion/* "$pkgdir/$MINGW_PREFIX/share/git/completion/"
}
