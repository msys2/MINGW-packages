# Maintainer: Dirk Stolle

_realname=passagemath-repl
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=10.6.45
pkgrel=1
pkgdesc="passagemath: IPython kernel, Sage preparser, doctester (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/passagemath/passagemath'
msys2_references=(
  'purl: pkg:pypi/passagemath-repl'
)
license=('spdx:GPL-2.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-python-ipython"
         "${MINGW_PACKAGE_PREFIX}-python-ipykernel"
         "${MINGW_PACKAGE_PREFIX}-python-ipywidgets"
         "${MINGW_PACKAGE_PREFIX}-python-jupyter_client"
         "${MINGW_PACKAGE_PREFIX}-python-passagemath-environment"
         "${MINGW_PACKAGE_PREFIX}-python-passagemath-objects"
         # python-typing_extensions are only required for Python<3.13. so remove that dependency
         # when we update to Python 3.13 (or later).
         "${MINGW_PACKAGE_PREFIX}-python-typing_extensions")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-passagemath-setup"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools")
options=('!strip')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname/-/_}-${pkgver}.tar.gz")
sha256sums=('e16bff689dd076e40f537438ff0a3ee53533adeb75af06f7109c7cd762ba11a7')

build() {
  cp -r "${_realname/-/_}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  # fix wrong shebang created by python-installer
  sed -i 's|#!<launcher_dir>\\python.exe|#!/usr/bin/env sage-python|g' \
    "${pkgdir}${MINGW_PREFIX}/bin/sage-cleaner" \
    "${pkgdir}${MINGW_PREFIX}/bin/sage-eval" \
    "${pkgdir}${MINGW_PREFIX}/bin/sage-fixdoctests" \
    "${pkgdir}${MINGW_PREFIX}/bin/sage-ipython" \
    "${pkgdir}${MINGW_PREFIX}/bin/sage-notebook" \
    "${pkgdir}${MINGW_PREFIX}/bin/sage-preparse" \
    "${pkgdir}${MINGW_PREFIX}/bin/sage-run" \
    "${pkgdir}${MINGW_PREFIX}/bin/sage-run-cython" \
    "${pkgdir}${MINGW_PREFIX}/bin/sage-runtests" \
    "${pkgdir}${MINGW_PREFIX}/bin/sage-startuptime.py"

  sed -i 's|#!<launcher_dir>\\python.exe|#!/usr/bin/env python3|g' \
    "${pkgdir}${MINGW_PREFIX}/bin/sage-coverage"
}
