# Contributor: Edward E. <develinthedetail@gmail.com>

_realname=libheif
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.17.1
pkgrel=3
pkgdesc="HEIF image decoder/encoder library and tools (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://github.com/strukturag/libheif"
license=('spdx:LGPL-3.0 AND MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2"
             "${MINGW_PACKAGE_PREFIX}-cc")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-aom"
         "${MINGW_PACKAGE_PREFIX}-dav1d"
         "${MINGW_PACKAGE_PREFIX}-libde265"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libsharpyuv"
         "${MINGW_PACKAGE_PREFIX}-openjpeg2"
         "${MINGW_PACKAGE_PREFIX}-rav1e"
         $( [[ ${CARCH} == i686 ]] || \
            echo "${MINGW_PACKAGE_PREFIX}-svt-av1" )
         "${MINGW_PACKAGE_PREFIX}-x265"
         "${MINGW_PACKAGE_PREFIX}-zlib")
source=("https://github.com/strukturag/libheif/releases/download/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        https://github.com/strukturag/libheif/commit/1918de7f0f5813d2c20b18e954c17a27ac28a5b3.patch
        https://github.com/strukturag/libheif/commit/fe8d6b2a9e9c058147c628379b11e890dad328b3.patch
        https://github.com/strukturag/libheif/commit/316291a9d374da768c8b629d2f04ed18015a3608.patch)
sha256sums=('97d74c58a346887c1bbf98dcf0322c13b728286153d0f1be2b350f7107e49dba'
            '9238f41df47a937e2dcfa5cdf6bf0b17e069c15f2bdbdd2d4490bc7707b11ec0'
            '429bf8edd045f8edf5419290443cd2307451194ca88346cc2c562846f231421a'
            '1b3d5fbbe35779b5c417812fa1959d7de2acff60c289eecbcaea90525b866383')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  # https://github.com/strukturag/libheif/issues/1010
  patch -Np1 -i "${srcdir}"/1918de7f0f5813d2c20b18e954c17a27ac28a5b3.patch
  patch -Np1 -i "${srcdir}"/fe8d6b2a9e9c058147c628379b11e890dad328b3.patch
  patch -Np1 -i "${srcdir}"/316291a9d374da768c8b629d2f04ed18015a3608.patch
}

build() {
  mkdir -p "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  # Keep "-DX265_API_IMPORTS" flag due to https://github.com/strukturag/libheif/issues/357
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      "${extra_config[@]}" \
      -DBUILD_SHARED_LIBS=ON \
      -DENABLE_PLUGIN_LOADING=OFF \
      -DWITH_DEFLATE_HEADER_COMPRESSION=ON \
      -DWITH_DAV1D=ON \
      -DWITH_RAV1E=ON \
      -DWITH_SvtEnc=$( [[ ${CARCH} == i686 ]] &&
                        echo "OFF" || echo "ON" ) \
      -DWITH_JPEG_DECODER=ON \
      -DWITH_JPEG_ENCODER=ON \
      -DWITH_OpenJPEG_DECODER=ON \
      -DWITH_OpenJPEG_ENCODER=ON \
      -DX265_CFLAGS="-DX265_API_IMPORTS" \
      ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}

  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/examples/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING-examples"
}
