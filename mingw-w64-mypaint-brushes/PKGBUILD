# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=mypaint-brushes
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3.1
pkgrel=2
pkgdesc="Brushes for MyPaint (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/mypaint/mypaint-brushes"
msys2_repository_url='https://github.com/mypaint/mypaint-brushes'
msys2_references=(
  'archlinux: mypaint-brushes1'
)
license=('spdx:ISC')
depends=("${MINGW_PACKAGE_PREFIX}-libmypaint")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools")
source=(https://github.com/mypaint/mypaint-brushes/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz)
sha256sums=('e6d0f51adb2ad507c12aa40a78265638cb21c53f4eb761c274d044677afaa1ff')
noextract=(${_realname}-${pkgver}.tar.gz)

prepare() {
  [[ -d "${srcdir}"/${_realname}-${pkgver} ]] && rm -rf "${srcdir}"/${_realname}-${pkgver}

  tar --exclude=README -xzf ${_realname}-${pkgver}.tar.gz

  cd ${_realname}-${pkgver}
  ./autogen.sh
}

build() {
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  "${srcdir}/${_realname}-${pkgver}"/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX}

  make
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}
  make -j1 DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/Licenses.md \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/Licenses.md
}
