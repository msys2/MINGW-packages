# Maintainer: Rafa≈Ç Brzegowy <rafal.brzegowy@yahoo.com>

_realname=calculix-ccx
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.21
pkgrel=1
mklver=2024.0.0
iompver=2024.0.2
pkgdesc="CalculiX: 3D Structural Finite Element Program - Solver (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64')
url="http://www.calculix.de/"
license=('GPL2')
depends=("${MINGW_PACKAGE_PREFIX}-arpack"
         "${MINGW_PACKAGE_PREFIX}-arpack64"
	 "${MINGW_PACKAGE_PREFIX}-openblas"
	 "${MINGW_PACKAGE_PREFIX}-openblas64"
         "${MINGW_PACKAGE_PREFIX}-spooles")
makedepends=("${MINGW_PACKAGE_PREFIX}-fc")
source=("http://www.dhondt.de/ccx_${pkgver}.src.tar.bz2"
	"http://www.dhondt.de/gpl-2.0.txt"
        "ccx_mingw.patch"
	"ccx_ooc.patch"
	"https://files.pythonhosted.org/packages/83/a2/e1e489f2bf4fae3c4e95faf93a7bbb4ba24bfd921481f1200355032075a1/mkl-${mklver}-py2.py3-none-win_amd64.whl"
	"https://files.pythonhosted.org/packages/ed/5a/1c62bdd815acfa4e5fb11c5ad385e395f98dae08093783b7629b1e10d7f2/mkl_include-${mklver}-py2.py3-none-win_amd64.whl"
	"https://files.pythonhosted.org/packages/62/73/d609f9f19470afa2e2702781ddef184e80f357b4af5866dcfffc13b3c11b/intel_openmp-${iompver}-py2.py3-none-win_amd64.whl")
sha256sums=('52A20EF7216C6E2DE75EAE460539915640E3140EC4A2F631A9301E01EDA605AD'
	    '8177F97513213526DF2CF6184D8FF986C675AFB514D4E68A404010521B880643'
            'FDA694B64E40F5ADE4DEBEA7DA56608EB2D05EF111CF6D3D61E6EEBF372DAD8A'
	    'DD81DF3DE137A05B45E39FE61830337E6405EC566148598DAB2828A6D9780E8A'
	    '6C92752440A8CCB9A1E7C0DF4ABE0DB232F879B34FC31238175ACFB5BC892D22'
	    'D04CB011F9BC8FC6590B49A2217DE51AA9917AFAB0CAAA23CF6E3133DE842591'
	    'D33028DC417ABF10080561BD5E84DCCBA344FF6A7D1CDB16950374F27AD8DA35')

prepare() {
    cd "${srcdir}/CalculiX/ccx_${pkgver}/src"
    patch -Np1 < ../../../ccx_mingw.patch
    patch -Np1 < ../../../ccx_ooc.patch
}

build()
{
    cd "${srcdir}/CalculiX/ccx_${pkgver}/src"
    make CC=${CC} CCX_VERSION=${pkgver} -f Makefile_mingw_i4
    make CC=${CC} CCX_VERSION=${pkgver} -f Makefile_mingw_i8
}

package() 
{
    install -Dm755 ${srcdir}/CalculiX/ccx_${pkgver}/src/ccx_i4/ccx_i4.exe ${pkgdir}${MINGW_PREFIX}/bin/ccx.exe
    install -Dm755 ${srcdir}/CalculiX/ccx_${pkgver}/src/ccx_i8/ccx_i8.exe ${pkgdir}${MINGW_PREFIX}/bin/ccx_i8.exe
    
    cd "${srcdir}/mkl-${mklver}.data/data/Library/bin"
    cp mkl_avx2.2.dll ${pkgdir}${MINGW_PREFIX}/bin/
    cp mkl_avx512.2.dll ${pkgdir}${MINGW_PREFIX}/bin/
    cp mkl_core.2.dll ${pkgdir}${MINGW_PREFIX}/bin/
    cp mkl_intel_thread.2.dll ${pkgdir}${MINGW_PREFIX}/bin/
    cp mkl_rt.2.dll ${pkgdir}${MINGW_PREFIX}/bin/
    
    cd "${srcdir}/intel_openmp-${iompver}.data/data/bin"
    cp libiomp5md.dll ${pkgdir}${MINGW_PREFIX}/bin/
    
    install -Dm755 ${srcdir}/mkl-${mklver}.dist-info/LICENSE.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE_mkl
    install -Dm755 ${srcdir}/gpl-2.0.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
