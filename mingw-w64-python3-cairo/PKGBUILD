# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=pycairo
pkgbase=mingw-w64-python3-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-python3-cairo"
pkgver=1.10.0
pkgrel=7
pkgdesc="Python 3 bindings for the cairo graphics library (mingw-w64)"
url="http://www.cairographics.org/pycairo"
arch=('any')
license=('LGPL' 'MPL')
depends=("${MINGW_PACKAGE_PREFIX}-python3" "${MINGW_PACKAGE_PREFIX}-cairo")
source=(http://cairographics.org/releases/${_realname}-${pkgver}.tar.bz2
        'setup.py::https://cgit.freedesktop.org/pycairo/plain/setup.py?id=cd463341de185094455cf5869a442aa02b8b812e'
        0001-msys2-port-to-subprocess.check_output.patch
        setup-package-dir.patch
        head-init.patch)
sha256sums=('9aa4078e7eb5be583aeabbe8d87172797717f95e8c4338f0d4a17b683a7253be'
            '049b2afe6032d4e794680cbb4dcc642ec743cbcbc7cee8aaad587431fdb1dab2'
            'bbd943874a097353dcbbadc2d40b49772f6c8e514889d55f8a56935a20215fe2'
            '40fa12ab2a98b6e677afe5e0b47d3907e6f53f53961b586566d0e19be7665a63'
            '00567503e89a0e7bb8bc8cd3583b5fcff4b8e00d7a646f76e766d798abae65a3')

prepare() {
  cd "${srcdir}/pycairo-${pkgver}"

  cp "${srcdir}/setup.py" .
  patch -p1 -i "${srcdir}/0001-msys2-port-to-subprocess.check_output.patch"
  patch -p0 -i "${srcdir}/setup-package-dir.patch"
  patch -p1 -i "${srcdir}/head-init.patch"
}

build() {
  cd "${srcdir}/pycairo-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py build
}

package() {
  cd "${srcdir}/pycairo-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--exec-prefix=;--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py install \
    --prefix=${MINGW_PREFIX} --root="${pkgdir}"
}
