# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_pyname=Sphinx
_realname=sphinx
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=8.1.3
pkgrel=2
pkgdesc="Python documentation generator (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://www.sphinx-doc.org/"
msys2_repository_url="https://github.com/sphinx-doc/sphinx"
msys2_references=(
  'purl: pkg:pypi/sphinx'
)
license=('spdx:BSD-2-Clause')
options=('!strip')
depends=("${MINGW_PACKAGE_PREFIX}-python-babel"
         "${MINGW_PACKAGE_PREFIX}-python-colorama"
         "${MINGW_PACKAGE_PREFIX}-python-docutils"
         "${MINGW_PACKAGE_PREFIX}-python-imagesize"
         "${MINGW_PACKAGE_PREFIX}-python-jinja"
         "${MINGW_PACKAGE_PREFIX}-python-packaging"
         "${MINGW_PACKAGE_PREFIX}-python-pygments"
         "${MINGW_PACKAGE_PREFIX}-python-requests"
         "${MINGW_PACKAGE_PREFIX}-python-snowballstemmer"
         "${MINGW_PACKAGE_PREFIX}-python-sphinx-alabaster-theme"
         "${MINGW_PACKAGE_PREFIX}-python-sphinxcontrib-applehelp"
         "${MINGW_PACKAGE_PREFIX}-python-sphinxcontrib-devhelp"
         "${MINGW_PACKAGE_PREFIX}-python-sphinxcontrib-htmlhelp"
         "${MINGW_PACKAGE_PREFIX}-python-sphinxcontrib-jsmath"
         "${MINGW_PACKAGE_PREFIX}-python-sphinxcontrib-qthelp"
         "${MINGW_PACKAGE_PREFIX}-python-sphinxcontrib-serializinghtml")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-flit-core")
if [[ ${CARCH} != i686 ]]; then
optdepends=("${MINGW_PACKAGE_PREFIX}-imagemagick: ext.imageconverter"
            "${MINGW_PACKAGE_PREFIX}-texlive-latex-extra: for generation of PDF documentation")
fi
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-defusedxml"
              "${MINGW_PACKAGE_PREFIX}-python-pytest")
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('43c1911eecb0d3e161ad78611bc905d1ad0e523e4ddc202a58a821773dc4c927')

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m pytest || warning "test failed"
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE.rst "${pkgdir}${MINGW_PREFIX}"/share/licenses/python-${_realname}/LICENSE.rst
}
