# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_pyname=Sphinx
_realname=sphinx
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
provides=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
replaces=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=5.3.0
pkgrel=1
pkgdesc="Python documentation generator (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
license=('BSD')
url="https://www.sphinx-doc.org/"
options=(!strip)
depends=("${MINGW_PACKAGE_PREFIX}-python-babel"
         "${MINGW_PACKAGE_PREFIX}-python-colorama"
         "${MINGW_PACKAGE_PREFIX}-python-docutils"
         "${MINGW_PACKAGE_PREFIX}-python-imagesize"
         "${MINGW_PACKAGE_PREFIX}-python-jinja"
         "${MINGW_PACKAGE_PREFIX}-python-packaging"
         "${MINGW_PACKAGE_PREFIX}-python-pygments"
         "${MINGW_PACKAGE_PREFIX}-python-requests"
         "${MINGW_PACKAGE_PREFIX}-python-snowballstemmer"
         "${MINGW_PACKAGE_PREFIX}-python-sphinx-alabaster-theme"
         "${MINGW_PACKAGE_PREFIX}-python-sphinxcontrib-applehelp"
         "${MINGW_PACKAGE_PREFIX}-python-sphinxcontrib-devhelp"
         "${MINGW_PACKAGE_PREFIX}-python-sphinxcontrib-htmlhelp"
         "${MINGW_PACKAGE_PREFIX}-python-sphinxcontrib-jsmath"
         "${MINGW_PACKAGE_PREFIX}-python-sphinxcontrib-qthelp"
         "${MINGW_PACKAGE_PREFIX}-python-sphinxcontrib-serializinghtml")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-flit-core")
optdepends=(
  "${MINGW_PACKAGE_PREFIX}-imagemagick: ext.imageconverter"
  "${MINGW_PACKAGE_PREFIX}-texlive-latex-extra: for generation of PDF documentation"
)
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-pytest")
source=("${_pyname}-${pkgver}.tar.gz"::"https://pypi.org/packages/source/S/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha256sums=('51026de0a9ff9fc13c05d74913ad66047e104f56a129ff73e174eb5c3ee794b5')

prepare() {
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_pyname}-${pkgver}" "python-build-${MSYSTEM}"
}

build() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  #2 failed, 1165 passed, 21 skipped, 3 xfailed, 30 xpassed, 3 warnings
  ${MINGW_PREFIX}/bin/python -m pytest || warning "test failed"
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}"/share/licenses/python-${_realname}/LICENSE
}
