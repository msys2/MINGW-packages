# Maintainer: Ryuta Suzuki <oroppas@gmail.com>

_realname=path.py
pkgbase=mingw-w64-python-path
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-path"
         "${MINGW_PACKAGE_PREFIX}-python2-path")
pkgver=11.5.0
pkgrel=1
pkgdesc="File system based database that uses python pickles (mingw-w64)"
url="https://pypi.org/project/path.py/"
arch=('any')
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools-scm"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools-scm"
             "${MINGW_PACKAGE_PREFIX}-python3-importlib-metadata"
             "${MINGW_PACKAGE_PREFIX}-python2-importlib-metadata"
             "${MINGW_PACKAGE_PREFIX}-python2-backports.os")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python3-pytest-runner"
              "${MINGW_PACKAGE_PREFIX}-python2-pytest-runner"
              "${MINGW_PACKAGE_PREFIX}-python3-pytest-flake8"
              "${MINGW_PACKAGE_PREFIX}-python2-pytest-flake8")
source=("${_realname}-$pkgver.tar.gz"::"https://github.com/jaraco/path.py/archive/$pkgver.tar.gz")
sha512sums=('50612a6abd6bdce4127140aacbc935c48f141f6e6640b88544077632bfa27c30eea1b7cc6e7e8cd7ad9d9e7be0501f5928d00dcece67b7ca24f9e0380090980a')

prepare() {
  cd "${srcdir}"
  for builddir in python{2,3}-build-${CARCH}; do
    rm -rf ${builddir} || true
    cp -r "${_realname}-${pkgver}" "${builddir}"
  done
  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  for pver in {2,3}; do  
    msg "Python ${pver} build for ${CARCH}"  
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py build
  done  
}

check() {
  for pver in {2,3}; do
    msg "Python ${pver} test for ${CARCH}"
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py pytest || true
  done
}

package_python3-path() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3-importlib-metadata")

  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py install --root=${pkgdir} --prefix=${MINGW_PREFIX} --optimize=1 --skip-build
  install -d ${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}
  head -n 21 path.py > ${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE
}

package_python2-path() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2"
           "${MINGW_PACKAGE_PREFIX}-python2-importlib-metadata"
           "${MINGW_PACKAGE_PREFIX}-python2-backports.os")

  cd "${srcdir}/python2-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python2 setup.py install --root=${pkgdir} --prefix=${MINGW_PREFIX} --optimize=1 --skip-build
  install -d ${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}
  head -n 21 path.py > ${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE
}

package_mingw-w64-i686-python2-path() {
  package_python2-path
}

package_mingw-w64-i686-python3-path() {
  package_python3-path
}

package_mingw-w64-x86_64-python2-path() {
  package_python2-path
}

package_mingw-w64-x86_64-python3-path() {
  package_python3-path
}

# vim:set ts=2 sw=2 et:
