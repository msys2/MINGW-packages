# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname='firebird'
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=3.0.0.31844.65a81b1
pkgrel=1
pkgdesc="Cross-platform relational database offering many ANSI SQL standard features (mingw-w64)"
url="http://www.firebirdsql.org/"
license=('custom')
arch=('any')
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}" "${MINGW_PACKAGE_PREFIX}-libfbclient")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}" "${MINGW_PACKAGE_PREFIX}-libfbclient")
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}")
depends=(
         "${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-icu"
         "${MINGW_PACKAGE_PREFIX}-libtommath" # For Firebird 3.x
         "${MINGW_PACKAGE_PREFIX}-zlib"
        )
makedepends=(#"${MINGW_PACKAGE_PREFIX}-btyacc" # Maybe add check in firebird presence of system btyacc
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-libatomic_ops"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "git"
            )
options=('!debug' 'strip')
source=("${_realname}"::"git+https://github.com/Alexpux/firebird-git-svn.git"
        #"${_realname}"::"git+https://Alexpux@bitbucket.org/msys2/firebird.git#branch=mingw-w64-trunk"
        #"${_realname}"::"git+file:///v/Sources/GNU/firebird-git#branch=mingw-w64-trunk"
        firebird-mingw.diff
        firebird-msys.diff
        cmake-mingw.patch)
sha256sums=('SKIP'
            '43c853d2a1c4c18ed3e3baca6a75f5e1bf83e7700c52cf44f191cee5ab96f0ed'
            '6b01a0a726f3755d6615c0d68cde8b1ad29524bcef6d1fc209278626ede62b4a'
            '9cf9164fa747dbb14b7579cb3b4629f9480af7ddd3424172577eae8b34f38493')

pkgver() {
  cd "${_realname}"
  source ${srcdir}/${_realname}/src/misc/writeBuildNum.sh
  printf '%s.%s.%s.%s.%s' ${MajorVer} ${MinorVer} ${RevNo} ${BuildNum} "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${_realname}
  git am ${srcdir}/firebird-mingw.diff
  git am ${srcdir}/firebird-msys.diff
  patch -p1 -i ${srcdir}/cmake-mingw.patch
  
  cd ..

  mv "${_realname}" "${CARCH}"
  cd "${CARCH}"
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  # [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  # mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}

  # local _build_conf="Release"
  # if check_option "debug" "y"; then
    # _build_conf="Debug"
  # fi

  # ${MINGW_PREFIX}/bin/cmake.exe \
    # -G"MSYS Makefiles" \
    # -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    # -DCMAKE_BUILD_TYPE=${_build_conf} \
    # ../${_realname}

  # make -j1


  local _debug_conf=
  if check_option "debug" "y"; then
    _debug_conf="--enable-debug"
  fi

  cd ${srcdir}/${CARCH}
  CFLAGS+=" -D_POSIX_C_SOURCE"

  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared \
    ${_debug_conf} \
    --disable-rpath \
    --with-fbdoc=${MINGW_PREFIX}/share/${_realname}/doc

  CC=${MINGW_PREFIX}/bin/gcc \
  make -j1
}

package() {
  cd ${srcdir}/${CARCH}

  local _dir=Release
  if check_option "debug" "y"; then
    _dir=Debug
  fi

  mkdir -p ${pkgdir}
  cp -rf gen/${_dir}/firebird ${pkgdir}${MINGW_PREFIX}
  cp -rf ${srcdir}/${CARCH}/doc ${pkgdir}${MINGW_PREFIX}/
}
