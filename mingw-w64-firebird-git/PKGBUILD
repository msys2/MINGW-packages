# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=firebird
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=5.0.0.920.aca2b129
pkgrel=1
pkgdesc="Cross-platform relational database offering many ANSI SQL standard features (mingw-w64)"
url="https://www.firebirdsql.org/"
license=('spdx:Interbase-1.0 OR MPL-1.0')
arch=('any')
mingw_arch=('mingw64' 'mingw32' 'ucrt64' 'clang64' 'clang32')
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}" "${MINGW_PACKAGE_PREFIX}-libfbclient")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}" "${MINGW_PACKAGE_PREFIX}-libfbclient")
replaces=("${MINGW_PACKAGE_PREFIX}-libfbclient")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-icu"
         "${MINGW_PACKAGE_PREFIX}-libtomcrypt"
         "${MINGW_PACKAGE_PREFIX}-libtommath"
         "${MINGW_PACKAGE_PREFIX}-re2")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "unzip"
             "git")
_commit="aca2b129b444207e927c06755603db6050e310bf"
options=('!buildflags')
source=(${_realname}::git+https://github.com/FirebirdSQL/firebird.git#commit=${_commit}
        pathtools.cpp
        pathtools.h
        0101-configure-fixes.patch
        0102-mingw-makefiles-fixes.patch
        0103-substitute-extauth-makefile.patch
        0104-external-sources-fixes.patch
        0105-internal-sources-fixes.patch
        0106-resource-file-fixes.patch
        0107-pathtools-fixes.patch
        0108-some-postinstall-fixes.patch)
sha256sums=('SKIP'
            '2f357fe2c6cdec3bfd90979616cfd075aee2de51beeaa08cd7a4192878efa64e'
            '0e6014650703817bc106825bb6a92bea72d9d790e89975c88d058fba2a26eddc'
            'e7b05eb2b71790f871c4043217816599b4fd3a696e25c03b8f3753a2606f11f7'
            'e23843b6232972547f7d93fb5df74f70170c842151ec73a32629ed92bcdb70e4'
            'a8167bc30557f36d68e3d3141622941961cffac79e158ce1ef69cd92ca2f9679'
            '88959ee452b2950ebaac5e1a034cbb54eb95c886616a322664074704a02ba43e'
            '6c167f563db2319d9ffd81a9ade8684e0dab6779cee1f27fba26aad5ad937be6'
            'd98427df51bdb4fcbc48743c3af1a5bc06efa8d909bb4310179025aa6f8c35f9'
            '0d583aa3c878cfd00c5531527a65a731fbe080096ef7d04f747de0ce7007c8d6'
            '5dfc5abd19a18620da96b3e95addceb58f1cb058a1e61a08ca9f9a37d237169c')

pkgver() {
  cd ${_realname}
  source ${srcdir}/${_realname}/src/misc/writeBuildNum.sh
  printf '%s.%s.%s.%s.%s' ${MajorVer} ${MinorVer} ${RevNo} ${BuildNum} "$(git rev-parse --short=8 "${_commit}")"
}

prepare() {
  cd ${_realname}
  cp -f ${srcdir}/pathtools.h src/common
  cp -f ${srcdir}/pathtools.cpp src/common/os/win32

  patch -p1 -i ${srcdir}/0101-configure-fixes.patch
  patch -p1 -i ${srcdir}/0102-mingw-makefiles-fixes.patch
  patch -p1 -i ${srcdir}/0103-substitute-extauth-makefile.patch
  patch -p1 -i ${srcdir}/0104-external-sources-fixes.patch
  patch -p1 -i ${srcdir}/0105-internal-sources-fixes.patch
  patch -p1 -i ${srcdir}/0106-resource-file-fixes.patch
  patch -p1 -i ${srcdir}/0107-pathtools-fixes.patch
  patch -p1 -i ${srcdir}/0108-some-postinstall-fixes.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  [[ -d build-${MSYSTEM} ]] && rm -rf build-${MSYSTEM}
  cp -rf ${_realname} build-${MSYSTEM} && cd build-${MSYSTEM}

  if [[ ${MSYSTEM} == CLANG* ]]; then
    CXXFLAGS="-Wno-varargs -Wno-constant-conversion -Wno-tautological-constant-out-of-range-compare -Wno-c++11-narrowing"
  else
    CXXFLAGS="-Wno-class-memaccess -Wno-overflow -Wno-stringop-overread -Wno-stringop-overflow -Wno-narrowing"
  fi

  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --disable-rpath \
    --with-system-re2 \
    --with-fbbin=${MINGW_PREFIX}/bin \
    --with-fbsbin=${MINGW_PREFIX}/bin \
    --with-fbconf=${MINGW_PREFIX}/etc/${_realname} \
    --with-fbsecure-db=${MINGW_PREFIX}/etc/${_realname} \
    --with-fbmsg=${MINGW_PREFIX}/etc/${_realname} \
    --with-fblib=${MINGW_PREFIX}/lib \
    --with-fbinclude=${MINGW_PREFIX}/include \
    --with-fbdoc=${MINGW_PREFIX}/share/${_realname}/doc \
    --with-fbsample=${MINGW_PREFIX}/share/${_realname}/examples \
    --with-fbsample-db=${MINGW_PREFIX}/share/${_realname}/examples/empbuild \
    --with-fbintl=${MINGW_PREFIX}/share/${_realname}/intl \
    --with-fbmisc=${MINGW_PREFIX}/share/${_realname}/misc \
    --with-fbplugins=${MINGW_PREFIX}/share/${_realname}/plugins \
    --with-fbtzdata=${MINGW_PREFIX}/share/${_realname}/tzdata \
    --with-fblog=${MINGW_PREFIX}/share/${_realname} \
    --with-fbglock=${MINGW_PREFIX}/share/${_realname}

  make
}

package() {
  cd build-${MSYSTEM}

  local _target=
  if check_option "debug" "y"; then
    _target=Debug
  else
    _target=Release
  fi

  mkdir -p ${pkgdir}${MINGW_PREFIX}/{bin,etc/${_realname},include,lib,share/${_realname}/{doc,examples,intl,misc,plugins,tzdata}}
  cp -f gen/${_target}/firebird/bin/*                   ${pkgdir}${MINGW_PREFIX}/bin/
  cp -f gen/${_target}/firebird/lib/libfbclient.dll.a   ${pkgdir}${MINGW_PREFIX}/lib/
  cp -f gen/${_target}/firebird/lib/libib_util.dll.a    ${pkgdir}${MINGW_PREFIX}/lib/
  cp -rf gen/${_target}/firebird/include/*              ${pkgdir}${MINGW_PREFIX}/include/
  cp -f src/misc/pascal/ib_util.pas                     ${pkgdir}${MINGW_PREFIX}/include/
  cp -f src/include/gen/Firebird.pas                    ${pkgdir}${MINGW_PREFIX}/include/${_realname}/
  cp -f gen/${_target}/firebird/intl/*                  ${pkgdir}${MINGW_PREFIX}/share/${_realname}/intl/
  cp -f src/misc/intl.sql                               ${pkgdir}${MINGW_PREFIX}/share/${_realname}/misc/
  cp -rf src/misc/upgrade                               ${pkgdir}${MINGW_PREFIX}/share/${_realname}/misc/
  cp -rf doc/*                                          ${pkgdir}${MINGW_PREFIX}/share/${_realname}/doc/
  cp -rf gen/${_target}/firebird/examples/*             ${pkgdir}${MINGW_PREFIX}/share/${_realname}/examples/
  cp -rf gen/${_target}/firebird/plugins/*              ${pkgdir}${MINGW_PREFIX}/share/${_realname}/plugins/
  cp -rf gen/${_target}/firebird/tzdata/*               ${pkgdir}${MINGW_PREFIX}/share/${_realname}/tzdata/

  cp -f gen/${_target}/firebird/*.conf                  ${pkgdir}${MINGW_PREFIX}/etc/${_realname}/
  cp -f gen/${_target}/firebird/*.msg                   ${pkgdir}${MINGW_PREFIX}/etc/${_realname}/
  cp -f gen/${_target}/firebird/*.fdb                   ${pkgdir}${MINGW_PREFIX}/etc/${_realname}/

  # Remove build tools
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/{gpre_*,build*}.exe

  install -Dm644 builds/install/misc/IDPLicense.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/IDPLicense.txt
  install -Dm644 builds/install/misc/IPLicense.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/IPLicense.txt
}
