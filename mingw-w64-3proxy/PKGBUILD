# Contributor: Alexander Georgievskiy <galeksandrp@gmail.com>

_realname=3proxy
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
conflicts=("${MINGW_PACKAGE_PREFIX}-libproxy")
pkgver=0.9.4
pkgrel=1
pkgdesc='3proxy - tiny free proxy server (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
license=('BSD' 'Apache' 'GPL' 'LGPL')
url="https://github.com/z3APA3A/${_realname}"
msys2_references=(
  "cpe: cpe:/a:3proxy:3proxy"
)
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc")
source=(${_realname}-${pkgver}.tar.gz::"${url}/archive/${pkgver}.tar.gz"
        001-win-install.patch)
sha256sums=('b497f74d6cc7ee58ff824457427acc02c6f7a102e483816fbb1b2494942ef983'
            'a12af87ccc044e81365b5afdc853f5007b0ce3b55e715c8cf85dd73d0b7bd307')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/001-win-install.patch
  mkdir -p bin
}

build() {
  cd "${srcdir}"/${_realname}-${pkgver}

  make CC=${CC} LN=${CC} -f Makefile.win
}

package() {
  cd "${srcdir}"/${_realname}-${pkgver}
  make CC=${CC} LN=${CC} DESTDIR="${pkgdir}" prefix=${MINGW_PREFIX} install -f Makefile.win
}
