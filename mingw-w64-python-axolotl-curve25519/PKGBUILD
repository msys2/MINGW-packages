# Maintainer: Michel Le Bihan <michel@lebihan.pl>

_realname=axolotl-curve25519
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.4.1.post2
pkgrel=1
pkgdesc="Python wrapper for curve25519 library with ed25519 signatures (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='https://github.com/tgalal/python-axolotl-curve25519'
license=('spdx:GPL-3.0')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python-wheel"
             "${MINGW_PACKAGE_PREFIX}-cc")
source=("https://pypi.org/packages/source/p/python-${_realname}/python-${_realname}-${pkgver}.tar.gz"
        "0001-Correct-types-in-PyModuleDef.patch")
sha256sums=('0705a66297ebd2f508a60dc94e22881c754301eb81db93963322f6b3bdcb63a3'
            'b47d0eb47d4dd0534dc4fdfd6254f4e083c4cd6e94b393e078a7955a176c7ee0')

prepare() {
  cd "${srcdir}/python-${_realname}-${pkgver}"

  patch -Np1 -i "${srcdir}/0001-Correct-types-in-PyModuleDef.patch"
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}"
  cp -r "python-${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  msg "Python install for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl
}
