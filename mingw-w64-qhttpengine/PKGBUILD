# Maintainer: Nathan Osman <nathan@quickmediasolutions.com>

_realname=qhttpengine
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.0.1
pkgrel=1
pkgdesc="HTTP server for Qt applications (mingw-w64)"
arch=('any')
license=('MIT')
url='https://github.com/nitroshare/qhttpengine'
depends=("${MINGW_PACKAGE_PREFIX}-qt5>=5.4")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake>=3.2"
             "${MINGW_PACKAGE_PREFIX}-gcc")
source=("https://github.com/nitroshare/qhttpengine/archive/${pkgver}.tar.gz")
sha256sums=('6505cf889909dc29bab4069116656e7ca5a9e879f04935139439c5691a76c55e')

build() {
  mkdir ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    ${srcdir}/${_realname}-${pkgver}
  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
}
