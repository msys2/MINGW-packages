# Maintainer: Andrew Sun <adsun701@gmail.com>

_realname=zimg
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.6.1
pkgrel=1
pkgdesc="Scaling, colorspace conversion, and dithering library (mingw-w64)"
arch=('any')
url="https://github.com/sekrit-twc/zimg"
license=("WTFPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config" "tar")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" "${MINGW_PACKAGE_PREFIX}-winpthreads-git")
source=("release-${pkgver}.tar.gz::https://github.com/sekrit-twc/${_realname}/archive/release-${pkgver}.tar.gz")
noextract=("release-${pkgver}.tar.gz")

prepare(){
  cp "../release-${pkgver}.tar.gz" "${srcdir}/release-${pkgver}.tar.gz"
  tar -xzvf "${srcdir}/release-${pkgver}.tar.gz"
  cd "${srcdir}/${_realname}-release-${pkgver}"
  ./autogen.sh
}

build() {
  [[ -d "build-${MINGW_CHOST}" ]] && rm -rf "build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  ../${_realname}-release-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-x86simd \
    --enable-static \
    --enable-shared

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install
}

md5sums=('322e9aa72833c498510772be4da76665')
