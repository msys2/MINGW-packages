# Contributor: Rafa≈Ç Brzegowy <rafal.brzegowy@yahoo.com>

_realname=geneticalgorithm
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.0.1
_pkgver2=${pkgver//./}
pkgrel=2
pkgdesc="Genetic Algorithm Package for Python (mingw64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/rmsolgi/geneticalgorithm"
msys2_references=(
  'pypi: geneticalgorithm'
)
license=('spdx:custom')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-numpy"
         "${MINGW_PACKAGE_PREFIX}-python-matplotlib"
         "${MINGW_PACKAGE_PREFIX}-python-func-timeout")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python-wheel")
source=("https://github.com/rmsolgi/geneticalgorithm/archive/refs/tags/${_pkgver2}.tar.gz")
sha256sums=('DA63515A03A18E90518FB367727F77E13B100DB63253BE2408317D23EDBD5ADD')

prepare() {
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname}-${_pkgver2}" "python-build-${MSYSTEM}"
}

build() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
