# Support libraries for KiCad
_enable_packages3D=yes # When not yes, both packages3D and meta are not built.

_realname=kicad
pkgbase=mingw-w64-${_realname}-library
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}-footprints"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-symbols"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-templates"
         $( [[ ${_enable_packages3D} == "yes" ]] && echo "${MINGW_PACKAGE_PREFIX}-${_realname}-packages3D")
         $( [[ ${_enable_packages3D} == "yes" ]] && echo "${MINGW_PACKAGE_PREFIX}-${_realname}-meta") )
pkgver=7.0.9
pkgrel=1
pkgdesc="Support libraries for KiCad (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'mingw32' 'clang32')
url='https://www.kicad.org/'
license=("spdx:GPL-3.0-or-later")
groups=("${MINGW_PACKAGE_PREFIX}-eda")
depends=()
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             $( [[ ${_enable_packages3D} == "yes" ]] && echo "${MINGW_PACKAGE_PREFIX}-${_realname}") # needed for meta package
)
options=('!strip')
source=("https://gitlab.com/kicad/libraries/kicad-footprints/-/archive/${pkgver}/kicad-footprints-${pkgver}.tar.bz2"
        "https://gitlab.com/kicad/libraries/kicad-symbols/-/archive/${pkgver}/kicad-symbols-${pkgver}.tar.bz2"
        "https://gitlab.com/kicad/libraries/kicad-templates/-/archive/${pkgver}/kicad-templates-${pkgver}.tar.bz2")
sha256sums=('1307f37613c1971794940ee78ad49146ae10eb3525a0c9f427adb8900ddbee0e'
            'c1be547ab435cccf9c98d21c9a816ab3051ae8e306bcbcb4ac46a896c88210ab'
            '3f87dfe352cf12e73cf7769202101935fba547da5f04e1029bdd786ad0f9f0a4')
if [[ ${_enable_packages3D} == "yes" ]]; then
  source+=("https://gitlab.com/kicad/libraries/kicad-packages3D/-/archive/${pkgver}/kicad-packages3D-${pkgver}.tar.bz2")
  sha256sums+=('cf9dd300e4020f25e17835c7f930af1974d5953694d103b65bb0a41eb80bf19a')
fi

_build_cmake()
{
  msg2 "Build kicad-$1"
  [[ -d "${srcdir}"/build-$1-${MSYSTEM} ]] && rm -rf "${srcdir}"/build-$1-${MSYSTEM}
  mkdir -p "${srcdir}/build-$1-${MSYSTEM}" && cd "${srcdir}/build-$1-${MSYSTEM}"

    MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      ../${_realname}-$1-${pkgver}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

build() {
  _build_cmake "footprints"
  _build_cmake "symbols"
  _build_cmake "templates"
  if [[ ${_enable_packages3D} == "yes" ]]; then
    # echo "sha256sum"
    # sha256sum "${srcdir}"/kicad-packages3D-${pkgver}.tar.bz2 || true
    _build_cmake "packages3D"
  fi
}

package_footprints() {
  pkgdesc="KiCad footprints library (mingw-w64)"
  conflicts=(${MINGW_PACKAGE_PREFIX}-${_realname}-footprints-git)
  replaces=(${MINGW_PACKAGE_PREFIX}-${_realname}-footprints-git)
  depends=()
  optdepends=("${MINGW_PACKAGE_PREFIX}-${_realname}")

  cd "${srcdir}/build-footprints-${MSYSTEM}"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .
}

package_symbols() {
  pkgdesc="KiCad symbols library (mingw-w64)"
  conflicts=(${MINGW_PACKAGE_PREFIX}-${_realname}-symbols-git)
  replaces=(${MINGW_PACKAGE_PREFIX}-${_realname}-symbols-git)
  depends=()
  optdepends=("${MINGW_PACKAGE_PREFIX}-${_realname}")

  cd "${srcdir}/build-symbols-${MSYSTEM}"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .
}

package_templates() {
  pkgdesc="KiCad templates library (mingw-w64)"
  conflicts=(${MINGW_PACKAGE_PREFIX}-${_realname}-templates-git)
  replaces=(${MINGW_PACKAGE_PREFIX}-${_realname}-templates-git)

  cd "${srcdir}/build-templates-${MSYSTEM}"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .
}

package_packages3D() {
  pkgdesc="KiCad 3D models library (mingw-w64)"
  conflicts=(${MINGW_PACKAGE_PREFIX}-${_realname}-packages3D-git)
  replaces=(${MINGW_PACKAGE_PREFIX}-${_realname}-packages3D-git)

  cd "${srcdir}/build-packages3D-${MSYSTEM}"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .
}

package_meta() {
  pkgdesc="Meta package for KiCad containing the core application and support libraries (mingw-w64)"

  depends=(${MINGW_PACKAGE_PREFIX}-${_realname}
           ${MINGW_PACKAGE_PREFIX}-${_realname}-footprints
           ${MINGW_PACKAGE_PREFIX}-${_realname}-symbols
           ${MINGW_PACKAGE_PREFIX}-${_realname}-templates
           ${MINGW_PACKAGE_PREFIX}-${_realname}-packages3D)
  optdepends=()
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-${_realname}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
