_realname=libadalang-tools
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.0
pkgrel=1
_branch=24.0

pkgdesc="Libadalang-based tools for Ada: gnatpp, gnatmetric, gnatstub and gnattest (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
msys2_references=(
  'aur: libadalang-tools'
)

url="https://github.com/AdaCore/libadalang-tools"
license=('GPL-3.0-or-later WITH GCC-exception-3.1')
makedepends=("${MINGW_PACKAGE_PREFIX}-gprbuild"
             "which")
depends=("${MINGW_PACKAGE_PREFIX}-libadalang"
         "${MINGW_PACKAGE_PREFIX}-python-ipython"
         "${MINGW_PACKAGE_PREFIX}-templates_parser")
source=("${_realname}-git::git+https://github.com/AdaCore/libadalang-tools.git#branch=${_branch}")
sha256sums=('SKIP')

pkgver() {
  cd ${_realname}-git
  printf "%s.r%s.%s" "$_branch" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
  cd "${srcdir}/${_realname}-git/"

  export BUILD_MODE=prod
  make lib

  LIBRARY_TYPE=relocatable \
  make bin
}

package() {
  cd "${srcdir}/${_realname}-git/"
  export BUILD_MODE=prod
  export LIBRARY_TYPE=relocatable
  export DESTDIR="${pkgdir}${MINGW_PREFIX}"
  make install-lib
  make install-bin-strip
  # Copy License Files
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" \
    "${srcdir}/${_realname}-git"/LICENSE
}
