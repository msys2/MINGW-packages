_realname=libadalang-tools
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.0
pkgrel=1

pkgdesc="Libadalang-based tools for Ada: gnatpp, gnatmetric, gnatstub and gnattest (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
msys2_references=(
  'aur: libadalang-tools'
)

url="https://github.com/AdaCore/libadalang-tools"
license=('spdx:GPL-3.0-or-later WITH GCC-exception-3.1')
makedepends=("${MINGW_PACKAGE_PREFIX}-gprbuild"
             "which")
depends=("${MINGW_PACKAGE_PREFIX}-libadalang"
         "${MINGW_PACKAGE_PREFIX}-libvss"
         "${MINGW_PACKAGE_PREFIX}-python-ipython"
         "${MINGW_PACKAGE_PREFIX}-templates_parser")
source=("https://github.com/AdaCore/libadalang-tools/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('d96e91f2e022ad2856025226c0024aacb49f1bc6bbcf7d4644f1c3e2d7d30329')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}/"
  build_year=$(LANG=C date "+%Y")
  sed -i 's/:= "dev";/:= "${pkgver}";/' "src/utils-versions.ads"
  sed -i 's/:= "unknown";/:= "${build_year}";/' "src/utils-versions.ads"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}/"

  export BUILD_MODE=prod
  make lib

  LIBRARY_TYPE=relocatable \
  make bin
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}/"
  export BUILD_MODE=prod
  export LIBRARY_TYPE=relocatable
  export DESTDIR="${pkgdir}${MINGW_PREFIX}"
  make install-lib
  make install-bin-strip
  # Copy License Files
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" \
    "${srcdir}/${_realname}-${pkgver}"/LICENSE
}
