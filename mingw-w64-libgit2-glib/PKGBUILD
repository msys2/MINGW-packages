# Maintainer: Alberto Fanjul Alonso <albfan@gnome.org>

_realname=libgit2-glib
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
_pkgver=1.0.0
pkgver=$_pkgver.1
pkgrel=5
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
pkgdesc="GLib wrapper for libgit2 (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-libgit2>=0.99"
         "${MINGW_PACKAGE_PREFIX}-libssh2"
         "${MINGW_PACKAGE_PREFIX}-glib2")
makedepends=("${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-openssl"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-vala"
             "${MINGW_PACKAGE_PREFIX}-gtk-doc"
             "${MINGW_PACKAGE_PREFIX}-pygobject-devel"
             "${MINGW_PACKAGE_PREFIX}-cc")
options=('strip' 'staticlibs')
license=("LGPL2.1")
url="https://wiki.gnome.org/Projects/Libgit2-glib"
source=("https://download.gnome.org/sources/${_realname}/${_pkgver%.*}/${_realname}-${pkgver}.tar.xz"
        "install-no-symlinks.patch")
sha256sums=('460a5d6936950ca08d2d8518bfc90c12bb187cf6e674de715f7055fc58102b57'
            'c1a507403c0c3ba322332a9b7efe6a59ea2ae2d47183c6fc49a7ad9970dc1354')

prepare() {
  cd "${_realname}-${pkgver}"

  patch -Np1 -i "${srcdir}/install-no-symlinks.patch"
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="--prefix=" \
  meson \
    --prefix="${MINGW_PREFIX}" \
    --buildtype plain \
    -Dvapi=true \
    -Dtranslate_windows_paths=true \
    -Dgtk_doc=true \
    ../${_realname}-${pkgver}

  ninja
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  DESTDIR="${pkgdir}" ninja install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
