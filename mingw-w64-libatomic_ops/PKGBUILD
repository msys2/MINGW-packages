# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libatomic_ops
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=7.4.0
pkgrel=1
pkgdesc="Provides semi-portable access to hardware provided atomic memory operations"
arch=('any')
url="https://github.com/ivmai/libatomic_ops"
depends=("${_mingw_suff}-crt")
license=('GPL2' 'MIT')
source=(https://github.com/ivmai/libatomic_ops/archive/${_realname}-${pkgver//./_}.tar.gz)
md5sums=('87900bbf98f2ff7dce062e7b62e9d8ef')

prepare(){
	cd ${srcdir}/${_realname}-${_realname}-${pkgver//./_}
	sed -i 's#pkgdata#doc#' doc/Makefile.am
  
	autoreconf -fi
}

build() {
	mkdir ${srcdir}/build-${MINGW_CHOST}
	cd ${srcdir}/build-${MINGW_CHOST}
	../${_realname}-${_realname}-${pkgver//./_}/configure \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--target=${MINGW_CHOST} \
		--prefix=${MINGW_PREFIX} \
		--disable-static
	make
}

check() {
	cd ${srcdir}/build-${MINGW_CHOST}
	PATH="${srcdir}//build-${MINGW_CHOST}/src/.libs:$PATH" make check
}

package() {
	cd ${srcdir}/build-${MINGW_CHOST}
	make DESTDIR="${pkgdir}" install
	install -D -m644 ${srcdir}/${_realname}-${_realname}-${pkgver//./_}/doc/LICENSING.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname/LICENSE"
}
