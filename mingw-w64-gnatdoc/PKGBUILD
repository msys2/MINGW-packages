# Maintainer: Philipp Smirnov <psmirnov@airmail.cc>

_realname=gnatdoc
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.0
pkgrel=1

pkgdesc="GNAT documentation generation tool (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
msys2_references=(
  'aur: gnatdoc'
)

url="https://github.com/AdaCore/gnatdoc"
license=('spdx:GPL-3.0-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-gprbuild")
depends=("${MINGW_PACKAGE_PREFIX}-gnatcoll"
         "${MINGW_PACKAGE_PREFIX}-markdown")
source=("https://github.com/AdaCore/gnatdoc/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('bbd046bd5007dd5a7453221973c05bfbd3ff46ea4ebfed8dea471f9a5c6e4df8')

build() {
  cd "${srcdir}/${_realname}-${pkgver}/"
  export LIBRARY_TYPE=relocatable
  gprbuild -j0 -p -P gnat/libgnatdoc.gpr -XSUPERPROJECT=
  make all
  # make SCENARIO_VARIABLES="-XGPR_UNIT_PROVIDER_LIBRARY_TYPE=static -XGPR_UNIT_PROVIDER_BUILD=debug -XVSS_LIBRARY_TYPE=static -XMARKDOWN_LIBRARY_TYPE=static" all
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}/"

  gprinstall gnat/gnatdoc.gpr \
    --prefix="${pkgdir}${MINGW_PREFIX}" \
    --create-missing-dirs \
    -XGPR_UNIT_PROVIDER_LIBRARY_TYPE=relocatable \
    -XGPR2_LIBRARY_TYPE=relocatable

  gprinstall gnat/libgnatdoc.gpr    \
              --prefix="${pkgdir}${MINGW_PREFIX}"   \
              --create-missing-dirs

  # FIXME: License file missing from tarball
  ## Copy License Files
  #install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" \
  # "${srcdir}/${_realname}-${pkgver}"/COPYING*
}
