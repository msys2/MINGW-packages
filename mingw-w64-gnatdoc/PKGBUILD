_realname=gnatdoc
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.r445.3e94448
pkgrel=1
_branch=24.0

pkgdesc="GNAT documentation generation tool (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
msys2_references=(
  'aur: gnatdoc'
)

url="https://github.com/AdaCore/gnatdoc"
license=('GPL-3.0-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-gprbuild")
depends=("${MINGW_PACKAGE_PREFIX}-gnatcoll"
         "${MINGW_PACKAGE_PREFIX}-markdown"
         "${MINGW_PACKAGE_PREFIX}-gpr-unit-provider")
source=("git+https://github.com/AdaCore/gnatdoc.git#branch=${_branch}")
sha256sums=('SKIP')

pkgver() {
  cd ${_realname}
  printf "%s.r%s.%s" "$_branch" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
  cd "${srcdir}/${_realname}/"
  export LIBRARY_TYPE=relocatable
  gprbuild -j0 -p -P gnat/libgnatdoc.gpr -XSUPERPROJECT=
  make all
  # make SCENARIO_VARIABLES="-XGPR_UNIT_PROVIDER_LIBRARY_TYPE=static -XGPR_UNIT_PROVIDER_BUILD=debug -XVSS_LIBRARY_TYPE=static -XMARKDOWN_LIBRARY_TYPE=static" all
}

package() {
  cd "${srcdir}/${_realname}/"

  gprinstall gnat/gnatdoc.gpr \
    --prefix="${pkgdir}${MINGW_PREFIX}" \
    --create-missing-dirs \
    -XGPR_UNIT_PROVIDER_LIBRARY_TYPE=relocatable \
    -XGPR2_LIBRARY_TYPE=relocatable

  gprinstall gnat/libgnatdoc.gpr    \
              --prefix="${pkgdir}${MINGW_PREFIX}"   \
              --create-missing-dirs

  # FIXME: License file missing from tarball
  ## Copy License Files
  #install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" \
  # "${srcdir}/${_realname}-${pkgver}"/COPYING*
}
