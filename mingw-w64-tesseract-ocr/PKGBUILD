# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=tesseract-ocr
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.04.rc1.master
pkgrel=1
pkgdesc="Tesseract OCR (mingw-w64)"
arch=('any')
url="https://code.google.com/p/tesseract-ocr/"
license=("Apache License 2.0")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=(${MINGW_PACKAGE_PREFIX}-gcc-libs
        ${MINGW_PACKAGE_PREFIX}-leptonica
        ${MINGW_PACKAGE_PREFIX}-icu
        ${MINGW_PACKAGE_PREFIX}-cairo
        ${MINGW_PACKAGE_PREFIX}-pango
        ${MINGW_PACKAGE_PREFIX}-zlib)
options=('!libtool' 'strip')
source=("https://tesseract-ocr.googlecode.com/archive/master.tar.gz"
)
sha256sums=('SKIP'
)
prepare() {
  cd "$srcdir/${_realname}-master"
  ./autogen.sh
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  local -a extra_config
  if check_option "debug" "y"; then
    extra_config+=( --enable-debug )
  fi

  ${srcdir}/${_realname}-master/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    LIBLEPT_HEADERSDIR=${MINGW_PREFIX}/include \
    "${extra_config[@]}"

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install
  make training
  make DESTDIR="$pkgdir" training-install
}
