# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Maintainer: Ray Donnelly <mingw.android@gmail.com>
# Contributor: ShreeDevi Kumar

_realname=tesseract-ocr
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.04
pkgrel=1
pkgdesc="Tesseract OCR (mingw-w64)"
arch=('any')
url="https://code.google.com/p/tesseract-ocr/"
license=("Apache License 2.0")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")

depends=(
        ${MINGW_PACKAGE_PREFIX}-tesseract-ocr-eng
        ${MINGW_PACKAGE_PREFIX}-tesseract-ocr-osd
        ${MINGW_PACKAGE_PREFIX}-leptonica
        ${MINGW_PACKAGE_PREFIX}-icu
        ${MINGW_PACKAGE_PREFIX}-cairo
        ${MINGW_PACKAGE_PREFIX}-pango
        )

options=('!libtool' '!strip' 'debug')

source=("${_realname}"::"git+https://code.google.com/p/tesseract-ocr/"
        "0001-Fix-broken_MSC_VER-checks.patch"
        "0002-Use-intptr_t-for-pointer-cast-target.patch")
sha256sums=('SKIP'
            '69e00058dd7c32c2c205dc1247b07edd43b3ba30faa4af67b646a8f7b58c6494'
            '5a74ecf3d06f950064b7d570166629497940d491c85dda5b65defa66f65d47aa')

pkgver() {
  cd "$srcdir/$_realname"
  printf "%s" "$(git describe --tags --long --always | sed -e 's/-/\./g')" 
}

prepare() {
  cd "$srcdir"/${_realname}
  patch -p1 -i "${srcdir}"/0001-Fix-broken_MSC_VER-checks.patch
  patch -p1 -i "${srcdir}"/0002-Use-intptr_t-for-pointer-cast-target.patch
  ./autogen.sh
}

build() {
  cd "$srcdir"/${_realname}
  [ -d "${srcdir}"/build-${CARCH} ] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}
  local -a extra_config
  if check_option "debug" "y"; then
    extra_config+=( --enable-debug )
  fi

  ${srcdir}/${_realname}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    LIBLEPT_HEADERSDIR=${MINGW_PREFIX}/include \
    "${extra_config[@]}"
  
  make
}

check() {
  cd "${srcdir}/build-${CARCH}"
  make -j1 check
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make install DESTDIR="$pkgdir" 
  make training
  make training-install DESTDIR="$pkgdir"
}
