# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=tesseract-ocr
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.03.298e31465a44
pkgrel=1
pkgdesc="Tesseract OCR (mingw-w64)"
arch=('any')
url="https://code.google.com/p/tesseract-ocr/"
license=("Apache License 2.0")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=(${MINGW_PACKAGE_PREFIX}-gcc-libs
        ${MINGW_PACKAGE_PREFIX}-leptonica
        ${MINGW_PACKAGE_PREFIX}-icu
        ${MINGW_PACKAGE_PREFIX}-cairo
        ${MINGW_PACKAGE_PREFIX}-pango
        ${MINGW_PACKAGE_PREFIX}-zlib)
options=('!libtool' 'strip')
source=("https://tesseract-ocr.googlecode.com/archive/298e31465a445e54defedd076217ff24b1af3fc2.tar.gz"
        "0001-Fix-broken_MSC_VER-checks.patch"
        "0002-Use-intptr_t-for-pointer-cast-target.patch")
sha256sums=('SKIP'
            '69e00058dd7c32c2c205dc1247b07edd43b3ba30faa4af67b646a8f7b58c6494'
            '5a74ecf3d06f950064b7d570166629497940d491c85dda5b65defa66f65d47aa')
prepare() {
  cd "$srcdir/${_realname}-298e31465a44"
  patch -p1 -i "${srcdir}"/0001-Fix-broken_MSC_VER-checks.patch
  patch -p1 -i "${srcdir}"/0002-Use-intptr_t-for-pointer-cast-target.patch
  ./autogen.sh
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  local -a extra_config
  if check_option "debug" "y"; then
    extra_config+=( --enable-debug )
  fi

  ${srcdir}/${_realname}-298e31465a44/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    LIBLEPT_HEADERSDIR=${MINGW_PREFIX}/include \
    "${extra_config[@]}"

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install
  make training
  make DESTDIR="$pkgdir" training-install
}
