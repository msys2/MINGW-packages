# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>

_realname=umap-learn
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.5.3
pkgrel=3
pkgdesc='Uniform Manifold Approximation and Projection (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
msys2_references=(
  'pypi: umap-learn'
)
url="https://github.com/lmcinnes/umap"
license=('BSD')
depends=(
  "${MINGW_PACKAGE_PREFIX}-python-scikit-learn"
  "${MINGW_PACKAGE_PREFIX}-python-numba"
  "${MINGW_PACKAGE_PREFIX}-python-pynndescent"
  "${MINGW_PACKAGE_PREFIX}-python-tqdm"
)
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools")
source=("https://files.pythonhosted.org/packages/source/${_realname:0:1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('dbd57cb181c2b66d238acb5635697526bf24c798082daed0cf9b87f6a3a6c0c7')

prepare() {
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname}-$pkgver" "python-build-${MSYSTEM}"
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE.txt"
}
