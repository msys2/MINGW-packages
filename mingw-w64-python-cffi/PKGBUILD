# Contributor: Frederic Wang <fred.wang@free.fr>

_realname=cffi
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}" "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=1.3.0
pkgrel=1
pkgdesc='C Foreign Function Interface for Python (mingw-w64)'
arch=('any')
url='http://cffi.readthedocs.org/en/latest/'
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python2-pycparser"
             "${MINGW_PACKAGE_PREFIX}-python3-pycparser")
source=("https://pypi.python.org/packages/source/c/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('9daa53aff0b5cf64c85c10eab7ce6776880d0ee71b78cedeae196ae82b6734e9')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 < "${startdir}"/001-compile-and-setup-fixes.patch
  cd "${srcdir}"
  cp -r ${_realname}-${pkgver} python2-build
  cp -r ${_realname}-${pkgver} python3-build
}

package_python3-cffi() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3")

  cd ${srcdir}/python3-build
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}

package_python2-cffi() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2")

  cd ${srcdir}/python2-build
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE"
}

package_mingw-w64-i686-python2-cffi() {
  package_python2-cffi
}

package_mingw-w64-i686-python3-cffi() {
  package_python3-cffi
}

package_mingw-w64-x86_64-python2-cffi() {
  package_python2-cffi
}

package_mingw-w64-x86_64-python3-cffi() {
  package_python3-cffi
}
