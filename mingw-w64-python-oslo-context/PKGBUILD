# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=oslo-context
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}"  "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=2.21.0
pkgrel=1
pkgdesc="OpenStack Context library (mingw-w64)"
arch=('any')
url="https://pypi.python.org/pypi/oslo.context/$pkgver"
license=('Apache')
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-pbr"
             "${MINGW_PACKAGE_PREFIX}-python2-pbr"
             "${MINGW_PACKAGE_PREFIX}-python3-debtcollector"
             "${MINGW_PACKAGE_PREFIX}-python2-debtcollector")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python3-mock"
              "${MINGW_PACKAGE_PREFIX}-python2-mock"
              "${MINGW_PACKAGE_PREFIX}-python3-oslotest"
              "${MINGW_PACKAGE_PREFIX}-python2-oslotest"
              "${MINGW_PACKAGE_PREFIX}-python3-testrepository"
              "${MINGW_PACKAGE_PREFIX}-python2-testrepository")
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-$pkgver.tar.gz"::"https://github.com/openstack/oslo.context/archive/$pkgver.tar.gz")
sha512sums=('af8943894134bcf7ce533f91a4dffe5d7347225fe4b58506d18b702df580073575a50e93fda9a632d445beb0f23a2dda42b7c58c1be5d786ab1420afe93b05dd')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"
#  pushd "${_realname}-${pkgver}"
#    apply_patch_with_msg 0001-A-really-important-fix.patch \
#      0002-A-less-important-fix.patch
#  popd 
  for builddir in python{2,3}-build-${CARCH}; do
    rm -rf ${builddir} | true
    cp -r "oslo.context-${pkgver}" "${builddir}"
  done
  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
  # Set version for python-pbr
  export PBR_VERSION=$pkgver
}

# Note that build() is sometimes skipped because it's done in 
# the packages setup.py install for simplicity if you can do so.
# but sometimes, you want to do a check before install which would
# also trigger the build.
build() {
  for pver in {2,3}; do  
    msg "Python ${pver} build for ${CARCH}"  
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py build
  done  
}

check() {
  for pver in {2,3}; do
    msg "Python ${pver} test for ${CARCH}"
    cd "${srcdir}/python${pver}-build-${CARCH}"
    PYTHON=python${pver} ${MINGW_PREFIX}/bin/python${pver} setup.py testr || warning "Tests failed"
  done
}

package_python3-oslo-context() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3"
           "${MINGW_PACKAGE_PREFIX}-python3-pbr" 
           "${MINGW_PACKAGE_PREFIX}-python3-debtcollector")

  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}

package_python2-oslo-context() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2"
           "${MINGW_PACKAGE_PREFIX}-python2-pbr" 
           "${MINGW_PACKAGE_PREFIX}-python2-debtcollector")

  cd "${srcdir}/python2-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE"
}

package_mingw-w64-i686-python2-oslo-context() {
  package_python2-oslo-context
}

package_mingw-w64-i686-python3-oslo-context() {
  package_python3-oslo-context
}

package_mingw-w64-x86_64-python2-oslo-context() {
  package_python2-oslo-context
}

package_mingw-w64-x86_64-python3-oslo-context() {
  package_python3-oslo-context
}
