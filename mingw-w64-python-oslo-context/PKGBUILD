# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_pyname=oslo.context
_realname=oslo-context
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=5.7.0
pkgrel=1
pkgdesc="OpenStack Context library (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'archlinux: python-oslo-context'
  'purl: pkg:pypi/oslo-context'
)
msys2_repository_url='https://opendev.org/openstack/oslo.context/'
msys2_changelog_url='https://docs.openstack.org/releasenotes/oslo.context/'
msys2_documentation_url='https://docs.openstack.org/oslo.context/latest/'
msys2_issue_tracker_url='https://bugs.launchpad.net/oslo.context/'
url="https://docs.openstack.org/oslo.context/latest"
license=('spdx:Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-pbr"
         "${MINGW_PACKAGE_PREFIX}-python-debtcollector")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-mock"
              "${MINGW_PACKAGE_PREFIX}-python-oslotest")
source=("https://pypi.org/packages/source/${_realname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha256sums=('398c460b9cf7cb397ede7962223e4b88078fb2fbc53665a47a34e1b28890f4ce')

build () {
  # Set version for python-pbr
  export PBR_VERSION=${pkgver}

  cp -r "${_pyname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  PYTHON=${MINGW_PREFIX}/bin/python ${MINGW_PREFIX}/bin/stestr run || warning "Tests failed"
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
