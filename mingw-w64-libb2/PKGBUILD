# Maintainer: Peter Budai <peterbudai@hotmail.com>

_realname=libb2
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.98.1
pkgrel=1
pkgdesc="C library providing BLAKE2b, BLAKE2s, BLAKE2bp, BLAKE2sp "
arch=('any')
url="https://blake2.net"
license=('CC0')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/BLAKE2/${_realname}/archive/v$pkgver.tar.gz")
sha256sums=('10053dbc2fa342516b780a6bbf6e7b2a2360b8d49c5ac426936bf3df82526732')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  autoreconf -fiv
}

build() {

  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"

  "${srcdir}/${_realname}-${pkgver}"/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --enable-native=no \
      --enable-fat=yes

  make
}

check() {
  cd "${srcdir}/build-${CARCH}"
  make check
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install
}
