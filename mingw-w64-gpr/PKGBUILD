# Maintainer: Philipp Smirnov <psmirnov@airmail.cc>

_realname=gpr
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.r2416.965de29
pkgrel=1
_branch=24.0

pkgdesc="The framework for analyzing the GNAT Project (GPR) files (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
msys2_references=(
  'aur: gpr'
)

url="https://github.com/AdaCore/gpr"
license=('Apache-2.0 AND GPL-3.0-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-gprbuild"
             "${MINGW_PACKAGE_PREFIX}-python-e3-core")
depends=(
        # "${MINGW_PACKAGE_PREFIX}-gnatcoll-iconv"
        # "${MINGW_PACKAGE_PREFIX}-gnatcoll-gmp"
         "${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-funcy"
         "${MINGW_PACKAGE_PREFIX}-python-mako"
         "${MINGW_PACKAGE_PREFIX}-python-docutils"
         "which")
source=("git+https://github.com/AdaCore/gpr#branch=${_branch}")
sha256sums=('SKIP')

pkgver() {
  cd ${_realname}
  printf "%s.r%s.%s" "$_branch" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

prepare() {
  cd "${srcdir}/${_realname}"
  make setup prefix="${pkgdir}${MINGW_PREFIX}" GPR2KBDIR=${MINGW_PREFIX}/share/gprconfig
}

build() {
  cd "${srcdir}/${_realname}"

  make

  # make build-lib-static
  # make build-lib-static-pic
  # make build-lib-relocatable
  # make build-tools

  # make doc
  # make docgen
}

package() {
  cd "${srcdir}/${_realname}"

  # Without -j1 concurrent gprinstall processes may crash:
  #
  #     raised ADA.IO_EXCEPTIONS.END_ERROR : a-tigeli.adb:96
  #     make: *** [Makefile:209: install-lib-static-pic] Error 1
  #
  # Inspired by: https://github.com/AdaCore/gprbuild/issues/41#issuecomment-396234433
  #
  make -j1 install prefix="${pkgdir}${MINGW_PREFIX}"

  # These conflict with the binaries from 'gprbuild'.
  #
  rm "${pkgdir}${MINGW_PREFIX}"/bin/gprclean.exe
  rm "${pkgdir}${MINGW_PREFIX}"/bin/gprconfig.exe
  rm "${pkgdir}${MINGW_PREFIX}"/bin/gprinstall.exe
  rm "${pkgdir}${MINGW_PREFIX}"/bin/gprls.exe

  # Copy License Files
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" \
    "${srcdir}/${_realname}"/LICENSE*
}
