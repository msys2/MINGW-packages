# Maintainer: Andrew Chadwick <a.t.chadwick@gmail.com>

_realname=styrene
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}-git
pkgbase=mingw-w64-${_realname}-git
pkgver=0.2.0
pkgrel=1
pkgdesc="Repacks MSYS2 packages into installer .exes and standalone .zips (mingw-w64)"
url="https://github.com/achadwick/styrene"
arch=('any')
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
makedepends=('git')
depends=(
    "zip"
    "${MINGW_PACKAGE_PREFIX}-python3"
    "${MINGW_PACKAGE_PREFIX}-gcc"
    "${MINGW_PACKAGE_PREFIX}-binutils"
    "${MINGW_PACKAGE_PREFIX}-nsis"
)
license=('GPLv3+')
source=("${_realname}::git+https://github.com/achadwick/styrene.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}"/${_realname}
    git describe --tags | sed 's,-\(alpha\|beta\),\1,' \
      | sed 's,-,.,g' | sed 's,^v,,'
}

prepare() {
    cd "${srcdir}"/${_realname}
    git reset --hard HEAD
    git clean -fx
}

build() {
    cd "${srcdir}"/${_realname}
    rm -fr build
    python3 setup.py build
}

package() {
    cd "${srcdir}"/${_realname}
    MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
        ${MINGW_PREFIX}/bin/python3 setup.py install \
        --prefix="${MINGW_PREFIX}" --root="${pkgdir}"

    # De-hardcode script paths
    local MINGW_PREFIX_W=$(cygpath -m "${MINGW_PREFIX}")
    sed -i -e "s,${MINGW_PREFIX_W},${MINGW_PREFIX},g" \
        "${pkgdir}${MINGW_PREFIX}/bin/styrene"

    install -Dm644 COPYING "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/COPYING
}
