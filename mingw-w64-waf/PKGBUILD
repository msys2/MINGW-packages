# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=waf

pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.9.4
pkgrel=1
pkgdesc="General-purpose build system modelled after Scons (mingw-w64)"
arch=('any')
license=('BSD')
url="https://waf.io/"
makedepends=("setconf")
depends=("${MINGW_PACKAGE_PREFIX}-python3")
options=('!emptydirs')
source=("https://github.com/waf-project/${_realname}/archive/${_realname}-${pkgver}.tar.gz")
sha256sums=('b2460ddd27131f347387b768877bf35c015735c5cd6fb4044070eb1a95de2ac3')

prepare() {
  cd "${_realname}-${_realname}-$pkgver"
  # Extracting license
  head -n 30 waf-light | tail -n 25 > LICENSE
  # Python 3 fix
  sed -i '0,/env python/s//python3/' waf-light
}

build() {
  cd "${_realname}-${_realname}-$pkgver"
  plain "Configure ..."
  ${MINGW_PREFIX}/bin/python3 ./waf-light configure --prefix=${MINGW_PREFIX}
  plain "Make waf ..."
  ${MINGW_PREFIX}/bin/python3 ./waf-light --make-waf
}

package() {
  cd "${_realname}-${_realname}-$pkgver"

  ${MINGW_PREFIX}/bin/python3 ./waf-light install -f --destdir="$pkgdir" \
    --tools='compat,compat15,ocaml,go,cython,scala,erlang,cuda,gcj,boost,pep8,eclipse,unity'

  install -Dm755 waf "${pkgdir}${MINGW_PREFIX}/bin/waf"

  # Force the generation of .waf.admin files
  cd demos/c
  "${pkgdir}${MINGW_PREFIX}/bin/waf" configure build >& /dev/null
  cd ../..

  # Fix weird directory placement (FS#38216, FS#38300)
  mkdir -p "${pkgdir}${MINGW_PREFIX}/lib/waf"
  mv "${pkgdir}${MINGW_PREFIX}/bin/.waf3-$pkgver-"* "${pkgdir}${MINGW_PREFIX}/lib/waf/"
  setconf "${pkgdir}${MINGW_PREFIX}/bin/waf" base '"${MINGW_PREFIX}/lib/waf"'

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"

}
