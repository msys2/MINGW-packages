# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=speexdsp
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.2rc3
pkgrel=3
pkgdesc="DSP library derived from Speex (mingw-w64)"
arch=('any')
license=('BSD')
url="https://www.speex.org/"
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
options=('strip' '!libtool' 'staticlibs')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"  "${MINGW_PACKAGE_PREFIX}-pkg-config")
source=(https://downloads.xiph.org/releases/speex/${_realname}-${pkgver}.tar.gz)
sha256sums=('4ae688600039f5d224bdf2e222d2fbde65608447e4c2f681585e4dca6df692f1')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
}

build() {
  #export lt_cv_deplibs_check_method='pass_all'
  [[ -d "build-${MINGW_CHOST}" ]] && rm -rf "build-${MINGW_CHOST}"
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --enable-static

  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
