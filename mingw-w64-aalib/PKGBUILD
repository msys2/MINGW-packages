# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=aalib
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.4rc5
_realver=1.4.0
pkgrel=1
pkgdesc="A portable ASCII art graphic library (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="http://aa-project.sourceforge.net/aalib/"
license=('LGPL')
depends=()
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc")
source=(https://downloads.sourceforge.net/sourceforge/aa-project/${_realname}-${pkgver}.tar.gz
        0001-Apply-patch-for-MSYS2.patch
        0002-configure-src-tweak-Windows-link-flags.patch)
sha256sums=('fbddda9230cf6ee2a4f5706b4b11e2190ae45f5eda1f0409dc4f99b35e0a70ee'
            'cfe9bc8654e8f4b12fde68e0b14f6d6b6cd3a6cbc0fbaecb3e128f64c214c019'
            'baec2b331d4adf1c94489f658974993163ab327130e49a1f5eefe22743970c5f')

prepare() {
  cd ${_realname}-${_realver}

  # From https://gitlab.gnome.org/GNOME/gimp/-/tree/master/build/windows/patches
  patch -p1 -i "${srcdir}/0001-Apply-patch-for-MSYS2.patch"
  patch -p1 -i "${srcdir}/0002-configure-src-tweak-Windows-link-flags.patch"

  autoreconf -fiv
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  ../"${_realname}-${_realver}"/configure \
    --prefix="${MINGW_PREFIX}" \
    --build="${MINGW_CHOST}" \
    --host="${MINGW_CHOST}" \
    --target="${MINGW_CHOST}" \
    --enable-static \
    --enable-shared

  make
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/${_realname}-${_realver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
