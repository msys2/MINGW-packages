# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libtool
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=2.4.2
pkgrel=3
pkgdesc="A system independent dlopen wrapper for GNU libtool (mingw-w64)"
arch=('any')
url="http://www.gnu.org/software/libtool"
license=("GPL")
makedepends=("${_mingw_suff}-gcc")
groups=("${_mingw_suff}")
options=('staticlibs' 'strip')
source=("http://ftp.gnu.org/pub/gnu/libtool/libtool-${pkgver}.tar.xz"
        0002-cygwin-mingw-Create-UAC-manifest-files.mingw.patch
        0003-Pass-various-runtime-library-flags-to-GCC.mingw.patch
        0004-Fix-linking-with-fstack-protector.mingw.patch
        0005-Fix-seems-to-be-moved.patch
        0006-Fix-strict-ansi-vs-posix.patch
        0007-fix-cr-for-awk-in-configure.all.patch)
md5sums=('2ec8997e0c07249eb4cbd072417d70fe'
         '8aae1841b182ab5230a2d559d44e942f'
         '39eb4cd0fcdc5ebcf9644ec5701133ed'
         '510b803464d698200fdc20b289ff0d77'
         'b5214af34d770ff5d75295c7f77158e0'
         '658e5c7310d8fd1619620a6409901d1c'
         '2200a7e2cfa8f5f3f9811f03ed41c46a')

prepare() {
  cd ${srcdir}/libtool-${pkgver}
  patch -p1 -i ${srcdir}/0002-cygwin-mingw-Create-UAC-manifest-files.mingw.patch
  patch -p1 -i ${srcdir}/0003-Pass-various-runtime-library-flags-to-GCC.mingw.patch
  patch -p1 -i ${srcdir}/0004-Fix-linking-with-fstack-protector.mingw.patch
  patch -p1 -i ${srcdir}/0005-Fix-seems-to-be-moved.patch
  patch -p1 -i ${srcdir}/0006-Fix-strict-ansi-vs-posix.patch
  patch -p1 -i ${srcdir}/0007-fix-cr-for-awk-in-configure.all.patch
}

build() {
  mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}
  ../libtool-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST}
  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="$pkgdir" install
}

