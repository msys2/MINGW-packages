# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=libtool
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-libtool" "${MINGW_PACKAGE_PREFIX}-libltdl")
pkgver=2.5.1
pkgrel=1
pkgdesc="A generic library support script (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://www.gnu.org/software/libtool"
msys2_references=(
  "cpe: cpe:/a:gnu:libtool"
)
license=('spdx:LGPL-2.0-or-later WITH Libtool-exception')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "autoconf-wrapper"
  "automake-wrapper"
  "help2man"
  "m4"
)
source=(https://alpha.gnu.org/gnu/libtool/${_realname}-${pkgver}.tar.xz{,.sig}
        # https://ftp.gnu.org/gnu/libtool/${_realname}-${pkgver}.tar.xz{,.sig}
        0001-cygwin-mingw-Create-UAC-manifest-files.patch
        0002-Fix-seems-to-be-moved.patch
        0003-Fix-STRICT_ANSI-vs-POSIX.patch
        0004-libtool-msysize.patch
        0005-libtool-include-process.h.patch
        0006-Allow-statically-linking-Flang-support-libraries.patch
        0011-Pick-up-clang_rt-static-archives-compiler-internal-l.patch
        0012-Prefer-response-files-over-linker-scripts-for-mingw.patch
        0013-Allow-statically-linking-compiler-support-libraries.patch
        0014-Support-llvm-objdump-f-output.patch)
sha256sums=('d280e64c8cdbf145d76fb91b3b219edab01bdb43cb99402e4acb97d0100636fd'
            'SKIP'
            'f466257e65041f3e587997893ac992af545bef29784bf159ee906706f18c3797'
            '2aa9e18c53b1f3ad6635e7db03dadc3d18725c276e9db7325e7a5ac46ed0068f'
            '88c8b26d0a119607d1cdf24f1c85471b99d7f796e0174a2ca42aee354fdb0e64'
            'db9be63714f77c4e92b55c6457d053caee9578ada4c3214bee4b0ae93a149fec'
            '557ba4731e9d68ff8220bb85ffdc7506bdcda1ed26e9223298dd1d1a96de548d'
            'f5719e572f56a8272725d161f15010d6b0932b6a685fb34e4bc25586d8f9cc96'
            '6370a17a04e3a45e3bbc8fa138b7e5654c34b860d8530ae7cc2a92bf865f84a9'
            'c95a65e890b1ae6362807abc66809e72cf81aeea5f9f556e38f9752f974bf435'
            '8069e887aeeab7491f15e00547fa66d9b9e86407f5a23f37a6d8c7d165de752e'
            'db16cd322e0ebc578c906e94b0788810af17ce617c700a50db2e3c598dbbed7e')
validpgpkeys=('7C5FBB96BE82B954AC20DF5F6EAC957F8EEB55C0'  # Alex Ameen <alex.ameen.tx@gmail.com>
              'FA26CA784BE188927F22B99F6570EA01146F7354') # Ileana Dumitrescu <ileanadumi95@protonmail.com>

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"; do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}
# =========================================== #

prepare() {
  cd "${_realname}-${pkgver}"

  # Note: keep the patches in sync with msys libtool
  # Don't apply msysize patch
  apply_patch_with_msg \
    0001-cygwin-mingw-Create-UAC-manifest-files.patch \
    0002-Fix-seems-to-be-moved.patch \
    0003-Fix-STRICT_ANSI-vs-POSIX.patch \
    0005-libtool-include-process.h.patch \
    0006-Allow-statically-linking-Flang-support-libraries.patch \
    0011-Pick-up-clang_rt-static-archives-compiler-internal-l.patch \
    0012-Prefer-response-files-over-linker-scripts-for-mingw.patch \
    0013-Allow-statically-linking-compiler-support-libraries.patch \
    0014-Support-llvm-objdump-f-output.patch
}

build() {
  cd "${_realname}-${pkgver}"

  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}

  make
}

check() {
  cd "${_realname}-${pkgver}"

  make check -k || warning "Tests failed"
}

package_libtool() {
  depends=("${MINGW_PACKAGE_PREFIX}-libltdl=${pkgver}" "bash")

  cd "${_realname}-${pkgver}"

  make DESTDIR=${pkgdir} install-binSCRIPTS install-man install-info \
                         install-data-local

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${pkgname#${MINGW_PACKAGE_PREFIX}-}"

  # Fix paths
  local WINPREFIX=$(cygpath -wm ${MINGW_PREFIX})
  sed -i "s|${WINPREFIX}|${MINGW_PREFIX}|g" "${pkgdir}${MINGW_PREFIX}/bin/libtool"
}

package_libltdl() {
  pkgdesc="A system independent dlopen wrapper for GNU libtool (mingw-w64)"

  cd "${_realname}-${pkgver}"

  make DESTDIR=${pkgdir} install-libLTLIBRARIES install-includeHEADERS \
                         install-ltdlincludeHEADERS

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/libltdl/COPYING.LIB" -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${pkgname#${MINGW_PACKAGE_PREFIX}-}"
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
