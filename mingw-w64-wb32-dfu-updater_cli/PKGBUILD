# Maintainer: joy <chang.li@westberrytech.com>

_realname=wb32-dfu-updater_cli
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=0.0.3
pkgrel=1
pkgdesc='Device firmware upgrade utilities (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=('Apache2.0')
groups=("${MINGW_PACKAGE_PREFIX}-eda")
url='https://github.com/wb-Joy/wb32-dfu-updater_cli/'
depends=("${MINGW_PACKAGE_PREFIX}-libusb")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/wb-joy/wb32-dfu-updater_cli/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('538d591c61c86644259e1c27af9d2206ecfc8d64e4f636f3ea21b87a0f91cb1c')

prepare() {
    cd "${srcdir}/${_realname}-${pkgver}"
}

build() {
    [[ -d "${srcdir}/build-${MSYSTEM}" ]] && rm -rf "${srcdir}/build-${MSYSTEM}"
    mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

    MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake.exe \
      -G"Ninja" \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DCMAKE_BUILD_TYPE=Release \
      ../${_realname}-${pkgver}

    ${MINGW_PREFIX}/bin/cmake.exe --build ./
}

package() {
    cd "${srcdir}/build-${MSYSTEM}"

    DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install ./
}
