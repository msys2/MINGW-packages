# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>

_realname=sccache
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.7.6
pkgrel=1
pkgdesc='Shared compilation cache (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/mozilla/sccache"
msys2_references=(
  'archlinux: sccache'
)
license=('spdx:Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-openssl" "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
source=("https://github.com/mozilla/sccache/archive/v$pkgver/${_realname}-${pkgver}.tar.gz")
sha256sums=('c6ff8750516fe982c9e9c20fb80d27c41481a22bf9a5a2346cff05724110bd42')

prepare() {
  cp -r ${_realname}-${pkgver} build-${MSYSTEM}

  cargo fetch \
    --locked \
    --manifest-path build-${MSYSTEM}/Cargo.toml
}

build() {
  WINAPI_NO_BUNDLED_LIBRARIES=1 \
  ${MINGW_PREFIX}/bin/cargo build \
    --release \
    --frozen \
    --manifest-path build-${MSYSTEM}/Cargo.toml \
    --features native-zlib
}

check() {
  WINAPI_NO_BUNDLED_LIBRARIES=1 \
  ${MINGW_PREFIX}/bin/cargo test \
    --release \
    --frozen \
    --manifest-path build-${MSYSTEM}/Cargo.toml \
    --features native-zlib
}

package() {
  WINAPI_NO_BUNDLED_LIBRARIES=1 \
  ${MINGW_PREFIX}/bin/cargo install \
    --frozen \
    --offline \
    --no-track \
    --path build-${MSYSTEM} \
    --root ${pkgdir}${MINGW_PREFIX} \
    --features native-zlib

  install -Dm644 ${_realname}-${pkgver}/LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
