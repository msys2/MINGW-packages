# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>

_zstd_sys_ver='2.0.9+zstd.1.5.5'

_realname=sccache
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.8.1
pkgrel=1
pkgdesc='Shared compilation cache (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/mozilla/sccache"
msys2_references=(
  'archlinux: sccache'
  'purl: pkg:cargo/sccache'
)
license=('spdx:Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-zstd")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-openssl")
options=('!strip' '!lto')
source=("https://github.com/mozilla/sccache/archive/v$pkgver/${_realname}-${pkgver}.tar.gz"
        "zstd-sys-${_zstd_sys_ver}.tar.gz::https://crates.io/api/v1/crates/zstd-sys/${_zstd_sys_ver}/download"
        "link-zstd-dynamically.patch")
sha256sums=('30b951b49246d5ca7d614e5712215cb5f39509d6f899641f511fb19036b5c4e5'
            '9e16efa8a874a0481a574084d34cc26fdb3b99627480f785888deb6386506656'
            '48f4900ceb02d3aaf9a1020f33d56629156e96759f456c0e7ca18bfcf910767b')

_env() {
  export OPENSSL_NO_VENDOR=1
  export PKG_CONFIG_ALL_DYNAMIC=1
  export ZSTD_SYS_USE_PKG_CONFIG=1
  export WINAPI_NO_BUNDLED_LIBRARIES=1
}

prepare() {
  cd "${srcdir}/zstd-sys-${_zstd_sys_ver}"
  patch -Np1 -i "${srcdir}/link-zstd-dynamically.patch"

  cd "${srcdir}/${_realname}-${pkgver}"
  cat >> Cargo.toml <<END

[patch.crates-io]
zstd-sys = { path = "../zstd-sys-${_zstd_sys_ver}" }
END

  cargo update -p zstd-sys
  cargo fetch --locked
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  _env
  cargo build --release --frozen
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  _env
  cargo test --release --frozen
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  _env
  cargo install \
    --frozen \
    --offline \
    --no-track \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
