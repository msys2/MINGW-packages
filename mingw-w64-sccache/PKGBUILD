# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>

_realname=sccache
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.6.0
pkgrel=1
pkgdesc='Shared compilation cache (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
msys2_references=(
  'archlinux: sccache'
)
url="https://github.com/mozilla/sccache"
license=('spdx:Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-openssl" "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
source=("https://github.com/mozilla/sccache/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('ffa687a872f590ba0bfeacb54ef16f25f78426207614681f85b183fabe515400')

prepare() {
  cp -r ${_realname}-${pkgver} build-${MSYSTEM}
  ${MINGW_PREFIX}/bin/cargo fetch \
    --locked \
	--manifest-path build-${MSYSTEM}/Cargo.toml
}

build() {
  export WINAPI_NO_BUNDLED_LIBRARIES=1
  ${MINGW_PREFIX}/bin/cargo build \
	--release \
	--frozen \
	--manifest-path build-${MSYSTEM}/Cargo.toml \
	--features all \
	--features native-zlib
}

check() {
  ${MINGW_PREFIX}/bin/cargo test \
	--release \
	--frozen \
	--manifest-path build-${MSYSTEM}/Cargo.toml \
	--features all \
	--features native-zlib
}

package() {
  ${MINGW_PREFIX}/bin/cargo install \
	--frozen \
	--offline \
	--no-track \
	--path build-${MSYSTEM} \
	--root ${pkgdir}${MINGW_PREFIX} \
	--features all \
	--features native-zlib

  install -Dm644 ${_realname}-${pkgver}/LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
