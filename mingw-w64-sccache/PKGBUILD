# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>

_realname=sccache
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.7.7
pkgrel=1
pkgdesc='Shared compilation cache (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/mozilla/sccache"
msys2_references=(
  'archlinux: sccache'
)
license=('spdx:Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-openssl" "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
source=("https://github.com/mozilla/sccache/archive/v$pkgver/${_realname}-${pkgver}.tar.gz")
sha256sums=('a5f5dacbc8232d566239fa023ce5fbc803ad56af2910fa1558b6e08e68e067e0')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  cargo fetch --locked
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  WINAPI_NO_BUNDLED_LIBRARIES=1 \
  ${MINGW_PREFIX}/bin/cargo build \
    --release \
    --frozen \
    --features native-zlib
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  WINAPI_NO_BUNDLED_LIBRARIES=1 \
  ${MINGW_PREFIX}/bin/cargo test \
    --release \
    --frozen \
    --features native-zlib
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  WINAPI_NO_BUNDLED_LIBRARIES=1 \
  ${MINGW_PREFIX}/bin/cargo install \
    --frozen \
    --offline \
    --no-track \
    --path . \
    --root ${pkgdir}${MINGW_PREFIX} \
    --features native-zlib

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
