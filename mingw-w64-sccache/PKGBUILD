# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>

_realname=sccache
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.8.0
pkgrel=1
pkgdesc='Shared compilation cache (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/mozilla/sccache"
msys2_references=(
  'archlinux: sccache'
  'purl: pkg:cargo/sccache'
)
license=('spdx:Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-zstd")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-openssl")
options=('!strip' '!lto')
source=("https://github.com/mozilla/sccache/archive/v$pkgver/${_realname}-${pkgver}.tar.gz"
        "link-zstd-dynamically.patch")
sha256sums=('e78c7a65982e2ab1dc2e5580e548bb1bf6f47a0f20e58dcba8856fc97640f2d2'
            '2d7ff7bf732dea8448a93900c9c43b7e31431b25baaa69e54729baf5ef4dba09')

_env() {
  export OPENSSL_NO_VENDOR=1
  export PKG_CONFIG_ALL_DYNAMIC=1
  export ZSTD_SYS_USE_PKG_CONFIG=1
  export WINAPI_NO_BUNDLED_LIBRARIES=1
}

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  mkdir -p .cargo
  cargo vendor --locked --versioned-dirs >> .cargo/config.toml

  patch -Np1 -i "${srcdir}/link-zstd-dynamically.patch"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  _env
  cargo build --release --frozen
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  _env
  cargo test --release --frozen native-zlib
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  _env
  cargo install \
    --frozen \
    --offline \
    --no-track \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
