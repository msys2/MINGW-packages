# Maintainer: Sylvain Rouquette <srouquette@gmail.com>

_realname=paper-terminal
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.0.0
pkgrel=1
pkgdesc="Print Markdown to a paper in your terminal (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64' 'mingw64')
url='https://github.com/foxfriends/paper-terminal'
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             'git')
options=('!strip' '!lto')
source=("git+${url}.git#tag=v${pkgver}")
sha256sums=('327cf24a409c24b6f68584b40d9ebf8ea43d8e69784766194260a034c9b90300')

prepare() {
  cd "${_realname}"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${_realname}"

  export WINAPI_NO_BUNDLED_LIBRARIES=1
  cargo build --release --frozen
}

check() {
  cd "${_realname}"

  cargo test --release --frozen
}

package() {
  cd "${_realname}"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"
}
