_realname=ada-libfswatch
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.r17.00fb794
pkgrel=1
_branch='24.0'
_commit=
pkgdesc="Ada binding to the libfswatch library (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64')
license=('GPL-3.0-only WITH GCC-exception-3.1')
url="https://github.com/AdaCore/ada_libfswatch"
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc-ada"
             "${MINGW_PACKAGE_PREFIX}-gprbuild")
depends=("${MINGW_PACKAGE_PREFIX}-gnatcoll"
         "${MINGW_PACKAGE_PREFIX}-fswatch")
source=("${_realname}-git"::"git+https://github.com/AdaCore/ada_libfswatch.git#branch=${_branch}"
        '0001-Use-system-libfswatch-install.patch'
        '0002-Build-dynamic-library.patch'
        '0003-Makefile-allow-passing-custom-flags-to-gprbuild.patch')
sha256sums=('SKIP'
            '534b4737f69f3eab397e512a334583d936c7f22d812ec87580d78bd7694d74ef'
            '57fca6d3ede522986bfad63d15e7be84bce29d3223b6898dd96623c6fed1433b'
            '9d1f8af2fc1ff42381badb54ee499324517172c14bcb6d234e29b0a53832bb3a')

pkgver() {
  cd ${_realname}-git
  printf "%s.r%s.%s" "$_branch" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

prepare() {
  cd "${srcdir}/${_realname}-git"
  patch -p1 -i "$srcdir/0001-Use-system-libfswatch-install.patch"
	patch -p1 -i "$srcdir/0002-Build-dynamic-library.patch"
	patch -p1 -i "$srcdir/0003-Makefile-allow-passing-custom-flags-to-gprbuild.patch"
}

build() {
  cd "${srcdir}/${_realname}-git"
  make
}

package() {
  cd "${srcdir}/${_realname}-git"
  make install DESTDIR="${pkgdir}${MINGW_PREFIX}"

  # ada_libfswatch wants to replace libfswatch.dll,
  # which is already provided by the fswatch package
  rm "${pkgdir}/${MINGW_PREFIX}/bin/libfswatch.dll"

  # Copy License Files
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" \
    "${srcdir}/${_realname}-git"/COPYING*
}
