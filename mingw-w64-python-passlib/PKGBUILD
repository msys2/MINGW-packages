# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=passlib
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-$_realname")
pkgver=1.7.4
pkgrel=1
pkgdesc="A password hashing library for Python (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
msys2_references=(
  'pypi: passlib'
)
url="https://passlib.readthedocs.io/"
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-python")
optdepends=(#"${MINGW_PACKAGE_PREFIX}-python-fastpbkdf2: accelerate PBKDF2-based hashes"
            #"${MINGW_PACKAGE_PREFIX}-python-scrypt: accelerate SCrypt hashes"
            "${MINGW_PACKAGE_PREFIX}-python-bcrypt: accelerate Bcrypt hashes")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools")
source=("https://pypi.io/packages/source/p/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('defd50f72b65c5402ab2c573830a6978e5f202ad0d984793c8dde2c4152ebe04')

prepare() {
  cd "${srcdir}"
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}"
}

build() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  "${MINGW_PREFIX}"/bin/python setup.py install -O1 --skip-build \
      --root="${pkgdir}" --prefix="${MINGW_PREFIX}"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
  install -Dm644 README "${pkgdir}${MINGW_PREFIX}/share/doc/python-${_realname}/README"
}

# vim:set ts=2 sw=2 et:
