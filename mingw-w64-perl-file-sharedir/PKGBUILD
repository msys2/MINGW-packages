# Maintainer: @naveen521kk on Github, Naveen M K <naveen521kk@gmail.com>

_perlname=File-ShareDir
_realname="${_perlname,,}"
pkgbase=mingw-w64-perl-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-perl-${_realname}")
pkgver=1.118
pkgrel=1
pkgdesc="Locate per-dist and per-module shared files (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://metacpan.org/release/${_perlname}"
groups=("${MINGW_PACKAGE_PREFIX}-perl-modules")
provides=(
)
depends=(
  "${MINGW_PACKAGE_PREFIX}-perl"
  "${MINGW_PACKAGE_PREFIX}-perl-carp"
  "${MINGW_PACKAGE_PREFIX}-perl-warnings"
  "${MINGW_PACKAGE_PREFIX}-perl-class-inspector"
  "${MINGW_PACKAGE_PREFIX}-perl-file-spec"
  "${MINGW_PACKAGE_PREFIX}-perl-params-util"
  "${MINGW_PACKAGE_PREFIX}-perl-list-moreutils"
)
options=('!emptydirs')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-cc"
  $([[ ${MINGW_PACKAGE_PREFIX} != *-clang-* ]] || \
    echo "${MINGW_PACKAGE_PREFIX}-gcc-compat")
  "${MINGW_PACKAGE_PREFIX}-perl-extutils-makemaker"
  "${MINGW_PACKAGE_PREFIX}-perl-file-sharedir-install"
)
license=('perl_5')
source=(
  'https://cpan.metacpan.org/authors/id/R/RE/REHSACK/File-ShareDir-1.118.tar.gz'
  'patchmakefile.py'
)
sha256sums=('3bb2a20ba35df958dc0a4f2306fc05d903d8b8c4de3c8beefce17739d281c958'
            '00fe5ced42aa635714909a8eba1347a6c132011a9bce68e9c75b27ebd64628f2')

build() {
  cd "$srcdir/${_perlname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL
  export pkgdir=$pkgdir
  python $srcdir/patchmakefile.py
  mingw32-make
}

package() {
  cd "$srcdir/${_perlname}-${pkgver}"
  mingw32-make DESTDIR="${pkgdir}/" install
}
