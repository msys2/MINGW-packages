# Maintainer: @naveen521kk on Github <naveen521kk@gmail.com>

_realname=dnslookup
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.11.2
pkgrel=2
pkgdesc='Simple command line utility to make DNS lookups (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/ameshkov/dnslookup"
msys2_repository_url='https://github.com/ameshkov/dnslookup'
msys2_references=(
  'purl: pkg:golang/github.com/ameshkov/dnslookup'
)
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-go" "${MINGW_PACKAGE_PREFIX}-cc" "git")
options=('!strip')
source=("$url/archive/v$pkgver/$_realname-$pkgver.tar.gz")
sha256sums=('0f4b74a4ac8c967b089eebc62fbf65334e76d42da4c4524e8182bb8400aa119d')

build() {
    cd "${_realname}-$pkgver"
    export GOOS=windows
    export GOROOT=${MINGW_PREFIX}/lib/go
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GO_LDFLAGS="-s -w"
    export GOFLAGS="-trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external"

    make VERSION=$pkgver build
}

check(){
    cd "${_realname}-$pkgver"
    make test
}

package() {
    cd "${_realname}-$pkgver"
    install -Dm755 "dnslookup.exe" "${pkgdir}${MINGW_PREFIX}/bin/dnslookup.exe"
    install -Dm644 "LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
    install -Dm644 "README.md" "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/README.md"
}
