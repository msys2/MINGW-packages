# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: JÃ¼rgen Pfeifer <juergen@familiepfeifer.de>
# modified: Greg Jung. Disable qt, java, ada
_realname=plplot
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=5.11.0
pkgrel=2
arch=('any')
pkgdesc="Scientific plotting software (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gcc-libgfortran"
         "${MINGW_PACKAGE_PREFIX}-qhull"
         "${MINGW_PACKAGE_PREFIX}-shapelib"
         "${MINGW_PACKAGE_PREFIX}-wxWidgets"
        )
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc-fortran"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")
options=('strip' 'staticlibs')
license=('LGPL')
url="http://plplot.sourceforge.net"
source=(http://downloads.sourceforge.net/sourceforge/plplot/${_realname}-${pkgver}.tar.gz
	fix_cairo_togit.patch
        fix_wingcc_togit.patch
        find-gd.patch)
sha1sums=('215c60b86ae1381813e5c837ea6afa6d7708127d'
          '27ac87e2945f83ac651fd6b8246b17b44bca8680'
          '07c7de553e8e08c19c51e5f353dd3a64873a577c'
          '7800cad653ebcedf596a01e545116f6ffd04f0f9')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/find-gd.patch
  patch -p1 -i ${srcdir}/fix_cairo_togit.patch
}

build() {
  # cmake's FindSWIG doesn't work properly on MSYS,
  # so we provide SWIG here
 # SWIG_DIR_U=`${MINGW_PREFIX}/bin/swig -swiglib`
 # SWIG_DIR=`cygpath -m ${SWIG_DIR_U}`
# more stuff we shouldn't need:
#     -DCMAKE_Fortran_COMPILER=${MINGW_PREFIX}/bin/gfortran.exe \
#     -DSWIG_EXECUTABLE="${SWIG_EXECUTABLE}" \
#    -DSWIG_DIR="${SWIG_DIR}" \
#    -DQHULL_INCLUDE_DIR=${MINGW_PREFIX}/include \

  # cmake may be confused if there is a Windows installation
  # of the official Python MSI. Please do the Python-enabled
  # build only, if no official Python is installed.
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G "MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=ON \
    -DDEFAULT_NO_CAIRO_DEVICES=ON \
    -DWITH_FREETYPE=OFF \
    -DWITH_CSA=OFF \
    -DENABLE_lua=OFF \
    -DENABLE_python=OFF \
    -DENABLE_DYNDRIVERS=OFF \
    -DENABLE_ada=OFF \
    -DENABLE_java=OFF \
    -DENABLE_qt=OFF \
    -DENABLE_itcl=OFF \
    -DENABLE_itk=OFF \
    -DENABLE_tcl=OFF \
    -DENABLE_tk=OFF \
    ../${_realname}-${pkgver}

  make V=1
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
  mkdir -p ${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname
  cp -pf ${srcdir}/${_realname}-${pkgver}/Copyright \
    ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}
  cp -pf ${srcdir}/${_realname}-${pkgver}/COPYING* \
    ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}
}
