_realname=gnatcoll-xref
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.0
pkgrel=1

pkgdesc="GNAT Component Collection - Cross-referencing Ada and SPARK source code, parsing *.ali and *.gli files (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
msys2_references=(
  'aur: gnatcoll-xref'
)

url="https://github.com/AdaCore/gnatcoll-db"
license=('spdx:GPL-3.0-or-later WITH GCC-exception-3.1')
makedepends=("${MINGW_PACKAGE_PREFIX}-gprbuild")
depends=("${MINGW_PACKAGE_PREFIX}-gnatcoll-iconv"
         "${MINGW_PACKAGE_PREFIX}-gnatcoll-sql"
         "${MINGW_PACKAGE_PREFIX}-gnatcoll-sqlite")
_repo_name=gnatcoll-db
source=("https://github.com/AdaCore/gnatcoll-db/archive/v${pkgver}/${_repo_name}-${pkgver}.tar.gz")
sha256sums=('FIXME')

_makeopts=(BUILD=PROD PROCESSORS="$(nproc)" ENABLE_SHARED=yes)

build() {
  cd "${srcdir}/${_repo_name}-${pkgver}/xref"
  make "${_makeopts[@]}"
}

package() {
  cd "${srcdir}/${_repo_name}-${pkgver}/xref"
  make install "${_makeopts[@]}" prefix="${pkgdir}${MINGW_PREFIX}"
  install -D -m644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" \
    "${srcdir}/${_repo_name}-${pkgver}"/COPYING*
}
