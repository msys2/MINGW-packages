####
#
# Based on packages found at these URLs
#     https://github.com/Alexpux/MINGW-packages/tree/master/mingw-w64-wxPython
#     https://github.com/Alexpux/MINGW-packages/tree/master/mingw-w64-wxWidgets
#
# Maintainer: Tim S <stahta01@gmail.com>
#
####

# Packages that are assumed to be installed when building this package.
#   pacman -S --needed --asdeps base-devel mingw-w64-i686-toolchain mingw-w64-x86_64-toolchain


_realname=wxpython3
_namesuffix="-git"
_py_foldername=wxPython
_wx_foldername=wxWidgets
_wxver=3.0.3
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}${_namesuffix}"
pkgbase=mingw-w64-${_realname}${_namesuffix}
pkgver=3.0.3.0.r5369.g8960e7ee0b
pkgrel=1
pkgdesc="A wxWidgets GUI toolkit for Python (mingw-w64)"
arch=('any')
license=("custom:wxWindows")
url="https://www.wxwidgets.org/"
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python2-setuptools"
  "git"
)
depends=(
  "${MINGW_PACKAGE_PREFIX}-python2"
  "${MINGW_PACKAGE_PREFIX}-wxWidgets"
)
conflicts=("${MINGW_PACKAGE_PREFIX}-wxPython" "${MINGW_PACKAGE_PREFIX}-${_realname}")
options=('strip' 'staticlibs' 'buildflags')
source=("git+https://github.com/wxWidgets/wxPython.git"
  https://github.com/wxWidgets/wxWidgets/releases/download/v${_wxver}/wxWidgets-${_wxver}.tar.bz2
  "003-wxpython-3.0.2-fix-config.patch"
  "004-wxpython-3.0.2-fix-cast-error.patch"
)
sha256sums=('SKIP'
            '08c8033f48ec1b23520f036cde37b5ae925a6a65f137ded665633ca159b9307b'
            '61ca97f814b85b1274e751c694f9e3adc31ce1e22c56c9ca9c59e0337a36fef5'
            'd7c4de5594149d2bba4a0ceb95d6a6a0c3c0e43ab9e876dce22440ccaad3b5f0')

prepare() {
  cd "${srcdir}"/${_py_foldername}
  # revert changes done by wxpython patch files
  git checkout -- config.py
  git checkout -- src/helpers.cpp
  patch -p2 -i "${srcdir}"/003-wxpython-3.0.2-fix-config.patch
  patch -p2 -i "${srcdir}"/004-wxpython-3.0.2-fix-cast-error.patch

  # Make sure scripts use python2
  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
  # search and replace in files without file extensions
  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -type f ! -name '*.*')
}

pkgver() {
  if [[ ! -f "${srcdir}"/${_py_foldername}/cfg_version.py ]]; then
    printf "0"
  else
    cd "${srcdir}"/${_py_foldername}
    local   major=$(head -n 40 cfg_version.py | sed -e 's:#.*$::g' | grep 'VER_MAJOR'   | sed -e 's/"//g' | sed -e 's:^.*=::g' | tr -d '[:space:]')
    local   minor=$(head -n 40 cfg_version.py | sed -e 's:#.*$::g' | grep 'VER_MINOR'   | sed -e 's/.* //' | tr '\n' '.' | sed 's/.$/\n/')
    local release=$(head -n 40 cfg_version.py | sed -e 's:#.*$::g' | grep 'VER_RELEASE' | sed -e 's/.* //' | tr '\n' '.' | sed 's/.$/\n/')
    local  subrel=$(head -n 40 cfg_version.py | sed -e 's:#.*$::g' | grep 'VER_SUBREL'  | sed -e 's/"//g' | sed -e 's:^.*=::g' | tr -d '[:space:]')
    local    flag=$(head -n 40 cfg_version.py | sed -e 's:#.*$::g' | grep 'VER_FLAGS'   | sed -e 's/"//g' | sed -e 's:^.*=::g' | tr -d '[:space:]')
    printf "%s.%s.%s.%s%s.r%s.g%s" "$major" "$minor" "$release" "$subrel" "$flag" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  fi
}

build() {
  cd "${srcdir}"/${_py_foldername}
  export PYTHON=${MINGW_PREFIX}/bin/python2
  export WXWIN="${srcdir}"/${_wx_foldername}-${_wxver}
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=;--install-headers=;-install-data=" \
  ${MINGW_PREFIX}/bin/python2 setup.py WXPORT=msw BUILD_ACTIVEX=0 UNICODE=1 COMPILER=mingw32 build
}

package() {
  cd "${srcdir}"/${_py_foldername}
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=;--install-headers=;-install-data=" \
  ${MINGW_PREFIX}/bin/python2 setup.py NO_HEADERS=0 WXPORT=msw BUILD_ACTIVEX=0 UNICODE=1 COMPILER=mingw32 \
    install --prefix=${MINGW_PREFIX} --root="${pkgdir}"

  install -D -m644 "${srcdir}"/${_wx_foldername}-${_wxver}/docs/licence.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_py_foldername}/LICENSE"
}
