# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

source "$(dirname ${BASH_SOURCE[0]})"/../mingw-w64-PKGBUILD-common/kde-frameworks
_realname=breeze-icons
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=6.13.0
pkgrel=1
pkgdesc="Breeze icon theme (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'archlinux: breeze-icons'
)
msys2_repository_url='https://invent.kde.org/frameworks/breeze-icons/'
url='https://community.kde.org/Frameworks/'
license=('spdx:LGPL-2.0-or-later')
depends=(
  "${MINGW_PACKAGE_PREFIX}-qt6-base"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-7zip"
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-extra-cmake-modules"
  "${MINGW_PACKAGE_PREFIX}-ninja"
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-python-lxml"
)
groups=("${MINGW_PACKAGE_PREFIX}-kf6")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}-qt5")
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}-qt5")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${_realname}-${pkgver}.tar.xz"{,.sig})
noextract=("${_realname}-${pkgver}.tar.xz")
sha256sums=('76da054482c0b97e0ae68b94aaca9198c500288ded78b34bdb0b3cb88c473b5d'
            'SKIP')
validpgpkeys=(
  'E0A3EB202F8E57528E13E72FD7574483BB57B18D' # Jonathan Esk-Riddell <jr@jriddell.org>
  '90A968ACA84537CC27B99EAF2C8DF587A6D4AAC1' # Nicolas Fella <nicolas.fella@kde.org>
)

prepare() {
  # qrcAlias program needs proper symlinks
  7z x -bso0 -bsp0 "${_realname}-${pkgver}.tar.xz"
  7z x -bso0 -bsp0 -ttar "${_realname}-${pkgver}.tar"
}

build() {
  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  # See craft-blueprints-kde/kde/frameworks/tier1/breeze-icons/breeze-icons.py.
  # Install Qt resource files only. SVG files are not required by KDE apps in Windows.
  _kde_build_env
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${_KDE_INSTALL_DIRS[@]}" \
      -DBUILD_TESTING=OFF \
      -DPython_EXECUTABLE=${MINGW_PREFIX}/bin/python.exe \
      -DPython_ROOT_DIR=${MINGW_PREFIX} \
      -DPython_FIND_REGISTRY=NEVER \
      -DBINARY_ICONS_RESOURCE=ON \
      -DSKIP_INSTALL_ICONS=ON \
      -DICONS_LIBRARY=ON \
      "${_extra_config[@]}" \
      -S "${_realname}-${pkgver}" \
      -B "build-${MSYSTEM}"

  "${MINGW_PREFIX}"/bin/cmake.exe --build "build-${MSYSTEM}"
}

package() {
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install "build-${MSYSTEM}"
  cp "${pkgdir}${MINGW_PREFIX}"/bin/data/{icons/breeze/breeze-icons.rcc,icontheme.rcc}
}
