# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=editrights
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.03
pkgrel=3
pkgdesc="Edit special privileges of Windows accounts (mingw-w64)"
arch=('any')
url="http://cygwin.org/"
license=('custom')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
source=("http://cygwin.cybermirror.org/release/${_realname}/${_realname}-${pkgver}-1-src.tar.xz"
        'uncygwinize.patch')
md5sums=('f24c3c33c30516f13fc84b765020e1e7'
         'ad733a23daeb1d46e98c80d334929846')

prepare(){
  tar -xjf ${_realname}-${pkgver}-1.src/${_realname}-${pkgver}.tar.bz2
  patch -p1 -i "${srcdir}/uncygwinize.patch"
}

build() {
  cd ${_realname}
  make clean
  make CC=${MINGW_CHOST}-gcc
}

package() {
  cd ${_realname}
  make DESTDIR="${pkgdir}" PREFIX=${MINGW_PREFIX} install
  install -Dm0644 LICENCE ${pkgdir}${MINGW_PREFIX}/share/licenses/editrights/LICENCE
  mv ${pkgdir}${MINGW_PREFIX}/share/doc/{Cygwin,editrights}
}
