# Maintainer: Zach Bacon <zachbacon@budgiemedia.rocks>

_realname=faudio
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
#provides="${MINGW_PACKAGE_PREFIX}-${_realname}"
#replaces="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1001.ba87c18
pkgrel=1
pkgdesc="FAudio - Accuracy-focused XAudio reimplementation for open platforms (git) (mingw-w64)"
arch=('any')
url="https://github.com/FNA-XNA/FAudio"
license=('zlib')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ffmpeg"
             "${MINGW_PACKAGE_PREFIX}-SDL2"
             'git')
options=('strip' 'staticlibs')
source=("${_realname}"::"git+https://github.com/FNA-XNA/FAudio.git#branch=master")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}"/${_realname}
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -G'MSYS Makefiles' \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      "${extra_config[@]}" \
      ../${_realname}

  make
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/${_realname}/LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
  mv ${pkgdir}${MINGW_PREFIX}/lib/cmake ${pkgdir}${MINGW_PREFIX}/share/
}