# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=ncurses
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=5.9
pkgrel=1
pkgdesc="System V Release 4.0 curses emulation library"
arch=('any')
depends=("${_mingw_suff}-libgnurx")
makedepends=("${_mingw_suff}-libgnurx" "${_mingw_suff}-gcc")
#groups=("${_mingw_suff}")
url="http://www.gnu.org/software/ncurses/"
license=('MIT')
source=(http://ftp.gnu.org/pub/gnu/ncurses/${_realname}-${pkgver}.tar.gz)
md5sums=('8cb9c412e5f2d96bc6f459aa8c6282a1')

build() {
  cd ${srcdir}/$_realname-$pkgver

  CFLAGS+=" -D__USE_MINGW_ANSI_STDIO=1"
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
	--host=${MINGW_CHOST} \
	--target=${MINGW_CHOST} \
    --without-ada \
	--with-cxx \
	--enable-pc-files \
	--disable-rpath \
	--enable-colorfgbg \
	--disable-symlinks \
	--enable-warnings \
	--enable-assertions \
	--disable-home-terminfo \
	--enable-database \
	--enable-sp-funcs \
	--enable-term-driver \
	--enable-interop \
	--enable-widec

  make
}

package() {
  cd ${srcdir}/$_realname-$pkgver
  make DESTDIR=${pkgdir} install
  cp -r ${pkgdir}${MINGW_PREFIX}/include/ncursesw ${pkgdir}${MINGW_PREFIX}/include/ncurses
  cp ${pkgdir}${MINGW_PREFIX}/lib/libncursesw.a ${pkgdir}${MINGW_PREFIX}/lib/libncurses.a
  rm -rf ${pkgdir}${MINGW_PREFIX}/man
}
