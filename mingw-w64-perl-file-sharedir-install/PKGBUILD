# Maintainer: @naveen521kk on Github, Naveen M K <naveen521kk@gmail.com>

_perlname=File-ShareDir-Install
_realname="${_perlname,,}"
pkgbase=mingw-w64-perl-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-perl-${_realname}")
pkgver=0.13
pkgrel=1
pkgdesc="Install shared files (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://metacpan.org/release/${_perlname}"
groups=("${MINGW_PACKAGE_PREFIX}-perl-modules")
provides=(
)
depends=(
  "${MINGW_PACKAGE_PREFIX}-perl"
  "${MINGW_PACKAGE_PREFIX}-perl-file-spec"
  "${MINGW_PACKAGE_PREFIX}-perl-strict"
  "${MINGW_PACKAGE_PREFIX}-perl-warnings"
  "${MINGW_PACKAGE_PREFIX}-perl-exporter"
  "${MINGW_PACKAGE_PREFIX}-perl-io-dir"
  "${MINGW_PACKAGE_PREFIX}-perl-carp"
)
options=('!emptydirs')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-cc"
  $([[ ${MINGW_PACKAGE_PREFIX} != *-clang-* ]] || \
    echo "${MINGW_PACKAGE_PREFIX}-gcc-compat")
)
license=('perl_5')
source=(
  'https://cpan.metacpan.org/authors/id/E/ET/ETHER/File-ShareDir-Install-0.13.tar.gz'
  'patchmakefile.py'
)
sha256sums=('45befdf0d95cbefe7c25a1daf293d85f780d6d2576146546e6828aad26e580f9'
            '00fe5ced42aa635714909a8eba1347a6c132011a9bce68e9c75b27ebd64628f2')

build() {
  cd "$srcdir/${_perlname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL
  export pkgdir=$pkgdir
  python $srcdir/patchmakefile.py
  mingw32-make
}

package() {
  cd "$srcdir/${_perlname}-${pkgver}"
  mingw32-make DESTDIR="${pkgdir}/" install
}
