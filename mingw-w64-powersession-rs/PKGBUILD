_realname=powersession-rs
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.1.11
pkgrel=1
pkgdesc="Asciinema-compatible terminal session recorder for Windows (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64')
url='https://github.com/Watfaq/PowerSession-rs'
msys2_references=(
  'purl: pkg:cargo/PowerSession'
)
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             'git')
source=("git+$url#tag=v$pkgver")
sha256sums=('1d40d73e8d6865a367c8d121d052c4ddeda6a9b5cb1fbcf46b06019f962b2ed6')

prepare() {
  cd "${srcdir}/${_realname}"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${srcdir}/${_realname}"

  cargo build --release --frozen
}

check() {
  cd "${srcdir}/${_realname}"

  cargo test --release --frozen
}

package() {
  cd "${srcdir}/${_realname}"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
