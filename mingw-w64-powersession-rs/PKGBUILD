_realname=powersession-rs
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.1.11.r15.13032ce
pkgrel=1
pkgdesc="Asciinema-compatible terminal session recorder for Windows (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://github.com/Watfaq/PowerSession-rs'
msys2_references=(
  'purl: pkg:cargo/PowerSession'
)
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             'git')
source=("git+$url#commit=13032ce8d1daf1ed9230f4b56a638429e1c5453d")
sha256sums=('f8f9e25611b63ad0ab8da8ca3975130087b90a0068015f76adce1eac41267c3e')

pkgver() {
  cd "$_realname"

  # The v0.1.11 tag was created before v0.1.10 and out of the main branch
  local tag="v0.1.11"
  local rev=$(git rev-list --count ${tag}..HEAD)
  local hash=$(git rev-parse --short HEAD)
  echo "0.1.11.r${rev}.${hash}"
}

prepare() {
  cd "${srcdir}/${_realname}"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${srcdir}/${_realname}"

  cargo build --release --frozen
}

check() {
  cd "${srcdir}/${_realname}"

  cargo test --release --frozen
}

package() {
  cd "${srcdir}/${_realname}"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
