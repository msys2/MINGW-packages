# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=py2exe
pkgbase=mingw-w64-python-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-${_realname}
pkgver=0.13.0.2
pkgrel=2
pkgdesc='A distutils extension to create standalone Windows programs from Python code (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'pypi: py2exe'
)
msys2_repository_url='https://github.com/py2exe/py2exe'
url='http://www.py2exe.org/'
license=('spdx:MIT AND MPL-2.0')
depends=(
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-python-cachetools"
  "${MINGW_PACKAGE_PREFIX}-python-pefile"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python-build"
  "${MINGW_PACKAGE_PREFIX}-python-installer"
  "${MINGW_PACKAGE_PREFIX}-python-setuptools"
  "${MINGW_PACKAGE_PREFIX}-cc")
source=(https://github.com/py2exe/py2exe/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz
        001-setup-fix.patch
        002-mingw-fix.patch
        003-python-3.12-fix.patch)
sha256sums=('ef2907f5b82ec8b2259bea41a2d7d798ecd67aa4a92a74a06550b9123dadd8a4'
            'd4c0a3c2ab54a78cf594b1db8b7c60cde2574776565ec264901b660db893a287'
            'bb898c7b64b700200e9fe421797bffaf85de9f52f3cc82301cf35e0991699477'
            '5cf808033564db2eb9eacaf257cac4045d9de124d5406911a527414d2ec6255e')

prepare() {
  cd "${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/001-setup-fix.patch
  patch -p1 -i ${srcdir}/002-mingw-fix.patch

  # https://github.com/rkbennett/memimporter/blob/main/source/_memimporter.c
  patch -p1 -i ${srcdir}/003-python-3.12-fix.patch
}

build() {
  # build separate zipextimporter wheel for _memimporter pyd (#20382)
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}-zipextimporter"
  pushd "${srcdir}/python-build-${MSYSTEM}-zipextimporter"
  mv setup_zipextimporter.py setup.py
  python -m build --wheel --skip-dependency-check --no-isolation
  popd

  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "python-build-${MSYSTEM}-zipextimporter"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
  python -m installer --prefix=${MINGW_PREFIX} --destdir="${pkgdir}" dist/*.whl

  local _pythonpath=`python -c "import sysconfig; print(sysconfig.get_path('platlib'))"`
  local _site_packages="${pkgdir}$(cygpath ${_pythonpath})"
  rm -rf "${_site_packages}"/zipextimporter*

  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
  python -m installer --prefix=${MINGW_PREFIX} --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
