# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=osgbullet
pkgbase=mingw-w64-${_realname}-svn
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}-svn" "${MINGW_PACKAGE_PREFIX}-${_realname}-debug-svn")
_ver_base=1.0.1
pkgver=r385
pkgrel=1
pkgdesc="Bullet physics and OpenSceneGraph integration (mingw-w64)"
arch=('any')
license=('LGPL')
url="https://code.google.com/p/osgbullet/"
depends=("${MINGW_PACKAGE_PREFIX}-bullet"
         "${MINGW_PACKAGE_PREFIX}-OpenSceneGraph"
         "${MINGW_PACKAGE_PREFIX}-osgworks")
makedepends=("subversion"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-OpenSceneGraph"
             "${MINGW_PACKAGE_PREFIX}-OpenSceneGraph-debug"
             "${MINGW_PACKAGE_PREFIX}-osgworks"
             "${MINGW_PACKAGE_PREFIX}-osgworks-debug")
options=(!strip staticlibs !buildflags)
source=("${_realname}"::"svn+http://osgbullet.googlecode.com/svn/trunk/")
sha256sums=('SKIP')
noextract=( 'osgOcean-Resources-1.0.1.rar' )

pkgver() {
  cd "$srcdir/$_realname"
  local ver="$(svnversion)"
  printf "r%s" "${ver//[[:alpha:]]}"
}

prepare() {
  cd "$srcdir/${_realname}"
}

build() {
  plain "No build step"
}

package_release() {
  provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
  conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")

  [[ -d ${srcdir}/build-release ]] && rm -rf ${srcdir}/build-release
  mkdir -p ${srcdir}/build-release && cd ${srcdir}/build-release

  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX:PATH=${pkgdir}${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DosgWorks_DIR=${MINGW_PREFIX}/lib \
    ../${_realname}

  make
  make install
}

package_debug() {
  depends=("${MINGW_PACKAGE_PREFIX}-${_realname}-svn=$pkgver" "${MINGW_PACKAGE_PREFIX}-OpenSceneGraph-debug" "${MINGW_PACKAGE_PREFIX}-osgworks-debug")
  provides=("${MINGW_PACKAGE_PREFIX}-${_realname}-debug")
  conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}-debug")

  [[ -d ${srcdir}/build-debug ]] && rm -rf ${srcdir}/build-debug
  mkdir -p ${srcdir}/build-debug && cd ${srcdir}/build-debug

  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX:PATH=${pkgdir}${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Debug \
    -DosgWorks_DIR=${MINGW_PREFIX}/lib \
    ../${_realname}

  make
  make install

  rm -rf ${pkgdir}${MINGW_PREFIX}/include
  rm -rf ${pkgdir}${MINGW_PREFIX}/lib/flagpoll
  rm -rf ${pkgdir}${MINGW_PREFIX}/lib/*.cmake
  rm -rf ${pkgdir}${MINGW_PREFIX}/share/osgBullet/data
  rm -rf ${pkgdir}${MINGW_PREFIX}/share/osgBullet/extra
}

package_mingw-w64-i686-osgbullet-svn() {
  package_release
}

package_mingw-w64-i686-osgbullet-debug-svn() {
  package_debug
}

package_mingw-w64-x86_64-osgbullet-svn() {
  package_release
}

package_mingw-w64-x86_64-osgbullet-debug-svn() {
  package_debug
}
