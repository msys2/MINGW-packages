# Maintainer: Joan Karadimov <joan.karadimov@gmail.com>

_realname=dcraw
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
conflicts=("${MINGW_PACKAGE_PREFIX}-gimp-ufraw")
pkgver=9.28
pkgrel=2
pkgdesc="Decoder for any raw image from any digital camera"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://www.dechifro.org/dcraw/"
msys2_references=(
  "cpe: cpe:/a:dave_coffin:dcraw"
  "cpe: cpe:/a:dcraw_project:dcraw"
)
license=("GPLv2")
depends=("${MINGW_PACKAGE_PREFIX}-lcms2"
         "${MINGW_PACKAGE_PREFIX}-jasper"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc")
source=("https://www.dechifro.org/${_realname}/${_realname}.c")
sha256sums=('d18d9e43a096eea04eee2148e53068f8fa45ce95395d97128b1aa37b477eab43')

prepare() {
  cd "${srcdir}"
}

build() {
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  ${CC} -o ${_realname} -O4 ../${_realname}.c -lm -ljasper -ljpeg -llcms2 -lws2_32
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  install -Dm0755 "${_realname}.exe" "${pkgdir}${MINGW_PREFIX}/bin/${_realname}.exe"
}
