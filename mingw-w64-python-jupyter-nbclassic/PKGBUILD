# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=nbclassic
pkgbase=mingw-w64-python-jupyter-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-jupyter-${_realname}
pkgver=1.0.0
pkgrel=2
pkgdesc='Jupyter Notebook as a Jupyter Server extension (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
msys2_references=(
  'archlinux: jupyter-nbclassic'
  'pypi: nbclassic'
)
url='https://jupyter.org/'
license=('spdx:BSD-3-Clause')
depends=(
    "${MINGW_PACKAGE_PREFIX}-python"
    "${MINGW_PACKAGE_PREFIX}-python-argon2_cffi"
    "${MINGW_PACKAGE_PREFIX}-python-ipykernel"
    "${MINGW_PACKAGE_PREFIX}-python-ipython_genutils"
    "${MINGW_PACKAGE_PREFIX}-python-jinja"
    "${MINGW_PACKAGE_PREFIX}-python-jupyter-nbconvert"
    "${MINGW_PACKAGE_PREFIX}-python-jupyter-nbformat"
    "${MINGW_PACKAGE_PREFIX}-python-jupyter_client"
    "${MINGW_PACKAGE_PREFIX}-python-jupyter_core"
    "${MINGW_PACKAGE_PREFIX}-python-jupyter_notebook_shim"
    "${MINGW_PACKAGE_PREFIX}-python-jupyter_server"
    "${MINGW_PACKAGE_PREFIX}-python-nest-asyncio"
    "${MINGW_PACKAGE_PREFIX}-python-prometheus-client"
    "${MINGW_PACKAGE_PREFIX}-python-pyzmq"
    "${MINGW_PACKAGE_PREFIX}-python-send2trash"
    "${MINGW_PACKAGE_PREFIX}-python-terminado"
    "${MINGW_PACKAGE_PREFIX}-python-tornado"
    "${MINGW_PACKAGE_PREFIX}-python-traitlets")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-jupyter-packaging"
             "${MINGW_PACKAGE_PREFIX}-python-babel")
options=(!strip)
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('0ae11eb2319455d805596bf320336cda9554b41d99ab9a3c31bf8180bffa30e3')

prepare() {
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}"
}

build() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
