# Maintainer: Artem Konoplin <konoplin@gmail.com>
# Contributor: Ray Donnelly <mingwandroid@gmail.com>

_realname=mlt
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=7.6.0
pkgrel=1
pkgdesc="An open source multimedia framework (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64')
url="https://www.mltframework.org"
license=('GPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-dlfcn"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-swig")
depends=("${MINGW_PACKAGE_PREFIX}-SDL2"
         "${MINGW_PACKAGE_PREFIX}-fftw"
         "${MINGW_PACKAGE_PREFIX}-ffmpeg"
         "${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2")
source=("https://github.com/mltframework/mlt/archive/v$pkgver.tar.gz"
        001-fix-linking-mltgdk.patch
        002-fix-dll-install-destination.patch
        003-fix-detect-cpu-arch.patch)
sha256sums=('49f3c7902432a5a873ebce8406d901ac73623ff3dba7265b6e8b55cfe8220201'
            'd26e716cd5066c1ce6795231b300b0750a95767633a865a410468b596092a1a1'
            '8f708557514f878525ea0799d69e9329b5ed665282cdda27f2231f77a0951028'
            'a93aeab65e3de788abf56abfff9aea8439bcea855263ae5d5f79d564129a0e83')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/001-fix-linking-mltgdk.patch
  patch -p1 -i "${srcdir}"/002-fix-dll-install-destination.patch
  patch -p1 -i "${srcdir}"/003-fix-detect-cpu-arch.patch
}

build() {
  [[ -d "${srcdir}/build-${MSYSTEM}" ]] && rm -rf "${srcdir}/build-${MSYSTEM}"
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX="  \
    ${MINGW_PREFIX}/bin/cmake \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX}  \
    -G"Ninja"  \
    -DPython3_EXECUTABLE=${MINGW_PREFIX}/bin/python \
    -DWINDOWS_DEPLOY=OFF \
    -DSWIG_PYTHON=ON \
    -DMOD_RTAUDIO=$([[ ${MINGW_PACKAGE_PREFIX} != *-clang-* ]] && echo "ON" || echo "OFF") \
    -DMOD_GDK=$([[ ${CARCH} == "x86_64" ]] && echo "ON" || echo "OFF") \
    "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .
}
