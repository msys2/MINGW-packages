# Maintainer: Martell Malone <martellmalone@gmail.com>

_realname=wineditline
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.101
pkgrel=4
pkgdesc="port of the NetBSD Editline library (mingw-w64)"
arch=('any')
license=('BSD')
url="http://mingweditline.sourceforge.net"
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-cmake")
options=('staticlibs')
source=("http://sourceforge.net/projects/mingweditline/files/${_realname}-${pkgver}.tar.bz2"
        '001-fix-installing.patch'
        '002-fix-exports.patch'
        '003-dont-link-with-def.patch')
md5sums=('1e9e09b38898367da4f9aa2c287ae79d'
         '2837bc5d5ce4e826b4d9bccf1bc978d3'
         '966d8b0b0d96e6879a5c100a49322271'
         '6a879ef5f7f5dcbcba5e01c3f03fcaeb')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-fix-installing.patch
  patch -p1 -i ${srcdir}/002-fix-exports.patch
  patch -p1 -i ${srcdir}/003-dont-link-with-def.patch

  rm -rf bin32 bin64 lib32 lib64
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}
  
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    ../${_realname}-${pkgver}

  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
