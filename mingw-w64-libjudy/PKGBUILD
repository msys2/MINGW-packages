# Maintainer: Yang Kun <ikspress@outlook.com>

_realname=libjudy
_forkver=netdata2
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.0.5
pkgrel=1
pkgdesc='C library for creating and accessing dynamic arrays'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
license=('spdx:LGPL-2.1-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc")
url='http://judy.sourceforge.net'
source=("https://github.com/netdata/libjudy/archive/v${pkgver}-${_forkver}/${_realname}-${pkgver}-${_forkver}.tar.gz"
        "0001-judy-no-undefined.patch"
        # https://sourceforge.net/p/judy/patches/6/
        "https://sourceforge.net/p/judy/patches/6/attachment/0001-automake-Makefile.am-use-relocatable-srcdir-when-app.patch"
        "https://sourceforge.net/p/judy/patches/6/attachment/0002-doc-Makefile.am-mkdir-p-man-man3-and-parallel-buildi.patch"
        "https://sourceforge.net/p/judy/patches/6/attachment/0003-Win64-Fixup-WORD_T-and-UL-suffixes-for-llp64-data-mo.patch")
sha256sums=('e623a06bf091758b43f5cd97c3609f8109442d1a0441bc44819b60f2861c61b1'
            '4ab00dee64c79c3296ae7bc12429411bf7e361aedd583f268c0ad40c2083080e'
            '96e0ad7536f45e9bbbf2f6e9f6de12f74f1a9c349fcbd0d44db52b3c1231140a'
            '73da528014ee64b239d5db2aaf3d69682424e9cf6318dbc034e2e52e05deecff'
            '30dfe143bda003cbcfe90d90328f8caf8122406145d7c03f3c1b19620a11359f')

apply_patch_with_msg() {
  for _patch in "$@"; do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

prepare() {
  cd "${_realname}-${pkgver}-${_forkver}"

  apply_patch_with_msg 0001-judy-no-undefined.patch

  # Apply patches taken from SourceForge
  apply_patch_with_msg \
    0001-automake-Makefile.am-use-relocatable-srcdir-when-app.patch \
    0002-doc-Makefile.am-mkdir-p-man-man3-and-parallel-buildi.patch \
    0003-Win64-Fixup-WORD_T-and-UL-suffixes-for-llp64-data-mo.patch

  autoreconf -fi
}

build() {
  mkdir -p "build-${MSYSTEM}" && cd "build-${MSYSTEM}"

  ../${_realname}-${pkgver}-${_forkver}/configure \
    --prefix="${MINGW_PREFIX}" \
    --build="${MINGW_CHOST}" \
    --host="${MINGW_CHOST}" \
    --target="${MINGW_CHOST}"

  make
}

package() {
  cd "build-${MSYSTEM}"

  make install DESTDIR="${pkgdir}"

  # Add section number to file extension
  for file in "${pkgdir}${MINGW_PREFIX}/share/man/man3/"*; do
    mv "${file}" "${file%.*}.3"
  done

  install -Dm644 "${srcdir}/${_realname}-${pkgver}-${_forkver}/COPYING" -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
}
