_realname=SmallBASIC
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=12.26
pkgrel=1
_commit=6219fa6fe2346c92c7cf5cfc8b4da60b7c20f8e3
pkgdesc="SmallBASIC is a fast and easy to learn BASIC language interpreter (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://smallbasic.github.io/"
license=('spdx:GPL-3.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-freetype"
         "${MINGW_PACKAGE_PREFIX}-SDL2"
         "${MINGW_PACKAGE_PREFIX}-libmicrohttpd")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             $([[ $MINGW_PACKAGE_PREFIX == *-clang-* ]] && echo \
               "${MINGW_PACKAGE_PREFIX}-gcc-compat")
             "${MINGW_PACKAGE_PREFIX}-autotools"
             vim
             git)
options=('strip')
source=("git+https://github.com/smallbasic/SmallBASIC#commit=${_commit}"
        0001-link-everything-dynamically.patch)
sha256sums=('SKIP'
            '320fcd65425e20d95110ff5461acdaea56d4aa855c94286100267e1c3078779b')

prepare() {
  cd "${srcdir}"/${_realname}
  patch -i ${srcdir}/0001-link-everything-dynamically.patch
  ./autogen.sh
}

build() {
  [[ -d ${srcdir}/build-${MSYSTEM} ]] && rm -rf ${srcdir}/build-${MSYSTEM}
  cp -rf ${_realname} build-${MSYSTEM} && cd build-${MSYSTEM}

# configure and build multiple times to build all versions of SmallBASIC
# so far we build the console version, the sdl version, and the web server version
# these are what included in a SmallBASIC release
# there are other versions like the fltk version, but we don't build them

# build console version
  ./configure --prefix=${MINGW_PREFIX}
  make
  make DESTDIR="${pkgdir}" install

# build sdl version
  ./configure --prefix=${MINGW_PREFIX} --enable-sdl
  make
  make DESTDIR="${pkgdir}" install

# build web server version
  ./configure --prefix=${MINGW_PREFIX} --enable-web
  make
  make DESTDIR="${pkgdir}" install
}

package() {
  cd ${srcdir}/build-${MSYSTEM}
  # do nothing as we have moved make DESTDIR="${pkgdir}" install into the build step
}
