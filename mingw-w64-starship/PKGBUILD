# Maintainer: Naveen M K <naveen521kk@gmail.com>

_realname=starship
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.18.1
pkgrel=1
pkgdesc="The cross-shell prompt for astronauts (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://starship.rs"
msys2_repository_url="https://github.com/starship/starship"
msys2_references=(
  'archlinux: starship'
)
license=('spdx:ISC')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-zlib")
optdepends=("${MINGW_PACKAGE_PREFIX}-ttf-font-nerd: Nerd Font Symbols preset")
options=('!strip' '!lto') # done at build stage
source=("https://github.com/starship/starship/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('2ab61ae3d2e256266191f670a76a35fd06310ada2777efa0f2b6d2602071d13b')
noextract=("${_realname}-${pkgver}.tar.gz")

prepare() {
  tar -xzf "${srcdir}/${_realname}-${pkgver}.tar.gz" || true
  cd "${srcdir}/${_realname}-${pkgver}"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

_env() {
  export LIBZ_SYS_STATIC=0
  export WINAPI_NO_BUNDLED_LIBRARIES=1
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  _env
  ${MINGW_PREFIX}/bin/cargo build \
    --release \
    --frozen
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  _env
  ${MINGW_PREFIX}/bin/cargo install \
    --frozen \
    --offline \
    --no-track \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE -t "${pkgdir}${MINGW_PREFIX}"/share/licenses/starship/

  local _complete="./target/release/starship completions"
  $_complete bash | install -Dm644 /dev/stdin "${pkgdir}${MINGW_PREFIX}"/share/bash-completion/completions/starship
  $_complete fish | install -Dm644 /dev/stdin "${pkgdir}${MINGW_PREFIX}"/share/fish/vendor_completions.d/starship.fish
  $_complete zsh | install -Dm644 /dev/stdin "${pkgdir}${MINGW_PREFIX}"/share/zsh/site-functions/_starship
}
