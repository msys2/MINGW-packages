# Maintainer: Raphael Amorim <rapha850@gmail.com>
_realname=rio
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.2.4
pkgrel=1
pkgdesc="A hardware-accelerated GPU terminal emulator focusing to run in desktops and browsers (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/raphamorim/rio"
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
source=("https://github.com/raphamorim/${_realname}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('2b7cd1a87164fc2a79c17362a0bb9e96034230a727031bd5f8a75b322aec514d')

prepare() {
  cd "${_realname}-${pkgver}"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${_realname}-${pkgver}"

  cargo build -p rioterm --release --frozen
}

check() {
  cd "${_realname}-${pkgver}"

  cargo test -p rioterm --release --frozen
}

package() {
  cd "${_realname}-${pkgver}"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path frontends/rioterm \
    --root "${pkgdir}${MINGW_PREFIX}"
}
