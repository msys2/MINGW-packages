

_pyname=archinfo
pkgbase=mingw-w64-python-${_pyname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_pyname}")
pkgver=9.2.138
pkgrel=1
pkgdesc="Classes with architecture-specific information useful to other projects (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/angr/archinfo"
msys2_references=(
  'purl: pkg:pypi/archinfo'
)
license=('spdx:BSD-2-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python-build"
  "${MINGW_PACKAGE_PREFIX}-python-installer"
  "${MINGW_PACKAGE_PREFIX}-python-setuptools"
)
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-pytest")
optdepends=(
  "${MINGW_PACKAGE_PREFIX}-python-capstone"
  "${MINGW_PACKAGE_PREFIX}-python-unicorn"
  "${MINGW_PACKAGE_PREFIX}-python-keystone"
  "${MINGW_PACKAGE_PREFIX}-python-pyvex"
)
source=("${url}/archive/v${pkgver}/${_pyname}-${pkgver}.tar.gz")
sha512sums=('965e230b57bafc5027d0e8389ff90556c08f8983b274561852bd9b6d2ad410d429475f0f9ca53bd744d6a1e758aa21761e2b605c6abbe2ca13e6a74935df16af')


build() {
  cp -r "${_pyname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "python-build-${MSYSTEM}"

  python -m pytest
}

package() {
  cd "python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_pyname}/LICENSE"
  install -Dm644 README.md "${pkgdir}${MINGW_PREFIX}/share/doc/python-${_pyname}/README.md"
}

