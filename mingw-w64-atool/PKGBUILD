# Maintainer: Gore Liu <goreliu@126.com>

_realname=atool
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.39.0
pkgrel=1
pkgdesc="A script for managing file archives of various types (mingw-w64)"
arch=('any')
url="http://www.nongnu.org/atool/"
license=('GPL3')
depends=('file' 'perl')
optdepends=('bzip2: for using atool with bzip2 compressed archives'
            'gzip: for using atool with gzip compressed archives'
            'xz: for using atool with lzma compressed archives'
            'p7zip: for using atool with 7z archives'
            'tar: for using atool with tar archives'
            'zip: for using atool for creating zip archives'
            'unzip: for using atool for unpacking archives')
source=(http://savannah.nongnu.org/download/$_realname/$_realname-$pkgver.tar.gz)
sha256sums=('aaf60095884abb872e25f8e919a8a63d0dabaeca46faeba87d12812d6efc703b')

build() {
  cd "$srcdir"/${_realname}-${pkgver}
  [ -d "${srcdir}"/build-${CARCH} ] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}

  make
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install DESTDIR="${pkgdir}"
}
