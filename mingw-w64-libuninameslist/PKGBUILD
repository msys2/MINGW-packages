# Maintainer: Jeremy Tan <jtanx@outlook.com>

_realname=libuninameslist
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=20211114
pkgrel=1
pkgdesc="Library of Unicode annotation data (mingw-w64)"
arch=(any)
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://libuninameslist.sourceforge.io/"
license=("BSD")
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools" "${MINGW_PACKAGE_PREFIX}-cc")
options=('staticlibs' 'strip')
source=(${_realname}-dist-${pkgver}.tar.gz::https://github.com/fontforge/libuninameslist/releases/download/${pkgver}/libuninameslist-dist-${pkgver}.tar.gz
        "https://github.com/fontforge/libuninameslist/commit/3d04207.patch")
sha256sums=('f5f69090de4a483721207a9df7de5327c13c812a1d23de074d8f0496bc2b740d'
            'ff4a9248d9542e8cbc25495c78f27a977d30a0dfa620790394b640412f0ed478')

prepare() {
  cd "${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/3d04207.patch"
  autoreconf -fiv
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  cp -rf ${srcdir}/${_realname}-${pkgver} ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --enable-static

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make test || true
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 ${srcdir}/${_realname}-${pkgver}/LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
