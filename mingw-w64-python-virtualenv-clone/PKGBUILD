# Contributor: @ifurther <55025025+ifurther@users.noreply.github.com>

_realname=virtualenv-clone
pkgbase=mingw-w64-python-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-${_realname}
pkgver=0.5.7
pkgrel=3
pkgdesc="A script for cloning a non-relocatable virtualenv."
url="https://github.com/edwardgeorge/virtualenv-clone"
license=('spdx:MIT')
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
msys2_references=(
  'pypi: virtualenv-clone'
)
depends=(
    ${MINGW_PACKAGE_PREFIX}-python
    ${MINGW_PACKAGE_PREFIX}-python-virtualenv
)
makedepends=(${MINGW_PACKAGE_PREFIX}-python-setuptools)
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('418ee935c36152f8f153c79824bb93eaf6f0f7984bae31d3f48f350b9183501a')

prepare() {
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname}-${pkgver/v/}" "python-build-${MSYSTEM}"
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE.txt"
  
  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*-script.py*; do
    sed -e '1 { s|^#!.*$|#!/usr/bin/env pythonw.exe|g }' -i ${_f}
  done
}
