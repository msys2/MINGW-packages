# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_basename=tauri
_realname=cargo-${_basename}
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.5.10
pkgrel=1
pkgdesc="Command line interface for building Tauri apps (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/tauri-apps/tauri"
msys2_references=(
  'archlinux: cargo-tauri'
)
license=('spdx:MIT OR Apache-2.0')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
source=("${url}/archive/${_basename}-cli-v${pkgver}/${_basename}-${_basename}-cli-v${pkgver}.tar.gz")
noextract=("${_basename}-${_basename}-cli-v${pkgver}.tar.gz")
sha256sums=('3bf84fa4b84065b0a9cf5dfda3dc940b8b4f1eb38c7f8a0516aed132c82d294d')

prepare() {
  [[ -d "${srcdir}/${_basename}-${_basename}-cli-v${pkgver}" ]] && rm -rf "${srcdir}/${_basename}-${_basename}-cli-v${pkgver}"

  export MSYS=winsymlinks:lnk
  tar zxf "${srcdir}/${_basename}-${_basename}-cli-v${pkgver}.tar.gz"

  cd "${srcdir}/${_basename}-${_basename}-cli-v${pkgver}/tooling/cli"

  local _target="${CARCH}-pc-windows-gnu"
  if [[ $MINGW_PACKAGE_PREFIX == *-clang-aarch64 ]]; then
    _target="${CARCH}-pc-windows-gnullvm"
  fi
  "${MINGW_PREFIX}/bin/cargo.exe" fetch --locked --target "${_target}"
}

build() {
  cp -r "${_basename}-${_basename}-cli-v${pkgver}" "build-${MSYSTEM}"
  cd "build-${MSYSTEM}/tooling/cli"

  "${MINGW_PREFIX}/bin/cargo.exe" build \
    --release \
    --frozen
}

check() {
  cd "${srcdir}/build-${MSYSTEM}/tooling/cli"

  "${MINGW_PREFIX}/bin/cargo.exe" test \
    --release \
    --frozen
}

package() {
  cd "${srcdir}/build-${MSYSTEM}/tooling/cli"

  install -Dm755 "target/release/${_realname}.exe" "${pkgdir}${MINGW_PREFIX}/bin/${_realname}.exe"
  install -Dm644 "README.md" "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/README.md"
  install -Dm644 "LICENSE_MIT" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE_MIT"
  install -Dm644 "LICENSE_APACHE-2.0" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE_APACHE-2.0"
}
