# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_basename=tauri
_realname=cargo-${_basename}
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.5.0
pkgrel=1
pkgdesc="Command line interface for building Tauri apps (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64' 'clang32')
url="https://github.com/tauri-apps/tauri"
license=('MIT' 'Apache')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
options=('staticlibs' 'strip')
source=("${_basename}-${_basename}-cli-v${pkgver}.tar.gz::${url}/archive/${_basename}-cli-v${pkgver}.tar.gz")
noextract=("${_basename}-${_basename}-cli-v${pkgver}.tar.gz")
sha256sums=('a22239c0a904cb80c76d9f03c162515517fd0f8e50e098cfb90c17108d577571')

prepare() {
  [[ -d "${srcdir}/${_basename}-${_basename}-cli-v${pkgver}" ]] && rm -rf "${srcdir}/${_basename}-${_basename}-cli-v${pkgver}"

  export MSYS=winsymlinks:lnk
  tar zxf "${srcdir}/${_basename}-${_basename}-cli-v${pkgver}.tar.gz"

  cd "${srcdir}/${_basename}-${_basename}-cli-v${pkgver}/tooling/cli"

  "${MINGW_PREFIX}/bin/cargo.exe" fetch --locked
}

build() {
  msg "Cargo build for ${MSYSTEM}"
  cd "${srcdir}"
  cp -r "${_basename}-${_basename}-cli-v${pkgver}" "build-${MSYSTEM}"
  cd "build-${MSYSTEM}/tooling/cli"

  "${MINGW_PREFIX}/bin/cargo.exe" build \
    --release \
    --locked
}

check() {
  cd "${srcdir}/build-${MSYSTEM}/tooling/cli"

  "${MINGW_PREFIX}/bin/cargo.exe" test \
    --release \
    --locked
}

package() {
  cd "${srcdir}/build-${MSYSTEM}/tooling/cli"

  install -Dm755 "target/release/${_realname}.exe" "${pkgdir}${MINGW_PREFIX}/bin/${_realname}.exe"
  install -Dm644 "README.md" "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/README.md"
  install -Dm644 "LICENSE_MIT" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE_MIT"
  install -Dm644 "LICENSE_APACHE-2.0" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE_APACHE-2.0"
}

