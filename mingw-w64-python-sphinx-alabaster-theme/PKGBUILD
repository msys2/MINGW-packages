# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=sphinx-alabaster-theme
_pyname=alabaster
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}" "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=0.7.12
pkgrel=1
pkgdesc="Modified Kr Sphinx doc theme (mingw-w64)"
arch=('any')
license=('BSD')
url='https://github.com/bitprophet/alabaster'
makedepends=("${MINGW_PACKAGE_PREFIX}-python2-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python2-pygments"
              "${MINGW_PACKAGE_PREFIX}-python3-pygments")
options=('staticlibs')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/bitprophet/alabaster/archive/${pkgver}.tar.gz")
sha256sums=('2f413c2a7e16f6b18d38f86a236b89e50e5f6ca79094ff948e5e58828f95c430')

prepare() {
  cd ${srcdir}
  for pver in {2,3}; do 
    rm -rf python${pver}-build-${CARCH} | true
    cp -r "${_pyname}-${pkgver}" "python${pver}-build-${CARCH}"
  done
}

build() {
  for pver in {2,3}; do  
    msg "Python ${pver} build for ${CARCH}"  
    cd "${srcdir}/python${pver}-build-${CARCH}"
    ${MINGW_PREFIX}/bin/python${pver} setup.py build
  done  
}

check() {
  cd "${srcdir}"
  for pver in {2,3}; do  
    msg "Python ${pver} test for ${CARCH}"  
    cd "${srcdir}/python${pver}-build-${CARCH}" 
    ${MINGW_PREFIX}/bin/python${pver} setup.py test
  done  
}

package_python2-sphinx-alabaster-theme() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2")

  cd "${srcdir}/python2-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE"
}

package_python3-sphinx-alabaster-theme() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3")

  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}

package_mingw-w64-i686-python2-sphinx-alabaster-theme() {
  package_python2-sphinx-alabaster-theme
}

package_mingw-w64-i686-python3-sphinx-alabaster-theme() {
  package_python3-sphinx-alabaster-theme
}

package_mingw-w64-x86_64-python2-sphinx-alabaster-theme() {
  package_python2-sphinx-alabaster-theme
}

package_mingw-w64-x86_64-python3-sphinx-alabaster-theme() {
  package_python3-sphinx-alabaster-theme
}
