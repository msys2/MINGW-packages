# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=pygtk
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-python2-${_realname}"
pkgver=2.24.0
pkgrel=5
pkgdesc="Python bindings for the GTK widget set (mingw-w64)"
arch=('any')
url="http://www.pygtk.org"
license=('LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-python2-cairo"
         "${MINGW_PACKAGE_PREFIX}-python2-gobject2"
         "${MINGW_PACKAGE_PREFIX}-libglade")
makedepends=("${MINGW_PACKAGE_PREFIX}-python2-numpy")
options=('staticlibs' 'strip')
source=(https://download.gnome.org/sources/${_realname}/${pkgver%.*}/${_realname}-${pkgver}.tar.bz2
        001-python27.patch
        002-no-undefined.patch
        003-python-m4.patch
        004-pygtk-demo.patch)
md5sums=('a1051d5794fd7696d3c1af6422d17a49'
         '12acfacd26f19c504a0a2d0edeb66121'
         'edd96a4d9f4b7a1b05094a78f9cc811b'
         '2f1b6f04e36474e58abb259e22cc8309'
         '0bc4e06ae97f52403b366592a5881316')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/001-python27.patch
  patch -p1 -i ${srcdir}/002-no-undefined.patch
  patch -p1 -i ${srcdir}/003-python-m4.patch
  patch -p1 -i ${srcdir}/004-pygtk-demo.patch

  autoreconf -fi -I m4
  #WANT_AUTOMAKE=latest ./autogen.sh
}

build() {
  rm -rf python2-build
  mkdir python2-build
  #cp -r ${_realname}-${pkgver} ${builddir}
  pushd python2-build > /dev/null
    PYTHON=${MINGW_PREFIX}/bin/python2 \
    ../${_realname}-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST}
    make VERBOSE=1
  popd > /dev/null
}

package() {
  cd python2-build
  make DESTDIR="${pkgdir}" install
  install -m644 ${srcdir}/${_realname}-${pkgver}/gtk/gtk-extrafuncs.defs "${pkgdir}${MINGW_PREFIX}/share/pygtk/2.0/defs/"
  sed -i -e 's#env python$#env python2#' "${pkgdir}${MINGW_PREFIX}"/lib/pygtk/2.0/{,demos/}*.py
}
