# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=pygtk
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-python2-pygtk"
pkgver=2.24.0
pkgrel=1
pkgdesc="Python bindings for the GTK widget set (mingw-w64)"
arch=(any)
url="http://www.pygtk.org"
license=('LGPL')
groups=("${_mingw_suff}")
depends=("${_mingw_suff}-python2-cairo"
			"${_mingw_suff}-python2-gobject2"
			"${_mingw_suff}-libglade")
makedepends=("${_mingw_suff}-python2-numpy"
			"${_mingw_suff}-python2-gobject2")
options=('staticlibs' 'strip')
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver%.*}/${_realname}-${pkgver}.tar.bz2
		python27.patch
		no-undefined.patch
		python-m4.patch)
md5sums=('a1051d5794fd7696d3c1af6422d17a49'
         '12acfacd26f19c504a0a2d0edeb66121'
         'edd96a4d9f4b7a1b05094a78f9cc811b'
         '2f1b6f04e36474e58abb259e22cc8309')

prepare() {
	cd "${srcdir}/${_realname}-${pkgver}"
	patch -p1 -i ${srcdir}/python27.patch
	patch -p1 -i ${srcdir}/no-undefined.patch
	patch -p1 -i ${srcdir}/python-m4.patch
	autoreconf -fi -I m4
	#WANT_AUTOMAKE=latest ./autogen.sh
}

build() {
	cd "$srcdir"

	rm -rf python2-build
	mkdir python2-build
	#cp -r ${_realname}-$pkgver $builddir
	pushd python2-build
		PYTHON=${MINGW_PREFIX}/bin/python2 \
		../${_realname}-$pkgver/configure --prefix=${MINGW_PREFIX} \
			--build=${MINGW_CHOST} \
			--host=${MINGW_CHOST} \
			--target=${MINGW_CHOST}
		make VERBOSE=1
	popd
}

package() {
	pkgdesc="Python 2 bindings for GObject2 (mingw-w64)"
	depends=("${_mingw_suff}-glib2" "${_mingw_suff}-python2-cairo" "${_mingw_suff}-libffi" "${_mingw_suff}-gobject-introspection" "${_mingw_suff}-pygobject2-devel")

	cd python2-build
	make DESTDIR="$pkgdir" install
	install -m644 ${srcdir}/${_realname}-$pkgver/gtk/gtk-extrafuncs.defs "${pkgdir}${MINGW_PREFIX}/share/pygtk/2.0/defs/"
	sed -i -e 's#env python$#env python2#' "${pkgdir}${MINGW_PREFIX}"/lib/pygtk/2.0/{,demos/}*.py
}
