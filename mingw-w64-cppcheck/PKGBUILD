# Maintainer: Yusuke Sasaki <yusuke.sasaki.nuem@gmail.com>

_realname=cppcheck
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.84
pkgrel=1
pkgdesc="static analysis of C/C++ code (mingw-w64)"
arch=('any')
url="https://cppcheck.sourceforge.io/"
license=('GPL3')
depends=()
makedepends=("${MINGW_PACKAGE_PREFIX}-docbook-xsl"
             "${MINGW_PACKAGE_PREFIX}-qt5")
optdepends=("${MINGW_PACKAGE_PREFIX}-qt5: cppcheck-gui"
            "${MINGW_PACKAGE_PREFIX}-python2-pygments: cppcheck-htmlreport")
source=(${_realname}-${pkgver}.tar.gz::"https://github.com/danmar/cppcheck/archive/${pkgver}.tar.gz"
        '0010-change-language-files-path.patch'
        '0020-change-cfg-path.patch')
sha256sums=('aaa6293d91505fc6caa6982ca3cd2d949fa1aac603cabad072b705fdda017fc5'
            'a15a73396a2fa346d29df52c3143b46618454d0bc0f12f0ad891d5a4a856cb56'
            '3d54eb5abb99f88846e0d2a1e21793904efbe7179464631ca05985d03642d134')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/0010-change-language-files-path.patch"
  patch -p1 -i "${srcdir}/0020-change-cfg-path.patch"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  cp -r "${srcdir}/${_realname}-${pkgver}" "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}
  
  LANG='en_US.UTF-8' make \
    PREFIX=${MINGW_PREFIX} \
    CXX=${MINGW_PREFIX}/bin/g++ \
    LD=${MINGW_PREFIX}/bin/ld \
    SRCDIR=build \
    CFGDIR=${MINGW_PREFIX}/share/cppcheck/cfg \
    HAVE_RULES=yes \
    LDFLAGS=-lshlwapi RDYNAMIC=

  make man \
    DB2MAN=${MINGW_PREFIX}/share/xml/docbook/xsl-stylesheets-1.79.2/manpages/docbook.xsl \
    CFGDIR=${MINGW_PREFIX}/share/cppcheck/cfg

  cd gui
  lrelease gui.pro
  ${MINGW_PREFIX}/bin/qmake HAVE_RULES=yes HAVE_QCHART=yes CONFIG+=release
  make \
    CXX=${MINGW_PREFIX}/bin/g++ \
    LD=${MINGW_PREFIX}/bin/ld \
    PREFIX=${MINGW_PREFIX} \
    SRCDIR=build \
    CFGDIR=${MINGW_PREFIX}/share/cppcheck/cfg \
    HAVE_RULES=yes \
    HAVE_QCHART=yes
}

check() {
  cd "${srcdir}"/build-${CARCH}
   LANG='en_US.UTF-8' make test \
     SRCDIR=build \
     CXX=${MINGW_PREFIX}/bin/g++ \
     LD=${MINGW_PREFIX}/bin/ld \
     PREFIX="${MINGW_PREFIX}" \
     CFGDIR=${MINGW_PREFIX}/share/cppcheck/cfg \
     HAVE_RULES=yes \
     HAVE_QCHART=yes \
     LDFLAGS=-lshlwapi RDYNAMIC=
}

package() {
  cd "${srcdir}"/build-${CARCH}

  LANG='en_US.UTF-8' make install \
    DESTDIR="${pkgdir}" \
    CXX=${MINGW_PREFIX}/bin/g++ \
    LD=${MINGW_PREFIX}/bin/ld \
    PREFIX="${MINGW_PREFIX}" \
    SRCDIR=build \
    CFGDIR=${MINGW_PREFIX}/share/cppcheck/cfg \
    HAVE_QCHART=yes \
    HAVE_RULES=yes

  # man and COPYING
  install -Dpm644 cppcheck.1 "${pkgdir}${MINGW_PREFIX}/share/man/man1/cppcheck.1"
  install -Dpm644 COPYING    "${pkgdir}${MINGW_PREFIX}/share/licenses/cppcheck/COPYING"

  # GUI
  install -m755 build/gui/cppcheck-gui.exe "${pkgdir}${MINGW_PREFIX}/bin"
  install -d "${pkgdir}${MINGW_PREFIX}/share/cppcheck/cfg/lang"
  install -D cfg/* -t "${pkgdir}${MINGW_PREFIX}/share/cppcheck/cfg"
  install -D gui/*.qm -t "${pkgdir}${MINGW_PREFIX}/share/cppcheck/cfg/lang"
}

# vim: set ft=sh ts=2 sw=2 et :
