# Maintainer: Yusuke Sasaki <yusuke.sasaki.nuem@gmail.com>

_realname=cppcheck
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.10.3
pkgrel=1
pkgdesc="static analysis of C/C++ code (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64' 'clang32')
url="https://cppcheck.sourceforge.io/"
license=('GPL3')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-pcre")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-docbook-xsl"
             "${MINGW_PACKAGE_PREFIX}-qt5-base"
             "${MINGW_PACKAGE_PREFIX}-qt5-charts"
             "${MINGW_PACKAGE_PREFIX}-qt5-tools"
             "${MINGW_PACKAGE_PREFIX}-clang"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-tinyxml2")
optdepends=("${MINGW_PACKAGE_PREFIX}-qt5-charts: cppcheck-gui"
            "${MINGW_PACKAGE_PREFIX}-python-pygments: cppcheck-htmlreport")
source=(${_realname}-${pkgver}.tar.gz::"https://github.com/danmar/cppcheck/archive/${pkgver}.tar.gz"
        '001-cppcheck-2.2-change-language-files-path.patch'
        '002-cppcheck-2.2-change-cfg-path.patch')
sha256sums=('8aae5e116daeaaf5d19f3efa61b91c06f161cb97412a1d1af6e1e20686e48967'
            'e741bfafbeb3ade454a42563a1c4617ae3dc87fd6a6f409e1ae4b735b68997d3'
            '6f53ad14361717b3f994033483c14ad35c5790f703e03fe8aebaca83936ea61b')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/001-cppcheck-2.2-change-language-files-path.patch"
  patch -p1 -i "${srcdir}/002-cppcheck-2.2-change-cfg-path.patch"
}

build() {
  [[ -d "${srcdir}"/build-${MSYSTEM} ]] && rm -rf "${srcdir}"/build-${MSYSTEM}
  mkdir -p "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

  declare -a extra_config
  if check_option "debug" "y"; then
    extra_config+=(-DCMAKE_BUILD_TYPE=Debug)
  else
    extra_config+=(-DCMAKE_BUILD_TYPE=Release)
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=;-DFILESDIR=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DUSE_MATCHCOMPILER=ON \
    -DFILESDIR=${MINGW_PREFIX}/share/cppcheck \
    -DHAVE_RULES=ON \
    -DBUILD_GUI=ON \
    -DWITH_QCHART=ON \
    -DENABLE_OSS_FUZZ=OFF \
    -DUSE_BUNDLED_TINYXML2=OFF \
    ${extra_config[@]} \
    ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake --build .

  # man
  cd "${srcdir}"/${_realname}-${pkgver}
  make man \
    DB2MAN=${MINGW_PREFIX}/share/xml/docbook/xsl-stylesheets-1.79.2/manpages/docbook.xsl \
    FILESDIR=${MINGW_PREFIX}/share/cppcheck
  # help
  qhelpgenerator gui/help/online-help.qhcp -o gui/help/online-help.qhc
}

check() {
  cd "${srcdir}"/build-${MSYSTEM}
  ${MINGW_PREFIX}/bin/cmake -DBUILD_TESTS=ON ../${_realname}-${pkgver}
  ${MINGW_PREFIX}/bin/cmake --build .
  cd bin && ./testrunner -g -q
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .
  # Fix location of language files
  install -d "${pkgdir}"${MINGW_PREFIX}/share/${_realname}/lang
  mv "${pkgdir}"${MINGW_PREFIX}/bin/cppcheck_*.qm "${pkgdir}"${MINGW_PREFIX}/share/${_realname}/lang/
  # Remove freedesktop (Linux) files
  rm -r "${pkgdir}"${MINGW_PREFIX}/share/applications
  rm -r "${pkgdir}"${MINGW_PREFIX}/share/icons

  cd "${srcdir}"/${_realname}-${pkgver}
  # man and COPYING
  install -Dpm644 cppcheck.1 "${pkgdir}${MINGW_PREFIX}/share/man/man1/cppcheck.1"
  install -Dpm644 COPYING    "${pkgdir}${MINGW_PREFIX}/share/licenses/cppcheck/COPYING"

  # Install extra files
  install -Dpm644 htmlreport/cppcheck-htmlreport "${pkgdir}${MINGW_PREFIX}/bin/cppcheck-htmlreport"
  install -d "${pkgdir}${MINGW_PREFIX}/share/cppcheck/help"
  install -D gui/help/online-help.qch -t "${pkgdir}${MINGW_PREFIX}/share/cppcheck/help"
  install -D gui/help/online-help.qhc -t "${pkgdir}${MINGW_PREFIX}/share/cppcheck/help"
}
