# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

_realname=cairo
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.15.12
pkgrel=2
pkgdesc="Cairo vector graphics library (mingw-w64)"
arch=('any')
url="https://cairographics.org/"
license=(LGPL2.1 MPL1.1)
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-ghostscript"
             "${MINGW_PACKAGE_PREFIX}-glib2"
             "${MINGW_PACKAGE_PREFIX}-gtk2"
             "${MINGW_PACKAGE_PREFIX}-librsvg"
             "${MINGW_PACKAGE_PREFIX}-libspectre"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-poppler"
            )
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-freetype"
         "${MINGW_PACKAGE_PREFIX}-fontconfig"
         "${MINGW_PACKAGE_PREFIX}-lzo2"
         "${MINGW_PACKAGE_PREFIX}-pixman"
         "${MINGW_PACKAGE_PREFIX}-zlib"
        )
optdepends=("${MINGW_PACKAGE_PREFIX}-glib2: libcairo-gobject")
options=('strip' 'staticlibs')
source=(#"https://cairographics.org/releases/cairo-${pkgver}.tar.xz"
        https://cairographics.org/snapshots/cairo-${pkgver}.tar.xz
        0009-standalone-headers.mingw.patch
        0026-create-argb-fonts.all.patch
        0027-win32-print-fix-unbounded-surface-assertion.patch
        0001-win32-introduce-new-flag-to-mark-surfaces-supporting.patch
        0002-win32-CAIRO_WIN32_SURFACE_CAN_RGB_BRUSH-and-other-ar.patch
        0003-win32-Allow-gdi-operations-for-argb32-surfaces-allow.patch)
sha256sums=('7623081b94548a47ee6839a7312af34e9322997806948b6eec421a8c6d0594c9'
            '234de8c5d4c28b03c19e638a353e8defb2de0367a634c002b0ea7d2877bd0756'
            '6db6c44fbdb4926d09afa978fe80430186c4b7b7d255059602b1f94c6a079975'
            '7e244c20eec8c7b287dbee1d34de178d9b0c419dc4c2b11c90eaf626c92bf781'
            'f4fc46accb941c08fa017efe05ee871dd23ab630adc261f0ddf55d2d1e6e6722'
            'f9deb2b1d74e8d2b94e4fe742bd81329e76e52383ba53446eba9ab3a2fd180e9'
            'ea3eff7cf71ae074b846a28987e0d8f4dcf836e34bdbb04f38e7344398f43126')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0009-standalone-headers.mingw.patch
  patch -p1 -i ${srcdir}/0026-create-argb-fonts.all.patch
  patch -p1 -i ${srcdir}/0027-win32-print-fix-unbounded-surface-assertion.patch

  # https://lists.cairographics.org/archives/cairo/2018-April/028606.html
  patch -p1 -i ${srcdir}/0001-win32-introduce-new-flag-to-mark-surfaces-supporting.patch
  patch -p1 -i ${srcdir}/0002-win32-CAIRO_WIN32_SURFACE_CAN_RGB_BRUSH-and-other-ar.patch
  patch -p1 -i ${srcdir}/0003-win32-Allow-gdi-operations-for-argb32-surfaces-allow.patch

  autoreconf -fi
}

build() {
  # export lt_cv_deplibs_check_method='pass_all'
  rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}
  CFLAGS+=" -Wno-implicit-function-declaration"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-win32 \
    --enable-win32-font \
    --enable-png \
    --enable-shared \
    --enable-static \
    --enable-gobject \
    --enable-tee \
    --disable-xlib \
    --disable-xcb \
    --enable-fc \
    --enable-ft \
    --disable-silent-rules \
    ac_cv_prog_GS=${MINGW_PREFIX}/bin/gsc

  make #-j1 V=1
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  # Licenses
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING"          "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING-LGPL-2.1" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING-LGPL-2.1"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING-MPL-1.1"  "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING-MPL-1.1"
}
