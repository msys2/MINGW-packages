# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=shishi
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=r3583.485d3bd
pkgrel=1
pkgdesc="a GNU implementation of the Kerberos 5 network security system (mingw-w64)"
arch=('any')
url="https://www.gnu.org/software/shishi/"
license=("GPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-gettext" "wget" "git")
options=('strip' 'staticlibs')
depends=("${MINGW_PACKAGE_PREFIX}-gnutls"
         "${MINGW_PACKAGE_PREFIX}-libidn"
         "${MINGW_PACKAGE_PREFIX}-libgcrypt"
         "${MINGW_PACKAGE_PREFIX}-libgpg-error"
         "${MINGW_PACKAGE_PREFIX}-libtasn1"
        )
source=("${_realname}"::"git+git://git.savannah.gnu.org/shishi.git"
        "001-fix-search-libgcrypt.patch"
        "003-no-shishid.patch"
        "004-fix-download-po.patch"
       )
md5sums=('SKIP'
         'f2b2114a4dddb11e240df88086d9d33e'
         'b5af8571cc41670bd017d5fe7e4b48d9'
         '2a3d4a60719b801adf5835898c049420')

pkgver() {
  cd "$srcdir/$_realname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${_realname}
  patch -p1 -i ${srcdir}/001-fix-search-libgcrypt.patch
  patch -p1 -i ${srcdir}/003-no-shishid.patch
  patch -p1 -i ${srcdir}/004-fix-download-po.patch

  WANT_AUTOMAKE=latest autoreconf -fi
  make update-po
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd build-${MINGW_CHOST}
  
  ../${_realname}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --enable-static

  make -j1
}

package() {
  cd build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
}
