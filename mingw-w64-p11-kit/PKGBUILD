# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Saul Ibarra Corretge <saghul@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=p11-kit
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.23.1
pkgrel=3
pkgdesc="Library to work with PKCS#11 modules"
arch=('any')
url="http://p11-glue.freedesktop.org"
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-libtasn1"
         "${MINGW_PACKAGE_PREFIX}-libffi"
         "${MINGW_PACKAGE_PREFIX}-gettext")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "gtk-doc")
options=('!debug' 'strip' 'staticlibs')
source=(${url}/releases/${_realname}-${pkgver}.tar.gz{,.sig}
        0000-no-proxy.mingw.patch
        0001-allow-gtkdocize.all.patch
        0002-fix-includes.patch
        0003-fix-test-path.all.patch
        0004-fix-abspath-check.all.patch
        0006-p11-kit-relocation.patch
        0007-mmap-CreateFile-with-FILE_SHARE_READ.patch
        0008-add-debugging-to-mmap.patch
        0009-add-debugging-to-path.patch
        0010-fix-transport-test.patch
        0011-p11-kit-spawn-external.patch)
md5sums=('96f073270c489c9a594e1c9413f42db8'
         'SKIP'
         '70330b415eb5167e5a530c13bb96c212'
         'b7a706e19b5e29d96889ca2410a151df'
         'e5c7e6d8a83cd6d5809f5be8725dbe73'
         '30c965a6c7f92d1f9d88c92a7840f052'
         '9a91b743f18126ff7d4d08e43c38d0a3'
         'e7d5989b75a5520054f497754398c2ba'
         '3a37950e2a7ab95a4e691c02ff0603ab'
         '0dcd0d7e10d5d898f32da8866580d640'
         '27793fc62cf1fbfe52964ba8099d2b44'
         '8ecfeb942a3de9cb7355519c0eb62f79'
         '3cd8e579c1163be32be3b5bcaf45e711')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  rm -f common/pathtools.c common/pathtools.h > /dev/null 2>&1 || true
  patch -p1 -i ${srcdir}/0000-no-proxy.mingw.patch
  patch -p1 -i ${srcdir}/0001-allow-gtkdocize.all.patch
  patch -p1 -i ${srcdir}/0002-fix-includes.patch
  patch -p1 -i ${srcdir}/0003-fix-test-path.all.patch
  patch -p1 -i ${srcdir}/0004-fix-abspath-check.all.patch
  patch -p1 -i ${srcdir}/0006-p11-kit-relocation.patch
  patch -p1 -i ${srcdir}/0007-mmap-CreateFile-with-FILE_SHARE_READ.patch
  # patch -p1 -i ${srcdir}/0007-add-debugging-to-mmap.patch
  # patch -p1 -i ${srcdir}/0008-add-debugging-to-path.patch
  patch -p1 -i ${srcdir}/0010-fix-transport-test.patch
  patch -p1 -i ${srcdir}/0011-p11-kit-spawn-external.patch

  autoreconf -vfi
  gtkdocize
}

build() {
  local -a extra_config

  if check_option "debug" "y"; then
    extra_config+=( --enable-debug=yes )
  else
    extra_config+=( --enable-debug=no )
  fi

  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  
  PKG_CONFIG_PATH="${PKG_CONFIG_PATH}:/usr/share/pkgconfig" \
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --sysconfdir=${MINGW_PREFIX}/etc \
    --localstatedir=${MINGW_PREFIX}/var \
    --with-trust-paths="${MINGW_PREFIX}/etc/pki/ca-trust/source:${MINGW_PREFIX}/share/pki/ca-trust-source" \
    --disable-silent-rules \
    "${extra_config[@]}"

  MSYS2_ARG_CONV_EXCL="-DBINDIR=;-DDATA_DIR=;-DP11_SYSTEM_CONFIG_FILE=;-DP11_SYSTEM_CONFIG_MODULES=;-DP11_PACKAGE_CONFIG_MODULES=;-DP11_MODULE_PATH=;-DDATADIR=;-DSYSCONFDIR=;-DPRIVATEDIR=;-DP11_USER_CONFIG_FILE=;-DP11_USER_CONFIG_MODULES=" \
    make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  MSYS2_ARG_CONV_EXCL="-DBINDIR=;-DDATA_DIR=;-DP11_SYSTEM_CONFIG_FILE=;-DP11_SYSTEM_CONFIG_MODULES=;-DP11_PACKAGE_CONFIG_MODULES=;-DP11_MODULE_PATH=;-DDATADIR=;-DSYSCONFDIR=;-DPRIVATEDIR=;-DP11_USER_CONFIG_FILE=;-DP11_USER_CONFIG_MODULES=" \
    make DESTDIR="${pkgdir}" install
}
