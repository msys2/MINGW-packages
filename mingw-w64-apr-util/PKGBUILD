# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=apr-util
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.5.4
pkgrel=2
pkgdesc="The Apache Portable Runtime (mingw-w64)"
arch=('any')
url="http://apr.apache.org/"
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-libtool")
depends=("${MINGW_PACKAGE_PREFIX}-apr"
         "${MINGW_PACKAGE_PREFIX}-expat"
         "${MINGW_PACKAGE_PREFIX}-sqlite3")
options=('!libtool')
license=('APACHE')
source=(http://www.apache.org/dist/apr/${_realname}-${pkgver}.tar.bz2{,.asc}
        plugins.patch)
md5sums=('2202b18f269ad606d70e1864857ed93c'
         'SKIP'
         '9db9f3c4b14749988e9133aaa0658fcd')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/plugins.patch
  #./buildconf --with-apr=${MINGW_PREFIX}
  autoreconf -fi
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --with-apr=${MINGW_PREFIX} \
    --with-expat=${MINGW_PREFIX} \
    --without-gdbm \
    --without-ldap \
    --without-pgsql \
    --with-sqlite3=${MINGW_PREFIX}

  make -j1
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
