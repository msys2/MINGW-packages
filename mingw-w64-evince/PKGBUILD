# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=evince
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.14.1
pkgrel=2
arch=('any')
pkgdesc="Document (PostScript, PDF) viewer (mingw-w64)"
options=(strip staticlibs)
depends=("${MINGW_PACKAGE_PREFIX}-glib2"
        "${MINGW_PACKAGE_PREFIX}-cairo"
        "${MINGW_PACKAGE_PREFIX}-gtk3"
        "${MINGW_PACKAGE_PREFIX}-libgxps"
        "${MINGW_PACKAGE_PREFIX}-libspectre"
        "${MINGW_PACKAGE_PREFIX}-libtiff"
        "${MINGW_PACKAGE_PREFIX}-djvulibre"
        "${MINGW_PACKAGE_PREFIX}-gsettings-desktop-schemas"
        "${MINGW_PACKAGE_PREFIX}-poppler"
        "${MINGW_PACKAGE_PREFIX}-adwaita-icon-theme")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-pkg-config"
            "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
            "make"
            "libtool"
            "automake-wrapper"
            "autoconf"
            "intltool"
            "itstool"
            "gettext-devel"
            "yelp-tools")
license=("GPL 2")
url="http://www.gnome.org"
install=evince-${CARCH}.install
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver%.*}/${_realname}-$pkgver.tar.xz
        001-give-cc-to-gir-scanner.mingw.patch)
sha256sums=('13ec728d6957aa18ba21a3a66504dd52b8607596337f30f0908b62b5fcc14507'
            '2b704ea359fd73fb6ee905868b4b61246bb143cf783d7f970425a5236daa0686')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-give-cc-to-gir-scanner.mingw.patch
  #autopoint --force
  AUTOPOINT='intltoolize --automake --copy' autoreconf -f -i
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --enable-introspection \
    --disable-dbus \
    --disable-browser-plugin \
    --disable-nautilus
  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install
}
