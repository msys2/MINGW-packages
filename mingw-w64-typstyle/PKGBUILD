# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=typstyle
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.14.0
pkgrel=1
pkgdesc="Beautiful and reliable typst code formatter (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://typstyle-rs.github.io/typstyle'
msys2_repository_url='https://github.com/typstyle-rs/typstyle'
license=('spdx:Apache-2.0')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust" 'git')
source=("git+${msys2_repository_url}#tag=v${pkgver}")
sha256sums=('b8c8b2df82bcc8546a1c9a364197f2f3bd99af4566e3f63a061ffc71e47e91f5')

prepare() {
  cd "${_realname}"

  cargo fetch \
    --locked \
    --target "${RUST_CHOST}" \
    --config 'net.git-fetch-with-cli=true'
}

build() {
  cd "${_realname}"

  cargo build --release --frozen -p typstyle
}

check() {
  cd "${_realname}"

  cargo test --release --frozen -p typstyle
}

package() {
  cd "${_realname}"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path crates/typstyle \
    --root "${pkgdir}${MINGW_PREFIX}"

  local _target='target/release/typstyle completions'
  install -Dm644 <($_target bash) "${pkgdir}${MINGW_PREFIX}/share/bash-completion/completions/${_realname}"
  install -Dm644 <($_target fish) "${pkgdir}${MINGW_PREFIX}/share/fish/vendor_completions.d/${_realname}.fish"
  install -Dm644 <($_target zsh) "${pkgdir}${MINGW_PREFIX}/share/zsh/site-functions/_${_realname}"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
