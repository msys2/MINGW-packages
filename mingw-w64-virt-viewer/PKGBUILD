# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=virt-viewer
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=5.0
pkgrel=1
arch=('any')
pkgdesc="Displaying the graphical console of a virtual machine (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-spice-gtk"
         "${MINGW_PACKAGE_PREFIX}-gtk-vnc"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-opus")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "gtk-doc"
             "${MINGW_PACKAGE_PREFIX}-icoutils")
options=('strip' 'staticlibs')
license=("LGPL 2.1")
url="https://virt-manager.org/"
source=("https://virt-manager.org/download/sources/virt-viewer/virt-viewer-${pkgver}.tar.gz")
sha256sums=('0b2ded8b927a782a20fcf93b5377051285eec5677501b593d650213e79b88b2e')

prepare() {
  cd $srcdir/${_realname}-${pkgver}
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --with-spice-gtk

  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
