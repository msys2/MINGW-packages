# Maintainer: Richard Shaw <hobbes1069@gmail.com>

_realname=fltk
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3.3
pkgrel=1
pkgdesc="C++ user interface toolkit (mingw-w64)"
arch=('any')
url="http://www.fltk.org"
license=('LGPLv2+ with exceptions')
groups=("${MINGW_PACKAGE_PREFIX}")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
        "${MINGW_PACKAGE_PREFIX}-libiconv"
        "${MINGW_PACKAGE_PREFIX}-gettext"
        "${MINGW_PACKAGE_PREFIX}-zlib"
        "${MINGW_PACKAGE_PREFIX}-expat"
        "${MINGW_PACKAGE_PREFIX}-libpng"
        "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('strip' 'staticlibs' 'buildflags')
source=("http://fltk.org/pub/fltk/${pkgver}/${_realname}-${pkgver}-source.tar.gz")
md5sums=('9ccdb0d19dc104b87179bd9fd10822e3')

build() {
  [[ -d ${srcdir}/build-${CARCH} ]] && rm -rf ${srcdir}/build-${CARCH}
  # Upstream uses a custom Makefile so out of source builds don't work.
  cp -rf ${_realname}-${pkgver} build-${CARCH}
  cd build-${CARCH}
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --enable-shared
    make VERBOSE=1
}

package() {
  cd ${srcdir}/build-${CARCH}
  make DESTDIR="${pkgdir}" install
}
