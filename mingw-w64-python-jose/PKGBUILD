# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=jose
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-$_realname")
pkgver=3.3.0
pkgrel=1
pkgdesc="A JOSE implementation in Python (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/mpdavis/python-jose"
msys2_references=(
  'pypi: python-jose'
)
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python-rsa"
         "${MINGW_PACKAGE_PREFIX}-python-six"
         "${MINGW_PACKAGE_PREFIX}-python-ecdsa"
         "${MINGW_PACKAGE_PREFIX}-python-future")
optdepends=("${MINGW_PACKAGE_PREFIX}-python-cryptography: one option for better performance"
            "${MINGW_PACKAGE_PREFIX}-python-pycryptodome: one option for better performance")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python-wheel")
source=("https://pypi.io/packages/source/p/python-${_realname}/python-${_realname}-${pkgver}.tar.gz")
sha256sums=('55779b5e6ad599c6336191246e95eb2293a9ddebd555f796a65f838f07e5d78a')

prepare() {
  cd "${srcdir}"

  cp -r "python-${_realname}-${pkgver}" "python-build-${MSYSTEM}"
}

build() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
  install -Dm644 README.rst "${pkgdir}${MINGW_PREFIX}/share/doc/python-${_realname}/README.rst"
}

# vim:set ts=2 sw=2 et:
