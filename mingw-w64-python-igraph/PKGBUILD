_realname=igraph
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.11.9
pkgrel=3
pkgdesc="Python interface to the igraph high performance graph library, primarily aimed at complex network research and analysis. (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://pypi.org/project/igraph/'
msys2_references=(
  'purl: pkg:pypi/igraph'
)
license=('spdx:GPL-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-python" "${MINGW_PACKAGE_PREFIX}-python-texttable" "$MINGW_PACKAGE_PREFIX-igraph")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build" "${MINGW_PACKAGE_PREFIX}-python-installer" 
"$MINGW_PACKAGE_PREFIX-python-setuptools" "$MINGW_PACKAGE_PREFIX-pkgconf" "$MINGW_PACKAGE_PREFIX-cc" "$MINGW_PACKAGE_PREFIX-cmake"
)
options=('!strip')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz"
        )
sha256sums=('c57ce44873abcfcfd1d61d7d261e416d352186958e7b5d299cf244efa6757816')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}"
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  IGRAPH_USE_PKG_CONFIG=1   ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}


package() {
  msg "Python install for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
