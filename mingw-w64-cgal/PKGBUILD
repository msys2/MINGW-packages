# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=cgal
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.5.1
pkgrel=2
_pkgid=34139
pkgdesc="Computational Geometry Algorithms Library (mingw-w64)"
arch=('any')
url="http://www.cgal.org"
license=("LGPL, GPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-eigen3"
             "${MINGW_PACKAGE_PREFIX}-boost")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-mpfr"
         "${MINGW_PACKAGE_PREFIX}-boost"
         "${MINGW_PACKAGE_PREFIX}-gmp")
optdepends=("${MINGW_PACKAGE_PREFIX}-eigen3: for some packages, see the CGAL manual"
            #"${MINGW_PACKAGE_PREFIX}qt4: for CGAL_Qt4"
            )
options=('strip' 'staticlibs')
source=(http://gforge.inria.fr/frs/download.php/$_pkgid/CGAL-$pkgver.tar.xz
        cgal-4.4-mingw.patch
        convex_hull_fix.patch)
sha1sums=('97c60d3b12727b4fbbcce45a0965941bb1d60cbe'
          '1d2b06fe6faa0cb1ee748fa14f2de92f9e9e091f'
          'a4256b91ecf6230d17751444bb304e87f9d68c88')

prepare() {
  cd "${srcdir}"/CGAL-${pkgver%.*}
  patch -p1 -i ${srcdir}/cgal-4.4-mingw.patch
  patch -p1 -i ${srcdir}/convex_hull_fix.patch
}

build() {
  mkdir $srcdir/build-${MINGW_CHOST}
  cd $srcdir/build-${MINGW_CHOST}

  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    ../CGAL-${pkgver%.*}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make install
}
