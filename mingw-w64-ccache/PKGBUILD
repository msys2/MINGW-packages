# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=ccache
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.2.2
pkgrel=2
pkgdesc="A compiler cache (mingw-w64)"
arch=('any')
url="https://ccache.samba.org/"
license=("MIT")
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' 'strip')
source=(https://samba.org/ftp/ccache/${_realname}-${pkgver}.tar.bz2{,.asc})
sha256sums=('440f5e15141cc72d2bfff467c977020979810eb800882e3437ad1a7153cce7b2'
            'SKIP')

prepare() {
  cd ${_realname}-${pkgver}
  #./autogen.sh
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST}  && cd build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST}
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
