# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=zlib-ng
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}" "${MINGW_PACKAGE_PREFIX}-${_realname}-compat")
pkgver=2.3.3
pkgrel=1
pkgdesc='zlib replacement with optimizations for next generation systems (mingw-w64)'
url='https://github.com/zlib-ng/zlib-ng'
msys2_references=(
  'anitya: 115592'
  'archlinux: zlib-ng'
  'gentoo: sys-libs/zlib-ng'
)
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
license=("spdx:Zlib")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-ninja"
  "${MINGW_PACKAGE_PREFIX}-cc"
)
source=("${url}/archive/refs/tags/${pkgver}/${_realname}-${pkgver}.tar.gz"
        "0001-match-existing-zlib-naming.patch")
sha256sums=('f9c65aa9c852eb8255b636fd9f07ce1c406f061ec19a2e7d508b318ca0c907d1'
            '552965cd9a6dcad46a12429dbda491bd4693e0209cb695f41412524c2fa12f20')
b2sums=('36fed55807a98bb17edbb53b5b9456c3023dcf5aea9208b453d1cd187c56bba56a6818c3a14a39bf271a7f6bcee5203cc1e4f7e8d1961e965661a232afd0778f'
        'a1c2a6b9c44495479a845bbd8d40662bfac8b48b112f779af07d51f5a9b5211168954e65e54e37bf97c9028acefdf3d2522b8e7a21ee6cbd3c3b3d32d954fc70')

prepare() {
  cd "${_realname}-${pkgver}"
  # 1) Fix the zlib-ng naming to match the .pc file
  # 2) Make the compat naming the same as our current zlib
  patch -p1 -i "${srcdir}/0001-match-existing-zlib-naming.patch"
}

build() {
  cd "${_realname}-${pkgver}"

  declare -a _extra_config
  _extra_config=(
    -G Ninja
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}"
    -Wno-dev
    -DWITH_GTEST=OFF
  )

  # gcc 15.1 toolchain crashes sometimes when linking tests
  if [[ ${MSYSTEM} != CLANG* ]]; then
    _extra_config+=("-DZLIB_ENABLE_TESTS=OFF")
  fi

  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  echo "Building zlib-ng"
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake -B build-static \
      -DBUILD_SHARED_LIBS=OFF \
      "${_extra_config[@]}"
  cmake --build build-static

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake -B build \
      -DBUILD_SHARED_LIBS=ON \
      "${_extra_config[@]}"
  cmake --build build

  echo "Building zlib-ng-compat"
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake -B build-compat-static \
      -DBUILD_SHARED_LIBS=OFF \
      "${_extra_config[@]}" \
      -DZLIB_COMPAT=ON
  cmake --build build-compat-static

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake -B build-compat \
      -DBUILD_SHARED_LIBS=ON \
      "${_extra_config[@]}" \
      -DZLIB_COMPAT=ON
  cmake --build build-compat
}

check() {
  cd "${_realname}-${pkgver}"
  echo "Checking zlib-ng"
  ctest --output-on-failure --test-dir build
  echo "Checking zlib-ng-compat"
  ctest --output-on-failure --test-dir build-compat
}

package_zlib-ng() {
  cd "${_realname}-${pkgver}"

  DESTDIR="${pkgdir}" cmake --install build-static
  DESTDIR="${pkgdir}" cmake --install build
  install -Dm 644 LICENSE.md -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
  install -Dm 644 README.md -t "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}"
}

package_zlib-ng-compat() {
  pkgdesc+=" (zlib compat)"
  provides=("${MINGW_PACKAGE_PREFIX}-zlib")
  conflicts=("${MINGW_PACKAGE_PREFIX}-zlib")

  cd "${_realname}-${pkgver}"
  DESTDIR="${pkgdir}" cmake --install build-compat-static
  DESTDIR="${pkgdir}" cmake --install build-compat
  install -Dm 644 LICENSE.md -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}-compat"
  install -Dm 644 README.md -t "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}-compat"
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
