# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=pyopenssl
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=25.3.0
pkgrel=3
pkgdesc="Python wrapper module around the OpenSSL library (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://www.pyopenssl.org'
msys2_repository_url='https://github.com/pyca/pyopenssl/'
msys2_changelog_url='https://github.com/pyca/pyopenssl/blob/main/CHANGELOG.rst'
msys2_references=(
  'archlinux: python-pyopenssl'
  'purl: pkg:pypi/pyopenssl'
  "cpe: cpe:/a:pyopenssl:pyopenssl"
  "cpe: cpe:/a:pyopenssl_project:pyopenssl"
)
license=('spdx:Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-python-cryptography"
         "${MINGW_PACKAGE_PREFIX}-python-typing_extensions")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-pytest")
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('c981cb0a3fd84e8602d7afc209522773b94c1c2446a3c710a75b06fe1beae329')

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
