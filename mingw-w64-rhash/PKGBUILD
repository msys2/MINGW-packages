# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=rhash
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.4.4
pkgrel=1
pkgdesc="Great utility for computing hash sums (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://sourceforge.net/projects/rhash/'
depends=("${MINGW_PACKAGE_PREFIX}-gettext")
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc")
license=('custom' 'BSD')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/rhash/RHash/archive/v${pkgver}.tar.gz"{,.asc}
        rhash-mingw-install.patch
        rhash-all-mingw.patch)
sha256sums=('1e40fa66966306920f043866cbe8612f4b939b033ba5e2708c3f41be257c8a3e'
            'SKIP'
            'caa5124d54350f93c493a4b3818e1ec749d2df990835cc4a38ffbd7e0f97f0df'
            'c999da390c4c19ff6c39dd7a1a85b8f07300b9bb72b1507a6255c341b8c2fc4a')
validpgpkeys=('2875F6B1C2D27A4F0C8AF60B2A714497E37363AE')

prepare() {
  cd "${srcdir}"/RHash-${pkgver}
  patch -Np1 -i ../rhash-mingw-install.patch
  patch -Np1 -i ../rhash-all-mingw.patch
}

build() {
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  cp -R "${srcdir}"/RHash-${pkgver} "${srcdir}"/build-${CARCH}
  cd "${srcdir}"/build-${CARCH}
  ./configure \
     --prefix=${MINGW_PREFIX} \
     --enable-lib-static \
     --enable-lib-shared
  make
}

check() {
  cd "${srcdir}"/build-${CARCH}
  make test test-shared test-static || true
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make -j1 install DESTDIR="${pkgdir}"
  make -j1 install install-gmo DESTDIR="${pkgdir}"

  # license
  # fully qualify path for this because there might be switch in
  # build systems.
  install -Dm644 ${srcdir}/RHash-${pkgver}/COPYING \
    "${pkgdir}${MINGW_PREFIX}"/share/licenses/rhash/LICENSE
}
