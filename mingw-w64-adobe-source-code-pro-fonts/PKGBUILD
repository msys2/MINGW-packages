# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=adobe-source-code-pro-fonts
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.038ro+1.058it+1.018var
_relver=2.038R-ro/1.058R-it/1.018R-VAR
pkgrel=1
pkgdesc="Monospaced font family for user interface and coding environments (mingw-w64)"
url="https://adobe-fonts.github.io/source-code-pro/"
arch=(any)
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
license=(custom)
_tarname=source-code-pro-${_relver//\//-}
source=("${_tarname}.tar.gz::https://github.com/adobe-fonts/source-code-pro/archive/${_relver}.tar.gz"
        https://github.com/adobe-fonts/source-code-pro/releases/download/variable-fonts/SourceCodeVariable-Roman.otf
        https://github.com/adobe-fonts/source-code-pro/releases/download/variable-fonts/SourceCodeVariable-Italic.otf)
sha256sums=('d8f879fefd66ecb7291ea2d73984b9c0a97fb499424e58290ba6b81787d0c725'
            'af8fdd265f6208816fde44062a27b79ce2a594ded44ea96055a1655b6869992d'
            'b2ca3a3c1fe0701ad74aa7c66c37972d07b1237197a816a1a5646c7e42a11353')

package() {
  cd "${_tarname}"
  install -d "${pkgdir}${MINGW_PREFIX}/share/fonts/${_realname%-fonts}"
  install -t "${pkgdir}${MINGW_PREFIX}/share/fonts/${_realname%-fonts}" -m644 OTF/*.otf ../*.otf
  install -Dm644 LICENSE.md "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.md"
}

# vim:set ts=2 sw=2 et:
