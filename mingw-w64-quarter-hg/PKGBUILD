# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=quarter

pkgbase=mingw-w64-${_realname}-hg
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-hg"
pkgver=r488.550ec597831a
pkgrel=1
pkgdesc="Light-weight glue library that provides seamless integration between Coin and Qt5 (mingw-w64)"
arch=('any')
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-qt5"
    "${MINGW_PACKAGE_PREFIX}-coin3d")
makedepends=("mercurial" "${MINGW_PACKAGE_PREFIX}-cmake" "${MINGW_PACKAGE_PREFIX}-gcc")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
url='https://bitbucket.org/Coin3D/simage/overview'
source=("${_realname}"::"hg+https://bitbucket.org/Alexpux/quarter")
options=(!strip staticlibs)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_realname"
  printf "r%s.%s" "$(hg identify -n)" "$(hg identify -i)"
}

prepare() {
  cd ${srcdir}/${_realname}
}

build() {
  export lt_cv_deplibs_check_method='pass_all'
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
        ../"${_realname}"/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --disable-static \
    --enable-optimization \
    --disable-html \
    --disable-debug \
    --enable-pkgconfig \
    --with-coin=${MINGW_PREFIX} \
    --with-qt=${MINGW_PREFIX} \
                --without-x \
                sim_ac_qt_debug=false
  make -j1
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install

  # Workaround - need to build MSYS2 with separate /usr
  [[ -d ${pkgdir}/usr ]] && {
    cp -rf ${pkgdir}/usr${MINGW_PREFIX}/* ${pkgdir}${MINGW_PREFIX}/
    rm -rf ${pkgdir}/usr
  }
  mv -f ${pkgdir}${MINGW_PREFIX}/plugins/bin/*.dll ${pkgdir}${MINGW_PREFIX}/plugins/designer/
  rm -rf ${pkgdir}${MINGW_PREFIX}/plugins/bin
}
