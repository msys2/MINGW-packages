# Maintainer: @naveen521kk on Github, Naveen M K <naveen521kk@gmail.com>

_realname=jaraco.functools
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=4.4.0
pkgrel=3
pkgdesc='Functools like those found in stdlib (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'anitya: 20165'
  'archlinux: python-jaraco.functools'
  'purl: pkg:pypi/jaraco-functools'
)
url="https://github.com/jaraco/jaraco.functools"
msys2_changelog_url='https://github.com/jaraco/jaraco.functools/blob/main/NEWS.rst'
license=('spdx:MIT')
depends=(
  "${MINGW_PACKAGE_PREFIX}-python-more-itertools"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python-build"
  "${MINGW_PACKAGE_PREFIX}-python-installer"
  "${MINGW_PACKAGE_PREFIX}-python-setuptools"
  "${MINGW_PACKAGE_PREFIX}-python-setuptools-scm"
)
source=("https://pypi.org/packages/source/${_realname::1}/${_realname/./_}/${_realname/./_}-${pkgver}.tar.gz")
sha256sums=('da21933b0417b89515562656547a77b4931f98176eb173644c0d35032a33d6bb')

build() {
  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION="${pkgver}"

  cp -r "${_realname/./_}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
