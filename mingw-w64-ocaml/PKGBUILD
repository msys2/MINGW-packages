# Maintainer: Junjie Mao <eternal.n08@gmail.com>

_realname=ocaml
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=4.02.1
pkgrel=1
pkgdesc='An industrial strength programming language supporting functional, imperative and object-oriented styles.'
groups=("${MINGW_PACKAGE_PREFIX}")
arch=('any')
url='http://ocaml.org/'
license=('Q Public Licence 1.0' 'GPL2')
depends=("${MINGW_PACKAGE_PREFIX}-flexdll")
makedepends=("diffutils")
source=("http://caml.inria.fr/pub/distrib/ocaml-4.02/ocaml-4.02.1.tar.gz"
        "0001-Rename-Makefile.mingw-to-Makefile.mingw32.patch"
	"0002-Adapt-to-msys2-directory-layout.patch"
	"0003-Install-manuals-in-msys2.patch"
	"0004-camlheader-has-the-extension-.exe-in-msys2.patch"
	"0005-Install-libraries-to-lib-ocaml-instead-of-lib.patch")
sha1sums=('6af8c67f2badece81d8e1d1ce70568a16e42313e'
          '5b56a2ecff40d53158578d5a41e1eb992f3dcdc6'
          'ee16e23932922f6f9fcc48072d564b90a3b66246'
          'a780a6c94aaedcd0d19a7fbc26e09776b581a8af'
          'e1f6b201c25b61d39404406c82684f6c8d70d23a'
          '17010515bd9715ed36300165643d8f3ad01288cf')

env=${MINGW_PREFIX#/}
_pkg="${_realname}-${pkgver}"
_pkgsrc="${env}-${_pkg}"
noextract=(${_pkg}.tar.gz)
prepare() {
  for prog in ar as ld ranlib; do
    no_prefix=${MINGW_PREFIX}/bin/${prog}.exe
    with_prefix=${MINGW_PREFIX}/bin/${CARCH}-w64-mingw32-${prog}.exe
    if [ -f "${no_prefix}" ] && [ ! -f "${with_prefix}" ]; then
  	cp ${no_prefix} ${with_prefix}
  	echo ${no_prefix}, ${with_prefix}
    fi
  done

  cd $startdir/
  [ -d ${srcdir}/${_pkgsrc} ] && rm -rf ${srcdir}/${_pkgsrc}
  tar -xzf ${startdir}/${_pkg}.tar.gz -C ${srcdir}

  cd ${srcdir}
  mv ${_pkg} ${_pkgsrc}
  cd ${_pkgsrc}

  patch -p1 -i ${srcdir}/0001-Rename-Makefile.mingw-to-Makefile.mingw32.patch
  patch -p1 -i ${srcdir}/0002-Adapt-to-msys2-directory-layout.patch
  patch -p1 -i ${srcdir}/0003-Install-manuals.patch
  patch -p1 -i ${srcdir}/0004-camlheader-has-the-extension-.exe-in-msys2.patch
  patch -p1 -i ${srcdir}/0005-Install-libraries-to-lib-ocaml-instead-of-lib.patch
}

build() {
  cd ${srcdir}/${_pkgsrc}
  cp config/m-nt.h config/m.h
  cp config/s-nt.h config/s.h
  cp config/Makefile.${env} config/Makefile

  make -f Makefile.nt world PREFIX="$(cygpath -m ${MINGW_PREFIX})" -j1
  make -f Makefile.nt bootstrap PREFIX="$(cygpath -m ${MINGW_PREFIX})" -j1
  make -f Makefile.nt opt PREFIX="$(cygpath -m ${MINGW_PREFIX})" -j1
  make -f Makefile.nt opt.opt PREFIX="$(cygpath -m ${MINGW_PREFIX})" -j1
}

check() {
  # The testsuite refuses to compile due to lack of headers such as sys/resource.h
  plain "skip"
}

package() {
  cd ${srcdir}/${_pkgsrc}
  make -f Makefile.nt install PREFIX="${pkgdir}${MINGW_PREFIX}" -j1
  [ -f LICENSE ] && install -Dm644 LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE

  mkdir -p ${pkgdir}/etc/profile.d
  echo '[ "$MSYSTEM" = "'$(echo ${env} | tr "[:lower:]" "[:upper:]")'" ] && export OCAMLLIB='${MINGW_PREFIX}'/lib/ocaml' > ${pkgdir}/etc/profile.d/ocaml.${env}.sh
}
