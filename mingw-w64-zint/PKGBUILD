# Maintainer: MVZ Labor Ludwigsburg <edv@mvz-labor-lb.de>
# vim: ft=bash

# Inspired by https://github.com/archlinux/svntogit-community/blob/packages/zint/trunk/PKGBUILD

_realname=zint
pkgbase=mingw-w64-${_realname}
pkgname=(
  "${MINGW_PACKAGE_PREFIX}-zint"
  "${MINGW_PACKAGE_PREFIX}-zint-qt"
)
pkgver=2.10.0
pkgrel=1
pkgdesc='Barcode encoding library supporting over 50 symbologies (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="http://zint.org.uk/"
license=('spdx:GPL-3.0-or-later')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-ninja"
  "${MINGW_PACKAGE_PREFIX}-qt5-tools"
)
source=(
  "https://downloads.sourceforge.net/${_realname}/${_realname}-${pkgver}-src.tar.gz"
  "000-use-default-CMake-modules-install-path.patch"
)
sha256sums=(
  'bb97e98a32e140c344e92c8da84a9df413dca16083f2fcdc29791bec77350339'
  '832279704b3e8610d92df28b98eea0344b7079a6511134ad5d3474141ae2e261'
)

prepare() {
    cd ${_realname}-${pkgver}-src

    patch -Np0 -i "${srcdir}/000-use-default-CMake-modules-install-path.patch"
}

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake -S ${_realname}-${pkgver}-src -B build -G Ninja \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      "${extra_config[@]}" \

  ${MINGW_PREFIX}/bin/cmake --build build
}

package_zint() {
  depends=("${MINGW_PACKAGE_PREFIX}-libpng")

  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install build

  rm "${pkgdir}${MINGW_PREFIX}/bin/zint-qt.exe"

  install -Dm644 "${srcdir}/${_realname}-${pkgver}-src/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}

package_zint-qt() {
  pkgdesc='Zint Barcode Studio GUI (mingw-w64)'
  depends=("${MINGW_PACKAGE_PREFIX}-qt5-base" "${MINGW_PACKAGE_PREFIX}-zint")

  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install build/backend_qt
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install build/frontend_qt

  rm -r "${pkgdir}${MINGW_PREFIX}/include"
  rm -r "${pkgdir}${MINGW_PREFIX}/lib"
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :


for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
