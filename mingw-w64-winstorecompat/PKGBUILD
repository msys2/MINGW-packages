# Maintainer: Martell Malone <martellmalone@gmail.com>

_realname=winstorecompat
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=13.0.0.r453.gfd36ef357
pkgrel=1
_commit='fd36ef357f4964ad84628b2c69c15791b2f1f559'
pkgdesc="MinGW-w64 winRT compat library (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://www.mingw-w64.org/"
msys2_repository_url="https://sourceforge.net/p/mingw-w64/mingw-w64/ci/master/tree/mingw-w64-libraries/winstorecompat/"
license=('spdx:MIT')
groups=("${MINGW_PACKAGE_PREFIX}-toolchain")
provides=("${MINGW_PACKAGE_PREFIX}-winstorecompat-git=${pkgver}")
conflicts=("${MINGW_PACKAGE_PREFIX}-winstorecompat-git")
replaces=("${MINGW_PACKAGE_PREFIX}-winstorecompat-git")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-crt"
             "${MINGW_PACKAGE_PREFIX}-headers"
             "git")
options=('!buildflags' '!emptydirs')
source=("mingw-w64"::"git+https://git.code.sf.net/p/mingw-w64/mingw-w64#commit=$_commit")
sha256sums=('eaa8e8a79667560584e66df2aac8ad582b53fb6b342740b3edd7b7afddbc6f07')

pkgver() {
  cd "${srcdir}/mingw-w64"
  git describe --long ${_commit} | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//g'
}

build() {
  mkdir -p "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

  "${srcdir}"/mingw-w64/mingw-w64-libraries/winstorecompat/configure \
    --prefix=${MINGW_PREFIX}

  make
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}
  make DESTDIR="${pkgdir}" install
}
