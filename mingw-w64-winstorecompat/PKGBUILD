# Maintainer: Martell Malone <martellmalone@gmail.com>

_realname=winstorecompat
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=13.0.0.r411.gea5ddeff2
pkgrel=1
_commit='ea5ddeff273572004824f56d3e037e295558b52b'
pkgdesc="MinGW-w64 winRT compat library (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://www.mingw-w64.org/"
msys2_repository_url="https://sourceforge.net/p/mingw-w64/mingw-w64/ci/master/tree/mingw-w64-libraries/winstorecompat/"
license=('spdx:MIT')
groups=("${MINGW_PACKAGE_PREFIX}-toolchain")
provides=("${MINGW_PACKAGE_PREFIX}-winstorecompat-git=${pkgver}")
conflicts=("${MINGW_PACKAGE_PREFIX}-winstorecompat-git")
replaces=("${MINGW_PACKAGE_PREFIX}-winstorecompat-git")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-crt"
             "${MINGW_PACKAGE_PREFIX}-headers"
             "git")
options=('!buildflags' '!emptydirs')
source=("mingw-w64"::"git+https://git.code.sf.net/p/mingw-w64/mingw-w64#commit=$_commit")
sha256sums=('8b52166a6bc5eb5c6d4a366f3ee6765ae90219400884823d44c93327bb7aef88')

pkgver() {
  cd "${srcdir}/mingw-w64"
  git describe --long ${_commit} | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//g'
}

build() {
  mkdir -p "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

  "${srcdir}"/mingw-w64/mingw-w64-libraries/winstorecompat/configure \
    --prefix=${MINGW_PREFIX}

  make
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}
  make DESTDIR="${pkgdir}" install
}
