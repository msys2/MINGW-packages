# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=vcdimager
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.7.24
pkgrel=2
pkgdesc="A full-featured mastering suite for authoring disassembling and analyzing Video CD's and Super Video CD's (mingw-w64)"
arch=("any")
url="http://www.vcdimager.org/"
license=('GPL')
depends=("${MINGW_PACKAGE_PREFIX}-libcdio"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-popt")
source=("https://ftp.gnu.org/gnu/vcdimager/${_realname}-${pkgver}.tar.gz"
        001-vcdimager-0.7.24-libcdio-0.83.patch
        002-no-undefined.patch
        003-static-vcd-lib.patch)
sha256sums=('075d7a67353ff3004745da781435698b6bc4a053838d0d4a3ce0516d7d974694'
            '2dec35e45361c5d7ca11b342f66463b5ce667db3df9d4dc2ac63f101fa183959'
            'f09c49cc77bcfeaab2e23349a30fc57657e37426cc5d7f4ff63b7d79ce4e8111'
            '356a711f8201755b2ae330dfa37919ed08f3a6fed2d17a896751fae89a357e44')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i "$srcdir/001-vcdimager-0.7.24-libcdio-0.83.patch"
  patch -p1 -i "$srcdir/002-no-undefined.patch"
  patch -p1 -i "$srcdir/003-static-vcd-lib.patch"

  autoreconf -fvi
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared

  make
}

package() {
  cd build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
}
