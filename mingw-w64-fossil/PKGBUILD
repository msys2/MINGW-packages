# Contributor (MSYS2): David Macek <david.macek.0@gmail.com>
# Maintainer (Arch Linux): Anatol Pomozov <anatol pomozov at gmail>
# Contributor (Arch Linux): Daniel YC Lin <dlin.tw at gmail>
# Contributor (Arch Linux): Konstantin Plotnikov <kostyapl at gmail dot com>

_realname=fossil
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.27
_pkgsha=99675884
pkgrel=2
pkgdesc='Simple, high-reliability, distributed software configuration management (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://www.fossil-scm.org/'
msys2_references=(
  "cpe: cpe:/a:fossil-scm:fossil"
  "cpe: cpe:/a:fossil_scm:fossil"
)
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-readline"
         "${MINGW_PACKAGE_PREFIX}-sqlite3"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc" "${MINGW_PACKAGE_PREFIX}-autotools" "${MINGW_PACKAGE_PREFIX}-tcl")
optdepends=("${MINGW_PACKAGE_PREFIX}-putty: SSH transport")
source=("https://www.fossil-scm.org/home/tarball/${_pkgsha}/${_realname}-src-${pkgver}.tar.gz"
        'readline.patch')
sha256sums=('0405a96ba4d286b46fb5c3217d6c13391a2c637da90c51a927ee0c31c58f9064'
            '2990609868434ea4dceb6ff11c88a21d443dfe65ae9dbdbcf906972401ecdcfb')

prepare() {
  cd "${srcdir}/${_realname}-src-${pkgver}"
  patch -p1 -i "${srcdir}/readline.patch"
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL=--prefix \
  ../${_realname}-src-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --json \
    --with-openssl=${MINGW_PREFIX} \
    --with-sqlite=${MINGW_PREFIX} \
    --with-tcl=${MINGW_PREFIX} \
    --with-tcl-private-stubs=1 \
    --with-zlib=${MINGW_PREFIX}

  make \
    CFLAGS+="-DBROKEN_MINGW_CMDLINE=not_actually_broken"
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/${_realname}-src-${pkgver}/COPYRIGHT-BSD2.txt" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
