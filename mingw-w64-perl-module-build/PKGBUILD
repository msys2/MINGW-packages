# Maintainer: @naveen521kk on Github, Naveen M K <naveen521kk@gmail.com>

_perlname=Module-Build
_realname="${_perlname,,}"
pkgbase=mingw-w64-perl-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-perl-${_realname}")
pkgver=0.4231
pkgrel=1
pkgdesc="Build and install Perl modules (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://metacpan.org/release/${_perlname}"
groups=("${MINGW_PACKAGE_PREFIX}-perl-modules")
provides=(
  "${MINGW_PACKAGE_PREFIX}-perl-module-build-base"
  "${MINGW_PACKAGE_PREFIX}-perl-module-build-compat"
  "${MINGW_PACKAGE_PREFIX}-perl-module-build-config"
  "${MINGW_PACKAGE_PREFIX}-perl-module-build-cookbook"
  "${MINGW_PACKAGE_PREFIX}-perl-module-build-dumper"
  "${MINGW_PACKAGE_PREFIX}-perl-module-build-notes"
  "${MINGW_PACKAGE_PREFIX}-perl-module-build-ppmmaker"
  "${MINGW_PACKAGE_PREFIX}-perl-module-build-platform-default"
  "${MINGW_PACKAGE_PREFIX}-perl-module-build-platform-macos"
  "${MINGW_PACKAGE_PREFIX}-perl-module-build-platform-unix"
  "${MINGW_PACKAGE_PREFIX}-perl-module-build-platform-vms"
  "${MINGW_PACKAGE_PREFIX}-perl-module-build-platform-vos"
  "${MINGW_PACKAGE_PREFIX}-perl-module-build-platform-windows"
  "${MINGW_PACKAGE_PREFIX}-perl-module-build-platform-aix"
  "${MINGW_PACKAGE_PREFIX}-perl-module-build-platform-cygwin"
  "${MINGW_PACKAGE_PREFIX}-perl-module-build-platform-darwin"
  "${MINGW_PACKAGE_PREFIX}-perl-module-build-platform-os2"
  "${MINGW_PACKAGE_PREFIX}-perl-module-build-podparser"
)
depends=(
  "${MINGW_PACKAGE_PREFIX}-perl"
  "${MINGW_PACKAGE_PREFIX}-perl-file-compare"
  "${MINGW_PACKAGE_PREFIX}-perl-extutils-cbuilder"
  "${MINGW_PACKAGE_PREFIX}-perl-module-metadata"
  "${MINGW_PACKAGE_PREFIX}-perl-text-parsewords"
  "${MINGW_PACKAGE_PREFIX}-perl-getopt-long"
  "${MINGW_PACKAGE_PREFIX}-perl-file-find"
  "${MINGW_PACKAGE_PREFIX}-perl-extutils-parsexs"
  "${MINGW_PACKAGE_PREFIX}-perl-cwd"
  "${MINGW_PACKAGE_PREFIX}-perl-text-abbrev"
  "${MINGW_PACKAGE_PREFIX}-perl-extutils-mkbootstrap"
  "${MINGW_PACKAGE_PREFIX}-perl-file-basename"
  "${MINGW_PACKAGE_PREFIX}-perl-file-copy"
  "${MINGW_PACKAGE_PREFIX}-perl-extutils-install"
  "${MINGW_PACKAGE_PREFIX}-perl-tap-harness"
  "${MINGW_PACKAGE_PREFIX}-perl-cpan-meta"
  "${MINGW_PACKAGE_PREFIX}-perl-extutils-manifest"
  "${MINGW_PACKAGE_PREFIX}-perl-pod-man"
  "${MINGW_PACKAGE_PREFIX}-perl-file-path"
  "${MINGW_PACKAGE_PREFIX}-perl-file-spec"
  "${MINGW_PACKAGE_PREFIX}-perl-data-dumper"
  "${MINGW_PACKAGE_PREFIX}-perl-version"
  "${MINGW_PACKAGE_PREFIX}-perl-perl-ostype"
  "${MINGW_PACKAGE_PREFIX}-perl-extutils-install"
  "${MINGW_PACKAGE_PREFIX}-perl-extutils-manifest"
)
options=('!emptydirs')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-cc"
  $([[ ${MINGW_PACKAGE_PREFIX} != *-clang-* ]] || \
    echo "${MINGW_PACKAGE_PREFIX}-gcc-compat")
)
license=('perl_5')
source=(
  'https://cpan.metacpan.org/authors/id/L/LE/LEONT/Module-Build-0.4231.tar.gz'
  'patchmakefile.py'
)
sha256sums=('7e0f4c692c1740c1ac84ea14d7ea3d8bc798b2fb26c09877229e04f430b2b717'
            '00fe5ced42aa635714909a8eba1347a6c132011a9bce68e9c75b27ebd64628f2')

build() {
  cd "$srcdir/${_perlname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL
  export pkgdir=$pkgdir
  python $srcdir/patchmakefile.py
  mingw32-make
}

package() {
  cd "$srcdir/${_perlname}-${pkgver}"
  mingw32-make DESTDIR="${pkgdir}/" install
}
