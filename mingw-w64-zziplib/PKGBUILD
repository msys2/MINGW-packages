# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=zziplib
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.13.72
pkgrel=4
pkgdesc="A lightweight library that offers the ability to easily extract data from files archived in a single zip file (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://zziplib.sourceforge.net/"
msys2_repository_url="https://github.com/gdraheim/zziplib"
msys2_references=(
  "cpe: cpe:/a:zziplib:zziplib"
  "cpe: cpe:/a:zziplib_project:zziplib"
)
license=("spdx:LGPL-2.0-or-later OR MPL-1.1")
depends=("${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-python"
             "zip")
optdepends=("${MINGW_PACKAGE_PREFIX}-SDL: SDL_rwops for ZZipLib")
source=("https://github.com/gdraheim/zziplib/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "010-extern.patch"
        "020-cmake-install-pkgconfig.patch::https://github.com/gdraheim/zziplib/commit/eaab833a6017e1faeba6a017c050ac000d54b290.patch"
        "030-cmake-remove-libs-number.patch")
sha256sums=('93ef44bf1f1ea24fc66080426a469df82fa631d13ca3b2e4abaeab89538518dc'
            'ea4a2a6c49e9a71d89ea88e33bf855604d6ea3055f0f9cecde2898a02044811f'
            '8844fae61418eafb843933102752f6761c9465a16ad7f435d467aae476201cda'
            '79d55dbc9b4f4c9f018b8569e14d52ba94087abfa89b8da624707d957ef73042')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -p1 -i "${srcdir}"/010-extern.patch
  patch -p1 -i "${srcdir}"/020-cmake-install-pkgconfig.patch
  patch -p1 -i "${srcdir}"/030-cmake-remove-libs-number.patch
}

build() {
  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  echo "Building static library"
  CFLAGS+=" -Wno-incompatible-function-pointer-types -Wno-incompatible-pointer-types -Wno-int-conversion" \
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake.exe \
      -G'Ninja' \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
      "${_extra_config[@]}" \
      -DBUILD_SHARED_LIBS=OFF \
      -DBUILD_STATIC_LIBS=ON \
      -S ${_realname}-${pkgver} \
      -B build-${MSYSTEM}-static

  ${MINGW_PREFIX}/bin/cmake.exe --build build-${MSYSTEM}-static

  echo "Building shared library"
  CFLAGS+=" -Wno-incompatible-function-pointer-types -Wno-incompatible-pointer-types -Wno-int-conversion" \
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake.exe \
      -G'Ninja' \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
      "${_extra_config[@]}" \
      -DBUILD_SHARED_LIBS=ON \
      -DBUILD_STATIC_LIBS=OFF \
      -S ${_realname}-${pkgver} \
      -B build-${MSYSTEM}-shared

  ${MINGW_PREFIX}/bin/cmake.exe --build build-${MSYSTEM}-shared
}

package() {
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install build-${MSYSTEM}-static

  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install build-${MSYSTEM}-shared

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/docs/COPYING.LIB \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/COPYING.LIB
  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/docs/COPYING.MPL \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/COPYING.MPL
}
