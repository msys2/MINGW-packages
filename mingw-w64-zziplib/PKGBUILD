# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=zziplib
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.13.72
pkgrel=1
pkgdesc="A lightweight library that offers the ability to easily extract data from files archived in a single zip file (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64')
url="https://zziplib.sourceforge.io/"
license=("LGPL, MPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-SDL"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
depends=("${MINGW_PACKAGE_PREFIX}-zlib")
optdepends=("${MINGW_PACKAGE_PREFIX}-SDL: SDL_rwops for ZZipLib")
options=('staticlibs' 'strip')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/gdraheim/zziplib/archive/v${pkgver}.tar.gz"
        "010-extern.patch"
         020-cmake.patch::https://github.com/gdraheim/zziplib/commit/eaab833a6017e1faeba6a017c050ac000d54b290.patch)
sha256sums=('93ef44bf1f1ea24fc66080426a469df82fa631d13ca3b2e4abaeab89538518dc'
            'ea4a2a6c49e9a71d89ea88e33bf855604d6ea3055f0f9cecde2898a02044811f'
            '5e05fd37ac12ef3be88a38a066d70a507a638f16f1703d8ab3b2e9af764c3b55')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -p1 -i ${srcdir}/010-extern.patch
  patch -p1 -i ${srcdir}/020-cmake.patch
}

build() {
  export PYTHON=${MINGW_PREFIX}/bin/python
  export PERL=/usr/bin/perl
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -G'Ninja' \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DBUILD_SHARED_LIBS=ON \
      -DBUILD_STATIC_LIBS=OFF \
      -DBUILD_TESTS=OFF \
      -DMSVC_STATIC_RUNTIME=OFF \
      -DZZIPMMAPPED=ON \
      -DZZIPFSEEKO=ON \
      -DZZIPWRAP=ON \
      -DZZIPSDL=ON \
      -DZZIPBINS=ON \
      -DZZIPTEST=OFF \
      -DZZIPDOCS=ON \
      ../${_realname}-${pkgver}

  ninja
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  DESTDIR="${pkgdir}" ninja install
}
