# Maintainer: @naveen521kk on Github, Naveen M K <naveen521kk@gmail.com>

_perlname=Params-ValidationCompiler
_realname="${_perlname,,}"
pkgbase=mingw-w64-perl-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-perl-${_realname}")
pkgver=0.30
pkgrel=1
pkgdesc="Build an optimized subroutine parameter validator once, use it forever (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://metacpan.org/release/${_perlname}"
groups=("${MINGW_PACKAGE_PREFIX}-perl-modules")
provides=(
  "${MINGW_PACKAGE_PREFIX}-perl-params-validationcompiler-compiler"
  "${MINGW_PACKAGE_PREFIX}-perl-params-validationcompiler-exceptions"
)
depends=(
  "${MINGW_PACKAGE_PREFIX}-perl"
  "${MINGW_PACKAGE_PREFIX}-perl-exception-class"
  "${MINGW_PACKAGE_PREFIX}-perl-exporter"
  "${MINGW_PACKAGE_PREFIX}-perl-eval-closure"
  "${MINGW_PACKAGE_PREFIX}-perl-scalar-util"
  "${MINGW_PACKAGE_PREFIX}-perl-list-util"
  "${MINGW_PACKAGE_PREFIX}-perl-overload"
  "${MINGW_PACKAGE_PREFIX}-perl-b"
  "${MINGW_PACKAGE_PREFIX}-perl-carp"
  "${MINGW_PACKAGE_PREFIX}-perl-warnings"
  "${MINGW_PACKAGE_PREFIX}-perl-strict"
  "${MINGW_PACKAGE_PREFIX}-perl-class-xsaccessor"
  "${MINGW_PACKAGE_PREFIX}-perl-sub-util"
)
options=('!emptydirs')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-cc"
  $([[ ${MINGW_PACKAGE_PREFIX} != *-clang-* ]] || \
    echo "${MINGW_PACKAGE_PREFIX}-gcc-compat")
)
license=('artistic_2')
source=(
  'https://cpan.metacpan.org/authors/id/D/DR/DROLSKY/Params-ValidationCompiler-0.30.tar.gz'
  'patchmakefile.py'
)
sha256sums=('dc5bee23383be42765073db284bed9fbd819d4705ad649c20b644452090d16cb'
            '00fe5ced42aa635714909a8eba1347a6c132011a9bce68e9c75b27ebd64628f2')

build() {
  cd "$srcdir/${_perlname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL
  export pkgdir=$pkgdir
  python $srcdir/patchmakefile.py
  mingw32-make
}

package() {
  cd "$srcdir/${_perlname}-${pkgver}"
  mingw32-make DESTDIR="${pkgdir}/" install
}
