# Maintainer: JÃ¼rgen Pfeifer <juergen@familiepfeifer.de>

_realname=gtkada
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.0
pkgrel=1
pkgdesc="GtkAda is a Gtk3+ binding for Ada using the OOP and other features of this programming language (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64')
conflicts=("${MINGW_PACKAGE_PREFIX}-GtkAda-svn")
url="https://github.com/AdaCore/gtkada"
options=('!staticlibs' 'strip')
msys2_references=(
  'aur: gtkada'
)
license=('spdx:GPL-3.0-or-later WITH GCC-exception-3.1')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc-ada"
             "${MINGW_PACKAGE_PREFIX}-gprbuild-bootstrap"
             "${MINGW_PACKAGE_PREFIX}-autotools")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-ada"
         "${MINGW_PACKAGE_PREFIX}-gtk3")
source=("https://github.com/AdaCore/gtkada/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "gtkada-mingw.diff"
        "space_issue.patch"
        "003-Honour-DESTDIR-in-gprinstall-uninstall.patch")
sha256sums=('50aa5694eebe9c8bb3096651f45355f66859e0b4ab2171af23355af7f205cea2'
            '292eed321ceadd5814f42d0a1483de32ee8c13d5a006b9a8c87d092bd4d993ca'
            'c1d134ea9f91b6720965d8be0516196657f5aab06139f3c601696ed3cd8e7975'
            '915ec7a49db3271f9385317a2a6e214f4dd81bd3b92ded041a8ccf8c98f5ae31')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  # patch -p0 < contrib/gtkada-3-win32.diff # no such file or directory
  # patch -p1 < ${srcdir}/gtkada-mingw.diff
  # patch -p1 < ${srcdir}/space_issue.patch
  patch -p0 < ${srcdir}/003-Honour-DESTDIR-in-gprinstall-uninstall.patch
}

build() {
  [[ -d "${srcdir}"/build-${MSYSTEM} ]] && rm -rf "${srcdir}"/build-${MSYSTEM}
  cp -r "${srcdir}"/${_realname}-${pkgver} "${srcdir}"/build-${MSYSTEM}
  cd "${srcdir}"/build-${MSYSTEM}

  if [ -f Makefile ]; then
    make distclean
  fi
  ../${_realname}-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --enable-shared \
    --disable-static-pic \
    --disable-static \
    --disable-gtktest \
    --with-GL=no

  make
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}
  make -j1 DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}${MINGW_PREFIX}/share/examples
  rm -rf ${pkgdir}${MINGW_PREFIX}/share/doc

  # Copy License Files
  mkdir -p ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}
  cp -pf ${srcdir}/${_realname}-${pkgver}/COPYING* \
    ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}
}
