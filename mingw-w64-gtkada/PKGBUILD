# Maintainer: JÃ¼rgen Pfeifer <juergen@familiepfeifer.de>

_realname=gtkada
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.0
pkgrel=2
pkgdesc="An Ada graphical toolkit based on GTK (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64')
conflicts=("${MINGW_PACKAGE_PREFIX}-GtkAda-svn")
url="https://github.com/AdaCore/gtkada"
msys2_references=(
  'aur: gtkada'
  'gentoo: dev-ada/gtkada'
)
license=('spdx:GPL-3.0-or-later WITH GCC-exception-3.1')
makedepends=("${MINGW_PACKAGE_PREFIX}-gprbuild-bootstrap"
             "${MINGW_PACKAGE_PREFIX}-autotools")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-ada"
         "${MINGW_PACKAGE_PREFIX}-gtk3")
source=("https://github.com/AdaCore/gtkada/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('c75202ae6c37c5556b3f1b2bc4297fe53730d5ede55c91e9f4a11c59c4833844')

build() {
  [[ -d "${srcdir}"/build-${MSYSTEM} ]] && rm -rf "${srcdir}"/build-${MSYSTEM}
  cp -r "${srcdir}"/${_realname}-${pkgver} "${srcdir}"/build-${MSYSTEM}
  cd "${srcdir}"/build-${MSYSTEM}

  if [ -f Makefile ]; then
    make distclean
  fi
  ../${_realname}-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --enable-shared \
    --disable-static \
    --disable-gtktest \
    --with-GL=no

  make -j1
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}
  make -j1 DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}${MINGW_PREFIX}/share/examples
  rm -rf ${pkgdir}${MINGW_PREFIX}/share/doc

  # Copy License Files
  mkdir -p ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}
  cp -pf ${srcdir}/${_realname}-${pkgver}/COPYING* \
    ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}
}
