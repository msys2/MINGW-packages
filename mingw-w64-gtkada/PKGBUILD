# Maintainer: JÃ¼rgen Pfeifer <juergen@familiepfeifer.de>

_realname=gtkada
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=3.8.2
pkgrel=2
pkgdesc="GtkAda is a Gtk3+ binding for Ada using the OOP and other features of this programming language"
arch=('any')
conflicts=("${MINGW_PACKAGE_PREFIX}-GtkAda-svn")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
url="https://libre.adacore.com/tools/gtkada/"
options=('!staticlibs' 'strip')
license=('GPL3' 'custom')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc-ada")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-ada"
         "${MINGW_PACKAGE_PREFIX}-gtk3")
source=("http://www.pfeifers-software.de/files/GNAT/${_realname}-gpl-${pkgver}-src.tgz"
        "gtkada-mingw.diff"
        "space_issue.patch")
sha256sums=('1c0062e1ec80225d6d39f85e664a84e8be95196d8348acee2dc18e0c6e296ec5'
            '4288a6b325b907a4a05b3d0cd7a67297ad260881b7cd00f58ac2a112c5dc5886'
            'c1d134ea9f91b6720965d8be0516196657f5aab06139f3c601696ed3cd8e7975')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}-src
  patch -p0 < contrib/gtkada-3-win32.diff
  patch -p1 < ${srcdir}/gtkada-mingw.diff
  patch -p1 < ${srcdir}/space_issue.patch
}

build() {
  cd ${srcdir}/${_realname}-${pkgver}-src
  if [ -f Makefile ]; then
    make distclean
  fi
  ./configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --enable-shared \
    --disable-static \
    --with-GL=no

    make -j1
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}-src
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}${MINGW_PREFIX}/share/examples
  rm -rf ${pkgdir}${MINGW_PREFIX}/share/doc

  # Copy License Files
  mkdir -p ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}
  cp -pf ${srcdir}/${_realname}-${pkgver}-src/COPYING* \
    ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}
}
