# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>

_realname=botocore
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.29.58
pkgrel=1
pkgdesc='A low-level interface to a growing number of Amazon Web Services (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://github.com/boto/botocore"
license=('spdx:Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-python-dateutil"
         "${MINGW_PACKAGE_PREFIX}-python-docutils"
         "${MINGW_PACKAGE_PREFIX}-python-jmespath"
         "${MINGW_PACKAGE_PREFIX}-python-urllib3")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools")
source=("$url/archive/$pkgver.tar.gz")
sha256sums=('dd24bf0ceed866b7daf24a8e8cc82362626688c21ef56f49e1b1b135b855f8b8')

prepare() {
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}"
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE.txt"
}
