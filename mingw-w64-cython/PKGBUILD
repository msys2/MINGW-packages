# Contributor: Runar Tenfjord < runar dot tenfjord at gmail dot com >
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=cython
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-cython")
pkgver=0.29.14
pkgrel=2
pkgdesc="C-Extensions for Python (mingw-w64)"
arch=('any')
url="https://cython.org/"
license=('APACHE')
depends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
install=${_realname}-${CARCH}.install
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/cython/cython/archive/${pkgver}.tar.gz")
sha256sums=('6a3370fda27c020c172805c6257f222624bdd5f73653bb93f4af4e9f282d17f7')

prepare() {
  cd "${srcdir}"
  rm -rf python-build-${CARCH}| true
  cp -r "${_realname}-${pkgver}" "python-build-${CARCH}"
  sed -e "s|\/usr\/bin\/python|${MINGW_PREFIX}\/bin\/python|" -i python-build-${CARCH}/setup.py
}

build() {
  cd "${srcdir}/python-build-${CARCH}"
  DISTUTILS_DEBUG=1 \
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  DISTUTILS_DEBUG=1 \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
                              --root="${pkgdir}" --optimize=1 --skip-build

  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  # fix python command in files
  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*.py; do
    sed -e "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" -i ${_f}
  done
}
