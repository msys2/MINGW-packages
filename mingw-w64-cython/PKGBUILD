# Contributor: Runar Tenfjord < runar dot tenfjord at gmail dot com >
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=cython
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-cython" "${MINGW_PACKAGE_PREFIX}-cython2")
pkgver=0.25.2
pkgrel=2
pkgdesc="C-Extensions for Python (mingw-w64)"
arch=('any')
url="http://www.cython.org"
license=('APACHE')
makedepends=(
            "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
            "${MINGW_PACKAGE_PREFIX}-python2-setuptools"
            "${MINGW_PACKAGE_PREFIX}-gcc"
            )
_dtoken="b7/67/7e2a817f9e9c773ee3995c1e15204f5d01c8da71882016cac10342ef031b"
source=("https://pypi.python.org/packages/${_dtoken}/Cython-${pkgver}.tar.gz")
sha256sums=('f141d1f9c27a07b5a93f7dc5339472067e2d7140d1c5a9e20112a5665ca60306')

prepare() {
  cd "${srcdir}"
  for pver in {2,3}; do 
    rm -rf python${pver}-build-${CARCH}| true
    cp -r "${_realname}-${pkgver}" "python${pver}-build-${CARCH}"
    sed -e "s|\/usr\/bin\/python|${MINGW_PREFIX}\/bin\/python${pver}|" -i python${pver}-build-${CARCH}/setup.py
  done
}

package_cython() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools")
  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  DISTUTILS_DEBUG=1 \
    ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
                                --root="${pkgdir}" --optimize=1

  # Our setuptools {gui,cli}{,-32,-64}.exe have special-case handling for /usr/bin/env such that
  # they look in PATH.
  sed -i 's|#!.*|#!/usr/bin/env python3|' ${pkgdir}/${MINGW_PREFIX}/bin/cy{gdb,thon}-script.py
}

package_cython2() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2-setuptools")
  cd "${srcdir}/python2-build-${CARCH}"
  DISTUTILS_DEBUG=1 \
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
                                --root="${pkgdir}" --optimize=1

  # Our setuptools {gui,cli}{,-32,-64}.exe have special-case handling for /usr/bin/env such that
  # they look in PATH.
  sed -i 's|#!.*|#!/usr/bin/env python2|' ${pkgdir}/${MINGW_PREFIX}/bin/cy{gdb,thon}-script.py

  mv ${pkgdir}${MINGW_PREFIX}/bin/cygdb{,2}.exe
  mv ${pkgdir}${MINGW_PREFIX}/bin/cython{,2}.exe
  mv ${pkgdir}${MINGW_PREFIX}/bin/cythonize{,2}.exe
  if [ -f ${pkgdir}${MINGW_PREFIX}/bin/cygdb.exe.manifest ]; then
    mv ${pkgdir}${MINGW_PREFIX}/bin/cygdb{,2}.exe.manifest
    mv ${pkgdir}${MINGW_PREFIX}/bin/cython{,2}.exe.manifest
    mv ${pkgdir}${MINGW_PREFIX}/bin/cythonize{,2}.exe.manifest
  fi

  mv ${pkgdir}${MINGW_PREFIX}/bin/cygdb{,2}-script.py
  mv ${pkgdir}${MINGW_PREFIX}/bin/cython{,2}-script.py
  mv ${pkgdir}${MINGW_PREFIX}/bin/cythonize{,2}-script.py
}

package_mingw-w64-i686-cython() {
  package_cython
}

package_mingw-w64-i686-cython2() {
  package_cython2
}

package_mingw-w64-x86_64-cython() {
  package_cython
}

package_mingw-w64-x86_64-cython2() {
  package_cython2
}
