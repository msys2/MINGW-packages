# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libssh
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.7.3
pkgrel=1
pkgdesc="Library for accessing ssh client services through C libraries (mingw-w64)"
arch=('any')
url="http://www.libssh.org/"
license=("LGPL")
install=libssh-${CARCH}.install
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-cmake")
depends=("${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' 'strip')
validpgpkeys=('8DFF53E18F2ABC8D8F3C92237EE0FC4DCC014E3D') # Andreas Schneider <asn@cryptomilk.org>
source=(https://red.libssh.org/attachments/download/195/${_realname}-${pkgver}.tar.xz
        https://red.libssh.org/attachments/download/194/${_realname}-${pkgver}.tar.asc
        mingw-as-unix.patch
        mingw-DATADIR-conflict.patch
        fix-threads-libraryname-in-config.patch)
sha256sums=('26ef46be555da21112c01e4b9f5e3abba9194485c8822ab55ba3d6496222af98'
            'SKIP'
            'd0790ab4e47d90b7fa5a15a2b08430b65db72a8d88fac8f5883e48a7fdc6d6c4'
            '444a66b1926f49c54df844c22263496c2d86e124c8bcdbd194c4581d06140c1b'
            'a69b444d4a9b1861991661d5f3ca8d2ab705fac1f5766a79730328b0bdd86691')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/mingw-as-unix.patch
  patch -p1 -i ${srcdir}/mingw-DATADIR-conflict.patch
  patch -p1 -i ${srcdir}/fix-threads-libraryname-in-config.patch
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_STATIC_LIB=ON \
    ../${_realname}-${pkgver}

  make all
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install
}
