# Maintainer: Martell Malone <martellmalone@gmail.com>

_realname=libconfig
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.5
pkgrel=1
pkgdesc="C/C++ Configuration File Library (mingw-w64)"
arch=('any')
url="https://www.hyperrealm.com/libconfig/libconfig.html"
license=('LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('!libtool' '!emptydirs')
source=("https://www.hyperrealm.com/libconfig/packages/${_realname}-${pkgver}.tar.gz")
sha256sums=('e31daa390d8e4461c8830512fe2e13ba1a3d6a02a2305a02429eec61e68703f6')

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --enable-static

  make lib
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}/lib"
  make DESTDIR="${pkgdir}" install
}
