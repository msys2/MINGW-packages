# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=yelp-tools
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=3.32.1
pkgrel=1
pkgdesc="Tools for creating Yelp documentation"
arch=('any')
license=('GPL')
url="https://www.gnome.org"
depends=("intltool"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-libxslt" 
         "${MINGW_PACKAGE_PREFIX}-itstool"
         "${MINGW_PACKAGE_PREFIX}-python3-mallard-ducktype")
options=('strip' '!libtool' 'staticlibs')
source=(https://download.gnome.org/sources/${_realname}/${pkgver:0:4}/${_realname}-$pkgver.tar.xz
        yelp-no-symlinks.patch)
sha256sums=('99a7c312a5fcb427870dc198af02801eb0f8ea63317e20110fc0303eb44636e3'
            'f5cdcdc450d2689196e37855f1fa3bcd2b4459563b1bf147c8b01e7e8b735979')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/yelp-no-symlinks.patch
  autoreconf -ivf
}

build() {
  export PYTHON=${MINGW_PREFIX}/bin/python3
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}
  ${srcdir}/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${CHOST} \
    --host=${CHOST} \
    --sysconfdir=${MINGW_PREFIX}/etc \
    --localstatedir=${MINGW_PREFIX}/var

  make
}

check() {
  cd ${srcdir}/build-${CARCH}
  make -k check
}

package() {
  cd ${srcdir}/build-${CARCH}
  make DESTDIR="${pkgdir}" install
  local WIN_PREFIX="$(cygpath -m /)"
  for ff in yelp-build yelp-check yelp-new; do
    sed -e "s| xsltproc| ${MINGW_PREFIX}/bin/xsltproc|g" \
     -e "s|${WIN_PREFIX}|${MINGW_PREFIX}|g" \
     -i ${pkgdir}/${MINGW_PREFIX}/bin/$ff
  done
  for ff in mal-ncx mal-opf; do
    sed -e "s|${WIN_PREFIX}|${MINGW_PREFIX}|g" \
     -i ${pkgdir}${MINGW_PREFIX}/share/yelp-tools/xslt/$ff.xsl
  done
}
