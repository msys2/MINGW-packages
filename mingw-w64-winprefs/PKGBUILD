# Maintainer: Andrew Udvare <audvare@gmail.com>

_realname=winprefs
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.4.0
pkgrel=1
pkgdesc="Tool to export registry paths to script and code formats (native component only)."
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}" "${MINGW_PACKAGE_PREFIX}-${_realname}w")
msys2_references=()
license=('spdx:MIT')
url="https://github.com/Tatsh/winprefs"
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-cmocka"
             "${MINGW_PACKAGE_PREFIX}-ninja")
source=("https://github.com/Tatsh/${_realname}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('0e8a957c1f68fcf296943ec235ca198487e3f6010f06c55bd1274e612ad51b26')

build() {
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -G Ninja \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -S "${_realname}-${pkgver}" \
    -B "build-${MSYSTEM}"

  cmake --build "build-${MSYSTEM}"
}

check() {
  cmake -DBUILD_TESTS=ON -S ../${_realname}-${pkgver} -B "build-${MSYSTEM}" -DCMAKE_BUILD_TYPE=Debug
  cmake --build "build-${MSYSTEM}-Debug" --config Debug
  ctest --test-dir "build-${MSYSTEM}-Debug" --output-on-failure || true
}

package() {
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}/bin/cmake" --install "build-${MSYSTEM}" --config Release
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE.txt" \
    "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.txt"
}
