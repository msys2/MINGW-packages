# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=field3d
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.7.2
pkgrel=4
pkgdesc="Open source library for storing voxel data (mingw-w64)"
arch=('any')
url="https://sites.google.com/site/field3d/"
license=("custom")
depends=("${MINGW_PACKAGE_PREFIX}-boost"
         "${MINGW_PACKAGE_PREFIX}-hdf5"
         "${MINGW_PACKAGE_PREFIX}-openexr")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-doxygen"
             "${MINGW_PACKAGE_PREFIX}-gcc")
options=('strip' 'buildflags')
source=(${_realname}-${pkgver}.tar.gz::https://github.com/imageworks/Field3D/archive/v${pkgver}.tar.gz
        cmake-mingw.patch
        win32-fix-exported-symbols.patch)
sha256sums=('8f7c33ecb4489ed626455cf3998d911a079b4f137f86814d9c37c5765bf4b020'
            'e1f1d529a2d8ef3dd64b9149b6728a0001aad4469e324c9135c53795e82cb9d8'
            '087a28c98a48f0ea1451b1a1d34e5fc102caecb45cb1219f7d65df5b18015b26')

prepare() {
  cd ${srcdir}/Field3D-${pkgver}
  patch -p1 -i ${srcdir}/cmake-mingw.patch
  patch -p1 -i ${srcdir}/win32-fix-exported-symbols.patch
}

build() {
  cd ${srcdir}
  [[ -d shared-${MINGW_CHOST} ]] && rm -r shared-${MINGW_CHOST}
  mkdir shared-${MINGW_CHOST} && cd shared-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DBUILD_SHARED_LIBS=ON \
    ../Field3D-${pkgver}
  make
  
  cd ${srcdir}
  [[ -d static-${MINGW_CHOST} ]] && rm -r static-${MINGW_CHOST}
  mkdir static-${MINGW_CHOST} && cd static-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DBUILD_SHARED_LIBS=OFF \
    ../Field3D-${pkgver}
  make
}

package() {
  cd "${srcdir}"/shared-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install
  
  cd "${srcdir}"/static-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install
  
  install -Dm644 "${srcdir}/Field3D-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
