# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=ghostscript
_tarname=ghostpdl
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}" "${MINGW_PACKAGE_PREFIX}-ghostxps" "${MINGW_PACKAGE_PREFIX}-ghostpcl") 
pkgver=9.26
pkgrel=2
arch=('any')
license=('AGPL' 'custom')
pkgdesc="An interpreter for the PostScript language (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-dbus"
         "${MINGW_PACKAGE_PREFIX}-freetype"
         "${MINGW_PACKAGE_PREFIX}-fontconfig"
         "${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2"
         "${MINGW_PACKAGE_PREFIX}-gsfonts"
         "${MINGW_PACKAGE_PREFIX}-jbig2dec"
         "${MINGW_PACKAGE_PREFIX}-libiconv"
         "${MINGW_PACKAGE_PREFIX}-libidn"
         "${MINGW_PACKAGE_PREFIX}-libpaper"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libjpeg"
         "${MINGW_PACKAGE_PREFIX}-libtiff"
         "${MINGW_PACKAGE_PREFIX}-lcms2"
         "${MINGW_PACKAGE_PREFIX}-openjpeg2"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-gtk3" "${MINGW_PACKAGE_PREFIX}-gnutls")
optdepends=("${MINGW_PACKAGE_PREFIX}-gtk3: needed for gsx")
url="https://www.ghostscript.com/"
source=(https://github.com/ArtifexSoftware/ghostpdl-downloads/releases/download/gs${pkgver/./}/${_tarname}-${pkgver}.tar.gz
        001-mingw-build.patch
        002-ghostscript-sys-zlib.patch
        003-libspectre.patch
        mingw-pcl.patch
        mingw-xps.patch)
options=('staticlibs' 'strip')
sha256sums=('b0d4916b669a63ed861f563c4607cc1230949c53431ab0798db8e680fdacc777'
            '8855fd391358c97c83a7166803fd95f87fa4df790c6125965bd7c37920cb9579'
            '9cf9805e6b8329e83656c16475c9fc7b7b8c3e459a782b4755abdabbb8f22df4'
            '5b0ef04d59658885f183a6c820a5dbbb89e57e4c43774bc6c90ae4fb742b2be7'
            'SKIP'
            'SKIP')
noextract=(${_tarname}-${pkgver}.tar.gz)

prepare() {
  [[ -d ${srcdir}/${_tarname}-${pkgver} ]] && rm -rf ${srcdir}/${_tarname}-${pkgver}
  tar -xzf ${srcdir}/${_tarname}-${pkgver}.tar.gz -C ${srcdir} || true

  cd "${srcdir}/${_tarname}-${pkgver}"
  patch -p1 -i ${srcdir}/001-mingw-build.patch
  patch -p1 -i ${srcdir}/002-ghostscript-sys-zlib.patch
  patch -p1 -i ${srcdir}/003-libspectre.patch

  patch -p1 -i ${srcdir}/mingw-pcl.patch
  patch -p1 -i ${srcdir}/mingw-xps.patch
  sed -e "s|/usr|${MINGW_PREFIX}/${MINGW_CHOST}|g" -i base/all-arch.mak base/unix-aux.mak

  # force it to use system-libs
  # openjpeg use included and also use libcms2mt
#  rm -rf freetype jpeg lcms2 libpng openjpeg tiff zlib
   rm -r cups/libs expat ijs jbig2dec jpeg libpng openjpeg tiff zlib

  #cd base
  libtoolize --force --copy --install
  autoconf -f -i
}

build()
{
  cd ${srcdir}/${_tarname}-${pkgver}
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --disable-contrib \
    --enable-fontconfig \
    --with-fontpath=${MINGW_PREFIX}/share/fonts/gsfonts \
    --enable-dbus \
    --enable-freetype \
    --disable-cups \
    --enable-openjpeg \
    --enable-gtk \
    --with-libiconv=gnu \
    --with-libidn \
    --with-libpaper \
    --with-system-libtiff \
    --with-jbig2dec \
    --without-x \
    --with-drivers=ALL

  make so-only
  #make gs.a
}

package_ghostscript() {
  cd ${srcdir}/${_tarname}-${pkgver}
  make DESTDIR="${pkgdir}" soinstall

  #install -D -m644 "${srcdir}"/${_realname}-${pkgver}/gs.a "${pkgdir}"/${MINGW_PREFIX}/lib/libgs.a

  mkdir -p "${pkgdir}${MINGW_PREFIX}"/share/${_realname}/${pkgver}/
  cp -rf "${srcdir}"/${_tarname}-${pkgver}/doc "${pkgdir}${MINGW_PREFIX}"/share/${_realname}/${pkgver}/

  install -D -m644 "${srcdir}"/${_tarname}-${pkgver}/LICENSE "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSE

  cp ${pkgdir}${MINGW_PREFIX}/bin/{gsc,gswin32c}.exe
  cp ${pkgdir}${MINGW_PREFIX}/bin/{gsc,gs}.exe

  # remove unwanted localized man-pages
  rm -rf "${pkgdir}${MINGW_PREFIX}"/share/man/[^man1]*
}

package_ghostxps() {
  pkgdesc="${pkgdesc/PostScript/XPS document}"
  depends=("${MINGW_PACKAGE_PREFIX}-ghostscript=${pkgver}-${pkgrel}")

  cd ${_tarname}-${pkgver}

  install -Dt ${pkgdir}${MINGW_PREFIX}/bin sobin/gxpsc.exe
  cp ${pkgdir}${MINGW_PREFIX}/bin/{gxpsc,gxpswin32c}.exe
  cp ${pkgdir}${MINGW_PREFIX}/bin/{gxpsc,gxps}.exe

  install -Dt ${pkgdir}${MINGW_PREFIX}/bin sobin/libgxps-${pkgver%.*}.dll
  install -Dt ${pkgdir}${MINGW_PREFIX}/lib sobin/libgxps.dll.a

  install -Dt ${pkgdir}${MINGW_PREFIX}/share/licenses/ghostxps -m644 LICENSE
}

package_ghostpcl() {
  pkgdesc="${pkgdesc/PostScript/PCL 6}"
  depends=("${MINGW_PACKAGE_PREFIX}-ghostscript=${pkgver}-${pkgrel}")

  cd ${_tarname}-${pkgver}

  install -Dt ${pkgdir}${MINGW_PREFIX}/bin sobin/gpcl6c.exe
  cp ${pkgdir}${MINGW_PREFIX}/bin/{gpcl6c,gpcl6win32c}.exe
  cp ${pkgdir}${MINGW_PREFIX}/bin/{gpcl6c,gpcl6}.exe

  install -Dt ${pkgdir}${MINGW_PREFIX}/bin sobin/libgpcl6-${pkgver%.*}.dll
  install -Dt ${pkgdir}${MINGW_PREFIX}/lib sobin/libgpcl6.dll.a

  install -Dt ${pkgdir}${MINGW_PREFIX}/share/licenses/ghostpcl -m644 LICENSE
}

package_mingw-w64-i686-ghostscript() {
  package_ghostscript
}

package_mingw-w64-i686-ghostxps() {
  package_ghostxps
}

package_mingw-w64-i686-ghostpcl() {
  package_ghostpcl
}

package_mingw-w64-x86_64-ghostscript() {
  package_ghostscript
}

package_mingw-w64-x86_64-ghostxps() {
  package_ghostxps
}

package_mingw-w64-x86_64-ghostpcl() {
  package_ghostpcl
}

