# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=eog
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.14.3
pkgrel=1
arch=('any')
pkgdesc="Eye of GNOME graphics viewer program (mingw-w64)"
options=(strip staticlibs)
depends=("${MINGW_PACKAGE_PREFIX}-gtk3"
         "${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2"
         "${MINGW_PACKAGE_PREFIX}-shared-mime-info"
         "${MINGW_PACKAGE_PREFIX}-libpeas"
         "${MINGW_PACKAGE_PREFIX}-gsettings-desktop-schemas"
         "${MINGW_PACKAGE_PREFIX}-adwaita-icon-theme"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-libexif"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-librsvg"
         "${MINGW_PACKAGE_PREFIX}-gettext"
         "${MINGW_PACKAGE_PREFIX}-gobject-introspection")
options=('strip' 'staticlibs')
license=("GPL 2")
url="http://www.gnome.org"
install=eog-${CARCH}.install
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver%.*}/${_realname}-$pkgver.tar.xz
        001-win.patch
        002-relocation-support.patch
        003-fix-install-dirs.patch
        004-give-cc-to-gir-scanner.mingw.patch)
sha256sums=('c05a3c0a75c3c5d42f41eadca9e23753d1fa59bc37fb5826909b468b9be1cf59'
            '1309ea222055d951fbe5c1c9be0245adecdfe221ee9ce0b2cef8fab3597d3864'
            '2a1b947202e765011ee37c40786baee96c04e23962a481bcee36757cabc3c669'
            'abc9df56f5fc2213ba6efdcdad269534496d6eda900194b4000910dacf4042d9'
            '8fd79bb5f0e411d682abd7cffa3b9b8476cf8a073ea17c2e0bd1a386e3b060e0')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  patch -p1 -i ${srcdir}/001-win.patch
  patch -p1 -i ${srcdir}/002-relocation-support.patch
  patch -p1 -i ${srcdir}/003-fix-install-dirs.patch
  patch -p1 -i ${srcdir}/004-give-cc-to-gir-scanner.mingw.patch
  autoreconf -isf
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --enable-introspection \
    --without-cms \
    --without-x

  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install
}
