# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=eog
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=49.1
pkgrel=1
pkgdesc="Eye of GNOME graphics viewer program (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://wiki.gnome.org/Apps/EyeOfGnome"
msys2_repository_url="https://gitlab.gnome.org/GNOME/eog"
msys2_references=(
  "cpe: cpe:/a:gnome:eog"
)
license=("spdx:GPL-2.0-or-later")
depends=("${MINGW_PACKAGE_PREFIX}-adwaita-icon-theme"
         "${MINGW_PACKAGE_PREFIX}-cairo"
         "${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2"
         "${MINGW_PACKAGE_PREFIX}-gettext-runtime"
         "${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-gobject-introspection-runtime"
         "${MINGW_PACKAGE_PREFIX}-gsettings-desktop-schemas"
         "${MINGW_PACKAGE_PREFIX}-gtk-update-icon-cache"
         "${MINGW_PACKAGE_PREFIX}-gtk3"
         "${MINGW_PACKAGE_PREFIX}-libexif"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-libpeas"
         "${MINGW_PACKAGE_PREFIX}-librsvg"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-libhandy"
         "${MINGW_PACKAGE_PREFIX}-shared-mime-info"
         "${MINGW_PACKAGE_PREFIX}-zlib")
optdepends=("${MINGW_PACKAGE_PREFIX}-libavif: Load .avif"
            "${MINGW_PACKAGE_PREFIX}-libheif: Load .heif, .heic, and .avif"
            "${MINGW_PACKAGE_PREFIX}-libjxl: Load .jxl"
            "${MINGW_PACKAGE_PREFIX}-webp-pixbuf-loader: Load .webp")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-gettext-tools"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             "${MINGW_PACKAGE_PREFIX}-gtk-doc"
             "${MINGW_PACKAGE_PREFIX}-yelp-tools")
source=(https://download.gnome.org/sources/${_realname}/${pkgver%.*}/${_realname}-$pkgver.tar.xz
        002-relocation-support.patch
        win.patch
        003-gui-app.patch
        004-symbol-vis.patch)
sha256sums=('fadd12e14ba9cd2be6989dfbcef40001645b63440ceed8e04867b07587b066dc'
            '359e034b352a2cb2277a7444dc925b578260c0fed32159df412a3681049d947b'
            '866513d8a7a7c0db63f4cefadba4ae905d4e9b3e8367f2dc41f7be036ccba3b6'
            '0569f7d5273c03d03095f455fa16974bf7a7cb83e2218190514530007bfc7b69'
            '5466698c4b9f2de9530b64ffb2f090b1d9dee8e0b577e8fce75a5d21211aa0cf')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  patch -p1 -i "${srcdir}"/002-relocation-support.patch
  patch -p1 -i "${srcdir}"/win.patch
  patch -p1 -i "${srcdir}"/003-gui-app.patch
  patch -p1 -i "${srcdir}"/004-symbol-vis.patch
}

build() {
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  MSYS2_ARG_CONV_EXCL="--prefix=" \
  meson setup \
    --prefix=${MINGW_PREFIX} \
    --buildtype plain \
    -Dxmp=false \
    -Dcms=false \
    -Dlibportal=false \
    -Dintrospection=true \
    ../${_realname}-${pkgver}

  meson compile
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" meson install

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/COPYING \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
