# Contributor: Fabien Chouteau <chouteau@adacore.com>

_realname=gnatcoll-iconv
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.0
pkgrel=1

pkgdesc="GNAT Components Collection - Interface to libiconv (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64')
replaces=("${MINGW_PACKAGE_PREFIX}-gnatcoll")
conflicts=("${MINGW_PACKAGE_PREFIX}-gnatcoll")
url="https://github.com/AdaCore/gnatcoll-bindings"
msys2_references=(
  'aur: gnatcoll-iconv'
  'gentoo: dev-ada/gnatcoll-bindings'
)
license=('spdx:GPL-3.0-or-later WITH GCC-exception-3.1')
makedepends=("${MINGW_PACKAGE_PREFIX}-gprbuild"
             "${MINGW_PACKAGE_PREFIX}-python3")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gcc-ada"
         "${MINGW_PACKAGE_PREFIX}-gnatcoll-core"
         "${MINGW_PACKAGE_PREFIX}-libiconv")
source=("https://github.com/AdaCore/gnatcoll-bindings/archive/v${pkgver}/gnatcoll-bindings.tar.gz"
        'iconv.gpr')
sha256sums=('d123733ca9a9206572e77bfbffbe0cd50e96ea8f8e276055048dc209e5327791'
            'c0951f33fcc65ee9a333621135941e5ffee7884445e943538ec744762d628f26')
options=('strip')

prepare() {
  echo "${pkgver}" > "${srcdir}/gnatcoll-bindings-${pkgver}/version_information"
}

build() {
  cd "${srcdir}/gnatcoll-bindings-${pkgver}/iconv"
  ${MINGW_PREFIX}/bin/python3 setup.py build --jobs="$(nproc)" --reconfigure
}

package() {
  cd "${srcdir}/gnatcoll-bindings-${pkgver}/iconv"
  python3 setup.py install --prefix="${pkgdir}${MINGW_PREFIX}"

  # In GNAT Community gnatcoll_iconv is built with libiconv embedded in
  # libgnatcoll_iconv.dll. This is not what we want here, so we install
  # an iconv.gpr with Externally_Built attribute.

  # Copy external gpr file
  cp -pf "${srcdir}/iconv.gpr" "${pkgdir}${MINGW_PREFIX}/share/gpr/"

  # Add with for external gpr files
  sed -i 's/with "gnatcoll";/with "gnatcoll"; with "iconv";/' "${pkgdir}${MINGW_PREFIX}/share/gpr/gnatcoll_iconv.gpr"

  # Copy License Files
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" \
    "${srcdir}/gnatcoll-bindings-${pkgver}"/COPYING*
}
