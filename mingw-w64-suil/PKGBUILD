
_realname=suil
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.10.24
pkgrel=1
pkgdesc="A lightweight C library for loading and wrapping LV2 plugin UIs (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://drobilla.net/software/suil.html"
msys2_repository_url="https://gitlab.com/lv2/suil/"
msys2_changelog_url='https://gitlab.com/lv2/suil/-/blob/main/NEWS'
msys2_documentation_url="https://drobilla.net/docs/suil/"
msys2_references=(
  'archlinux: suil'
)
license=("spdx:0BSD OR ISC")
depends=("${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-gtk2"
         "${MINGW_PACKAGE_PREFIX}-lv2")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-meson"
  "${MINGW_PACKAGE_PREFIX}-pkgconf"
  # The project doesn't support gtk3, qt5 or qt6 on Windows
  #"${MINGW_PACKAGE_PREFIX}-gtk3"
  #"${MINGW_PACKAGE_PREFIX}-qt5-base"
  #"${MINGW_PACKAGE_PREFIX}-qt6-base"
)
#optdepends=(
#  "${MINGW_PACKAGE_PREFIX}-gtk3: Support for wrapping Qt5 in GTK3"
#  "${MINGW_PACKAGE_PREFIX}-qt5-base: support for wrapping Qt5 in GTK3 and GTK2"
#  "${MINGW_PACKAGE_PREFIX}-qt6-base: support for wrapping Qt6 in GTK3 and GTK2"
#)
source=("https://download.drobilla.net/${_realname}-${pkgver}.tar.xz"{,.sig}
        "001-no-x11-qt6-on-windows.patch")
sha256sums=('0d15d407c8b1010484626cb63b3317ba0a012edf3308b66b0f7aa389bd99603b'
            'SKIP'
            'df8940d58b3f6e7f3c7a6b82e40d40d0f324b4f3c1b023014fa504cf66e26c67')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3') # David Robillard <d@drobilla.net>

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/001-no-x11-qt6-on-windows.patch
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
  ${MINGW_PREFIX}/bin/meson.exe setup \
    --buildtype=plain \
    --auto-features=enabled \
    --prefix=${MINGW_PREFIX} \
    -Dcocoa=disabled \
    -Dgtk3=disabled \
    -Dqt5=disabled \
    -Dqt6=disabled \
    -Dx11=disabled \
    -Ddocs=disabled \
    ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/meson.exe compile
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/meson.exe install --destdir="${pkgdir}"

  install -vDm 644 "${srcdir}"/${_realname}-${pkgver}/COPYING -t "${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname/"
  install -vDm 644 "${srcdir}"/${_realname}-${pkgver}/{AUTHORS,NEWS,README.md} -t "${pkgdir}${MINGW_PREFIX}/share/doc/$_realname/"
}
