# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=mujs
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3.8
pkgrel=1
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'archlinux: mujs'
  "cpe: cpe:/a:artifex:mujs"
)
msys2_repository_url='https://cgit.ghostscript.com/cgi-bin/cgit.cgi/mujs.git/'
url="https://mujs.com/"
license=('spdx:ISC')
pkgdesc='A lightweight Javascript interpreter designed for embedding in other software (mingw-w64)'
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-readline")
source=("https://mujs.com/downloads/${_realname}-${pkgver}.tar.gz"
        "https://raw.githubusercontent.com/unicode-org/unicodetools/refs/heads/main/unicodetools/data/ucd/16.0.0/UnicodeData.txt"
        "https://raw.githubusercontent.com/unicode-org/unicodetools/refs/heads/main/unicodetools/data/ucd/16.0.0/SpecialCasing.txt"
        "001-makefile-fix.patch")
sha256sums=('506d34882f2620a2fdeb6db63dbb7a8ffd98f417689d8f3c84f2feac275e39a9'
            'ff58e5823bd095166564a006e47d111130813dcf8bf234ef79fa51a870edb48f'
            '8d5de354eef79f2395a54c9c7dcebbaf3d30fc962d0f85611ea97aa973a0c451'
            'f4020f8a0f3b5d3bcfcb27d5ed9df8b8bba631f60deb2e35e2426ef74e9cc545')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ../001-makefile-fix.patch

  # Avoid downloading during the build; also the download is unreliable from GHA.
  cp ../UnicodeData.txt .
  cp ../SpecialCasing.txt .
}

build() {
  cd ${_realname}-${pkgver}
  make prefix=${MINGW_PREFIX} release
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" prefix=${MINGW_PREFIX} install

  install -Dm644 COPYING $pkgdir${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
  install -Dm644 README $pkgdir${MINGW_PREFIX}/share/doc/${_realname}/README
  cd docs/; for i in *; do
    install -Dm644 $i "${pkgdir}"${MINGW_PREFIX}/share/doc/${_realname}/$i
  done
}
