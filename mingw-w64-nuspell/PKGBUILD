_realname=nuspell
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=5.0.0
pkgrel=1
pkgdesc="Fast and safe spellchecking C++ library (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://nuspell.github.io/"
license=(LGPL3+)
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-catch")
depends=("${MINGW_PACKAGE_PREFIX}-icu")
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/nuspell/nuspell/archive/v${pkgver}.tar.gz")
sha256sums=('855d4771d225dcce1e48d098be6a2d69629c635b79b53f9e095a35adc68f0ea1')
sha512sums=('92605c5f0d413b86711fea3b993d656552d5e72440303d3e71907a503d060ead032dfeb0dd8dbb790f35599b8cca8854bdd8650f0560793d9c62a11aebccc2e5')

build() {
  local build_dir="${srcdir}/build-${MINGW_CHOST}"
  [[ -d "$build_dir" ]] && rm -rf "$build_dir"
  mkdir "$build_dir" && cd "$build_dir"
  
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe ../"${_realname}-${pkgver}" \
    -G "MSYS Makefiles" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}"

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  ctest
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install

  # Licenses
  cd "${srcdir}/${_realname}-${pkgver}"
  install -Dm644 COPYING      "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
  install -Dm644 COPYING.LESSER "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING.LESSER"
}
