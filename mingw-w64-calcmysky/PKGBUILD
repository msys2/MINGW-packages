
_realname=calcmysky
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.3.1
pkgrel=1
pkgdesc="Planetary atmosphere light scattering simulator"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://10110111.github.io/CalcMySky"
license=("spdx:GPL-3.0-only")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-doxygen"
  "${MINGW_PACKAGE_PREFIX}-glslang"
  "${MINGW_PACKAGE_PREFIX}-ninja"
  "${MINGW_PACKAGE_PREFIX}-qt6-tools"
  "${MINGW_PACKAGE_PREFIX}-shaderc"
)
depends=(
  "${MINGW_PACKAGE_PREFIX}-eigen3"
  "${MINGW_PACKAGE_PREFIX}-glm"
  "${MINGW_PACKAGE_PREFIX}-qt6-base"
  "${MINGW_PACKAGE_PREFIX}-vulkan-loader"
)
source=("https://github.com/10110111/CalcMySky/archive/refs/tags/v${pkgver}.tar.gz"
        "001-mingw.patch::https://github.com/10110111/CalcMySky/pull/17.patch"
        "002-missing-symbol-clang-static-lib.patch"
        "003-dll-install-location.patch")
sha256sums=('d284eaabcf21ad3d1f520a1d1b1b533f7c5f5a44ba92495dd38e238874e8e4d7'
            '290b26213e9b139742135329df28ffb87dbb63db6809634dee013d0272f62756'
            'e951d29ba317118692d892472be02b8d5395fba2b3b39cafa8335164444f0651'
            '2ffbc317cd73a8b0653b5691b411cd47634e6d075fcc3ed16cf3913271b38275')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  apply_patch_with_msg \
    "001-mingw.patch" \
    "002-missing-symbol-clang-static-lib.patch" \
    "003-dll-install-location.patch"
}

build() {
  [[ -d "build-${MSYSTEM}" ]] && rm -rf "build-${MSYSTEM}"
  mkdir -p "${srcdir}/build-${MSYSTEM}"
  cd "${srcdir}/build-${MSYSTEM}"

  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -G "Ninja" \
    "${_extra_config[@]}" \
    -DQT_VERSION=6 \
    ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake --build .

  ${MINGW_PREFIX}/bin/cmake --build . --target doc
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR=${pkgdir} ${MINGW_PREFIX}/bin/cmake.exe --install .

  install -vDm 644 ${srcdir}/${_realname}-${pkgver}/COPYING -t "${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname/"

  # Install documentation
  install -vDm 644 ${srcdir}/build-${MSYSTEM}/doc/doxygen/html/*.* -t "${pkgdir}${MINGW_PREFIX}/share/doc/$_realname/"
  install -vDm 644 ${srcdir}/build-${MSYSTEM}/doc/doxygen/html/search/*.* -t "${pkgdir}${MINGW_PREFIX}/share/doc/$_realname/search/"
}