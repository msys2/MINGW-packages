# Maintainer: Martell Malone <martellmalone@gmail.com>

_realname=luajit
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=v2.0.4.3.g575bfe5
pkgrel=1
pkgdesc="Just-in-time compiler and drop-in replacement for Lua 5.1 (mingw-w64)"
arch=('any')
url="http://luajit.org/"
license=('MIT')
depends=('winpty-git')
conflicts=("${MINGW_PACKAGE_PREFIX}-lua51")
provides=("${MINGW_PACKAGE_PREFIX}-lua51")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "git")
options=('strip' 'staticlibs')
source=("${_realname}"::"git+http://luajit.org/git/luajit-2.0.git"
        001-make-import-library.patch
        002-fix-pkg-config-file.patch)
md5sums=('SKIP'
         '668114665f129fa461bdebc53d9952c2'
         '2b3aec6c289fbfc40a32762e0e36324e')

pkgver() {
  cd "${srcdir}/${_realname}"
  git describe --tags | sed 's|-|.|g'
}

prepare() {
  cd "${srcdir}/${_realname}"
  patch -p1 -i ${srcdir}/001-make-import-library.patch
  patch -p1 -i ${srcdir}/002-fix-pkg-config-file.patch
  sed -i "s|export PREFIX= /usr/local|export PREFIX="${MINGW_PREFIX}"|g" ${srcdir}/${_realname}/Makefile
}

build() {
  cd "${srcdir}/${_realname}"
  make
}

package() {
  cd "${srcdir}/${_realname}"
  make DESTDIR=${pkgdir} install

  _exename=${_realname}
  echo "#!/usr/bin/env bash" > "${pkgdir}${MINGW_PREFIX}/bin/${_exename}"
  echo '/usr/bin/winpty "$( dirname ${BASH_SOURCE[0]} )/'${_exename}'.exe" "$@"' >> "${pkgdir}${MINGW_PREFIX}/bin/${_exename}"

  install -v -Dm644 "${srcdir}/${_realname}/src/lua51.dll" "${pkgdir}${MINGW_PREFIX}/bin/lua51.dll" || exit 1
  install -v -Dm644 "${srcdir}/${_realname}/src/luajit.exe" "${pkgdir}${MINGW_PREFIX}/bin/luajit.exe" || exit 1
  install -v -Dm644 "${srcdir}/${_realname}/src/libluajit-5.1.dll.a" "${pkgdir}${MINGW_PREFIX}/lib/libluajit-5.1.dll.a" || exit 1

  #cp ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/lua{jit,51}.pc
  cp ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/lua{jit,5.1}.pc
}
