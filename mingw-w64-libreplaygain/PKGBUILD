# Maintainer: Leandro Nini <drfiemost@email.it>

_realname=libreplaygain
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=475
pkgrel=3
pkgdesc="A library to adjust audio gain (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://musepack.net/"
license=("spdx:LGPL-2.1-or-later")
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools" "${MINGW_PACKAGE_PREFIX}-cc")
source=("https://files.musepack.net/source/${_realname}_r${pkgver}.tar.gz"
        "010-no-fpic.patch")
sha256sums=('8258bf785547ac2cda43bb195e07522f0a3682f55abe97753c974609ec232482'
            '200cc273d8038891ecb9ab6e91e22c151f756fed04ddb6591d1e619b2a741ad2')

prepare() {
  cd "${srcdir}"/${_realname}_r${pkgver}
  patch -p1 -i "${srcdir}"/010-no-fpic.patch
  autoreconf -i
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  ../${_realname}_r${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --enable-shared \
    --enable-static

  make
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
}
