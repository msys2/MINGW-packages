# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=c-ares
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.13.0
pkgrel=1
pkgdesc="C library that performs DNS requests and name resolves asynchronously (mingw-w64)"
arch=('any')
url="https://c-ares.haxx.se/"
license=("MIT, BSD")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
options=('strip' 'staticlibs')
source=("https://c-ares.haxx.se/download/${_realname}-${pkgver}.tar.gz"{,.asc}
        0001-Use-RPM-compiler-options.patch
        0002-Add-static-pkg-config.patch)
validpgpkeys=('914C533DF9B2ADA2204F586D78E11C6B279D5C91'
              '27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2')
sha256sums=('03f708f1b14a26ab26c38abd51137640cb444d3ec72380b21b20f1a8d2861da7'
            'SKIP'
            'f00faa8723fd530b027f96b987ef19138628a49afdf52ffb0554b36156fc9d74'
            '70be0786506122dee5c105e6c00b5a430e564ac18d4e8a3778679039f84d74da')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-Use-RPM-compiler-options.patch
  patch -p1 -i ${srcdir}/0002-Add-static-pkg-config.patch

  autoreconf -fi
}

build() {
  [[ -d "${srcdir}"/build-${CARCH}-shared ]] && rm -rf "${srcdir}"/build-${CARCH}-shared
  [[ -d "${srcdir}"/build-${CARCH}-static ]] && rm -rf "${srcdir}"/build-${CARCH}-static
  mkdir -p "${srcdir}"/build-${CARCH}-shared "${srcdir}"/build-${CARCH}-static
  
  msg "Building shared library..."
  (cd "${srcdir}"/build-${CARCH}-shared
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --disable-static
  make -j"$(nproc)"
  )

  msg "Building static lib..."
  (cd "${srcdir}"/build-${CARCH}-static
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-shared \
    --enable-static
  make -j"$(nproc)"
  )
}

package() {
  cd "${srcdir}"/build-${CARCH}-static
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}"/build-${CARCH}-shared
  make DESTDIR="${pkgdir}" install

  # msys2-runtime-devel provide nameser.h but mingw-w64 don't have it
  cp ${srcdir}/${_realname}-${pkgver}/nameser.h ${pkgdir}${MINGW_PREFIX}/include
}
