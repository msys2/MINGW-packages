# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_variant=-${KF5_VARIANT:-shared}
source "$(dirname ${BASH_SOURCE[0]})"/../mingw-w64-PKGBUILD-common/kde-frameworks5
_realname=kdiff3
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.10.7
pkgrel=1
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'archlinux: kdiff3'
)
msys2_repository_url="https://invent.kde.org/sdk/kdiff3/"
url="https://apps.kde.org/kdiff3/"
license=('GPL')
pkgdesc="A file comparator/merge tool (mingw-w64)"
groups=("${MINGW_PACKAGE_PREFIX}-kde-applications"
        "${MINGW_PACKAGE_PREFIX}-kdesdk")
depends=("${MINGW_PACKAGE_PREFIX}-kconfig-qt5"
         "${MINGW_PACKAGE_PREFIX}-kcoreaddons-qt5"
         "${MINGW_PACKAGE_PREFIX}-kconfigwidgets-qt5"
         "${MINGW_PACKAGE_PREFIX}-kcrash-qt5"
         "${MINGW_PACKAGE_PREFIX}-ki18n-qt5"
         "${MINGW_PACKAGE_PREFIX}-kio-qt5"
         "${MINGW_PACKAGE_PREFIX}-kparts-qt5"
         "${MINGW_PACKAGE_PREFIX}-kwidgetsaddons-qt5"
         "${MINGW_PACKAGE_PREFIX}-kxmlgui-qt5"
         "${MINGW_PACKAGE_PREFIX}-qt5-base")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-boost"
             "${MINGW_PACKAGE_PREFIX}-extra-cmake-modules"
             "${MINGW_PACKAGE_PREFIX}-kdoctools-qt5${_namesuff}")
source=("https://download.kde.org/stable/${_realname}/${_realname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('fe8b699d124cd4d2632b3c27aa06acec5caa8fcbf8b7648cf0c00d7a4b6ace50'
            'SKIP')
validpgpkeys=(EE56D710885D36FB1E4D21F3F442B36D614B06BC) # Michael Reeves <reeves.87@gmail.com>

build() {
  [[ -d "${srcdir}/build-${MSYSTEM}${_variant}" ]] && rm -rf "${srcdir}/build-${MSYSTEM}${_variant}"
  mkdir -p "${srcdir}/build-${MSYSTEM}${_variant}" && cd "${srcdir}/build-${MSYSTEM}${_variant}"

  local -a extra_config

  if [ "${_variant}" = "-static" ]; then
    extra_config+=( -DBUILD_SHARED_LIBS=NO )
  fi

  _kde_f5_build_env
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"Ninja" \
    -DCMAKE_BUILD_TYPE=$(_kde_f5_CMAKE_BUILD_TYPE) \
    "${_kde_f5_KDE_INSTALL_DIRS[@]}" \
    -DBUILD_TESTING=OFF \
    -DECM_DIR=${MINGW_PREFIX}/share/ECM \
    "${extra_config[@]}" \
    ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake.exe --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}${_variant}"

  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install .
}
