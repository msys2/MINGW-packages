# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=bullet
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}" "${MINGW_PACKAGE_PREFIX}-${_realname}-debug")
pkgver=2.85
pkgrel=1
pkgdesc="A 3D Collision Detection and Rigid Body Dynamics Library for games and  animation (mingw-w64)"
arch=('any')
url="http://www.bulletphysics.com/Bullet/"
license=('custom:zlib')
options=('!strip' 'staticlibs')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-freeglut")
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/bulletphysics/bullet3/archive/${pkgver}.tar.gz"
        mingw-fixes.patch
        promit-bulletcollisions.patch
        mingw-strlen-missing-include.patch
        workaround-mingw-gcc6-bug.patch)
sha256sums=('0dc3c9488c2b4176f916640ca57860d6689890d0f58c6fceed0f10ddc1fa082f'
            'b99a45c76dabae09b65291ffe4afc63aaab31c4667c091b8a319ca7b0e97f9e8'
            'a91c28949d0e1535caa4feb05973e67213bf055887f6e465a54c8b0b1fcb11cd'
            '5ff773be3814aaaf7776da5787a702188d7009fec2ca2434d6cf139f2b5a5177'
            '15050e81caf2ebf406d7cc6b78758b2d59ee786b79718a94c45352ca1d538338')

prepare () {
  cd ${srcdir}/bullet3-${pkgver}
  patch -p1 -i ${srcdir}/mingw-fixes.patch
  patch -p0 -i ${srcdir}/promit-bulletcollisions.patch
  patch -p1 -i ${srcdir}/workaround-mingw-gcc6-bug.patch
  patch -p1 -i ${srcdir}/mingw-strlen-missing-include.patch
}

build() {
  plain "No step"
}

build_release() {
  depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" "${MINGW_PACKAGE_PREFIX}-freeglut")
  install=${_realname}-${CARCH}.install

  [[ -d build-release ]] && rm -rf build-release
  mkdir -p build-release && pushd build-release
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX}\
    -DBUILD_SHARED_LIBS=1 \
    -DBUILD_EXTRAS=1 \
    -DINSTALL_LIBS=1 \
    -DUSE_GLUT=1 \
    -DCMAKE_CXX_FLAGS_RELEASE="-fpermissive" \
    -DINSTALL_EXTRA_LIBS=1 \
    -DCMAKE_BUILD_TYPE=Release \
    ../bullet3-${pkgver}

  make
  make DESTDIR=${pkgdir} install

  mkdir -p ${pkgdir}${MINGW_PREFIX}/share/bullet
  for f in $(find examples -type f -iname *.dll -o -iname *.exe); do
    cp ${f} ${pkgdir}${MINGW_PREFIX}/share/bullet/
  done
  cp -rf ${srcdir}/bullet3-${pkgver}/data/* ${pkgdir}${MINGW_PREFIX}/share/bullet/
  for f in gtest gwen OpenGLWindow; do
    cp lib/lib${f}.dll ${pkgdir}${MINGW_PREFIX}/share/bullet/
  done

  mv -f ${pkgdir}${MINGW_PREFIX}/lib/*.dll ${pkgdir}${MINGW_PREFIX}/bin/
}

build_debug() {
  depends=("${MINGW_PACKAGE_PREFIX}-${_realname}=${pkgver}-${pkgrel}")

  [[ -d build-debug ]] && rm -rf build-debug
  mkdir -p build-debug && pushd build-debug
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX}  \
    -DBUILD_SHARED_LIBS=1 \
    -DBUILD_EXTRAS=1 \
    -DINSTALL_LIBS=1 \
    -DUSE_GLUT=1 \
    -DCMAKE_CXX_FLAGS_RELEASE="-fpermissive" \
    -DINSTALL_EXTRA_LIBS=1 \
    -DCMAKE_BUILD_TYPE=Debug \
    -DCMAKE_DEBUG_POSTFIX="_d" \
    ../bullet3-${pkgver}

  make
  make DESTDIR=${pkgdir} install

  mv -f ${pkgdir}${MINGW_PREFIX}/lib/*.dll ${pkgdir}${MINGW_PREFIX}/bin/

  rm -rf ${pkgdir}${MINGW_PREFIX}/include
  rm -rf ${pkgdir}${MINGW_PREFIX}/lib/cmake
  rm -rf ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig

}

package_mingw-w64-i686-bullet() {
  build_release
}

package_mingw-w64-i686-bullet-debug() {
  build_debug
}

package_mingw-w64-x86_64-bullet() {
  build_release
}

package_mingw-w64-x86_64-bullet-debug() {
  build_debug
}
