# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=bullet
_mingw_suff=mingw-w64-${CARCH}
pkgname=("${_mingw_suff}-${_realname}")
_rev=r2704
pkgver=2.82
pkgrel=2
pkgdesc="A 3D Collision Detection and Rigid Body Dynamics Library for games and  animation (mingw-w64)"
arch=('any')
url="http://www.bulletphysics.com/Bullet/"
license=('custom:zlib')
depends=("${_mingw_suff}-gcc-libs") # "${_mingw_suff}-freeglut"
options=('strip' '!buildflags' 'staticlibs')
optdepends=('bullet-docs: documentation')
makedepends=("${_mingw_suff}-cmake")
source=("http://bullet.googlecode.com/files/bullet-${pkgver}-${_rev}.tgz"
    dont-try-find-glu.patch
    install-pkgconfig-files-mingw.patch)
md5sums=('70b3c8d202dee91a0854b4cbc88173e8'
         '53e346560fe8c50834a587bdacf082a7'
         'a888f0a035d6f88ab1fbfd1e40b8b960')

prepare () {
  cd ${srcdir}/bullet-${pkgver}-${_rev}
  patch -p1 -i ${srcdir}/dont-try-find-glu.patch
  patch -p1 -i ${srcdir}/install-pkgconfig-files-mingw.patch
  #sed -i "s|#define SIMD_EPSILON      FLT_EPSILON|#define SIMD_EPSILON 1.19209290E-07F|g" src/LinearMath/btScalar.h
  rm -f *.dll *.DLL
  rm -rf Glut
}

build() {
  mkdir -p ${pkgdir}${MINGW_PREFIX}
  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null
  
  cd ${srcdir}/bullet-${pkgver}-${_rev}
  mkdir -p build-${MINGW_CHOST} && pushd build-${MINGW_CHOST}
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX:PATH=${PREFIX_WIN} \
    -DBUILD_SHARED_LIBS=1 \
    -DBUILD_DEMOS=0 \
    -DBUILD_EXTRAS=1 \
    -DINSTALL_LIBS=1 \
    -DUSE_GLUT=0 \
    -DCMAKE_CXX_FLAGS_RELEASE="-fpermissive" \
    -DINSTALL_EXTRA_LIBS=1 \
    -DCMAKE_BUILD_TYPE=Release \
    ..
  make
}

package() {
  cd "${srcdir}/bullet-${pkgver}-${_rev}/build-${MINGW_CHOST}"
  make install  # DESTDIR="${pkgdir}${MINGW_PREFIX}" 
  install -d "${pkgdir}${MINGW_PREFIX}"/bin
  mv "${pkgdir}${MINGW_PREFIX}"/lib/*.dll "${pkgdir}${MINGW_PREFIX}"/bin/
  
  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/bullet.pc
}
