# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=hicolor-icon-theme
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.15
pkgrel=2
pkgdesc="Freedesktop.org Hicolor icon theme (mingw-w64)"
arch=('any')
url="http://icon-theme.freedesktop.org/wiki/HicolorTheme"
license=("GPL2")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
install=${_realname}-${CARCH}.install
options=('!libtool' 'strip' 'staticlibs')
source=("https://icon-theme.freedesktop.org/releases/${_realname}-${pkgver}.tar.xz")
sha256sums=('9cc45ac3318c31212ea2d8cb99e64020732393ee7630fa6c1810af5f987033cc')

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}"/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
