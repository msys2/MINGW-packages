# Maintainer: Diego Sogari <diego.sogari@gmail.com>

_realname=qtc-spellchecker-plugin
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
qtcsrc=qt-creator-opensource-src
qtcver=4.6.0
pkgver=1.2.0
pkgrel=1
pkgdesc="SpellChecker Plugin for Qt Creator (mingw-w64)"
arch=('any')
url="https://github.com/CJCombrink/SpellChecker-Plugin"
license=('LGPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-hunspell"
         "${MINGW_PACKAGE_PREFIX}-qt-creator")
source=("${_realname}-${pkgver}.tar.gz::https://codeload.github.com/CJCombrink/SpellChecker-Plugin/tar.gz/v${pkgver}"
        "https://download.qt.io/official_releases/qtcreator/4.6/${qtcver}/${qtcsrc}-${qtcver}.tar.gz")
sha256sums=('15a7f9b5c89a29f1c73028a5a2efbc11e98cbd23a256a9ab6bab186e10215056'
            '2f7205e93eba3b086877c1df137dd25314f592a9f952f965740d97925306b96c')
noextract=("${qtcsrc}-${qtcver}.tar.gz")

prepare() {
  cd "${srcdir}"
  bsdtar -x -f "${qtcsrc}-${qtcver}.tar.gz" --exclude share --exclude tests

  cd "SpellChecker-Plugin-${pkgver}"
  prefix=$(cygpath -m ${MINGW_PREFIX})
  sed -e "s|\(LOCAL_QTCREATOR_SOURCES=\).*|\1../${qtcsrc}-${qtcver}|g" \
      -e "s|\(LOCAL_IDE_BUILD_TREE=\).*|\1${prefix}|g" \
      -e "s|\(LOCAL_HUNSPELL_LIB_DIR=\).*|\1${prefix}/bin|g" \
      -e "s|\(LOCAL_HUNSPELL_SRC_DIR=\).*|\1${prefix}/include|g" \
      spellchecker_local_paths.pri.example > spellchecker_local_paths.pri
}

build() {
  rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  ln -fs `which windres` ${MINGW_CHOST}-windres
  PATH=`pwd`:$PATH

  ${MINGW_PREFIX}/bin/qmake -set CROSS_COMPILE "${MINGW_CHOST}-"
  ${MINGW_PREFIX}/bin/qmake -spec win32-g++ "CONFIG+=release" "../SpellChecker-Plugin-${pkgver}/spellchecker.pro"
  make -f Makefile.Release
}

package() {
  dir="lib/qtcreator/plugins"
  install -d "${pkgdir}${MINGW_PREFIX}/${dir}"
  install "${MINGW_PREFIX}/${dir}/SpellChecker"?.dll "${pkgdir}${MINGW_PREFIX}/${dir}"
}
