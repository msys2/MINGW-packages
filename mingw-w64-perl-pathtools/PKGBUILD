# Maintainer: @naveen521kk on Github, Naveen M K <naveen521kk@gmail.com>

_perlname=PathTools
_realname="${_perlname,,}"
pkgbase=mingw-w64-perl-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-perl-${_realname}")
pkgver=3.75
pkgrel=1
pkgdesc="Tools for working with directory and file names (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://metacpan.org/release/${_perlname}"
groups=("${MINGW_PACKAGE_PREFIX}-perl-modules")
provides=(
  "${MINGW_PACKAGE_PREFIX}-perl-cwd"
  "${MINGW_PACKAGE_PREFIX}-perl-file-spec"
  "${MINGW_PACKAGE_PREFIX}-perl-file-spec-amigaos"
  "${MINGW_PACKAGE_PREFIX}-perl-file-spec-cygwin"
  "${MINGW_PACKAGE_PREFIX}-perl-file-spec-epoc"
  "${MINGW_PACKAGE_PREFIX}-perl-file-spec-functions"
  "${MINGW_PACKAGE_PREFIX}-perl-file-spec-mac"
  "${MINGW_PACKAGE_PREFIX}-perl-file-spec-os2"
  "${MINGW_PACKAGE_PREFIX}-perl-file-spec-unix"
  "${MINGW_PACKAGE_PREFIX}-perl-file-spec-vms"
  "${MINGW_PACKAGE_PREFIX}-perl-file-spec-win32"
)
depends=(
  "${MINGW_PACKAGE_PREFIX}-perl"
  "${MINGW_PACKAGE_PREFIX}-perl-test-more"
  "${MINGW_PACKAGE_PREFIX}-perl-carp"
  "${MINGW_PACKAGE_PREFIX}-perl-scalar-util"
  "${MINGW_PACKAGE_PREFIX}-perl-file-basename"
)
options=('!emptydirs')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-cc"
  $([[ ${MINGW_PACKAGE_PREFIX} != *-clang-* ]] || \
    echo "${MINGW_PACKAGE_PREFIX}-gcc-compat")
  "${MINGW_PACKAGE_PREFIX}-perl-extutils-makemaker"
)
license=('perl_5')
source=(
  'https://cpan.metacpan.org/authors/id/X/XS/XSAWYERX/PathTools-3.75.tar.gz'
  'patchmakefile.py'
)
sha256sums=('a558503aa6b1f8c727c0073339081a77888606aa701ada1ad62dd9d8c3f945a2'
            '00fe5ced42aa635714909a8eba1347a6c132011a9bce68e9c75b27ebd64628f2')

build() {
  cd "$srcdir/${_perlname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL
  export pkgdir=$pkgdir
  python $srcdir/patchmakefile.py
  mingw32-make
}

package() {
  cd "$srcdir/${_perlname}-${pkgver}"
  mingw32-make DESTDIR="${pkgdir}/" install
}
