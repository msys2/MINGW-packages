# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libproxy
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.4.11
pkgrel=3
pkgdesc="A library that provides automatic proxy configuration management (mingw-w64)"
arch=('any')
url="https://libproxy.googlecode.com"
license=("LGPL")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-python2")
options=('staticlibs' 'strip')
source=("https://libproxy.googlecode.com/files/${_realname}-${pkgver}.tar.gz"
        001-fix-casting-error.patch
        002-fix-win32-close-scoket.patch
        003-fix-leaks.patch
        004-allow-bindings.patch
        005-modules.patch
        006-python-site-package-dir.patch)

sha256sums=('dc3f33de54163718f82b3e7c496a7de97f8862578414b8ecaad3cbfe4821864b'
            'f20ce4f17c1a858b3eac84a14226c5e368ea581b0b5ab3428f6e1f9d22d44124'
            '88e5988413ee2f24a893c4a64f43a7574a395ae63139d05e66a8eb1d14f14902'
            '598d0a4514843da1f07c25f2f3b25cc9cdecc90ec18a0487d431e4fcc9747f6f'
            '0c3182da531c117f80e05871af73d1c2c5d3828ee828f91bba212c9252747ddc'
            '35d27fadfc58c1dc457d901935467a4062814f6fcdd663f5d7f1d4cec4af2d44'
            'e5aa7cb0d32168ab8390b1ec5d0e7959ef5357fd50b047b257d04fe8ea6a0c67')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/001-fix-casting-error.patch
  patch -p1 -i ${srcdir}/002-fix-win32-close-scoket.patch
  patch -p1 -i ${srcdir}/003-fix-leaks.patch
  patch -p1 -i ${srcdir}/004-allow-bindings.patch
  patch -p1 -i ${srcdir}/005-modules.patch
  patch -p1 -i ${srcdir}/006-python-site-package-dir.patch
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=;-DLIBEXEC_INSTALL_DIR=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DLIBEXEC_INSTALL_DIR=${MINGW_PREFIX}/lib/libproxy \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS}" \
    -DCMAKE_C_FLAGS="${CFLAGS}" \
    -DWITH_PERL=OFF \
    -DWITH_PYTHON=ON \
    ../${_realname}-${pkgver}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install
}
