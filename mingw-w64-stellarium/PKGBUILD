
_realname=stellarium
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=23.3
pkgrel=1
pkgdesc="A free open source planetarium for your computer"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url="https://stellarium.org/"
license=("spdx:GPL-2.0-only")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-doxygen"
  "${MINGW_PACKAGE_PREFIX}-gettext"
  "${MINGW_PACKAGE_PREFIX}-graphviz"
  "${MINGW_PACKAGE_PREFIX}-ninja"
  "${MINGW_PACKAGE_PREFIX}-qt6-tools"
)
depends=(
  #"${MINGW_PACKAGE_PREFIX}-eigen3"
  #"${MINGW_PACKAGE_PREFIX}-glm"
  "${MINGW_PACKAGE_PREFIX}-qt6-base"
  "${MINGW_PACKAGE_PREFIX}-qt6-charts"
  "${MINGW_PACKAGE_PREFIX}-qt6-declarative"
  "${MINGW_PACKAGE_PREFIX}-qt6-multimedia"
  "${MINGW_PACKAGE_PREFIX}-qt6-positioning"
  "${MINGW_PACKAGE_PREFIX}-qt6-serialport"
  "${MINGW_PACKAGE_PREFIX}-qt6-svg"
  "${MINGW_PACKAGE_PREFIX}-zlib"
)
source=("https://github.com/Stellarium/stellarium/releases/download/v${pkgver}/${_realname}-${pkgver}.tar.xz"
        "001-gsatellite-mingw.patch"
        "002-remove-media-workaround.patch"
        "003-libstelMain-output.patch")
sha256sums=('179420e9e9f11278b78dc54e14458452c92055cc8923538748c8a0f961714fc2'
            'd1fc23bc2063e429f6a6fdd9433b2b0d91e55960f3185fee51949df9230f30c0'
            '699f7841f6ec7f3d69bc05640c6e0e78bd21071e3d1fccffae30d93e02745b99'
            'b8ed57cd8b921a1aa2bb7316f8c3b417448cd82f5a7764b678d8a24e425ecbc2')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  apply_patch_with_msg \
    001-gsatellite-mingw.patch \
    002-remove-media-workaround.patch \
    003-libstelMain-output.patch
}

build() {
  [[ -d "build-${MSYSTEM}" ]] && rm -rf "build-${MSYSTEM}"
  mkdir -p "${srcdir}/build-${MSYSTEM}"
  cd "${srcdir}/build-${MSYSTEM}"

  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
    _extra_config+=("-DSTELLARIUM_RELEASE_BUILD=ON")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
    _extra_config+=("-DSTELLARIUM_RELEASE_BUILD=OFF")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -G "Ninja" \
    "${_extra_config[@]}" \
    -DBUILD_SHARED_LIBS=ON \
    -DENABLE_QTWEBENGINE=OFF \
    -DENABLE_SPOUT=OFF \
    -DENABLE_SHOWMYSKY=OFF \
    -DUSE_SYSTEM_ZLIB=ON \
    -DCPACK_BINARY_NSIS=OFF \
    -DCPACK_SOURCE_ZIP=OFF \
    -DCPACK_SOURCE_7Z=OFF \
    -DUSE_PLUGIN_TELESCOPECONTROL=OFF \
    ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR=${pkgdir} ${MINGW_PREFIX}/bin/cmake.exe --install .

  install -vDm 644 ${srcdir}/${_realname}-${pkgver}/COPYING -t "${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname/"
}