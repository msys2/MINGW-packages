# Maintainer: Jonathan Armstrong <jda2158@gmail.com>

_realname=flashrom
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.2
pkgrel=3
pkgdesc='Utility for detecting, reading, writing, verifying and erasing flash chips (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://github.com/flashrom/flashrom"
license=('GPL2')
depends=("${MINGW_PACKAGE_PREFIX}-libftdi"
         "${MINGW_PACKAGE_PREFIX}-libusb"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread-git")
makedepends=("${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "patch")
source=("https://github.com/flashrom/flashrom/archive/refs/tags/v${pkgver}.tar.gz"
        0001-meson-check-version-script.patch)
sha256sums=('a5bac412cefb87bb426912fed46ccc38799d088a9b92dbe7bac38c5df016d9b2'
            '71e248541d0aed0f01b38dccdab257e3cf6e6d43513cf82a9f99812ac04271ef')

prepare() {
  cd "${_realname}-${pkgver}"

  # Workaround: DEF file should be used instead of MAP file
  patch -p1 -i "${srcdir}/0001-meson-check-version-script.patch"

  # Required for `flashrom --version` to report correctly.
  printf "VERSION = v${pkgver};\nMAN_DATE = $(date -u -r flashrom.8.tmpl +%Y-%m-%dT%H:%M:%SZ);" > versioninfo.inc
}

build() {
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    meson \
      -Dpciutils=false \
      -Dconfig_linux_mtd=false \
      -Dconfig_linux_spi=false \
      --prefix="${MINGW_PREFIX}" \
      --wrap-mode=nodownload \
      --auto-features=enabled \
      --buildtype=plain \
      ../${_realname}-${pkgver}

  meson compile
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  # TODO: check once master branch gets numbered release (i.e., tests/)
  meson test
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" meson install

  # To enable usage relative paths for next operations
  cd "${pkgdir}"/${MINGW_PREFIX}

  # Move content to standard dirs
  mv -v sbin/flashrom.exe bin/

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
 
# echo replace USB-UART drivers (if needed) with WinUSB (libusb) using zadig [https://zadig.akeo.ie/]
}
