# Maintainer: Oscar Fuentes <ofv@wanadoo.es>

_realname=tclx
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=8.6.1
pkgrel=1
pkgdesc="Extends Tcl by providing new operating system interface commands, extended file control and many other (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64' 'clang32')
url="https://tclx.sourceforge.io/"
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-tcl")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/flightaware/tclx/archive/v${pkgver}.tar.gz"
        '001-fix-detect-mingw.patch'
        '002-fix-func-declaration.patch'
        'random.c')
sha256sums=('7fddec99f23edcdf92e0e248edbe3b2043e7c51b599828a8f9be5b51978d44b2'
            '5018594a4889c41fcb1a669f29b971dff19759c0fb2d8939bb495bb3715dbf08'
            '1f7d6a6c6fa7d3535ff57cac9701bdc8a156bd0f6b6d3e9da69c91c3b9bb8fea'
            '32b1b0685d67f20d8f7943682d16caaa86915937f745471652835239af51750c')

prepare() {
  local dst=${srcdir}/${_realname}-${pkgver}/compat
  mkdir -p ${dst}
  cp random.c ${dst}

  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-fix-detect-mingw.patch
  patch -p1 -i ${srcdir}/002-fix-func-declaration.patch
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  [[ "${CARCH}" = 'i686' ]] || enable64bit='--enable-64bit'

  CFLAGS="-DUSE_INTERP_ERRORLINE $CFLAGS" \
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --with-tcl="/${MSYSTEM}/lib/" \
    $enable64bit
  make
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/license.terms ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/license.terms
}
