# Maintainer: Oscar Fuentes <ofv@wanadoo.es>

_realname=tclx
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
_majorminor=8.4
pkgver=${_majorminor}.1
pkgrel=1
pkgdesc="Extends Tcl by providing new operating system interface commands, extended file control and many other."
arch=('any')
url="http://tclx.sourceforge.net/"
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-tcl")
source=("http://sourceforge.net/projects/tclx/files/TclX/$pkgver/${_realname}${pkgver}.tar.bz2"
	'random.c')
md5sums=('ac983708f23cf645c07058148f48440c'
         '59ad67a4a330de9062b594c37ea4d198')

prepare() {
    local dst=${srcdir}/${_realname}${_majorminor}/compat
    mkdir -p ${dst}
    cp random.c ${dst}
}

build() {
  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"

  [ "${MINGW_CHOST}" = 'x86_64-w64-mingw32' ] && enable64bit='--enable-64bit'

  CFLAGS="-DUSE_INTERP_ERRORLINE $CFLAGS" \
	"${srcdir}"/${_realname}${_majorminor}/configure \
	--prefix=${MINGW_PREFIX} \
	$enable64bit
  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="$pkgdir/" install

  local licdir=${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}
  mkdir -p ${licdir}
  cp "${srcdir}"/${_realname}${_majorminor}/license.terms ${licdir}
}
