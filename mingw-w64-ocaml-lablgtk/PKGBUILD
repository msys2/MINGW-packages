# Maintainer: Junjie Mao <eternal.n08@gmail.com>

_realname=ocaml-lablgtk
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.18.3
pkgrel=2
pkgdesc="An Ocaml interface to the GIMP Tool Kit (mingw-w64)"
arch=('any')
url='http://lablgtk.forge.ocamlcore.org/'
license=('LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-gtk2"
         "${MINGW_PACKAGE_PREFIX}-gtksourceview2"
         "${MINGW_PACKAGE_PREFIX}-gtkspell"
         "${MINGW_PACKAGE_PREFIX}-libglade"
         "${MINGW_PACKAGE_PREFIX}-libgnomecanvas"
         "${MINGW_PACKAGE_PREFIX}-libglade"
         "${MINGW_PACKAGE_PREFIX}-ocaml"
         "${MINGW_PACKAGE_PREFIX}-ocaml-findlib"
         )
makedepends=("${MINGW_PACKAGE_PREFIX}-flexdll"
             "${MINGW_PACKAGE_PREFIX}-ocaml-camlp4"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")
source=("https://forge.ocamlcore.org/frs/download.php/1479/lablgtk-${pkgver}.tar.gz"
        "0001-Fix-a-path-issue-during-installation.patch")
sha1sums=('b8ecc3b160fcbce73062f3bad23a2a3b1c2735d3'
          '8aae142b4c50038f9284c3427b22d75df6f3018e')

prepare() {
  cd ${srcdir}/lablgtk-${pkgver}
  patch -p1 -i ${srcdir}/0001-Fix-a-path-issue-during-installation.patch
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  cp -rf lablgtk-${pkgver} build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --disable-gtktest

  sed -i "s/-pthread/-lpthread/g" config.make
  make
  make opt
}

check() {
  # The package does not include an automatic test process
  plain "skip"
}

package() {
  cd ${srcdir}/${_pkgsrc}
  make install DESTDIR="${pkgdir}"

  for f in COPYING LGPL; do
    [ -f ${f} ] && install -Dm644 ${f} ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/${f}
  done

  # do not append to ld.conf
  rm ${pkgdir}${MINGW_PREFIX}/lib/ocaml/ld.conf
}
