# Maintainer: Junjie Mao <eternal.n08@gmail.com>

_realname=ocaml-lablgtk
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.18.3
pkgrel=1
pkgdesc='An Ocaml interface to the GIMP Tool Kit.'
groups=("${MINGW_PACKAGE_PREFIX}")
arch=('any')
url='http://lablgtk.forge.ocamlcore.org/'
license=('LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-ocaml"
	 "${MINGW_PACKAGE_PREFIX}-gtk2"
	 "${MINGW_PACKAGE_PREFIX}-libglade"
	 "${MINGW_PACKAGE_PREFIX}-librsvg"
	 "${MINGW_PACKAGE_PREFIX}-gtksourceview2"
	 "${MINGW_PACKAGE_PREFIX}-gtkspell"
	 "${MINGW_PACKAGE_PREFIX}-pkg-config"
	 "${MINGW_PACKAGE_PREFIX}-ocaml-findlib"
	 )
makedepends=("${MINGW_PACKAGE_PREFIX}-flexdll")
source=("https://forge.ocamlcore.org/frs/download.php/1479/lablgtk-2.18.3.tar.gz"
	"0001-Fix-a-path-issue-during-installation.patch")
sha1sums=('b8ecc3b160fcbce73062f3bad23a2a3b1c2735d3'
          '8aae142b4c50038f9284c3427b22d75df6f3018e')

env=${MINGW_PREFIX#/}
_pkg="lablgtk-${pkgver}"
_pkgsrc="${env}-${_pkg}"
noextract=(${_pkg}.tar.gz)
prepare() {
  cd $startdir/
  [ -d ${srcdir}/${_pkgsrc} ] && rm -rf ${srcdir}/${_pkgsrc}
  tar -xzf ${startdir}/${_pkg}.tar.gz -C ${srcdir}

  cd ${srcdir}
  mv ${_pkg} ${_pkgsrc}
  cd ${_pkgsrc}

  patch -p1 -i ${srcdir}/0001-Fix-a-path-issue-during-installation.patch
}

build() {
  cd ${srcdir}/${_pkgsrc}
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared \
    --disable-gtktest
  sed -i "s/-pthread/-lpthread/g" config.make
  make
  make opt
}

check() {
  # The package does not include an automatic test process
  plain "skip"
}

package() {
  cd ${srcdir}/${_pkgsrc}
  make install DESTDIR="${pkgdir}"

  for f in COPYING LGPL; do
    [ -f ${f} ] && install -Dm644 ${f} ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/${f}
  done

  # do not append to ld.conf
  rm ${pkgdir}${MINGW_PREFIX}/lib/ocaml/ld.conf
}
