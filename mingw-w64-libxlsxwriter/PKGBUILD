# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Sui Libin <bygreencn@gmail.com>

_realname=libxlsxwriter
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.1.4
pkgrel=1
pkgdesc="A C library for creating Excel XLSX files (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://github.com/jmcnamara/libxlsxwriter"
license=("custom")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         #"${MINGW_PACKAGE_PREFIX}-minizip"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' 'strip')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/jmcnamara/libxlsxwriter/archive/RELEASE_${pkgver}.tar.gz")
sha256sums=('b379eb35fdd9c653ebe72485b9c992f612c7ea66f732784457997d6e782f619b')

prepare() {
  cd "${srcdir}/${_realname}-RELEASE_${pkgver}"
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe -Wno-dev \
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_TESTS=ON \
    -DBUILD_EXAMPLES=OFF \
    -DUSE_SYSTEM_MINIZIP=OFF \
    -DCMAKE_BUILD_TYPE=Release \
    ../${_realname}-RELEASE_${pkgver}

  ${MINGW_PREFIX}/bin/cmake.exe --build ./
}

check(){
  cd ${srcdir}/build-${MINGW_CHOST}
  ${MINGW_PREFIX}/bin/ctest.exe || true
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install ./

  install -Dm644 "${srcdir}/${_realname}-RELEASE_${pkgver}/LICENSE.txt" "${pkgdir}/${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.txt"
}
