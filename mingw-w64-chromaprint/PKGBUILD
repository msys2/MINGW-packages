# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=chromaprint
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.4
pkgrel=1
pkgdesc="Library that implements a custom algorithm for extracting fingerprints from any audio source (mingw-w64)"
arch=('any')
url="http://acoustid.org/chromaprint/"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-gcc")
options=('strip' 'staticlibs')
source=("https://bitbucket.org/acoustid/${_realname}/downloads/${_realname}-${pkgver}.tar.gz")
sha256sums=('4b8ed053d99e35ca3543e41f581659307a705c677e1f7eded91f047a580cebe8')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  #patch -p1 -i ${srcdir}/001-no-undefined.patch
  sed -i 's|PROJECT_VERSION|chromaprint_VERSION|g' \
    "${srcdir}"/${_realname}-${pkgver}/libchromaprint.pc.cmake
}

build() {
  _common_opts=(
    -G"MSYS Makefiles"
    -DCMAKE_BUILD_TYPE=Release \
    -DFFT_LIB=kissfft \
    -DAUDIO_PROCESSOR_LIB="dummy" \
    -DBUILD_TESTS=off \
    -DBUILD_TOOLS=off \
  )

  msg "Build static version"
  [[ -d "${srcdir}/static-${MINGW_CHOST}" ]] && rm -r "${srcdir}/static-${MINGW_CHOST}"
  mkdir -p "${srcdir}/static-${MINGW_CHOST}" && cd "${srcdir}/static-${MINGW_CHOST}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DBUILD_SHARED_LIBS=off \
    "${_common_opts[@]}" \
    ../${_realname}-${pkgver}

  make

  msg "Build shared version"
  [[ -d "${srcdir}/shared-${MINGW_CHOST}" ]] && rm -r "${srcdir}/shared-${MINGW_CHOST}"
  mkdir -p "${srcdir}/shared-${MINGW_CHOST}" && cd "${srcdir}/shared-${MINGW_CHOST}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DBUILD_SHARED_LIBS=on \
    "${_common_opts[@]}" \
    ../${_realname}-${pkgver}

  make
}

package() {
  # static
  cd "${srcdir}/static-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install

  # shared
  cd "${srcdir}/shared-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install
}
