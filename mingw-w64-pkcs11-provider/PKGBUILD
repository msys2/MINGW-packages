_realname=pkcs11-provider
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.5.r92.gcff4036
pkgrel=1
pkgdesc="A pkcs#11 provider for OpenSSL 3.0+ (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/latchset/pkcs11-provider'
msys2_references=('archlinux: pkcs11-provider')
license=('COPYING')
makedepends=("${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-dlfcn"
             "git")
depends=("${MINGW_PACKAGE_PREFIX}-openssl")
# optdepends("${MINGW_PACKAGE_PREFIX}-p11-kit: seamless PKCS#11 modules integration")
_commit='cff40360e4b47d38911999c625249683b7445170'
source=("${_realname}"::"git+https://github.com/latchset/pkcs11-provider.git#commit=${_commit}"
        "patch.patch")
sha256sums=('54e253b1f2363f4f5fa626394538da62734dd641a47c352b5b9b9970827d0b12'
            '91d16126be2d86136a79d285627d263957129b02046269a1c3dfeba38c5016a6')

pkgver() {
  cd "${_realname}"

  git describe --long "${_commit}" | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//g'
}

prepare() {
  cd "${_realname}"

  patch -p1 -i ${srcdir}/patch.patch
}

build() {
  MSYS2_ARG_CONV_EXCL="--prefix=" \
  LDFLAGS="-ldl" CFLAGS="-pthread -Wno-incompatible-pointer-types -D_XOPEN_SOURCE=700" \
    meson setup \
      --prefix="${MINGW_PREFIX}" \
      --wrap-mode=nodownload \
      --auto-features=enabled \
      --buildtype=release \
      "build-${MSYSTEM}" \
      "${_realname}"

  meson compile -C "build-${MSYSTEM}"
}

check() {
  meson test -C "build-${MSYSTEM}"
}

package() {
  # meson install -C "build-${MSYSTEM}" --destdir "${pkgdir}"
  install -Dm755 "${srcdir}/build-${MSYSTEM}/src/pkcs11.dll" "${pkgdir}${MINGW_PREFIX}/lib/ossl-modules/pkcs11.dll"
  # install -Dm644 "${srcdir}/build-${MSYSTEM}/src/pkcs11.dll.a" "${pkgdir}${MINGW_PREFIX}/lib/ossl-modules/pkcs11.dll.a"
  install -Dm644 "${srcdir}/${_realname}/docs/provider-pkcs11.7" "${pkgdir}${MINGW_PREFIX}/share/man/man7/provider-pkcs11.7"

  install -Dm644 "${srcdir}/${_realname}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
