_realname=pkcs11-provider
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.2.0
pkgrel=1
pkgdesc="A pkcs#11 provider for OpenSSL 3.0+ (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/latchset/pkcs11-provider'
msys2_references=(
  'archlinux: pkcs11-provider'
  'anitya: 375566'
)
license=('spdx:Apache-2.0')
makedepends=("${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-dlfcn")
depends=("${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-dlfcn")
optdepends=("${MINGW_PACKAGE_PREFIX}-p11-kit: seamless PKCS#11 modules integration")
source=("https://github.com/latchset/pkcs11-provider/releases/download/v${pkgver}/${_realname}-${pkgver}.tar.xz"{,.asc}
        0002-Fix-i686-build-failures-in-cipher.c.patch
        0003-modulesdir.patch
        "patch.patch")
validpgpkeys=('7C7BD146943B206BB645B64594EAD67E004B65AB') # Simo Sorce <simo@redhat.com>
sha256sums=('36a2f13859f3e2a9c74d1d4064f8d406689b0201e25968aba952010ed73bfec2'
            'SKIP'
            'e6ba83e5210f188b609cd4485128cda91d2de8cbe0cf2ea9acb4b0a2af003c5c'
            '83a438499a0aa69807e6d77449fa7e85a9cda50e93a02ea80d2451a9c4f9406c'
            '939d8c259b2ec06052f1ef90c523740d3e7bd16c17bd1cdd270d9057dd27c062')
noextract=("${_realname}-${pkgver}.tar.xz")

prepare() {
  tar -xJf ${_realname}-${pkgver}.tar.xz || true
  cd "${_realname}-${pkgver}"

  patch -Np1 -i "${srcdir}"/patch.patch
  patch -Np1 -i "${srcdir}"/0002-Fix-i686-build-failures-in-cipher.c.patch
  patch -Np2 -i "${srcdir}"/0003-modulesdir.patch
}

build() {
  LDFLAGS="-ldl -lpthread" \
  MSYS2_ARG_CONV_EXCL="--prefix=;-Dmodulesdir" \
    meson setup \
      --prefix="${MINGW_PREFIX}" \
      --wrap-mode=nodownload \
      --auto-features=enabled \
      --buildtype=release \
      -Dmodulesdir="${MINGW_PREFIX}/lib/ossl-modules" \
      -Ddefault_pkcs11_module=libp11-kit-0.dll \
      "build-${MSYSTEM}" \
      "${_realname}-${pkgver}"

  meson compile -C "build-${MSYSTEM}"
}

package() {
  meson install -C "build-${MSYSTEM}" --destdir "${pkgdir}"

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
