# Maintainer: Fabien Chouteau <chouteau@adacore.com>

_realname=libadalang
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0
pkgrel=1

pkgdesc="Library for parsing and semantic analysis of Ada code (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64')
url="https://github.com/AdaCore/libadalang/"
msys2_references=(
  'aur: libadalang'
)
license=('spdx:GPL-3.0-or-later with GCC-exception-3.1')
makedepends=("${MINGW_PACKAGE_PREFIX}-gprbuild"
             "${MINGW_PACKAGE_PREFIX}-python-docutils"
             "${MINGW_PACKAGE_PREFIX}-python-e3-core"
             "${MINGW_PACKAGE_PREFIX}-python-funcy"
             "${MINGW_PACKAGE_PREFIX}-python-mako"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python-sphinx")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-ada"
         "${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gnatcoll-core"
         "${MINGW_PACKAGE_PREFIX}-gnatcoll-gmp"
         "${MINGW_PACKAGE_PREFIX}-gnatcoll-iconv"
         "${MINGW_PACKAGE_PREFIX}-gpr"
         "${MINGW_PACKAGE_PREFIX}-langkit")
source=("https://github.com/AdaCore/libadalang/archive/${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('c1e449e44650d5384cd013af0a2b19d9c334364b22817a7a521d9d19fbace428')

_library_types="static static-pic relocatable"
_build_mode="prod"

build() {
  [[ -d "${srcdir}"/build-${MSYSTEM} ]] && rm -rf "${srcdir}"/build-${MSYSTEM}
  cp -r "${srcdir}/${_realname}-${pkgver}" "${srcdir}"/build-${MSYSTEM}
  cd "${srcdir}"/build-${MSYSTEM}

  python manage.py generate

  python manage.py build \
    --library-types=static,static-pic,relocatable \
    --build-mode=${_build_mode} \
    --disable-java \
    --gargs="-R"

  cd build/python
  python setup.py build
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}

  # Install the Ada library with its C binding
  python manage.py install \
      --library-types static,static-pic,relocatable \
      --build-mode=${_build_mode} \
      "$pkgdir/${MINGW_PREFIX}"

  # Install the Python binding
  cd build/python

  # TODO: Fix Python bindings installation prefix (now it installs to /ucrt64/python, should go to site-packages)
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1 --skip-build
}
