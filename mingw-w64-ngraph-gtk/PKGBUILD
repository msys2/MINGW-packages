# Maintainer: Hiroyuki Ito <ZXB01226@nifty.com>

_realname=ngraph-gtk
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=6.07.07
pkgrel=1
arch=('any')
pkgdesc="create scientific 2-dimensional graphs (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-adwaita-icon-theme"
         "${MINGW_PACKAGE_PREFIX}-gsettings-desktop-schemas"
         "${MINGW_PACKAGE_PREFIX}-gtk3"
         "${MINGW_PACKAGE_PREFIX}-gtksourceview3"
         "${MINGW_PACKAGE_PREFIX}-readline"
         "${MINGW_PACKAGE_PREFIX}-gsl"
         "${MINGW_PACKAGE_PREFIX}-ruby")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
	     "intltool"
	     "gettext-devel")
options=('strip' 'staticlibs')
license=("GPL")
url="https://htrb.github.io/ngraph-gtk/"
source=("https://github.com/htrb/ngraph-gtk/releases/download/v${pkgver}/ngraph-gtk-${pkgver}.tar.gz")
sha256sums=("26e779e839c816e48de58d4906fcef0c67574a6f640a6b6211d502278c8ba9e0")

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  AUTOPOINT='initltoolize --automake --copy' autoreconf -if
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
  install -Dm644 "${pkgdir}${MINGW_PREFIX}/share/${_realname}/gtksourceview/ngraph-math.lang" "${pkgdir}${MINGW_PREFIX}/share/gtksourceview-3.0/language-specs/ngraph-math.lang"
}
