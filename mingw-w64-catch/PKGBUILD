# Maintainer: David Grayson <davidegrayson@gmail.com>

_realname=catch
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3.5
pkgrel=1
pkgdesc="Multi-paradigm automated test framework for C++ and Objective-C (mingw-w64)"
arch=('any')
url='https://github.com/philsquared/Catch'
checkdepends=("${MINGW_PACKAGE_PREFIX}-gcc"
              "${MINGW_PACKAGE_PREFIX}-cmake")
license=('custom')
source=(${_realname}-${pkgver}.tar.gz::https://github.com/philsquared/Catch/archive/v${pkgver}.tar.gz
       "01-fix-paren-pragma.patch")
sha256sums=('8ce85366f33f73016a8c3209fa5eda6cad4695b582ecde35851278fbdc2122f2'
            'aaf0f445892c55b2e718d1b4c8c592d9232e66e48c562f6330fac410896c1c04')

prepare()
{
  cd "${srcdir}/Catch-${pkgver}"
  patch -p1 -i ../01-fix-paren-pragma.patch
}

check() {
  rm -rf "${srcdir}/test-${MINGW_CHOST}"
  mkdir -p "${srcdir}/test-${MINGW_CHOST}"
  cd "${srcdir}/test-${MINGW_CHOST}"
  cmake "../Catch-${pkgver}/projects/CMake" -G"MSYS Makefiles" -DCMAKE_CXX_FLAGS='--std=gnu++11'
  make
  ./SelfTest.exe
}

package() {
  cd "${srcdir}/Catch-${pkgver}"
  mkdir -p "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
  install -m644 LICENSE_1_0.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE_1_0.txt"
  mkdir -p "${pkgdir}${MINGW_PREFIX}/include/"
  install -m644 single_include/catch.hpp "${pkgdir}${MINGW_PREFIX}/include/"
}
