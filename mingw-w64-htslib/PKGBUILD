# Contributor: Teemu NÃ¤tkinniemi ("teepean") <tnatkinn@gmail.com>

_realname=htslib
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.15.1
pkgrel=3
pkgdesc="library for high-throughput sequencing data formats"
arch=('any')
mingw_arch=('mingw64' 'ucrt64')
url="https://github.com/samtools/htslib"
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-bzip2"
         "${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-xz"
         "${MINGW_PACKAGE_PREFIX}-libsystre"
         "${MINGW_PACKAGE_PREFIX}-libdeflate")
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-tools")
source=("https://github.com/samtools/htslib/releases/download/$pkgver/$_realname-$pkgver.tar.bz2")
sha256sums=('8d7f8bf9658226942eeab70af2a22aca618577eaa8fe2ed9416ee306d5351aa1')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  autoreconf -fvi
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-libcurl \
    --disable-plugins

  make
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  make check
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  make DESTDIR=$pkgdir install

  install -Dm644 ${srcdir}/${_realname}-${pkgver}/LICENSE $pkgdir${MINGW_PREFIX}/share/licenses/$_realname/LICENSE
}

