# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: AlexWMF <alexxwmf@gmail.com>

_realname=qwt
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-qt4"
pkgver=6.1.2
pkgrel=1
pkgdesc="Qt Widgets for Technical Applications for Qt4 (mingw-w64)"
arch=('any')
license=("custom:$pkgname")
url="http://qwt.sourceforge.net/"
depends=("${MINGW_PACKAGE_PREFIX}-qt4")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-make")
options=(!strip staticlibs !buildflags)
install=${_realname}-qt4-${CARCH}.install
source=("http://downloads.sourceforge.net/${_realname}/${_realname}-$pkgver.tar.bz2" \
        "qwtconfig-msys2.pri")
md5sums=('9c88db1774fa7e3045af063bbde44d7d'
         'ea5a595a3153d7330921e309b7e42e3d')

prepare() {
  mkdir -p ${pkgdir}${MINGW_PREFIX}
  local PREFIX_WIN=$(cygpath -am ${pkgdir}${MINGW_PREFIX})

  cd ${srcdir}/${_realname}-${pkgver}
  # copy our config file to the right place
  cp ${srcdir}/qwtconfig-msys2.pri qwtconfig.pri
  sed -s "s|%VALUE_QWT_INSTALL_PREFIX%|${PREFIX_WIN}|g" \
    -i qwtconfig.pri
}

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  ${MINGW_PREFIX}/bin/qmake qwt.pro

  ${MINGW_PREFIX}/bin/mingw32-make # VERBOSE=1
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  ${MINGW_PREFIX}/bin/mingw32-make -j1 install

  mkdir -p ${pkgdir}${MINGW_PREFIX}/bin/
  mv "${pkgdir}${MINGW_PREFIX}"/lib/*.dll "${pkgdir}${MINGW_PREFIX}"/bin/

  local PREFIX_WIN=$(cygpath -am ${pkgdir}${MINGW_PREFIX})

  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i ${pkgdir}${MINGW_PREFIX}/share/qt4/mkspecs/features/qwtconfig.pri
}
