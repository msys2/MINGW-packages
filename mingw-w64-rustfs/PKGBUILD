# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.su>

_realname=rustfs
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
_pkgver=1.0.0-alpha.81
pkgver=${_pkgver//-/_}
_consolever=0.0.12
pkgrel=1
pkgdesc="High-performance, memory-safe distributed object storage (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://rustfs.com/'
msys2_repository_url='https://github.com/rustfs'
license=('spdx:Apache-2.0')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-rust"
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-nasm"
  "${MINGW_PACKAGE_PREFIX}-ninja"
  "${MINGW_PACKAGE_PREFIX}-pkgconf"
  "${MINGW_PACKAGE_PREFIX}-rust-bindgen"
  "${MINGW_PACKAGE_PREFIX}-openssl"
  "${MINGW_PACKAGE_PREFIX}-flatbuffers"
  "${MINGW_PACKAGE_PREFIX}-protobuf"
  'git'
  'unzip'
)
source=(
  "${msys2_repository_url}/rustfs/archive/${_pkgver}/rustfs-${_pkgver}.tar.gz"
  "${msys2_repository_url}/console/releases/download/v${_consolever}/rustfs-console-v${_consolever}.zip"
  "aws-lc-sys.tar.gz::https://crates.io/api/v1/crates/aws-lc-sys/0.36.0/download"
  "aws-lc-sys-clang-fix.patch"
)
noextract=("rustfs-console-v${_consolever}.zip")
sha256sums=('e1929715015f2b9c1764364327649f1ce89cb2d6388c33c1eb2a8c327a68f939'
            '80daffe86d6d94736bfed13970b09f788bf691add1c8f53896ed7898d9f8451f'
            '43a442ece363113bd4bd4c8b18977a7798dd4d3c3383f34fb61936960e8f4ad8'
            '07cb2edffb4222b3a04e904b29f22b18946e953af39167c263754adb8ae1c910')

prepare() {
  cd "${srcdir}/${_realname}-${_pkgver}"

  unzip -o "${srcdir}/rustfs-console-v${_consolever}.zip" -d rustfs/static

  export RUSTUP_TOOLCHAIN=stable-${RUST_CHOST}

  rm rust-toolchain.toml

  patch -d ../aws-lc-sys-0.36.0 -p1 -i ../aws-lc-sys-clang-fix.patch
  cat >> Cargo.toml <<END

[patch.crates-io]
aws-lc-sys.path = "../aws-lc-sys-0.36.0"
END

  cargo update -p aws-lc-sys
  cargo fetch \
    --locked \
    --config='net.git-fetch-with-cli=true' \
    --target "${RUST_CHOST}"
}

build() {
  cd "${_realname}-${_pkgver}"

  export RUSTUP_TOOLCHAIN=stable-${RUST_CHOST}

  # Generate protobuf/flatbuffers code (uses protoc/flatc from distro)
  cargo run --release --frozen --bin gproto

  # Build RustFS
  cargo build --release --frozen -p rustfs --bins
}

package() {
  cd "${srcdir}/${_realname}-${_pkgver}"

  install -Dm755 target/release/rustfs.exe -t "${pkgdir}${MINGW_PREFIX}/bin/"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
