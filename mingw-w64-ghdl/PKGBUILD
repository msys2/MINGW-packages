####
#
# Based on packages found at these URLs
#     https://github.com/ghdl/ghdl/blob/master/dist/msys2-mingw/llvm/PKGBUILD
#     https://github.com/ghdl/ghdl/blob/master/dist/msys2-mingw/mcode/PKGBUILD
#     https://www.archlinux.org/packages/community/x86_64/ghdl-llvm/
#
# Contributor: Tim Stahlhut <stahta01@gmail.com>
#
####

# Normal build command
#   makepkg-mingw -sLf --nocheck --install

_realname=ghdl
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.37
pkgrel=1
pkgdesc='VHDL simulator'
arch=('any')
url='https://github.com/ghdl'
license=('GPL2')
#checkdepends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-clang" "${MINGW_PACKAGE_PREFIX}-gcc-ada")

validpgpkeys=('33C235A34C46AA3FFB293709A328C3A2C3C45C06') # Jakub Jelinek <jakub@redhat.com>
source=("${_realname}-$pkgver.tar.gz::${url}/${_realname}/archive/v${pkgver}.tar.gz"
        "fix-llvm10.patch")
sha256sums=('3008616201cc3b0b596872e4ad59d8bc36e6ee3ff798bdce066828fbdad041e4'
            'e6b2583a5dac0d5908442ad81c112dcfaadb1c8d59c6372b45a6384f324262b4')
prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  patch -p1 -i ${srcdir}/fix-llvm10.patch
}

build() {
  if [[ "$CARCH" == "i686" ]]; then
    echo 'Building ghdl-mcode...'

    [[ -d "${srcdir}"/build-mcode-${CARCH} ]] && rm -rf "${srcdir}"/build-mcode-${CARCH}
    mkdir -p "${srcdir}"/build-mcode-${CARCH} && cd "${srcdir}"/build-mcode-${CARCH}

    ../${_realname}-${pkgver}/configure \
        --prefix=${MINGW_PREFIX} \
        --disable-werror \
        --enable-checks \
        --enable-libghdl \
        --enable-synth \
        LDFLAGS=-static

    make GNATMAKE="gnatmake -j$(nproc)"
  fi

  if [[ "$CARCH" == "x86_64" ]]; then
    echo 'Building ghdl-llvm...'

    [[ -d "${srcdir}"/build-llvm-${CARCH} ]] && rm -rf "${srcdir}"/build-llvm-${CARCH}
    mkdir -p "${srcdir}"/build-llvm-${CARCH} && cd "${srcdir}"/build-llvm-${CARCH}

    ../${_realname}-${pkgver}/configure \
        --prefix=${MINGW_PREFIX} \
        --disable-werror \
        --enable-checks \
        --enable-libghdl \
        --enable-synth \
        --with-llvm-config="llvm-config --link-static" \
        LDFLAGS="-static"
     
    make GNATMAKE="gnatmake -j$(nproc)"
  fi
}

#check() {
#  cd $pkgbase-$pkgver/testsuite
#
#  echo 'Running tests for ghdl-mcode...'
#  GHDL="$srcdir"/ghdl-mcode/ghdl_mcode ./testsuite.sh
#
#  echo 'Running tests for ghdl-llvm...'
#  GHDL="$srcdir"/ghdl-llvm/ghdl1-llvm ./testsuite.sh
#}


_package_ghdl-mcode() {
  pkgdesc="$pkgdesc (mcode backend) (mingw-w64)"
  depends=('zlib-devel')

  cd "${srcdir}"/build-mcode-${CARCH}

  mkdir -p "${pkgdir}${MINGW_PREFIX}/lib"
  make DESTDIR="${pkgdir}" install

  # License
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/doc/licenses.rst ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/licenses.rst
}

_package_ghdl-llvm() {
  pkgdesc="$pkgdesc (LLVM backend) (mingw-w64)"
  depends=(
    'zlib-devel' 
    "${MINGW_PACKAGE_PREFIX}-clang" 
    "${MINGW_PACKAGE_PREFIX}-llvm"
  )
  options=(!emptydirs)

  cd "${srcdir}"/build-llvm-${CARCH}

  mkdir -p "${pkgdir}${MINGW_PREFIX}/lib"

  make DESTDIR="${pkgdir}" install

  # License
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/doc/licenses.rst ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/licenses.rst
}

package_mingw-w64-i686-ghdl() { _package_ghdl-mcode; }
package_mingw-w64-x86_64-ghdl() { _package_ghdl-llvm; }
