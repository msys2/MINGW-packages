# Maintainer: Daniel Kirchner <daniel@ekpyron.org>

_realname=oglp-git
_basename=oglp
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=r116.99543f0
pkgrel=1
pkgdesc="KTX conversion utilities format library (mingw-w64)"
arch=('any')
url="https://github.com/ekpyron/ktxutils"
license=("GPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake" git)
depends=("${MINGW_PACKAGE_PREFIX}-glm")
options=('staticlibs' 'strip')
source=('git+git://github.com/ekpyron/oglp')
md5sums=('SKIP')

pkgver() {
	cd "${srcdir}/$_basename"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/$_basename"
  rm -f build-${MINGW_CHOST}
  mkdir build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ${MINGW_PREFIX}/bin/cmake \
	-G "MSYS Makefiles" \
	-DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} ..
  make
}

package() {
  cd "${srcdir}/$_basename/build-${MINGW_CHOST}"
  make install

  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i ${pkgdir}${MINGW_PREFIX}/lib/cmake/oglp/oglp-config.cmake
}
