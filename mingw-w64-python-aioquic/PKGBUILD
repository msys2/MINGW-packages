# Maintainer: Antoine Martin <totaam@xpra.org>

_realname=aioquic
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.3.0
pkgrel=3
pkgdesc="QUIC and HTTP/3 implementation in Python (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'archlinux: python-aioquic'
  'gentoo: dev-python/aioquic'
  'purl: pkg:pypi/aioquic'
)
url='https://github.com/aiortc/aioquic'
msys2_changelog_url='https://aioquic.readthedocs.io/en/latest/changelog.html'
license=('spdx:BSD-3-Clause')
depends=(
  "${MINGW_PACKAGE_PREFIX}-openssl"
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-python-certifi"
  "${MINGW_PACKAGE_PREFIX}-python-cryptography"
  "${MINGW_PACKAGE_PREFIX}-python-pylsqpack"
  "${MINGW_PACKAGE_PREFIX}-python-pyopenssl"
  "${MINGW_PACKAGE_PREFIX}-python-service-identity"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-python-build"
  "${MINGW_PACKAGE_PREFIX}-python-installer"
  "${MINGW_PACKAGE_PREFIX}-python-setuptools"
)
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('28d070b2183e3e79afa9d4e7bd558960d0d53aeb98bc0cf0a358b279ba797c92')

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    python -m installer --prefix="${MINGW_PREFIX}" \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
