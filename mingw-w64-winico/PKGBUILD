# Maintainer: Oscar Fuentes <ofv@wanadoo.es>

_realname=winico
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.6
pkgrel=1
pkgdesc='Tk extension for Windows for enhanced icon handling and manipulation of an icon in the Windows taskbar and system tray.'
arch=('any') 
url="http://tktable.sourceforge.net/"
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-tk")
source=("http://sourceforge.net/projects/tktable/files/${_realname}/$pkgver/${_realname}${pkgver/./}src.zip"
        001-Fix-GCL_ICON.patch
        002-Fix-pointer-cast.patch)
md5sums=('e8351b14564e9a893757388a10fd2305'
         'dfd5791278c9737f5d8a58871543294d'
         '7bdedca1794128f09abcee5fc49711ba')

_dllname=libWinico${pkgver}.dll

prepare() {
  cd "${srcdir}/winico-${pkgver}"
  patch -Np1 -i "${srcdir}/001-Fix-GCL_ICON.patch"
  patch -Np1 -i "${srcdir}/002-Fix-pointer-cast.patch"
}

build() {
  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"

  source ${MINGW_PREFIX}/lib/tclConfig.sh
  source ${MINGW_PREFIX}/lib/tkConfig.sh

  ${MINGW_PREFIX}/bin/gcc -shared -o $_dllname -O2 \
    ${srcdir}/winico-${pkgver}/tkwinico.c \
    -DPACKAGE_NAME=\"Winico\" -DPACKAGE_VERSION=\"${pkgver}\" \
    -DUSE_TCL_STUBS=1 -DUSE_TK_STUBS=1 -Wno-implicit-int \
    $TK_STUB_LIB_SPEC $TCL_STUB_LIB_SPEC -lgdi32
}

package() {
  cd "${srcdir}/build-${CARCH}"
  _dst="$pkgdir/$MINGW_PREFIX/lib/winico${pkgver}"
  mkdir -p $_dst
  cp *.dll $_dst
  echo -e "This sofware has been modified by the MSYS2 project.\r\n\r\n" > $_dst/license.terms
  cat ${srcdir}/winico-${pkgver}/license.terms >> $_dst/license.terms
  echo "package ifneeded Winico ${pkgver} [list load [file join \$dir $_dllname]]" > $_dst/pkgIndex.tcl
}
