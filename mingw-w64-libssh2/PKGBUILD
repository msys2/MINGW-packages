# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libssh2
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-wincng")
pkgver=1.9.0
pkgrel=4
pkgdesc="A library implementing the SSH2 protocol as defined by Internet Drafts (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://libssh2.org"
license=("BSD")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-openssl"
             "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' 'strip')
source=("${url}/download/${_realname}-${pkgver}.tar.gz"
        fix-pkgconfig.patch
        fix-configure-ac.patch
        fix-wincng-multiple.patch)
sha256sums=('d5fb8bd563305fd1074dda90bd053fb2d29fc4bce048d182f96eaa466dfadafd'
            '7a6545f6d457ad008aacefe04a60727c02d33927c8a903745bf191f69cc8ba55'
            '956b2518618646f9134f0eee7c539ebfb3ddeeff48019831b53997464edbf4f1'
            'fa6a0f26df1086883b88d2f94e0c9b8ba80867dda8271df565d7a177af267c9e')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/fix-pkgconfig.patch
  patch -p1 -i ${srcdir}/fix-configure-ac.patch
  patch -p1 -i ${srcdir}/fix-wincng-multiple.patch
  autoreconf -fi
}

do_build() {
  builddir="${srcdir}/build-${CARCH}-$1"
  [[ -d "$builddir" ]] && rm -rf "${builddir}"
  mkdir -p "${builddir}" && cd "${builddir}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --with-crypto=$1

  make
}

build() {
  do_build openssl
  do_build wincng
}

do_package() {
  cd "${srcdir}/build-${CARCH}-$1"
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}

package_libssh2() {
  depends=("${MINGW_PACKAGE_PREFIX}-openssl"
          "${MINGW_PACKAGE_PREFIX}-zlib")
  provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
  conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}-wincng")
  do_package openssl
}

package_libssh2-wincng() {
  depends=("${MINGW_PACKAGE_PREFIX}-zlib")
  provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
  conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
  do_package wincng
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
