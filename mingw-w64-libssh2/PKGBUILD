# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libssh2
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.4.3
pkgrel=2
pkgdesc="A library implementing the SSH2 protocol as defined by Internet Drafts (mingw-w64)"
arch=('any')
url="http://www.libssh2.org"
license=("BSD")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config" "${_mingw_suff}-zlib")
depends=("${_mingw_suff}-openssl" "${_mingw_suff}-zlib")
groups=("${_mingw_suff}")
options=('staticlibs' 'strip')
source=("$url/download/${_realname}-$pkgver.tar.gz")
md5sums=('071004c60c5d6f90354ad1b701013a0b')

build() {
	mkdir -p "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
	cd "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
	"${srcdir}"/${_realname}-${pkgver}/configure \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--target=${MINGW_CHOST} \
		--with-openssl
	make
}

package() {
	cd "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
	make DESTDIR="$pkgdir" install
}
