_realname=rtl-sdr
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.0.1
pkgrel=1
pkgdesc="Driver for Realtek RTL2832U, allowing general purpose software defined radio (SDR) (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://osmocom.org/projects/rtl-sdr/wiki"
license=('GPL')
depends=("${MINGW_PACKAGE_PREFIX}-libusb>=1.0"
         "${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
source=("rtl-sdr.$pkgver.tgz::https://gitea.osmocom.org/sdr/rtl-sdr/archive/v$pkgver.tar.gz")
sha512sums=('ae481780ed6d084c8fa367c19c1d87007e4d4bf91f183e8e243924aa0369b386f7628f806af74f048978591549c7aeb40e6a5036840ddf52d7008f69edce9271')

build() {
  cd "$srcdir/$_realname"

  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DDETACH_KERNEL_DRIVER=ON -Wno-dev \
      "${extra_config[@]}" \
      -GNinja \
      ../$_realname

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .
}
