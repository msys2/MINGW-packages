# Maintainer: gym603 <gui_yuan_miao@163.com>

_realname=diamond
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.9.24
pkgrel=1
pkgdesc="Accelerated BLAST compatible local sequence aligner. (mingw-w64)"
arch=('any')
url="https://github.com/bbuchfink/diamond"
license=('GPL-3.0')
_archive=${_realname}-${pkgver}
source=(${_archive}.tar.gz::https://github.com/bbuchfink/diamond/archive/v${pkgver}.tar.gz
        0001-change-_MSC_VER-to-_WIN32.patch
        0002-include-intrin.h.patch
        0003-add-explicit-template-instantiation.patch)
sha256sums=('22E8FC3980C2F5D6B584D4FEFA3406172141697F7CB32B9742CB43A593B4FF24'
            '3D8AFDE916F613FB69F6B009AAF3BD17082E30122940C30EA24E3701970E3A79'
            '2B6B4DB040D6366DCA3658DA7ACB17C470362F01761898438DD0B2076778AFAC'
            '4E5055C7F04F77F5C8FE5332E22695E49DA5B7EAC1930735B15EC943A13E14E1')

makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "make"
             "${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-zlib")

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -b -p1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${srcdir}/${_archive}"

  #if [ ! -d .git ]; then
  #  git init
  #  git add .
  #  git commit -m "first commit"
  #fi

  apply_patch_with_msg \
    0001-change-_MSC_VER-to-_WIN32.patch \
    0002-include-intrin.h.patch \
    0003-add-explicit-template-instantiation.patch
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"
  ${MINGW_PREFIX}/bin/cmake \
    -G'MSYS Makefiles' \
    ../${_archive}/
  make -j1
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  ${MINGW_PREFIX}/bin/cmake -DCMAKE_INSTALL_PREFIX:PATH=${pkgdir}${MINGW_PREFIX} -P cmake_install.cmake

  cd "${srcdir}/${_archive}"
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
  install -d "${pkgdir}${MINGW_PREFIX}"/share/doc/${_realname}
  install -m644 {README.md,diamond_manual.pdf} "${pkgdir}${MINGW_PREFIX}"/share/doc/${_realname}
}
