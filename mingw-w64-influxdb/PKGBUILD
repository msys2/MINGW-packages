# Contributor: Liu Pak Hin <wl@m-labs.hk>

_realname=influxdb
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.8.10
pkgrel=1
pkgdesc="An open-source distributed time series database"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://www.influxdata.com/"
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-go")
source=("https://github.com/influxdata/influxdb/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('4f53c61f548bab7cb805af0d02586263d9a348dc18baf90efb142b029e2e7097')

build() {
  cd ${srcdir}/${_realname}-${pkgver}

  export GOOS=windows
  export GOROOT=${MINGW_PREFIX}/lib/go
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GO_LDFLAGS="-s -w"
  export GOFLAGS="-trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external"
  go clean ./...
  go install ./...
}

package() {
  cd ${srcdir}/${_realname}-$pkgver
  install -Dm755 "$(go env GOPATH)/bin/influx.exe" "${pkgdir}${MINGW_PREFIX}/bin/influx.exe"
  install -Dm755 "$(go env GOPATH)/bin/influx_inspect.exe" "${pkgdir}${MINGW_PREFIX}/bin/influx_inspect.exe"
  install -Dm755 "$(go env GOPATH)/bin/influx_stress.exe" "${pkgdir}${MINGW_PREFIX}/bin/influx_stress.exe"
  install -Dm755 "$(go env GOPATH)/bin/influxd.exe" "${pkgdir}${MINGW_PREFIX}/bin/influxd.exe"
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
