# Contributor: https://github.com/taozuhong

_realname=excelize
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-lib${_realname}"
pkgver=2.10.0
pkgrel=1
pkgdesc='Library for reading and writing Microsoft Excelâ„¢ (XLAM / XLSM / XLSX / XLTM / XLTX) spreadsheets in Go. (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/qax-os/excelize'
license=('spdx:BSD-3-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=(${MINGW_PACKAGE_PREFIX}-cmake
             ${MINGW_PACKAGE_PREFIX}-go
             ${MINGW_PACKAGE_PREFIX}-cc
             ${MINGW_PACKAGE_PREFIX}-ninja
             git)
options=('!strip')
source=("https://github.com/qax-os/excelize/archive/refs/tags/v${pkgver}.tar.gz"
		"main.go"
		"types_c.h"
        "excelize.pc.in")
sha256sums=('f6d7305a460721eb9af7f031dce586ccec5221253be12be1edbeb6f9d74ea41c'
			'93b0a4e7e49af550cdef277b74287f72c2107fe6758b4c9e12fe7a78bed15d55'
			'aea4104e368bf6d643c078f04adcc892c09931fd2c42ac8a9f95d07359360160'
            'aeb19680632e6a7bf15293a9fe013afc628e7360124f5ece5b38aa771be3fb69')

prepare() {
    cd "${srcdir}/excelize-${pkgver}"
}

build() {
    cd "${srcdir}"
    [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
    mkdir -p build-${MINGW_CHOST}
	
	cp -f "${srcdir}/main.go" "${srcdir}/excelize-${pkgver}"
    cp -f "${srcdir}/excelize.pc.in" "${srcdir}/build-${MINGW_CHOST}/excelize.pc"
    sed -i "s#MSYSTEM_PREFIX#$MSYSTEM_PREFIX#" "${srcdir}/build-${MINGW_CHOST}/excelize.pc"
    sed -i "s#VERSION#$pkgver#" "${srcdir}/build-${MINGW_CHOST}/excelize.pc"
    cd build-${MINGW_CHOST}

    export GOPROXY=direct 
    export GOOS=windows
    export GOROOT=${MINGW_PREFIX}/lib/go
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GO_LDFLAGS="-s -w"
    # export GOFLAGS="-trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external"
    
    go build -buildmode=c-archive -o excelize.a ${srcdir}/excelize-${pkgver}/main.go
    go build -buildmode=c-shared -o excelize.dll ${srcdir}/excelize-${pkgver}/main.go
}

package() {
    cd "build-${MINGW_CHOST}"
    install -Dm755 "excelize.dll" "${pkgdir}${MINGW_PREFIX}/bin/excelize.dll"
    install -Dm755 "excelize.a" "${pkgdir}${MINGW_PREFIX}/lib/excelize.a"
    install -Dm755 "excelize.pc" "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/excelize.pc"
    install -Dm755 "excelize.h" "${pkgdir}${MINGW_PREFIX}/include/excelize/excelize.h"
    install -Dm755 "${srcdir}/types_c.h" "${pkgdir}${MINGW_PREFIX}/include/excelize/types_c.h"
}
