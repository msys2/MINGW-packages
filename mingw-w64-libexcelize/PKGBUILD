# Contributor: https://github.com/taozuhong

_realname=excelize
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-lib${_realname}"
pkgver=2.9.0
pkgrel=1
pkgdesc='Library for reading and writing Microsoft Excelâ„¢ (XLAM / XLSM / XLSX / XLTM / XLTX) spreadsheets in Go. (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/qax-os/excelize'
license=('spdx:BSD-3-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=(${MINGW_PACKAGE_PREFIX}-cmake
             ${MINGW_PACKAGE_PREFIX}-go
             ${MINGW_PACKAGE_PREFIX}-cc
             ${MINGW_PACKAGE_PREFIX}-ninja)
options=('!strip')
source=("https://github.com/qax-os/excelize/archive/refs/tags/v${pkgver}.tar.gz"
        "main.go"
        "types_c.h")
sha256sums=('3b0238ac400a7685fd0fc426d7efb7c01e5034c9c82b4a8ed71ebb018601b5f7'
            '23511257886b24a87943c2e6d46eeffb3af7bf6f75181a78db2ab33d345d8030'
            '8149f0f6d7e2bc42ebe417200875c42c79cab91935c56ed15cc9c14d19eedb89')

build() {
    cd "${_realname}-${pkgver}"
    export GOPROXY=direct 
    export GOOS=windows
    export GOROOT=${MINGW_PREFIX}/lib/go
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GO_LDFLAGS="-s -w"
    export GOFLAGS="-trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external"
    
    go build -buildmode=c-shared -o libexcelize.dll main.go
}

package() {
    cd "${_realname}-${pkgver}"
    install -Dm755 "libexcelize.dll" "${pkgdir}${MINGW_PREFIX}/bin/libexcelize.dll"
    install -Dm755 "libexcelize.h" "${pkgdir}${MINGW_PREFIX}/include/libexcelize.h"
    install -Dm644 "LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
    install -Dm644 "README.md" "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/README.md"
}
