# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=python_ics
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=917.18
pkgrel=1
pkgdesc="Library for interfacing with Intrepid devices in Python (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/intrepidcs/python_ics/'
msys2_references=(
  'pypi: python-ics'
)
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python-wheel"
             "${MINGW_PACKAGE_PREFIX}-python-dunamai"
             "${MINGW_PACKAGE_PREFIX}-clang"
             'git')
source=("git+https://github.com/intrepidcs/python_ics.git#tag=v${pkgver}")
sha256sums=('d56973f3a9e8655dff26e7328bca2e9545099072c33d3c0a3824e7c4ee13aaff')

prepare() {
  git -C "${_realname}" submodule update --init --recursive
}

build() {
  cp -r "${_realname}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
