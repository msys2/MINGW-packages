# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_font=fira
_realname=ttf-${_font}code-nerd
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgdesc="Patched font Fira (Fura) Code from nerd fonts library (mingw-w64)"
pkgver=3.0.2
pkgrel=1
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://github.com/ryanoasis/nerd-fonts'
_urlbase="${url}/releases/download/v${pkgver}"
license=('spdx:OFL-1.1')
options=('!debug' '!strip')
groups=("${MINGW_PACKAGE_PREFIX}-nerd-fonts")
source=("${_font}-${pkgver}.tar.xz::${_urlbase}/FiraCode.tar.xz")
sha256sums=('76c1d691cea44b0cae4d6add56bb3ef52b83cedebb1c5f519b62d068f8586b93')
noextract=("${_font}-${pkgver}.tar.xz")

prepare() {
  rm -rf ${_font}
  mkdir ${_font}
  bsdtar xf ${_font}-${pkgver}.tar.xz -C ${_font}
}

package() {
  install -Dm644 ${_font}/*.ttf -t "${pkgdir}${MINGW_PREFIX}/share/fonts/TTF"
  install -Dm644 ${_font}/LICENSE -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/"
}
