# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=libgxps
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.2.3
pkgrel=1
arch=('any')
pkgdesc="A library to handling and rendering XPS documents (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-gtk3"
         "${MINGW_PACKAGE_PREFIX}-cairo"
         "${MINGW_PACKAGE_PREFIX}-lcms2"
         "${MINGW_PACKAGE_PREFIX}-libarchive"
         "${MINGW_PACKAGE_PREFIX}-libjpeg"
         "${MINGW_PACKAGE_PREFIX}-libxslt"
         "${MINGW_PACKAGE_PREFIX}-libpng")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "make"
             "intltool"
             "gettext-devel")
options=('strip' 'staticlibs')
license=("LGPL 2.1")
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver:0:3}/${_realname}-${pkgver}.tar.xz
        0001-give-cc-to-gir-scanner.all.patch
        0002-missing-include.patch
        0003-missing-cflags-libs.patch)
sha256sums=('e257f7d5b3c2893f78039bec5a7b0796bc2e6d18d6af5fb929d783e6043e6927'
            '93043f118701f0b668ca567875c3f6d4f3f1036680e76bc6f160b6263dd2d6c5'
            '85e7ce99a0286510f44dad9468faf991b4852f0e6e19c70842a4b38fc6f71919'
            'ce92eb203e18053eb33c5572a89d194ea078e1092ed460602665b1e56615ef60')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-give-cc-to-gir-scanner.all.patch
  patch -p1 -i ${srcdir}/0002-missing-include.patch
  patch -p1 -i ${srcdir}/0003-missing-cflags-libs.patch

  autoreconf -fi
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  mkdir -p docs/reference/html
  cp -rf ../${_realname}-${pkgver}/docs/reference/html/* docs/reference/html

  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
