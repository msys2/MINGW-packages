# Maintainer: Wu, Zhenyu <whenyu@ustc.edu>

_realname=jq-lsp
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.1.15
pkgrel=1
pkgdesc="jq language server (mingw-w64)"
arch=(any)
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url="https://github.com/wader/jq-lsp"
msys2_repository_url='https://github.com/wader/jq-lsp'
msys2_references=(
  'purl: pkg:golang/github.com/wader/jq-lsp'
)
license=('MIT')
makedepends=("$MINGW_PACKAGE_PREFIX-go" "$MINGW_PACKAGE_PREFIX-cc")
# LTO renders GO BUILDID unreproducible: https://gcc.gnu.org/bugzilla/show_bug.cgi?id=108534
options=(!lto)
source=("${url}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('34a693262ca1df0375701847962c43043ab4a2dd720ed637ce8f73d34243db97')

prepare() {
  cd "${_realname}-${pkgver}"
  mkdir -p build
}

build() {
  cd "${_realname}-${pkgver}"
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    -o build/jq-lsp.exe \
    .
}

package() {
  cd "${_realname}-${pkgver}"
  install -Dm 755 "build/${_realname}.exe" -t "${pkgdir}/$MINGW_PREFIX/bin"
  install -Dm 644 LICENSE  -t "${pkgdir}/$MINGW_PREFIX/share/licenses/${_realname}"
}

# vim:set ts=2 sw=2 et:
