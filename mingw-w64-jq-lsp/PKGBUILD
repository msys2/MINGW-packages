# Maintainer: Wu, Zhenyu <whenyu@ustc.edu>

_realname=jq-lsp
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.1.4
pkgrel=1
pkgdesc="jq language server (mingw-w64)"
arch=(any)
mingw_arch=('mingw32' 'mingw64' 'ucrt32' 'ucrt64' 'clang32' 'clang64' 'clangarm64')
url="https://github.com/wader/jq-lsp"
license=('MIT')
makedepends=("$MINGW_PACKAGE_PREFIX-go")
# LTO renders GO BUILDID unreproducible: https://gcc.gnu.org/bugzilla/show_bug.cgi?id=108534
options=(!lto)
source=("${url}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('709ce6485e53d84e8e4c5f993277633966da9676920a867995e19528be3500b1')

prepare() {
  cd "${_realname}-${pkgver}"
  mkdir -p build
}

build() {
  cd "${_realname}-${pkgver}"
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" ${_BUILDINFO}" \
    -o build/jq-lsp.exe \
    .
}

package() {
  cd "${_realname}-${pkgver}"
  install -Dm 755 "build/${_realname}.exe" -t "${pkgdir}/$MINGW_PREFIX/bin"
  install -Dm 644 LICENSE  -t "${pkgdir}/$MINGW_PREFIX/share/licenses/${_realname}"
}

# vim:set ts=2 sw=2 et:
