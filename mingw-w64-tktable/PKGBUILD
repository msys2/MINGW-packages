# Maintainer: Oscar Fuentes
# Contributor: Ray Donnelly <mingw.android@gmail.com>
# Based on Arch's PKGBUILD

_realname=tktable
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.12.1
pkgrel=1
_reltag=tktable-${pkgver//./-}
pkgdesc="A full-featured 2D table widget for Tk (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/bohagan1/TkTable/"
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-tk")
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc")
source=("https://github.com/bohagan1/TkTable/archive/${_reltag}.tar.gz")
sha256sums=('a35647bf168f5de071c312fba592d4187a927acc5f9ac65502b647d9ab48ccab')

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  ../TkTable-${_reltag}/configure \
    --prefix=${MINGW_PREFIX}

  make
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
}
