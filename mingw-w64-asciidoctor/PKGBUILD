# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=asciidoctor
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.5.5
pkgrel=3
pkgdesc="An implementation of AsciiDoc in Ruby (mingw-w64)"
arch=('any')
url='http://asciidoctor.org'
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-ruby")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
source=(https://rubygems.org/downloads/${_realname}-${pkgver}.gem)
install=${MINGW_PACKAGE_PREFIX}-ruby.install
sha256sums=('87effa3de95919df880aab04c6c29c89255ce671abce361da476208734a5424e')

check() {
  local _gemdir="$(ruby -e 'puts Gem.default_dir')"
  _gemdir="$(cygpath -u ${_gemdir})"

  ${MINGW_PREFIX}/bin/gem check --verbose \
    "${_realname}-${pkgver}.gem"
}

package() {
  local _gemdir="$(${MINGW_PREFIX}/bin/ruby -e 'puts Gem.default_dir')"
  _gemdir="$(cygpath -u ${_gemdir})"

  ${MINGW_PREFIX}/bin/gem install --ignore-dependencies --no-user-install --verbose \
    -i "${pkgdir}/${_gemdir}" -n "${pkgdir}${MINGW_PREFIX}/bin" \
    "${_realname}-${pkgver}.gem"

  local _ruby_exe=$(cygpath -m ${MINGW_PREFIX}/bin/ruby.exe)

  #for this conversion, we want the fully qualified ruby .exe path
  #so that we can be sure that we are calling the correct one.
  sed -e "s|${_ruby_exe}|${MINGW_PREFIX}/bin/ruby|g" \
     -i ${pkgdir}${MINGW_PREFIX}/bin/asciidoctor \
     -i ${pkgdir}${MINGW_PREFIX}/bin/asciidoctor-safe

  #for Win32 .bat files, we want to drop the path references as the path
  #refs are not needed since it's involved from the same dir.  If we did
  #need fully-qualified pathes, they would have to be fully-qualified and
  # in DOS format.
  local _inst_dir=$(cygpath -m ${pkgdir}/${MINGW_PREFIX}/bin/)
  local _w_ruby_exe=$(cygpath -w ${MINGW_PREFIX}/bin/ruby.exe | sed 's/\\/\\\\/g')
  sed  -e "s|${_w_ruby_exe}|ruby.exe|g" \
      -e "s|${_inst_dir}||g" \
     -i ${pkgdir}${MINGW_PREFIX}/bin/asciidoctor.bat \
     -i ${pkgdir}${MINGW_PREFIX}/bin/asciidoctor-safe.bat

  install -Dm644 "${pkgdir}/$_gemdir/gems/${_realname}-${pkgver}/LICENSE.adoc" \
    "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.adoc"
  install -Dm644 "${pkgdir}/${_gemdir}/gems/${_realname}-${pkgver}/man/asciidoctor.1" \
    "${pkgdir}${MINGW_PREFIX}/share/man/man1/asciidoctor.1"

  rm "${pkgdir}/${_gemdir}/cache/${_realname}-${pkgver}.gem"
  rm -rf "${pkgdir}${MINGW_PREFIX}/${_gemdir}/gems/${_realname}-${pkgver}/man"
}
