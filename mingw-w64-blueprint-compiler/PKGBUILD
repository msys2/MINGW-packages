# Maintainer: Mazhar Hussain <realmazharhussain@gmail.com>

_realname=blueprint-compiler
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=0.12.0
pkgrel=1
pkgdesc='A markup language for GTK user interfaces (mingw-w64)' 
url='https://jwestman.pages.gitlab.gnome.org/blueprint-compiler/'
license=('spdx:LGPL-3.0-or-later')
arch=(any)
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
depends=(${MINGW_PACKAGE_PREFIX}-python-gobject)
makedepends=(${MINGW_PACKAGE_PREFIX}-meson)
source=(https://gitlab.gnome.org/jwestman/blueprint-compiler/-/archive/v$pkgver/blueprint-compiler-v$pkgver.tar.gz)
sha256sums=('6dbb4ea851cec164030abded5949ea77ff92032e23527e1c0597d7efe0c36a81')

build() {
  MSYS2_ARG_CONV_EXCL="--prefix=" \
  "${MINGW_PREFIX}"/bin/meson setup \
    --prefix="${MINGW_PREFIX}" \
    $_realname-v$pkgver \
    build-${MSYSTEM}

  meson compile -C build-${MSYSTEM}
}

check() {
  meson test -C build-${MSYSTEM}
}

package() {
  meson install -C build-${MSYSTEM} --destdir="$pkgdir"
  install -Dm644 $_realname-v$pkgver/COPYING "$pkgdir/$MINGW_PREFIX"/share/licenses/$_realname/COPYING
}
