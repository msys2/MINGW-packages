# Contributor: Christoph Reiter <reiter.christoph@gmail.com>

_realname=atomicwrites
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}" "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=1.1.5
pkgrel=1
pkgdesc='Atomic file writes on POSIX (mingw-w64)'
url='https://github.com/untitaker/python-atomicwrites'
license=('MIT')
arch=('any')
makedepends=("${MINGW_PACKAGE_PREFIX}-python2-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools")
source=("https://pypi.io/packages/source/${_realname:0:1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('240831ea22da9ab882b551b31d4225591e5e447a68c5e188db5b89ca1d487585')

prepare() {
  cd "${srcdir}"

  for builddir in python{2,3}-build-${CARCH}; do  
    rm -rf $builddir | true
    cp -r "${_realname}-${pkgver}" "${builddir}"
  done
}

build() {  
  for pver in {2,3}; do  
    cd "${srcdir}/python${pver}-build-${CARCH}"
    MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
      ${MINGW_PREFIX}/bin/python${pver} setup.py build
  done
}

package_python3-atomicwrites() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3")

  cd "${srcdir}/python3-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/COPYING"
}

package_python2-atomicwrites() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2")

  cd "${srcdir}/python2-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/COPYING"
}

package_mingw-w64-i686-python2-atomicwrites() {
  package_python2-atomicwrites
}

package_mingw-w64-i686-python3-atomicwrites() {
  package_python3-atomicwrites
}

package_mingw-w64-x86_64-python2-atomicwrites() {
  package_python2-atomicwrites
}

package_mingw-w64-x86_64-python3-atomicwrites() {
  package_python3-atomicwrites
}
