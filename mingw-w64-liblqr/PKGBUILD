# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=liblqr
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=0.4.2
pkgrel=2
pkgdesc="A seam-carving C/C++ library called Liquid Rescale (mingw-w64)"
arch=('any')
url="http://liblqr.wikidot.com/"
license=("GPL")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config")
depends=("${_mingw_suff}-glib2")
options=('staticlibs' 'strip')
source=("http://liblqr.wikidot.com/local--files/en:download-page/${_realname}-1-$pkgver.tar.bz2")
md5sums=('915643d993da97e10665d48c0bf8f3d0')

build() {
  cd "$srcdir/${_realname}-1-$pkgver"
  mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}
  ../configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST}
  make
}

package() {
  cd "$srcdir/${_realname}-1-$pkgver/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install
}
