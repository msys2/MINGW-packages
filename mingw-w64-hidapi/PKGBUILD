# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=hidapi
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
_pre=rc1
pkgver=0.8.0${_pre}
pkgrel=1
pkgdesc="Library for communicating with USB and Bluetooth HID devices (mingw-w64)"
arch=('any')
url="http://www.signal11.us/oss/hidapi/"
license=("BSD")
makedepends=("${_mingw_suff}-gcc")
depends=("${_mingw_suff}-gcc-libs")
options=('staticlibs' 'strip')
source=("https://github.com/signal11/hidapi/archive/${_realname}-${pkgver//${_pre}/-${_pre}}.tar.gz")
md5sums=('069f9dd746edc37b6b6d0e3656f47199')

prepare() {
	cd ${srcdir}/${_realname}-${_realname}-${pkgver//${_pre}/-${_pre}}
	autoreconf -vif
}

build() {
	[[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
	mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
	../${_realname}-${_realname}-${pkgver//${_pre}/-${_pre}}/configure \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--enable-shared \
		--disable-static \
		--disable-testgui
	make
}

package() {
    cd "${srcdir}/build-${MINGW_CHOST}"
    make DESTDIR="$pkgdir" install
    #rm -r "$pkgdir/usr/${_arch}/share"
}
