# Maintainer: Naveen M K <naveen521kk@gmail.com>

_realname=dust
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.0.0
pkgrel=1
pkgdesc="A more intuitive version of du in rust (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/bootandy/dust'
msys2_references=(
  'archlinux: dust'
  'purl: pkg:cargo/du-dust'
)
license=('spdx:Apache-2.0')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
source=("${url}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "ntapi-link-ntdll-arm64.patch")
sha256sums=('34b72116ab6db9bdb97bc1e49dadf392a1619838204b44b0a4695539d54ffbe8'
            '9d55cf0896e0dd3001d3bf1e0bf210eaea5b35af4ee44770ba03a433a38e80df')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  cargo vendor \
    --locked \
    --versioned-dirs
  mkdir -p .cargo
  cat >> .cargo/config.toml <<END

[source.crates-io]
replace-with = "vendored-sources"

[source.vendored-sources]
directory = "vendor"
END

  patch -d vendor/ntapi-0.4.1 -Np1 -i "${srcdir}/ntapi-link-ntdll-arm64.patch"
}

build() {
  cp -r "${_realname}-${pkgver}" "build-${MSYSTEM}" && cd "build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/cargo build \
    --release \
    --frozen
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/cargo install \
    --frozen \
    --offline \
    --no-track \
    --path . \
    --root ${pkgdir}${MINGW_PREFIX}

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"

  install -Dm644 completions/dust.bash "${pkgdir}${MINGW_PREFIX}/share/bash-completion/completions/dust"
  install -Dm644 completions/dust.fish "${pkgdir}${MINGW_PREFIX}/share/fish/vendor_completions.d/dust.fish"
  install -Dm644 completions/_dust "${pkgdir}${MINGW_PREFIX}/share/zsh/site-functions/_dust"
  install -Dm644 man-page/dust.1 "${pkgdir}${MINGW_PREFIX}/share/man/man1/dust.1"
}
