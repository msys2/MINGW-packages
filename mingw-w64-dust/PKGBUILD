# Maintainer: Naveen M K <naveen521kk@gmail.com>

_realname=dust
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.8.6
pkgrel=1
pkgdesc="A more intuitive version of du in rust (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://github.com/bootandy/dust'
license=(spdx:Apache-2.0)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-rust"
)
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
source=("${_realname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz"
        "ntapi-link-ntdll-arm64.patch")
sha256sums=('feede818e814011207c5bfeaf06dd9fc95825c59ab70942aa9b9314791c5d6b6'
            '4a6a64884fd7e3da96b1df71adfe12fc909cd85470eb382d6282f5e2b5645fe1')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  cargo vendor \
    --locked \
    --versioned-dirs
  mkdir -p .cargo
  cat >> .cargo/config.toml <<END

[source.crates-io]
replace-with = "vendored-sources"

[source.vendored-sources]
directory = "vendor"
END

  patch -d vendor/ntapi-0.4.1 -Np1 -i "${srcdir}/ntapi-link-ntdll-arm64.patch"
}

build() {
  cd "${srcdir}"
  cp -r ${_realname}-${pkgver} build-${MSYSTEM}
  cd build-${MSYSTEM}
  ${MINGW_PREFIX}/bin/cargo build \
    --release \
    --frozen
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/cargo install \
    --frozen \
    --offline \
    --no-track \
    --path . \
    --root ${pkgdir}${MINGW_PREFIX}

  # Package licenses
  install -Dm644 LICENSE \
    "${pkgdir}${MINGW_PREFIX}"/share/licenses/$pkgname/LICENSE
}
