# Maintainer: Rafa≈Ç Brzegowy <rafal.brzegowy@yahoo.com>

_realname=mfrontgallery
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.1
pkgrel=1
pkgdesc='The MFrontGallery project, examples (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
depends=("${MINGW_PACKAGE_PREFIX}-tfel"
         "${MINGW_PACKAGE_PREFIX}-gnuplot")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
url="https://github.com/thelfer/MFrontGallery"
license=("LGPL-3")
source=("https://github.com/thelfer/MFrontGallery/archive/refs/tags/MFrontGallery-${pkgver}.tar.gz")
sha256sums=('428863a947215b8ad9bc24c2ec1ef14f838658a5c6209c4ffdfbb2f0355d867d')

build() {
  [[ -d "${srcdir}/build-${MSYSTEM}" ]] && rm -rf "${srcdir}/build-${MSYSTEM}"
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

    MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -GNinja \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -Denable-c=ON \
      -Denable-c++=ON \
      -Denable-excel=ON \
      -Denable-fortran=ON \
      -Denable-python=OFF \
      -Denable-java=OFF \
      -Denable-octave=OFF \
      -Denable-generic-behaviours=OFF \
      -Denable-castem=OFF \
      -Denable-castem-behaviours=OFF \
      -Denable-aster=ON \
      -Denable-cyrano=OFF \
      -Denable-ansys=ON \
      -Denable-europlexus=ON \
      -Denable-calculix=ON \
      -Denable-abaqus=ON \
      -Denable-diana-fea=ON \
      -Denable-zmat=OFF \
      -Denable-fortran-behaviours-wrappers=ON \
      -Denable-mfront-documentation-generation=ON \
      -Denable-portable-build=ON \
      ../${_realname}-${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  DESTDIR=${pkgdir} "${MINGW_PREFIX}"/bin/cmake.exe --install .
  install -Dm644 ${srcdir}/${_realname}-${_realname}-${pkgver}/LGPL-3.0.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
