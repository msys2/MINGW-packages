# Maintainer: Alexey Pavlov <Alexpux@gmail.com>
# Contributer: Zach Bacon <11doctorwhocanada@gmail.com>
_realname=ffmpeg
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.1.1
pkgrel=1
pkgdesc="Complete and free Internet live audio and video broadcasting solution (mingw-w64)"
arch=('any')
url="https://ffmpeg.org/"
license=('GPL')
options=('staticlibs' 'strip')
depends=(
        "${MINGW_PACKAGE_PREFIX}-bzip2"
        "${MINGW_PACKAGE_PREFIX}-celt"
        "${MINGW_PACKAGE_PREFIX}-fontconfig"
        "${MINGW_PACKAGE_PREFIX}-gnutls"
        "${MINGW_PACKAGE_PREFIX}-gsm"
        "${MINGW_PACKAGE_PREFIX}-lame"
        "${MINGW_PACKAGE_PREFIX}-libass"
        "${MINGW_PACKAGE_PREFIX}-libbluray"
        "${MINGW_PACKAGE_PREFIX}-libcaca"
        "${MINGW_PACKAGE_PREFIX}-libmodplug"
        "${MINGW_PACKAGE_PREFIX}-libtheora"
        "${MINGW_PACKAGE_PREFIX}-libvorbis"
        "${MINGW_PACKAGE_PREFIX}-libvpx"
        "${MINGW_PACKAGE_PREFIX}-libwebp"
        "${MINGW_PACKAGE_PREFIX}-openal"
        "${MINGW_PACKAGE_PREFIX}-opencore-amr"
        "${MINGW_PACKAGE_PREFIX}-openjpeg2"
        "${MINGW_PACKAGE_PREFIX}-opus"
        "${MINGW_PACKAGE_PREFIX}-rtmpdump"
        "${MINGW_PACKAGE_PREFIX}-schroedinger"
        "${MINGW_PACKAGE_PREFIX}-SDL"
        "${MINGW_PACKAGE_PREFIX}-speex"
        "${MINGW_PACKAGE_PREFIX}-wavpack"
        "${MINGW_PACKAGE_PREFIX}-x264"
        "${MINGW_PACKAGE_PREFIX}-x265"
        "${MINGW_PACKAGE_PREFIX}-xvidcore"
        "${MINGW_PACKAGE_PREFIX}-zlib"
        )
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config" "${MINGW_PACKAGE_PREFIX}-yasm")
source=(https://ffmpeg.org/releases/${_realname}-${pkgver}.tar.bz2{,.asc})
validpgpkeys=('FCF986EA15E6E293A5644F10B4322F04D67658D8')
sha256sums=('a5bca50a90a37b983eaa17c483a387189175f37ca678ae7e51d43e7610b4b3b4'
            'SKIP')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
}

build() {
  [[ -d "build-${MINGW_CHOST}" ]] && rm -rf "build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  
  local opt_conf="--enable-libopenjpeg"
  if [ "${CARCH}" = "i686" ]; then
    opt_conf=""
  fi

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --target-os=mingw32 \
    --arch=${CARCH%%-*} \
    --disable-debug \
    --disable-static \
    --enable-avresample \
    --enable-dxva2 \
    --enable-d3d11va \
    --enable-fontconfig \
    --enable-gnutls \
    --enable-gpl \
    --enable-libass \
    --enable-libbluray \
    --enable-libcaca \
    --enable-libcelt \
    --enable-libfreetype \
    --enable-libgsm \
    --enable-libmodplug \
    --enable-libmp3lame \
    --enable-libopencore_amrnb \
    --enable-libopencore_amrwb \
    ${opt_conf} \
    --enable-libopus \
    --enable-librtmp \
    --enable-libschroedinger \
    --enable-libspeex \
    --enable-libtheora \
    --enable-libvorbis \
    --enable-libx264 \
    --enable-libx265 \
    --enable-libxvid \
    --enable-libvpx \
    --enable-openal \
    --enable-libwavpack \
    --enable-pic \
    --enable-postproc \
    --enable-runtime-cpudetect \
    --enable-shared \
    --enable-static \
    --enable-swresample \
    --enable-version3 \
    --enable-zlib \
    --disable-doc
    #--enable-pthreads

  make -j1
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  # workaround for conflict with SDL main(), use it if you have SDL installed
  #make check CC_C="-c -Umain"
  make check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}${MINGW_PREFIX}"/bin/*.lib "${pkgdir}${MINGW_PREFIX}"/lib/

  #find ${pkgdir}${MINGW_PREFIX}/bin -type f -name "*.exe" -exec objcopy --subsystem console {} \;
  rm -f ${pkgdir}${MINGW_PREFIX}/lib/*.def
  rm -f ${pkgdir}${MINGW_PREFIX}/lib/*.lib
}
