# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=ffmpeg
_mingw_suff=mingw-w64-${CARCH}
pkgname=("${_mingw_suff}-${_realname}")
pkgver=2.1.3
pkgrel=2
pkgdesc="Complete and free Internet live audio and video broadcasting solution (mingw-w64)"
arch=('any')
url="http://ffmpeg.org/"
groups=("${_mingw_suff}")
license=('GPL')
options=('staticlibs' 'strip')
depends=(
		"${_mingw_suff}-bzip2"
		"${_mingw_suff}-fontconfig"
		"${_mingw_suff}-gnutls"
		"${_mingw_suff}-gsm"
		"${_mingw_suff}-lame"
		"${_mingw_suff}-libass"
		"${_mingw_suff}-libbluray"
		"${_mingw_suff}-libcaca"
		"${_mingw_suff}-libmodplug"
		"${_mingw_suff}-libtheora"
		"${_mingw_suff}-libvorbis"
		"${_mingw_suff}-libvpx"
		"${_mingw_suff}-opencore-amr"
		"${_mingw_suff}-openjpeg"
		"${_mingw_suff}-opus"
		"${_mingw_suff}-rtmpdump"
		"${_mingw_suff}-schroedinger"
		"${_mingw_suff}-SDL"
		"${_mingw_suff}-speex"
		"${_mingw_suff}-x264"
		"${_mingw_suff}-xvidcore"
		"${_mingw_suff}-zlib"
		"${_mingw_suff}-openal"
		"${_mingw_suff}-celt"
		"${_mingw_suff}-wavpack"
)
makedepends=("yasm" "${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config")
source=(http://ffmpeg.org/releases/$_realname-$pkgver.tar.bz2)
md5sums=('711b795bbc7b527c0f4a1828f324fd5a')

prepare() {
	cd "${srcdir}/${_realname}-${pkgver}"
}

build() {
	mkdir -p "${srcdir}/build-${MINGW_CHOST}"
	cd "${srcdir}/build-${MINGW_CHOST}"
	#CPPFLAGS+=" --save-temps"
	#CFLAGS+=" --save-temps"

	../${_realname}-${pkgver}/configure \
		--prefix=${MINGW_PREFIX} \
		--target-os=mingw32 \
		--arch=${CARCH%%-*} \
		--disable-debug \
		--disable-static \
		--enable-avresample \
		--enable-dxva2 \
		--enable-fontconfig \
		--enable-gnutls \
		--enable-gpl \
		--enable-libass \
		--enable-libbluray \
		--enable-libcaca \
		--enable-libcelt \
		--enable-libfreetype \
		--enable-libgsm \
		--enable-libmodplug \
		--enable-libmp3lame \
		--enable-libopencore_amrnb \
		--enable-libopencore_amrwb \
		--enable-libopenjpeg \
		--enable-libopus \
		--enable-librtmp \
		--enable-libschroedinger \
		--enable-libspeex \
		--enable-libtheora \
		--enable-libvorbis \
		--enable-libx264 \
		--enable-libxvid \
		--enable-libvpx \
		--enable-openal \
		--enable-libwavpack \
		--enable-pic \
		--enable-postproc \
		--enable-runtime-cpudetect \
		--enable-shared \
		--enable-swresample \
		--enable-version3 \
		--enable-zlib \
		--disable-doc \
		--extra-ldflags="-mno-windows"
		#--enable-pthreads
	make
}

check() {
	cd "${srcdir}/build-${MINGW_CHOST}"
	make check
}

package() {
	cd "${srcdir}/build-${MINGW_CHOST}"
	make DESTDIR="${pkgdir}" install
	mv "${pkgdir}${MINGW_PREFIX}"/bin/*.lib "${pkgdir}${MINGW_PREFIX}"/lib/
	
	find ${pkgdir}${MINGW_PREFIX}/bin -type f -name "*.exe" -exec objcopy --subsystem console {} \;
}
