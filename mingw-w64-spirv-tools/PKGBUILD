# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=spirv-tools
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2016.6
# SPIRV-Tools is unhelpfully versioned using commit messages like so:
# https://github.com/KhronosGroup/SPIRV-Tools/commit/5f5fa3a3f1009f0db845d0c9ed76a39fcfe5b20c
_commit=5f5fa3a3f1009f0db845d0c9ed76a39fcfe5b20c
_spirv_headers_commit=c470b68225a04965bf87d35e143ae92f831e8110
pkgrel=1
pkgdesc="API and commands for processing SPIR-V modules (mingw-w64)"
arch=('any')
url="https://www.khronos.org/vulkan/"
license=('custom')
groups=("${MINGW_PACKAGE_PREFIX}-vulkan-devel")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake" "python3" "git")
source=("git+https://github.com/KhronosGroup/SPIRV-Tools.git#commit=${_commit}"
        "git+https://github.com/KhronosGroup/SPIRV-Headers.git#commit=${_spirv_headers_commit}")
sha256sums=('SKIP'
            'SKIP')

build() {
  cd $srcdir/SPIRV-Tools
  [[ -d "${srcdir}"/build-${CARCH}-shared ]] && rm -rf "${srcdir}"/build-${CARCH}-shared
  mkdir -p "${srcdir}"/build-${CARCH}-shared && cd "${srcdir}"/build-${CARCH}-shared

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi
  msg2 "Building shared library"
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
     -G'MSYS Makefiles' \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      "${extra_config[@]}" \
      -DBUILD_SHARED_LIBS=ON \
      -DSPIRV-Headers_SOURCE_DIR=${srcdir}/SPIRV-Headers \
      $srcdir/SPIRV-Tools
  make
  msg2 "Building static library"
  [[ -d "${srcdir}"/build-${CARCH}-static ]] && rm -rf "${srcdir}"/build-${CARCH}-static
  mkdir -p "${srcdir}"/build-${CARCH}-static && cd "${srcdir}"/build-${CARCH}-static
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
     -G'MSYS Makefiles' \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      "${extra_config[@]}" \
      -DBUILD_SHARED_LIBS=OFF \
      -DSPIRV-Headers_SOURCE_DIR=${srcdir}/SPIRV-Headers \
      $srcdir/SPIRV-Tools
  make
}

package() {
  cd "${srcdir}"/build-${CARCH}-shared
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}"/build-${CARCH}-static
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/${MINGW_PREFIX}/share/licenses/${pkgname}"
  install -m644 $srcdir/SPIRV-Tools/LICENSE "${pkgdir}/${MINGW_PREFIX}/share/licenses/${pkgname}/"
}
