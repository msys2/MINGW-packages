# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=assimp
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}-git"
pkgver=r1803.c0871b3
pkgrel=1
pkgdesc="Portable Open Source library to import various well-known 3D model formats in an uniform manner (mingw-w64)"
arch=('any')
license=('BSD')
depends=("${_mingw_suff}-boost" "${_mingw_suff}-crt" "${_mingw_suff}-zlib")
makedepends=("${_mingw_suff}-cmake" "${_mingw_suff}-gcc" 'unzip')
provides=("${_mingw_suff}-${_realname}")
conflicts=("${_mingw_suff}-${_realname}")
url=('http://assimp.sourceforge.net/index.html')
source=("${_realname}"::"git+https://github.com/assimp/assimp.git")
options=(!strip staticlibs)
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_realname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	mkdir -p ${pkgdir}${MINGW_PREFIX}
	pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
	export PREFIX_WIN=`pwd -W`
	popd > /dev/null
	
	pushd ${MINGW_PREFIX} > /dev/null
	export PREFIX_DEPS=`pwd -W`
	popd > /dev/null

	[[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
	mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
	export DXSDK_DIR=${MINGW_PREFIX}/${MINGW_CHOST}
	
    ${MINGW_PREFIX}/bin/cmake.exe \
	  -G"MSYS Makefiles" \
	  -DCMAKE_INSTALL_PREFIX:PATH=${PREFIX_WIN} \
	  -DASSIMP_BUILD_ASSIMP_TOOLS=YES \
	  -DASSIMP_ENABLE_BOOST_WORKAROUND=OFF \
	  -DASSIMP_BUILD_STATIC_LIB=OFF \
	  -DCMAKE_BUILD_TYPE=RELEASE \
	  -DDirectX_DXERR_LIBRARY=${PREFIX_DEPS}/${MINGW_CHOST}/lib/libdxerr9.a \
	  ../${_realname}
    make
}

package() {
    cd "${srcdir}/build-${MINGW_CHOST}"
    make -j1 install
	mkdir -p ${pkgdir}${MINGW_PREFIX}/bin
	mv ${pkgdir}${MINGW_PREFIX}/lib/*.dll ${pkgdir}${MINGW_PREFIX}/bin/
	install -Dm644 "${srcdir}/${_realname}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
