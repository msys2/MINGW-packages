# Maintainer: Patrick Stewart <patstew@gmail.com>

_realname=jupyter_client
pkgbase=mingw-w64-python-jupyter_client
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}" "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=5.2.3
pkgrel=3
pkgdesc="The reference implementation of the Jupyter protocol (mingw-w64)"
arch=('any')
url="https://jupyter.org/"
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-python2-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/jupyter/jupyter_client/archive/${pkgver}.tar.gz")
sha256sums=('40d4dd389f34748a5fe8814240afdb24fe5672d54a794285a24288a8cfef4a10')

all_build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/${1} setup.py install \
    --prefix=${MINGW_PREFIX} --root=${pkgdir} --optimize=0

  find "${pkgdir}/" -name "*.pyc" -delete
  find "${pkgdir}/" -type d -empty -delete

  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  # fix python command in files
  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*; do
    sed -e "s|${PREFIX_WIN}/bin/|/usr/bin/env |g" -i ${_f}
  done
}

package_python3-jupyter_client() {
  local interpreter=python3
  depends=("${MINGW_PACKAGE_PREFIX}-python3-ipykernel"
           "${MINGW_PACKAGE_PREFIX}-python3-jupyter_core"
           "${MINGW_PACKAGE_PREFIX}-python3-pyzmq")

  all_build ${interpreter}

  install -Dm644 "COPYING.md" "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}

package_python2-jupyter_client() {
  local interpreter=python2
  depends=("${MINGW_PACKAGE_PREFIX}-python2-ipykernel"
           "${MINGW_PACKAGE_PREFIX}-python2-jupyter_core"
           "${MINGW_PACKAGE_PREFIX}-python2-pyzmq")

  all_build ${interpreter}

  install -Dm644 "COPYING.md" "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE"

  cd "${pkgdir}${MINGW_PREFIX}"/bin
  for _f in *; do
    mv ${_f} ${_f/%.*/}2${_f/${_f/%.*/}/}
  done
}

package_mingw-w64-i686-python2-jupyter_client() {
  package_python2-jupyter_client
}

package_mingw-w64-i686-python3-jupyter_client() {
  package_python3-jupyter_client
}

package_mingw-w64-x86_64-python2-jupyter_client() {
  package_python2-jupyter_client
}

package_mingw-w64-x86_64-python3-jupyter_client() {
  package_python3-jupyter_client
}
