# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>

_realname=boto3
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.42.44
pkgrel=1
pkgdesc='The AWS SDK for Python (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'archlinux: python-boto3'
  'gentoo: dev-python/boto3'
  'purl: pkg:pypi/boto3'
)
msys2_changelog_url="https://github.com/boto/boto3/blob/develop/CHANGELOG.rst"
url="https://github.com/boto/boto3"
msys2_repository_url='https://github.com/boto/boto3'
license=('spdx:Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-python-botocore"
         "${MINGW_PACKAGE_PREFIX}-python-jmespath"
         "${MINGW_PACKAGE_PREFIX}-python-s3transfer")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools")
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('d5601ea520d30674c1d15791a1f98b5c055e973c775e1d9952ccc09ee5913c4e')

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
