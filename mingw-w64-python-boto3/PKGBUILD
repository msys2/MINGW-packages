# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>

_realname=boto3
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.26.104
pkgrel=1
pkgdesc='The AWS SDK for Python (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://github.com/boto/boto3"
license=('spdx:Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-python-botocore"
         "${MINGW_PACKAGE_PREFIX}-python-jmespath"
         "${MINGW_PACKAGE_PREFIX}-python-s3transfer")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools")
source=("https://github.com/boto/boto3/archive/$pkgver.tar.gz")
sha256sums=('46b6b703ef1ef662e6f95271b8ecf95c3fbc588ccb3a5ab21e392fce32389be5')

prepare() {
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}"
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
