# Maintainer: Patrick Stewart <patstew@gmail.com>

pkgbase=mingw-w64-python-jupyter_core
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-jupyter_core" "${MINGW_PACKAGE_PREFIX}-python3-jupyter_core")
pkgver=4.1.0
pkgrel=1
pkgdesc="Core common functionality of Jupyter projects. (mingw-w64)"
arch=('any')
url="http://jupyter.org/"
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-python2-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools")

source=("jupyter_core-${pkgver}.tgz::https://github.com/jupyter/jupyter_core/archive/${pkgver}.tar.gz")
md5sums=('b7e928f965f68aef13fea1bf9d6384aa')

all_build() {
  cd "${srcdir}/jupyter_core-${pkgver}"
	
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/${1} setup.py install \
    --prefix=${MINGW_PREFIX} --root=${pkgdir} --optimize=0

  find "${pkgdir}/" -name "*.pyc" -delete
  find "${pkgdir}/" -type d -empty -delete
	
  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  # fix python command in files
  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*; do
    sed -e "s|${PREFIX_WIN}/bin/|/usr/bin/env |g" -i ${_f}
  done
}

package_python3-jupyter_core() {
  local interpreter=python3
  depends=("${MINGW_PACKAGE_PREFIX}-python3")

  all_build ${interpreter}
	
  install -Dm644 "COPYING.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-jupyter_core() {
  local interpreter=python2
  depends=("${MINGW_PACKAGE_PREFIX}-python2")
	
  all_build ${interpreter}
	
  install -Dm644 "COPYING.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	
	cd "${pkgdir}${MINGW_PREFIX}"/bin
  for _f in *; do
    mv ${_f} ${_f/%.*/}2${_f/${_f/%.*/}/}
  done
}

package_mingw-w64-i686-python2-jupyter_core() {
  package_python2-jupyter_core
}

package_mingw-w64-i686-python3-jupyter_core() {
  package_python3-jupyter_core
}

package_mingw-w64-x86_64-python2-jupyter_core() {
  package_python2-jupyter_core
}

package_mingw-w64-x86_64-python3-jupyter_core() {
  package_python3-jupyter_core
}
