# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=wxWidgets
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=3.0.0
pkgrel=1
pkgdesc="A C++ library that lets developers create applications for Windows, Linux and UNIX (mingw-w64)"
arch=('any')
license=("custom:wxWindows")
groups=("${_mingw_suff}")
url="http://www.wxwidgets.org/"
depends=("${_mingw_suff}-gcc-libs"
		"${_mingw_suff}-zlib"
		"${_mingw_suff}-xz"
		"${_mingw_suff}-expat"
		"${_mingw_suff}-libpng"
		"${_mingw_suff}-libjpeg-turbo"
		"${_mingw_suff}-libtiff"
		"${_mingw_suff}-cppunit")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-python2")
options=('strip' 'staticlibs' '!buildflags')
source=("https://sourceforge.net/projects/wxwindows/files/3.0.0/wxWidgets-3.0.0.tar.bz2"
		"wxWidgets-3.0.0-gcc-codelight.patch")
sha1sums=('756a9c54d1f411e262f03bacb78ccef085a9880a'
          '05a3471dcf735d86defe0df00dcb8e9ff0698290')

prepare() {
	cd ${srcdir}/${_realname}-${pkgver}
	patch -p1 -i ${srcdir}/wxWidgets-3.0.0-gcc-codelight.patch
}

build() {
	[[ -d ${srcdir}/build-${CARCH} ]] && rm -rf ${srcdir}/build-${CARCH}
	mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}
	#CPPFLAGS+=" -fno-keep-inline-dllexport"
	../${_realname}-${pkgver}/configure \
		--prefix=${MINGW_PREFIX} \
		--host=${MINGW_CHOST} \
		--target=${MINGW_CHOST} \
		--build=${MINGW_CHOST} \
		--with-msw \
		--disable-mslu \
		--enable-shared \
		--enable-iniconf \
		--enable-iff \
		--enable-permissive \
		--enable-monolithic \
		--enable-unicode \
		--enable-accessibility \
		--disable-precomp-headers
	make -j1 VERBOSE=1
}

package() {
	cd ${srcdir}/build-${CARCH}
	make DESTDIR="${pkgdir}" install

	mv ${pkgdir}${MINGW_PREFIX}/lib/*.dll ${pkgdir}${MINGW_PREFIX}/bin
}
