# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>
# Contributor: Zach Bacon <11doctorwhocanada@gmail.com>
# Contributor: Greg Jung <gvjung@gmail.com>

_realname=wxWidgets
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.0.2
pkgrel=11
pkgdesc="A C++ library that lets developers create applications for Windows, Linux and UNIX (mingw-w64)"
arch=('any')
license=("custom:wxWindows")
url="http://www.wxwidgets.org/"
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-cppunit"
         "${MINGW_PACKAGE_PREFIX}-expat"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libtiff"
         "${MINGW_PACKAGE_PREFIX}-xz"
         "${MINGW_PACKAGE_PREFIX}-zlib"
        )
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-python2")
options=('strip' 'staticlibs' 'buildflags')
source=(http://downloads.sourceforge.net/wxwindows/wxWidgets-${pkgver}.tar.bz2
	"winundef.h.modified"
        "wxWidgets-3.0.0-gcc-codelight.patch"
        "wxWidgets-3.0.2-msw-dc-orientation-fix.patch"
        "wxWidgets-3.0.2-relax-abi-compatibility-gcc.patch")
sha1sums=('6461eab4428c0a8b9e41781b8787510484dea800'
          'f5ab76a4b077b3dc6b8cfd211cbc98941910d787'
          '05a3471dcf735d86defe0df00dcb8e9ff0698290'
          '99999865476ce03bb75e8244806f3585fe9e71e2'
          '62dcd59adde91925e5c51422e192c035978cdedd')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/wxWidgets-3.0.0-gcc-codelight.patch
  patch -p1 -i ${srcdir}/wxWidgets-3.0.2-msw-dc-orientation-fix.patch
  patch -p1 -i ${srcdir}/wxWidgets-3.0.2-relax-abi-compatibility-gcc.patch
#
# 2015/08/02 Greg Jung:
# winundef.h will cause trouble if UNICODE is not pre-defined before including windows.h
# This is an uneccessary condition.
# My attempt to get it into WX: https://groups.google.com/forum/#!topic/wx-dev/MTYzgOqLdfU
#
  cp -p ${srcdir}/${_realname}-${pkgver}/include/wx/msw/winundef.h \
	${srcdir}/${_realname}-${pkgver}/include/wx/msw/winundef.h.original
  cp -p ${srcdir}/winundef.h.modified ${srcdir}/${_realname}-${pkgver}/include/wx/msw/winundef.h.modified
  cp -p ${srcdir}/winundef.h.modified ${srcdir}/${_realname}-${pkgver}/include/wx/msw/winundef.h
}

build() {
  #CXXFLAGS+=" -std=gnu++11"
  [[ -d ${srcdir}/build-${CARCH} ]] && rm -rf ${srcdir}/build-${CARCH}
  mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --enable-shared \
    --enable-iniconf \
    --enable-iff \
    --enable-permissive \
    --enable-unicode \
    --enable-graphics_ctx \
    --enable-accessibility \
    --disable-mediactrl \
    --disable-monolithic \
    --disable-mslu \
    --disable-precomp-headers \
    --with-msw \
    --with-opengl

  make #VERBOSE=1

  #CXXFLAGS+=" -std=gnu++11"
  [[ -d ${srcdir}/build-${CARCH}-static ]] && rm -rf ${srcdir}/build-${CARCH}-static
  mkdir -p ${srcdir}/build-${CARCH}-static && cd ${srcdir}/build-${CARCH}-static

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --enable-static \
    --disable-shared \
    --enable-iniconf \
    --enable-iff \
    --enable-permissive \
    --enable-unicode \
    --enable-graphics_ctx \
    --enable-accessibility \
    --disable-mediactrl \
    --disable-monolithic \
    --disable-mslu \
    --disable-precomp-headers \
    --with-msw \
    --with-opengl

  make #VERBOSE=1
}

package() {
  cd ${srcdir}/build-${CARCH}-static
  make DESTDIR="${pkgdir}" install
  mv ${pkgdir}${MINGW_PREFIX}/bin/wx-config{,-static}

  cd ${srcdir}/build-${CARCH}
  make DESTDIR="${pkgdir}" install
  
  mv ${pkgdir}${MINGW_PREFIX}/lib/*.dll ${pkgdir}${MINGW_PREFIX}/bin

  # License files
  cd "${srcdir}/${_realname}-${pkgver}/docs"
  install -Dm644 preamble.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/preamble.txt"
  install -Dm644 licence.txt  "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/licence.txt"
  install -Dm644 licendoc.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/licendoc.txt"
  install -Dm644 lgpl.txt     "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/lgpl.txt"
  install -Dm644 gpl.txt      "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/gpl.txt"
  install -Dm644 xserver.txt  "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/xserver.txt"
}
