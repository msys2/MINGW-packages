# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=winpthreads
_mingw_suff=mingw-w64-${CARCH}
pkgname=("${_mingw_suff}-${_realname}-svn" "${_mingw_suff}-libwinpthread-svn")
_ver_base=4.0.0
pkgver=4.0.0.6596
pkgrel=1
pkgdesc="MinGW-w64 winpthreads library"
arch=('any')
url="http://mingw-w64.sourceforge.net"
license=('custom')
groups=("${_mingw_suff}-toolchain" "${_mingw_suff}")
makedepends=("subversion" "${_mingw_suff}-gcc" "${_mingw_suff}-binutils" "${_mingw_suff}-crt-svn" "${_mingw_suff}-headers-svn")
options=('strip' '!buildflags' 'staticlibs' '!emptydirs' '!debug')
source=("${_realname}"::"svn+svn://svn.code.sf.net/p/mingw-w64/code/trunk/mingw-w64-libraries/winpthreads")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_realname}"
  local ver="$(svnversion)"
  printf "%s.%s" "${_ver_base}" "${ver//[[:alpha:]]}"
}

build() {

    cd ${srcdir}/${_realname}
    mkdir -p ${srcdir}/winpthreads-build-${MINGW_CHOST} && cd ${srcdir}/winpthreads-build-${MINGW_CHOST}

  declare -a extra_config
  if check_option "debug" "y"; then
    extra_config+=("CFLAGS=-O0 -g -DWINPTHREAD_DBG")
    extra_config+=("CXXFLAGS=-O0 -g -DWINPTHREAD_DBG")
    extra_config+=("CXXFLAGS=")
  fi

    ${srcdir}/${_realname}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared \
    "${extra_config[@]}"

    # I failed to figure out how to disable stripping, this
    # will have to do instead.
    if check_option "strip" "n"; then
      sed -i 's,^STRIP = .*$,STRIP = true,g'                Makefile
      sed -i 's,^STRIP=.*$,STRIP=true,g'                    libtool
      sed -i 's,old_striplib=.*$strip,old_striplib=true,g'  libtool
      sed -i 's,striplib=.*$,striplib=true,g'               libtool
    fi

    make
}

package_winpthreads() {
  depends=("${_mingw_suff}-crt-svn" "${_mingw_suff}-libwinpthread-svn=${pkgver}")
  provides=("${_mingw_suff}-${_realname}")
  conflicts=("${_mingw_suff}-${_realname}")

    cd ${srcdir}/winpthreads-build-${MINGW_CHOST}
    make DESTDIR=${pkgdir} install
  
  mkdir -p ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}
  mv ${pkgdir}${MINGW_PREFIX}/include ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}/
  mv ${pkgdir}${MINGW_PREFIX}/lib ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}/
  rm -rf ${pkgdir}${MINGW_PREFIX}/bin
  
  install -Dm644 ${srcdir}/${_realname}/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}

package_libwinpthread() {
  provides=("${_mingw_suff}-libwinpthread")
  conflicts=("${_mingw_suff}-libwinpthread")

    cd ${srcdir}/winpthreads-build-${MINGW_CHOST}
    make DESTDIR=${pkgdir} install
  
  rm -rf ${pkgdir}${MINGW_PREFIX}/include
  rm -rf ${pkgdir}${MINGW_PREFIX}/lib
}

# Wrappers for package functions

# 32-bit wrappers
package_mingw-w64-i686-winpthreads-svn() {
  package_winpthreads
}

package_mingw-w64-i686-libwinpthread-svn() {
  package_libwinpthread
}

# 64-bit wrappers
package_mingw-w64-x86_64-winpthreads-svn() {
  package_winpthreads
}

package_mingw-w64-x86_64-libwinpthread-svn() {
  package_libwinpthread
}
