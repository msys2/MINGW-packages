# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=winpthreads
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}-svn"
_ver_base=4.0.0
pkgver=4.0.0.6417
pkgrel=1
pkgdesc="MinGW-w64 winpthreads library"
arch=('any')
url="http://mingw-w64.sourceforge.net"
license=('custom')
groups=("${_mingw_suff}-toolchain" "${_mingw_suff}")
makedepends=("subversion" "${_mingw_suff}-gcc" "${_mingw_suff}-binutils" "${_mingw_suff}-crt-svn" "${_mingw_suff}-headers-svn")
provides=("${_mingw_suff}-${_realname}")
conflicts=("${_mingw_suff}-${_realname}")
depends=("${_mingw_suff}-crt-svn")
optdepends=()
backup=()
options=('!strip' '!buildflags' 'staticlibs' '!emptydirs' 'debug')
source=("${_realname}"::"svn+svn://svn.code.sf.net/p/mingw-w64/code/trunk/mingw-w64-libraries/winpthreads")
md5sums=(SKIP)

pkgver() {
  cd "${srcdir}/${_realname}"
  local ver="$(svnversion)"
  printf "%s.%s" "${_ver_base}" "${ver//[[:alpha:]]}"
}

build() {
    mkdir -p ${srcdir}/winpthreads-build-${MINGW_CHOST} && cd ${srcdir}/winpthreads-build-${MINGW_CHOST}

    ${srcdir}/${_realname}/configure \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
        --target=${MINGW_CHOST} \
		--enable-static \
		--enable-shared \
		CFLAGS="-O0 -g" \
		CXXFLAGS="-O0 -g" \
		LDFLAGS=""
    make
}

setup() {
    cd ${srcdir}/winpthreads-build-${MINGW_CHOST}
    make DESTDIR=${destdir} install
}

package() {
    cd ${srcdir}/winpthreads-build-${MINGW_CHOST}
    make DESTDIR=${pkgdir} install
	
	mkdir -p ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}
	mv ${pkgdir}${MINGW_PREFIX}/include ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}/
	mv ${pkgdir}${MINGW_PREFIX}/lib ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}/
	
	install -Dm644 ${srcdir}/${_realname}/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
