# Author: williamjcm <w.jcm59@gmail.com>
# Contributor: mosra <mosra@centrum.cz>
# Based on the Corrade Arch Linux PKGBUILD maintained by xyproto and the MSYS2/MinGW PKGBUILD templates

_realname=corrade
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2019.10
pkgrel=2
pkgdesc='C++11/C++14 multiplatform utility library'
arch=('any')
url='https://magnum.graphics/corrade/'
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake" "${MINGW_PACKAGE_PREFIX}-ninja" 'git')
# The .tar.gz / .zip download contains symlinks (.travis.yml), making tar and
# unzip on Windows grumpy because dangling symlinks are created. Could be fixed
# by manually extracting everything except symlinks, but that's hard to
# maintain. Downloading a Git tag works.
source=("${_realname}-${pkgver}"::"git+https://github.com/mosra/corrade.git#tag=v$pkgver"
        "001-corrade-2019.10-fix-corrademain-on-i686.patch")
sha256sums=('SKIP'
            '9b1434577f13c711e9bb703744e8042940725150481cce555bcf456ffb684675')

prepare() {
    cd "${srcdir}"/${_realname}-${pkgver}

    patch -p1 -i "${srcdir}"/001-corrade-2019.10-fix-corrademain-on-i686.patch
}

build() {
    mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}

    declare -a extra_config
    if check_option "debug" "n"; then
        extra_config+=("-DCMAKE_BUILD_TYPE=Release")
    else
        extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
    fi

    MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
        ${MINGW_PREFIX}/bin/cmake \
            -G'Ninja' \
            -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
            "${extra_config[@]}" \
            ../${_realname}-${pkgver}
    ninja
}

package() {
    cd "${srcdir}"/build-${CARCH}
    DESTDIR="${pkgdir}" ninja install

    install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" \
        "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
