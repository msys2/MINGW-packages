# Maintainer: ImperatorS79 <fevrier.simon@gmail.com>

_realname=gmsh
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.11.1
pkgrel=3
pkgdesc="3D finite element mesh generator with built-in CAD engine and post-processor (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://gmsh.info"
msys2_repository_url="https://gitlab.onelab.info/gmsh/gmsh/"
license=("spdx:GPL-2.0-or-later")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-eigen3"
             "${MINGW_PACKAGE_PREFIX}-petsc"
             "${MINGW_PACKAGE_PREFIX}-slepc")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-cairo"
         "${MINGW_PACKAGE_PREFIX}-fltk"
         "${MINGW_PACKAGE_PREFIX}-gmp"
         "${MINGW_PACKAGE_PREFIX}-libjpeg"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-med"
         "${MINGW_PACKAGE_PREFIX}-opencascade"
         "${MINGW_PACKAGE_PREFIX}-zlib")
source=(https://gmsh.info/src/gmsh-${pkgver}-source.tgz
        0001-fix-link-lib-name.patch
        0002-fix-dynamic-linking.patch
        0003-missing-include.patch::https://gitlab.onelab.info/gmsh/gmsh/-/commit/fb81a9c9.patch
        0004-missing-include-2.patch::https://gitlab.onelab.info/gmsh/gmsh/-/commit/aceb09c8.patch)
sha256sums=('c5fe1b7cbd403888a814929f2fd0f5d69e27600222a18c786db5b76e8005b365'
            'e575ae8fc757694affb6749dc9d52c336691b06159845655ef2ac27aebae8159'
            '4d3f5c4190529e3d45b786adab83f0d65e726357d7367f682ff95b733a88a8c3'
            'c315dc4912191b2821fe44b9e75799cb6503bf800010c6bb9d34ee0ed5f0398f'
            'beb9404bbb9377e6240b369dd70cfc317209202cb32bdc55f4e6fc5837b2da12')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${srcdir}/gmsh-${pkgver}-source"

  apply_patch_with_msg \
    0001-fix-link-lib-name.patch \
    0002-fix-dynamic-linking.patch \
    0003-missing-include.patch \
    0004-missing-include-2.patch
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  CFLAGS+=" -Wno-implicit-function-declaration" \
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${_extra_config[@]}" \
      -DENABLE_BUILD_DYNAMIC=ON \
      -DENABLE_BUILD_SHARED=ON \
      -DENABLE_CAIRO=ON \
      -DENABLE_OCC=ON \
      -DENABLE_OPENMP=ON \
      -DENABLE_OPENACC=ON \
      -DENABLE_PETSC=ON \
      -DENABLE_SLEPC=ON \
      -DGMSH_RELEASE=ON \
      ../gmsh-${pkgver}-source

  "${MINGW_PREFIX}"/bin/cmake --build .
}

check () {
  cd "${srcdir}/build-${MSYSTEM}"
  "${MINGW_PREFIX}"/bin/ctest .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake --install .

  mv "${pkgdir}${MINGW_PREFIX}"/lib/*.dll "${pkgdir}${MINGW_PREFIX}"/bin/
}
