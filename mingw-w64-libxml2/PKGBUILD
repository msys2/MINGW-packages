# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

_realname=libxml2
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.9.2
pkgrel=6
arch=('any')
pkgdesc="XML parsing library, version 2 (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gettext"
         "${MINGW_PACKAGE_PREFIX}-xz"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config" "${MINGW_PACKAGE_PREFIX}-python2")
options=('strip' 'staticlibs')
license=(MIT)
url="http://www.xmlsoft.org/"
install=${_realname}-${CARCH}.install
source=("http://xmlsoft.org/sources/${_realname}-${pkgver}.tar.gz"
        "mingw32-libxml2-static-build-compile-fix.patch"
        "libxml2-2.9.2-catalog-revert.patch"
        0004-detect-ws2tcpip.mingw.patch
        0005-fix-char-cast-warning.mingw.patch
        0006-fix-unused-var-warning.mingw.patch
        0007-fix-stat-redefinition.mingw.patch
        0008-include-winsock2.h-before-windows.h.mingw.patch
        0009-use-wstat-appropriate-for-mingw-w64.mingw.patch
        0010-mingw-w64-defines-lots-of-errnos.mingw.patch
        0011-more-winsock-inclusion-order-fixes.mingw.patch
        0012-socklen-is-signed-on-mingw-w64.mingw.patch
        0013-fix-field-type-signedness.all.patch
        0015-fix-unused-parameters-warning.all.patch
        0016-WARNING-to-be-fixed.all.patch
        0017-fix-const-warning.all.patch
        0018-function-declaration-isnt-prototype.all.patch
        0019-unused-flags.all.patch
        0020-fix-size_t-format-specifier.all.patch
        0023-fix-sitedir-detection.mingw.patch
        0024-shrext-pyd.mingw.patch
        0025-mingw-python-dont-remove-dot.patch)
md5sums=('9e6a9aca9d155737868b3dc5fd82f788'
         '0df377025082cd93cccbca547f048011'
         '29913184b0072167ae427fa91b2edd4a'
         'e3584e2079ba68c4c4c71208663602e0'
         '6db61e81820ad584b9cd6c7d0bae2d51'
         'e526d4e285579c5c8b9c61e889338454'
         '5588d05865d113a81b0f2c514a7f0d0c'
         'c86cd8dc742d323d243e6dc99dab346c'
         '1840d1087f7c3b21219b9d196c5a5930'
         '2784f4e947952ffc3475c111acb72d01'
         'ad9137ca850b62ada756a1706bcc65e1'
         '6b65d303079b21e1c93ec169d5f3f4d0'
         '543b01b756fa64fafb20819d20c984e6'
         '6adb6886030e33bd40c29724bece6ee8'
         'd9e7c8cbc18c76575fd870ca2e39b66a'
         'de11e27ada1d7408aae9b54ed77aafe6'
         'af8b3dbb628682b1b14272fa314b8cd6'
         '30610488e5c8e98f22e226541c95eca9'
         'cc26dda9ff1ddebfc5601b476244fdf6'
         'e19a328f9e6ae6b61bf00331696f7c44'
         'e6c6a26fbde5ead6bf00efbec603d5fd'
         '4a7a5b6027a5dbbfa1a3d7b5787e97bc')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -Np0 -i "${srcdir}"/mingw32-libxml2-static-build-compile-fix.patch
  patch -p1 -i ${srcdir}/libxml2-2.9.2-catalog-revert.patch

  patch -p1 -i ${srcdir}/0004-detect-ws2tcpip.mingw.patch
  patch -p1 -i ${srcdir}/0005-fix-char-cast-warning.mingw.patch
  patch -p1 -i ${srcdir}/0006-fix-unused-var-warning.mingw.patch
  patch -p1 -i ${srcdir}/0007-fix-stat-redefinition.mingw.patch
  patch -p1 -i ${srcdir}/0008-include-winsock2.h-before-windows.h.mingw.patch
  patch -p1 -i ${srcdir}/0009-use-wstat-appropriate-for-mingw-w64.mingw.patch
  patch -p1 -i ${srcdir}/0010-mingw-w64-defines-lots-of-errnos.mingw.patch
  patch -p1 -i ${srcdir}/0011-more-winsock-inclusion-order-fixes.mingw.patch
  patch -p1 -i ${srcdir}/0012-socklen-is-signed-on-mingw-w64.mingw.patch
  patch -p1 -i ${srcdir}/0013-fix-field-type-signedness.all.patch
  patch -p1 -i ${srcdir}/0015-fix-unused-parameters-warning.all.patch
  patch -p1 -i ${srcdir}/0016-WARNING-to-be-fixed.all.patch
  patch -p1 -i ${srcdir}/0017-fix-const-warning.all.patch
  patch -p1 -i ${srcdir}/0018-function-declaration-isnt-prototype.all.patch
  patch -p1 -i ${srcdir}/0019-unused-flags.all.patch
  patch -p1 -i ${srcdir}/0020-fix-size_t-format-specifier.all.patch
  patch -p1 -i ${srcdir}/0023-fix-sitedir-detection.mingw.patch
  patch -p1 -i ${srcdir}/0024-shrext-pyd.mingw.patch
  patch -p1 -i ${srcdir}/0025-mingw-python-dont-remove-dot.patch

  libtoolize --copy --force
  aclocal
  automake --add-missing
  autoconf
}

build() {

  # Static build
  mkdir -p ${srcdir}/build-static-${CARCH} && cd ${srcdir}/build-static-${CARCH}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build="${MINGW_CHOST}" \
    --without-python \
    --with-modules \
    --enable-static \
    --disable-shared \
    --with-threads=win32 \
    CFLAGS="${CFLAGS} -DLIBXML_STATIC_FOR_DLL"
  make

  # Shared build
  mkdir -p ${srcdir}/build-shared-${CARCH} && cd ${srcdir}/build-shared-${CARCH}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build="${MINGW_CHOST}" \
    --with-python=${MINGW_PREFIX}/bin/python2 \
    --with-modules \
    --disable-static \
    --enable-shared \
    --with-threads=win32

  # Hack for mingw python
  local PREFIX_DEPS=$(cygpath -am ${MINGW_PREFIX})
  find . -type f -name "Makefile" -exec sed -i "s|${PREFIX_DEPS}|${MINGW_PREFIX}|g" {} \;
  make
}

package() {
  # First install shared
  cd ${srcdir}/build-shared-${CARCH}

  make install DESTDIR="${pkgdir}"

  # then manually install static
  cd ${srcdir}/build-static-${CARCH}

  install -m 0644 .libs/libxml2.a "${pkgdir}${MINGW_PREFIX}"/lib/

  # License
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
  rm "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}-${pkgver}/Copyright" # This is the same file as COPYING
}
