# Maintainer: nalla <nalla@hamal.uberspace.de>

_realname=asciidoctor-extensions

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=79.4c6d08e
pkgrel=1
pkgdesc="This package installs asciidoctor plus extensions."
url="https://github.com/asciidoctor/asciidoctor"
arch=('any')
license=('GPL2')
install="${_realname}.install"

depends=("${MINGW_PACKAGE_PREFIX}-ruby")

source=("${_realname}"::"git+https://github.com/asciidoctor/asciidoctor-extensions-lab"
        "0001-man-inline-macro-enable-linkgit-syntax.patch")

md5sums=('SKIP'
         '1c6e5a44d07eb92224269ca5964f79fd')

pkgver() {
  cd "$srcdir/${_realname}"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

case "$CARCH" in
i686)
  LIBDIR="/mingw32/lib/${_realname}"
  ;;
x86_64)
  LIBDIR="/mingw64/lib/${_realname}"
  ;;
esac

build() {
  cd "$srcdir/${_realname}"
  git am "$srcdir/0001-man-inline-macro-enable-linkgit-syntax.patch"
}

package() {
  install -d -m755 "$pkgdir$LIBDIR"
  install -m755 "$srcdir/${_realname}"/lib/*.rb "$pkgdir$LIBDIR"
  
  for directory in `find $srcdir/${_realname}/lib/* -prune -type d`
  do
    extension="${directory##$srcdir/${_realname}/lib/}"

    install -d -m755 "$pkgdir$LIBDIR/$extension"
    install -m755 "$srcdir/${_realname}/lib/$extension"/* "$pkgdir$LIBDIR/$extension"
  done
}
