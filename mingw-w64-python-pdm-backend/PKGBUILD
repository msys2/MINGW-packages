# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=pdm-backend
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=2.3.3
pkgrel=2
pkgdesc="The build backend used by PDM that supports latest packaging standards (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'pypi: pdm-backend'
)
msys2_repository_url='https://github.com/pdm-project/pdm-backend'
url='https://backend.pdm-project.org/'
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-packaging"
         "${MINGW_PACKAGE_PREFIX}-python-pyproject-metadata"
         "${MINGW_PACKAGE_PREFIX}-python-tomli-w")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-pytest")
options=('!strip')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname/-/_}-${pkgver}.tar.gz"
        "python-pdm-backend-2.3.1-devendor.patch")
sha256sums=('a8616f628ec84353d7a0ba86b228dcf01bab5debc9e4d1a29e5311a52425d594'
            '56e05095adf7767b131fd29d0a446e13926e71bf98f9ffc2c71e378beaaae7fa')
noextract=("${_realname/-/_}-${pkgver}.tar.gz")

prepare() {
  echo "Extracting ${_realname/-/_}-${pkgver}.tar.gz ..."
  tar -xzf ${_realname/-/_}-${pkgver}.tar.gz || true
  cd "${srcdir}/${_realname/-/_}-${pkgver}"
  # copied from https://gitlab.archlinux.org/archlinux/packaging/packages/python-pdm-backend/-/blob/main/python-pdm-backend-2.3.1-devendor.patch
  patch -p1 -i "${srcdir}"/python-pdm-backend-2.3.1-devendor.patch
  rm -rf src/pdm/backend/_vendor
}

build() {
  cp -r "${_realname/-/_}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/python -m pytest || warning "Tests failed"
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
