# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

_realname=hunspell
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3.3
pkgrel=10
pkgdesc="Spell checker and morphological analyzer library and program (mingw-w64)"
arch=('any')
url="http://hunspell.sourceforge.net/"

# We omit MPL because it cannot be applied to the whole source, see COPYING
license=(GPL2+ LGPL2.1+)

makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=(${MINGW_PACKAGE_PREFIX}-gcc-libs
         ${MINGW_PACKAGE_PREFIX}-gettext)
options=('!libtool' 'strip' '!debug')
source=("http://download.sourceforge.net/${_realname}/${_realname}-${pkgver}.tar.gz"
        "hunspell-1.3.2-canonicalhost.patch"
        "hunspell-1.3.3-exit-on-MAX_PREFIXES_SUFFIXES.patch"
        "hunspell-1.3.3-inc-MAX_PREFIXES_SUFFIXES-to-2048.patch"
        "hunspell-1.3.3-relocate.patch"
        "hunspell-1.3.3-windows-sub-dicts-paths.patch")
md5sums=('4967da60b23413604c9e563beacc63b4'
         'f8a5c0f3692a390d98daf0406da59cdf'
         'c6cd1edc5bbfb803a6dc2ef602e048a3'
         'ebf66e5be21328c22df969245a9e75a8'
         '33eb82b23b50fb4f4821765686c99a38'
         '7d59a4f3c4c2f6b40cba5c54072cbd67')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Np1 -i "$srcdir"/hunspell-1.3.2-canonicalhost.patch
  patch -Np1 -i "$srcdir"/hunspell-1.3.3-exit-on-MAX_PREFIXES_SUFFIXES.patch
  patch -Np1 -i "$srcdir"/hunspell-1.3.3-inc-MAX_PREFIXES_SUFFIXES-to-2048.patch
  patch -Np1 -i "$srcdir"/hunspell-1.3.3-relocate.patch
  patch -Np1 -i "$srcdir"/hunspell-1.3.3-windows-sub-dicts-paths.patch

  autoreconf -fi
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  ../${_realname}-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --disable-rpath \
    --enable-threads=posix \
    --without-ui \
    --without-readline

  MSYS2_ARG_CONV_EXCL="-DBINDIR=;-DDATA_DIR=" \
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  MSYS2_ARG_CONV_EXCL="-DBINDIR=;-DDATA_DIR=" \
  make DESTDIR="${pkgdir}" install
  cp ${pkgdir}${MINGW_PREFIX}/lib/lib${_realname}-${pkgver%.*}.dll.a ${pkgdir}${MINGW_PREFIX}/lib/lib${_realname}.dll.a

  # Licenses
  cd "${srcdir}/${_realname}-${pkgver}"
  install -Dm644 COPYING      "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
  install -Dm644 COPYING.LGPL "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING.LGPL"
  install -Dm644 COPYING.MPL  "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING.MPL"
}
