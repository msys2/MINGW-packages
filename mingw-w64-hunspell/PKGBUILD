# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=hunspell
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.3.2
pkgrel=1
pkgdesc="Spell checker and morphological analyzer library and program (mingw-w64)"
arch=('any')
url="http://hunspell.sourceforge.net/"
license=("BSD")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config")
depends=(${_mingw_suff}-gcc-libs
		${_mingw_suff}-gettext)
options=('!libtool' 'strip')
source=("http://download.sourceforge.net/${_realname}/${_realname}-$pkgver.tar.gz"
		"hunspell-1.3.2-canonicalhost.patch")
md5sums=('3121aaf3e13e5d88dfff13fb4a5f1ab8'
         'f8a5c0f3692a390d98daf0406da59cdf')

prepare() {
  cd "$srcdir/${_realname}-$pkgver"
  patch -Np1 -i "$srcdir/hunspell-1.3.2-canonicalhost.patch"
  
  autoreconf -fi
}

build() {

  mkdir -p "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
  cd "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
  
  ${srcdir}/${_realname}-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
	--disable-rpath \
	--enable-threads=posix \
	--without-ui \
	--without-readline
	
  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}-build-${CARCH}"
  make DESTDIR="${pkgdir}" install
  cp ${pkgdir}${MINGW_PREFIX}/lib/lib${_realname}-${pkgver%.*}.dll.a ${pkgdir}${MINGW_PREFIX}/lib/lib${_realname}.dll.a
}
