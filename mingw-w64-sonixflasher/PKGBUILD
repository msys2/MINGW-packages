# Maintainer: Dimitris Mantzouranis <d3xter93@gmail.com>

_realname=sonixflasher
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=1.1.0
pkgrel=1
pkgdesc='CLI-based Flasher for Sonix 24x/26x MCUs (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
license=('spdx:GPL-3.0-only')
depends=("${MINGW_PACKAGE_PREFIX}-pkgconf"
         "${MINGW_PACKAGE_PREFIX}-hidapi")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             'make')
url='https://github.com/SonixQMK/SonixFlasherC'
source=("https://github.com/SonixQMK/SonixFlasherC/archive/${pkgver}.tar.gz")
sha256sums=('899beef48ca8eca0d8037794870f9f58f7cb463e3a3ddfeb2b5c446edbffbef9')

build() {
  cd "${srcdir}/SonixFlasherC-${pkgver}"

  make CC=${CC}
}

package() {
  cd "${srcdir}/SonixFlasherC-${pkgver}"

  install -Dm755 sonixflasher.exe "${pkgdir}${MINGW_PREFIX}/bin/sonixflasher.exe"
}