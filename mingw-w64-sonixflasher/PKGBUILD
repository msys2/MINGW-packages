# Maintainer: Dimitris Mantzouranis <d3xter93@gmail.com>

_realname=sonixflasher
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=2.0.7
pkgrel=1
pkgdesc='CLI-based Flasher for Sonix SN32F2xx MCUs (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
license=('spdx:GPL-3.0-only')
depends=("${MINGW_PACKAGE_PREFIX}-pkgconf"
         "${MINGW_PACKAGE_PREFIX}-hidapi")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             'make')
url='https://github.com/SonixQMK/SonixFlasherC'
source=("https://github.com/SonixQMK/SonixFlasherC/archive/${pkgver}.tar.gz")
sha256sums=('577f162c64833e19eef6213a6af0c506b534fe3db54ec6d61089e1dae28bcd3e')

build() {
  cd "${srcdir}/SonixFlasherC-${pkgver}"

  make CC=${CC}
}

package() {
  cd "${srcdir}/SonixFlasherC-${pkgver}"

  install -Dm755 sonixflasher.exe "${pkgdir}${MINGW_PREFIX}/bin/sonixflasher.exe"
}