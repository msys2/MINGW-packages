# Maintainer: Sylvain Rouquette <srouquette@gmail.com>

_realname=mdcat
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.7.0
pkgrel=1
pkgdesc="Fancy cat for Markdown (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64' 'mingw64')
url='https://github.com/swsnr/mdcat'
license=('spdx:MPL-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-curl")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
options=('!strip' '!lto')
source=("${url}/archive/refs/tags/${_realname}-${pkgver}.tar.gz")
sha256sums=('e372a82291a139f95d77c12325a2f595f47f6d6b4c2de70e50ab2117e975734f')
prepare() {
  cd "${_realname}-${_realname}-${pkgver}"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${_realname}-${_realname}-${pkgver}"

  export WINAPI_NO_BUNDLED_LIBRARIES=1
  cargo build --release --frozen
}

check() {
  cd "${_realname}-${_realname}-${pkgver}"

  cargo test --release --frozen
}

package() {
  cd "${_realname}-${_realname}-${pkgver}"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
