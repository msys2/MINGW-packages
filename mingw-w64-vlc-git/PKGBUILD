# Maintainer: Martell Malone <martellmalone@gmail.com>
# Contributor: Alexey Pavlov <alexpux@gmail.com>

_realname=vlc
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
_ver_base=2.2
pkgver=2.2.0.58010.e782ae3
pkgrel=1
pkgdesc="A multi-platform MPEG, VCD/DVD, and DivX player"
arch=('any')
url="http://www.videolan.org/vlc/"
license=('LGPL2.1' 'GPL2')
depends=("${MINGW_PACKAGE_PREFIX}-a52dec"
         "${MINGW_PACKAGE_PREFIX}-aribb24"
         "${MINGW_PACKAGE_PREFIX}-chromaprint"
         "${MINGW_PACKAGE_PREFIX}-faad2"
         "${MINGW_PACKAGE_PREFIX}-ffmpeg"
         "${MINGW_PACKAGE_PREFIX}-flac"
         "${MINGW_PACKAGE_PREFIX}-fluidsynth"
         "${MINGW_PACKAGE_PREFIX}-fribidi"
         "${MINGW_PACKAGE_PREFIX}-gnutls"
         "${MINGW_PACKAGE_PREFIX}-gsm"
         "${MINGW_PACKAGE_PREFIX}-libass"
         "${MINGW_PACKAGE_PREFIX}-libbluray"
         "${MINGW_PACKAGE_PREFIX}-libcaca"
         "${MINGW_PACKAGE_PREFIX}-libcddb"
         "${MINGW_PACKAGE_PREFIX}-libcdio"
         "${MINGW_PACKAGE_PREFIX}-libdca"
         "${MINGW_PACKAGE_PREFIX}-libdvdcss"
         "${MINGW_PACKAGE_PREFIX}-libdvdnav"
         "${MINGW_PACKAGE_PREFIX}-libdvbpsi"
         "${MINGW_PACKAGE_PREFIX}-libgme"
         "${MINGW_PACKAGE_PREFIX}-libgoom2"
         "${MINGW_PACKAGE_PREFIX}-libmad"
         "${MINGW_PACKAGE_PREFIX}-libmatroska"
         "${MINGW_PACKAGE_PREFIX}-libmpcdec"
         "${MINGW_PACKAGE_PREFIX}-libmpeg2"
         "${MINGW_PACKAGE_PREFIX}-libproxy"
         "${MINGW_PACKAGE_PREFIX}-librsvg"
         "${MINGW_PACKAGE_PREFIX}-libsamplerate"
         "${MINGW_PACKAGE_PREFIX}-libshout"
         "${MINGW_PACKAGE_PREFIX}-libssh2"
         "${MINGW_PACKAGE_PREFIX}-libtheora"
         "${MINGW_PACKAGE_PREFIX}-libvpx"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-lua"
         "${MINGW_PACKAGE_PREFIX}-opencv"
         "${MINGW_PACKAGE_PREFIX}-opus"
         "${MINGW_PACKAGE_PREFIX}-portaudio"
         "${MINGW_PACKAGE_PREFIX}-schroedinger"
         "${MINGW_PACKAGE_PREFIX}-speex"
         "${MINGW_PACKAGE_PREFIX}-taglib"
         "${MINGW_PACKAGE_PREFIX}-twolame"
         "${MINGW_PACKAGE_PREFIX}-vcdimager"
         "${MINGW_PACKAGE_PREFIX}-x264"
         "${MINGW_PACKAGE_PREFIX}-x265"
         "${MINGW_PACKAGE_PREFIX}-xpm-nox"
         "${MINGW_PACKAGE_PREFIX}-qt5"
         #"${MINGW_PACKAGE_PREFIX}-sdl_image"
         #"${MINGW_PACKAGE_PREFIX}-libtiger"
         #"${MINGW_PACKAGE_PREFIX}-zvbi"
         #"${MINGW_PACKAGE_PREFIX}-libupnp"
         #"${MINGW_PACKAGE_PREFIX}-libtar"
         
         )
makedepends=(
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "pkg-config"
             "${MINGW_PACKAGE_PREFIX}-ncurses"
             "git"
             # 'libkate'
             # 'live-media'
             # 'libnotify'
             # 'libdc1394'
             # 'libavc1394'
             # 'xosd'
             # 'projectm'
             # 'aalib'
             # 'libmtp'
             )
# optdepends=("${MINGW_PACKAGE_PREFIX}-libnotify: for notification plugin"
            # "${MINGW_PACKAGE_PREFIX}-ncurses: for ncurses interface support"
            # "${MINGW_PACKAGE_PREFIX}-libdvdcss: for decoding encrypted DVDs"
            # "${MINGW_PACKAGE_PREFIX}-libavc1394: for devices using the 1394ta AV/C"
            # "${MINGW_PACKAGE_PREFIX}-libdc1394: for IEEE 1394 plugin"
            # "${MINGW_PACKAGE_PREFIX}-libbluray: for Blu-Ray support"
            # "${MINGW_PACKAGE_PREFIX}-flac: for Free Lossless Audio Codec plugin"
            # "${MINGW_PACKAGE_PREFIX}-portaudio: for portaudio support"
            # "${MINGW_PACKAGE_PREFIX}-twolame: for TwoLAME mpeg2 encoder plugin"
            # "${MINGW_PACKAGE_PREFIX}-projectm: for ProjectM visualisation plugin"
            # "${MINGW_PACKAGE_PREFIX}-libcaca: for colored ASCII art video output"
            # "${MINGW_PACKAGE_PREFIX}-libgme: for libgme plugin"
            # "${MINGW_PACKAGE_PREFIX}-librsvg: for SVG plugin"
            # "${MINGW_PACKAGE_PREFIX}-libgoom2: for libgoom plugin"
            # "${MINGW_PACKAGE_PREFIX}-vcdimager: navigate VCD with libvcdinfo"
            # "${MINGW_PACKAGE_PREFIX}-aalib: for ASCII art plugin"
            # "${MINGW_PACKAGE_PREFIX}-libmtp: for MTP devices support"
            # "${MINGW_PACKAGE_PREFIX}-libcdio: for audio CD playback support"
            # "${MINGW_PACKAGE_PREFIX}-opus: for opus support"
            # "${MINGW_PACKAGE_PREFIX}-libssh2: for sftp support")
#backup=('usr/share/vlc/lua/http/.hosts'
#        'usr/share/vlc/lua/http/dialogs/.hosts')
options=('strip' '!emptydirs')
source=("${_realname}-${_ver_base}"::"git://git.videolan.org/${_realname}/${_realname}-${_ver_base}.git"
        #"${_realname}"::"git://git.videolan.org/${_realname}.git"
        0001-fix-redeclare.patch
        0002-msvcrt-lfind.patch
        0003-fix-prog-find-2.2.patch
        0004-skip-pthread-time-declares.patch
        0005-disablestatic.patch
        0010-fix_REENTRANT-2.2.patch
        0011-prefer-static-libs.patch)
md5sums=('SKIP'
         'a546326fd9beb9a04e066bad6c1945c9'
         'e1d4601bec65f1287f87a66b13f98226'
         '0ea45934d7819fe63384dd8ef95c9de2'
         'e17fb5732ceaa7897a5f5183764393ad'
         '1e544f65bd17b5af5b862098412f99f7'
         'd858696f47e0688b4706d598fd7e42ed'
         'bc2038d61add89c423dc7145cdc6c843')

pkgver() {
  cd $_realname-${_ver_base}
  local _ver=$(grep AC_INIT configure.ac | sed -e 's/AC_INIT([^,]\+,\s*\[\?\([0-9.a-z-]\+\).*/\1/')
  #or use local _ver=$_ver_base
  _ver=${_ver%-*}
  revision=$(git rev-list --count HEAD)
  hash=$(git log --pretty=format:'%h' -n 1)
  echo $_ver.$revision.$hash
}

prepare() {
  cd "${srcdir}"/${_realname}-${_ver_base}

  patch -Np1 -i "${srcdir}/0001-fix-redeclare.patch"
  patch -p1 -i  "${srcdir}/0002-msvcrt-lfind.patch"
  patch -p1 -i  "${srcdir}/0003-fix-prog-find-2.2.patch"
  patch -p1 -i  "${srcdir}/0004-skip-pthread-time-declares.patch"
  patch -p1 -i  "${srcdir}/0005-disablestatic.patch"
  patch -p1 -i  "${srcdir}/0010-fix_REENTRANT-2.2.patch"
  patch -Rp1 -i "${srcdir}/0011-prefer-static-libs.patch"

  ./bootstrap

  sed -i -e 's:truetype/freefont:TTF:g' modules/text_renderer/freetype.c
  sed -i -e 's:truetype/ttf-dejavu:TTF:g' modules/visualization/projectm.cpp
}

build() {

  mkdir -p ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}

  # Hack getting private QtGui directory
  local _qtver=$(qmake -query QT_VERSION)
  local _qtguiinc=$(pkg-config Qt5Gui --variable=includedir)
  local _privdir=${_qtguiinc}/QtGui/${_qtver}/QtGui
  CPPFLAGS+=" -I${_privdir}"

  # These are the current options I was able to build
  ${srcdir}/${_realname}-${_ver_base}/configure \
    --prefix=${MINGW_PREFIX} \
    --sysconfdir=${MINGW_PREFIX}/etc \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-qt \
    --disable-ncurses \
    --disable-dbus \
    --disable-sdl \
    --disable-telx \
    --enable-nls \
    LUAC==${MINGW_PREFIX}/bin/luac  LUA_LIBS="`pkg-config --libs lua`" \
    RCC==${MINGW_PREFIX}/bin/rcc

# These are the official options the vlc uses for windows
#    --enable-update-check \
#    --enable-lua \
#    --enable-faad \
#    --enable-flac \
#    --enable-theora \
#    --enable-twolame \
#    --enable-quicktime \
#    --enable-avcodec \
#    --enable-merge-ffmpeg \
#    --enable-dca \
#    --enable-mpc \
#    --enable-libass \
#    --enable-x264 \
#    --enable-schroedinger \
#    --enable-realrtsp \
#    --enable-live555 \
#    --enable-dvdread \
#    --enable-shout \
#    --enable-goom \
#    --enable-caca \
#    --disable-sdl \
#    --enable-qt \
#    --enable-skins2 \
#    --enable-sse \
#    --enable-mmx \
#    --enable-libcddb \
#    --enable-zvbi \
#    --disable-telx \
#    --enable-nls \

  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}

  make DESTDIR="${pkgdir}" install

  for res in 16 32 48 128; do
    install -D -m644 "${srcdir}/${_realname}-${_ver_base}/share/icons/${res}x${res}/vlc.png" \
                     "${pkgdir}/${MINGW_PREFIX}/share/icons/hicolor/${res}x${res}/apps/vlc.png"
  done

  #todo: patch vlc for plugins to be in share directory
  mv ${pkgdir}/${MINGW_PREFIX}/lib/vlc/plugins ${pkgdir}/${MINGW_PREFIX}/bin
}
