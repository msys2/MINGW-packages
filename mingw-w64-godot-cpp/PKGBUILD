# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>

_realname=godot-cpp
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=4.3
pkgrel=1
pkgdesc='C++ bindings for the Godot script API (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://www.godotengine.org/"
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-scons" "${MINGW_PACKAGE_PREFIX}-cc")
source=("https://github.com/godotengine/godot-cpp/archive/godot-${pkgver}-stable.tar.gz")
sha256sums=('e6dfe8ee1ab3eeaadc9dba2dcb5ef39c4a99635986148fa893ce5c414153743f')

prepare() {
  rm -rf "${_realname}-${pkgver}"
  mv "${_realname}-godot-${pkgver}-stable" "${_realname}-${pkgver}"
}

build() {
  cd "${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/scons ${MAKEFLAGS} \
    platform=windows \
    target=template_release \
    use_mingw=yes \
    generate_bindings=yes
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  mkdir -p "${pkgdir}${MINGW_PREFIX}/include"
  cd include
  find . -type f -exec install -Dm644 {} "${pkgdir}${MINGW_PREFIX}/include/godot/"{} \;
  cd ..
  install -D -m644 bin/libgodot-cpp.windows.*.a "${pkgdir}${MINGW_PREFIX}/lib/libgodot-cpp.a"
  install -D -m644 LICENSE.md "${pkgdir}${MINGW_PREFIX}/share/licenses/godot-cpp/LICENSE.md"
}
