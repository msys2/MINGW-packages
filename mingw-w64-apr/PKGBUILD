# Maintainer: Drew Waranis <drew@waran.is>

_realname=apr
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.5.2
pkgrel=2
pkgdesc="The Apache Portable Runtime (mingw-w64)"
arch=('any')
url="https://apr.apache.org/"
license=('APACHE')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-libtool")
source=(http://www.apache.org/dist/apr/${_realname}-${pkgver}.tar.bz2{,.asc}
        'apr_ssize_t.patch'
        'apr_wtypes.patch')
md5sums=('4e9769f3349fe11fc0a5e1b224c236aa'
         'SKIP'
         'b91081b97f838246446c5795371faf6a'
         '0326d335f035f8f13dea4296efb1faab')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -p0 -i ${srcdir}/apr_ssize_t.patch
  patch -p0 -i ${srcdir}/apr_wtypes.patch

  ./buildconf
  # autoreconf -fi
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  # Disable IPv6.
  ../${_realname}-${pkgver}/configure \
    --prefix="${MINGW_PREFIX}" \
    --includedir="${MINGW_PREFIX}/include/apr-1" \
    --with-installbuilddir="${MINGW_PREFIX}/share/apr-1/build" \
    --enable-nonportable-atomics \
    --with-devrandom=/dev/urandom \
    --disable-ipv6

  make
}

#check() {
#  cd "${srcdir}/build-${MINGW_CHOST}"
#  make -j1 check
#}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
