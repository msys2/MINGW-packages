# Maintainer: Drew Waranis <drew@waran.is>

_realname=apr
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.6.5
pkgrel=1
pkgdesc="The Apache Portable Runtime (mingw-w64)"
arch=('any')
url="https://apr.apache.org/"
license=('APACHE')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-libtool"
             "${MINGW_PACKAGE_PREFIX}-python2")
source=(https://www.apache.org/dist/apr/${_realname}-${pkgver}.tar.bz2{,.asc}
        'apr_ssize_t.patch'
        'apr_wtypes.patch')
sha256sums=('a67ca9fcf9c4ff59bce7f428a323c8b5e18667fdea7b0ebad47d194371b0a105'
            'SKIP'
            'cd75b79706dceba2af20f9e9a2d05032eacaf6f04edecb3a21f11594c0ccee4b'
            'b82dd98ec8cff2273fb071dc9f1d2ee7466905c9b82a12d3d83ce1cb5920a5d6')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -p0 -i ${srcdir}/apr_ssize_t.patch
  patch -p0 -i ${srcdir}/apr_wtypes.patch

  ./buildconf
  # autoreconf -fi
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  # Disable IPv6.
  ../${_realname}-${pkgver}/configure \
    --prefix="${MINGW_PREFIX}" \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared \
    --includedir="${MINGW_PREFIX}/include/apr-1" \
    --with-installbuilddir="${MINGW_PREFIX}/share/apr-1/build" \
    --enable-nonportable-atomics \
    --with-devrandom=/dev/urandom \
    --disable-ipv6

  make
}

#check() {
#  cd "${srcdir}/build-${MINGW_CHOST}"
#  make -j1 check
#}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
