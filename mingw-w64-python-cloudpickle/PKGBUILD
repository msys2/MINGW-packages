# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>

_realname=cloudpickle
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=2.0.0
pkgrel=1
pkgdesc='Extended pickling support for Python objects (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://github.com/cloudpipe/cloudpickle"
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-python")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-pytest"
"${MINGW_PACKAGE_PREFIX}-python-psutil"
"${MINGW_PACKAGE_PREFIX}-python-tornado"
"${MINGW_PACKAGE_PREFIX}-python-numpy")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools")
options=('!emptydirs')
source=("https://files.pythonhosted.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('5cd02f3b417a783ba84a4ec3e290ff7929009fe51f6405423cfccfadd43ba4a4')

prepare() {
  cd "$srcdir"
  rm -rf python-build-${CARCH} | true
  cp -r "${_realname}-${pkgver}" "python-build-${CARCH}"
}

build() {
  msg "Python build for ${CARCH}"
  cd "${srcdir}/python-build-${CARCH}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

check() {
  cd "${srcdir}/python-build-${CARCH}/tests/cloudpickle_testpkg"
  ${MINGW_PREFIX}/bin/python setup.py build || warning "Tests failed"
  cd ../..
  PYTHONPATH="$PWD/tests/cloudpickle_testpkg/build/lib/" \
  ${MINGW_PREFIX}/bin/pytest --deselect tests/cloudpickle_file_test.py::CloudPickleFileTests::test_pickling_special_file_handles \
    --deselect tests/cloudpickle_test.py::CloudPickleTest::test_pickling_file_handles \
    --deselect tests/cloudpickle_test.py::Protocol2CloudPickleTest::test_pickling_file_handles || warning "Tests failed"
}

package() {
  cd "${srcdir}/python-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
