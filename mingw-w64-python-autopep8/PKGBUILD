# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=autopep8
pkgbase=mingw-w64-python-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-${_realname}
pkgver=2.0.0
pkgrel=1
pkgdesc='A tool that automatically formats Python code to conform to the PEP 8 style guide (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=('MIT')
url='https://github.com/hhatto/autopep8'
depends=(
    ${MINGW_PACKAGE_PREFIX}-python
    ${MINGW_PACKAGE_PREFIX}-python-pycodestyle
    ${MINGW_PACKAGE_PREFIX}-python-tomli)
makedepends=(${MINGW_PACKAGE_PREFIX}-python-setuptools unzip)
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('8b1659c7f003e693199f52caffdc06585bb0716900bbc6a7442fd931d658c077')

prepare() {
  rm -rf python-build-${MSYSTEM} | true
  cp -r ${_realname}-${pkgver} python-build-${MSYSTEM}
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd ${srcdir}/python-build-${MSYSTEM}
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd ${srcdir}/python-build-${MSYSTEM}
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
