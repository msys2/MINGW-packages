# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=autopep8
pkgbase=mingw-w64-python-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-${_realname}
pkgver=1.7.0
pkgrel=1
pkgdesc='A tool that automatically formats Python code to conform to the PEP 8 style guide (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=('MIT')
url='https://github.com/hhatto/autopep8'
depends=(
    ${MINGW_PACKAGE_PREFIX}-python
    ${MINGW_PACKAGE_PREFIX}-python-pycodestyle
    ${MINGW_PACKAGE_PREFIX}-python-toml)
makedepends=(${MINGW_PACKAGE_PREFIX}-python-setuptools unzip)
source=(${_realname}-${pkgver}.zip::https://github.com/hhatto/${_realname}/archive/v${pkgver}.zip)
noextract=(${_realname}-${pkgver}.zip)
sha256sums=('42418ed3c7261afe97adc800c650cc71d6ffd154d410a3e4e0487b846c42b82c')

prepare() {
  [[ -d ${_realname}-${pkgver} ]] && rm -rf ${_realname}-${pkgver}
  unzip ${_realname}-${pkgver}.zip

  rm -rf python-build-${MSYSTEM} | true
  cp -r ${_realname}-${pkgver} python-build-${MSYSTEM}
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd ${srcdir}/python-build-${MSYSTEM}
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd ${srcdir}/python-build-${MSYSTEM}
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
