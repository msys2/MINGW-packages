# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=poetry
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.1.13
pkgrel=1
pkgdesc="Python dependency management and packaging made easy (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://github.com/python-poetry/poetry"
options=('!strip')
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-build")
depends=(
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-python-distlib"
  "${MINGW_PACKAGE_PREFIX}-python-poetry-core"
)
source=("${_realname}-$pkgver.tar.gz"::"https://github.com/python-poetry/${_realname}/archive/${pkgver}.tar.gz"
        "https://github.com/python-poetry/${_realname}/releases/download/${pkgver}/${_realname}-${pkgver}-win32.tar.gz"
        "0001-use-vendor.patch")
sha256sums=('c2f4691a00d82e717a5861f62c6234757852ed1cd9b662e1d2d803157e7a7018'
            '4a138652a857e7af3bc868a0423ae90d8c6ba47d22c23eabb24f8e09c93005a2'
            '8103304ec79463d967ef4f91cd6110576c40b3608e559a4eaef4dc36770e95ac')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -p1 -i ${srcdir}/0001-use-vendor.patch

  # be a proper namespace package, python3-only
  rm poetry/__init__.py

  # prepare vendored deps
  local _pyver=$(python3 -c "import sys; sys.stdout.write('.'.join(map(str, sys.version_info[:2])))")
  mv "${srcdir}/${_realname}/_vendor/py${_pyver}" "${srcdir}/vendor"
  cd "${srcdir}/vendor"
  rm -Rf build distlib* poetry*
  find . -name "*.exe" -delete
  find . -name "*.pyd" -delete
  find . -type d -empty -delete
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  local _pyver=$(python3 -c "import sys; sys.stdout.write('.'.join(map(str, sys.version_info[:2])))")
  local _vendor="${pkgdir}${MINGW_PREFIX}/lib/python${_pyver}/site-packages/poetry/_vendor"
  rm -Rf "${_vendor}"
  cp -R "${srcdir}/vendor" "${_vendor}"
  MSYS2_ARG_CONV_EXCL="-p" python -m compileall \
    -o 0 -o 1 -q -s"${pkgdir}" -p"/" "${pkgdir}${MINGW_PREFIX}/lib/python"*

  install -D -m644 LICENSE "${pkgdir}${MINGW_PREFIX}"/share/licenses/python-${_realname}/LICENSE
}
