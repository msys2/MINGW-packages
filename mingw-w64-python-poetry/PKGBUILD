# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=poetry
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.8.2
pkgrel=1
pkgdesc="Python dependency management and packaging made easy (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/python-poetry/poetry"
msys2_references=(
  'pypi: poetry'
)
license=('spdx:MIT')
depends=(
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-python-poetry-core"
  "${MINGW_PACKAGE_PREFIX}-python-poetry-plugin-export"
  "${MINGW_PACKAGE_PREFIX}-python-build"
  "${MINGW_PACKAGE_PREFIX}-python-cachecontrol"
  "${MINGW_PACKAGE_PREFIX}-python-cleo"
  "${MINGW_PACKAGE_PREFIX}-python-crashtest"
  "${MINGW_PACKAGE_PREFIX}-python-dulwich"
  "${MINGW_PACKAGE_PREFIX}-python-fastjsonschema"
  "${MINGW_PACKAGE_PREFIX}-python-installer"
  "${MINGW_PACKAGE_PREFIX}-python-keyring"
  "${MINGW_PACKAGE_PREFIX}-python-packaging"
  "${MINGW_PACKAGE_PREFIX}-python-pexpect"
  "${MINGW_PACKAGE_PREFIX}-python-pkginfo"
  "${MINGW_PACKAGE_PREFIX}-python-platformdirs"
  "${MINGW_PACKAGE_PREFIX}-python-pyproject-hooks"
  "${MINGW_PACKAGE_PREFIX}-python-requests"
  "${MINGW_PACKAGE_PREFIX}-python-requests-toolbelt"
  "${MINGW_PACKAGE_PREFIX}-python-shellingham"
  "${MINGW_PACKAGE_PREFIX}-python-tomlkit"
  "${MINGW_PACKAGE_PREFIX}-python-trove-classifiers"
  "${MINGW_PACKAGE_PREFIX}-python-virtualenv"
)
options=('!strip')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('49cceb3838104647c3e1021f3a4f13c6053704cc18d33f849a90fe687a29cb73')

prepare() {
  cd "${srcdir}"
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}"
}

build() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -D -m644 LICENSE "${pkgdir}${MINGW_PREFIX}"/share/licenses/python-${_realname}/LICENSE
}
