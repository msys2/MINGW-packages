# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=poetry
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.2.2
pkgrel=1
pkgdesc="Python dependency management and packaging made easy (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://github.com/python-poetry/poetry"
options=('!strip')
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-build")
depends=(
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-python-cachecontrol"
  "${MINGW_PACKAGE_PREFIX}-python-cachy"
  "${MINGW_PACKAGE_PREFIX}-python-cleo"
  "${MINGW_PACKAGE_PREFIX}-python-crashtest"
  "${MINGW_PACKAGE_PREFIX}-python-dulwich"
  "${MINGW_PACKAGE_PREFIX}-python-html5lib"
  "${MINGW_PACKAGE_PREFIX}-python-jsonschema"
  "${MINGW_PACKAGE_PREFIX}-python-keyring"
  "${MINGW_PACKAGE_PREFIX}-python-packaging"
  "${MINGW_PACKAGE_PREFIX}-python-pexpect"
  "${MINGW_PACKAGE_PREFIX}-python-pkginfo"
  "${MINGW_PACKAGE_PREFIX}-python-platformdirs"
  "${MINGW_PACKAGE_PREFIX}-python-poetry-core"
  "${MINGW_PACKAGE_PREFIX}-python-poetry-plugin-export"
  "${MINGW_PACKAGE_PREFIX}-python-requests"
  "${MINGW_PACKAGE_PREFIX}-python-requests-toolbelt"
  "${MINGW_PACKAGE_PREFIX}-python-shellingham"
  "${MINGW_PACKAGE_PREFIX}-python-tomlkit"
  "${MINGW_PACKAGE_PREFIX}-python-urllib3"
  "${MINGW_PACKAGE_PREFIX}-python-virtualenv"
)
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('6d9ed0b1b826a0a79190f2078d7d78483fa24bf2494f3b170e354eaa5e7b5ea1')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -D -m644 LICENSE "${pkgdir}${MINGW_PREFIX}"/share/licenses/python-${_realname}/LICENSE
}
