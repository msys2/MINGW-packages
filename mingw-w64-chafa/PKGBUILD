# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=chafa
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.12.3
pkgrel=1
pkgdesc="Image-to-text converter supporting a wide range of symbols and palettes, transparency, animations, etc. (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='https://hpjansson.org/chafa/'
license=('spdx:LGPL-3.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-cairo"
         "${MINGW_PACKAGE_PREFIX}-freetype"
         "${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-imagemagick"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-librsvg"
         "${MINGW_PACKAGE_PREFIX}-libtiff"
         "${MINGW_PACKAGE_PREFIX}-libwebp")
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-gtk-doc")
source=(https://github.com/hpjansson/chafa/releases/download/${pkgver}/chafa-${pkgver}.tar.xz{,.asc}
        https://github.com/hpjansson/chafa/commit/22621e8187f211680f3c130312eeb0d642e396b9.patch)
sha256sums=('2456a0b6c1150e25b64cd6a92810d59bed3f061f8b86f91aba5a77bc7cc76cfa'
            'SKIP'
            '7418b167136e5a9c0a7a5aca98c3f5d324ef1f8dc4a547c8c52ce06c710df8e7')
validpgpkeys=('C01EDE5BB0D91E26D003662EC76BB9FEEAD12EA7') # Hans Petter Jansson <hpj@suse.com>

prepare() {
  cd "${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/22621e8187f211680f3c130312eeb0d642e396b9.patch"
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  ../"${_realname}-${pkgver}"/configure \
    --prefix="${MINGW_PREFIX}" \
    --build="${MINGW_CHOST}" \
    --host="${MINGW_CHOST}" \
    --target="${MINGW_CHOST}" \
    --enable-static \
    --enable-shared

  make
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  make install DESTDIR="${pkgdir}"

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING.LESSER" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING.LESSER"
}
