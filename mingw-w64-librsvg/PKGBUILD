# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=librsvg
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.40.5
pkgrel=1
pkgdesc="A SVG viewing library (mingw-w64)"
arch=('any')
url="https://live.gnome.org/LibRsvg"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-pkg-config"
            "${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2"
            "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
            "${MINGW_PACKAGE_PREFIX}-vala"
            "setconf")
depends=("${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2" "${MINGW_PACKAGE_PREFIX}-pango" "${MINGW_PACKAGE_PREFIX}-libcroco")
optdepends=("${MINGW_PACKAGE_PREFIX}-gtk3: for rsvg-view-3")
options=('staticlibs' 'strip')
source=("http://ftp.gnome.org/pub/gnome/sources/librsvg/${pkgver%.*}/${_realname}-$pkgver.tar.xz"
        "0001-give-cc-to-gir-scanner.mingw.patch"
        "0002-gio-windows.patch"
        "0004-fix-fullpath-invocation-and-use-it.mingw.patch"
        "0005-hack-unixy-paths.patch")
sha256sums=('d14d7b3e25023ce34302022fd7c9b3a468629c94dff6c177874629686bfc71a7'
            '854f36c9bc1ad0f3a85ca2ec4e8b5c7d75ba035c45ad4915601210acf0321776'
            '9387edb852cade567c54b004690068922ca88fd783628b42d606188340dd2ba2'
            '41ffb473d8b1102c5098fa3172b358e4bc65f96df7a7953874b3ab6965fd9ccb'
            'eff60ec631f08ea69a26d216c41beea502c68614278a15eb4c73213a64f76ed4')

prepare() {
  cd "$srcdir/${_realname}-$pkgver"
  patch -p1 -i "$srcdir/0001-give-cc-to-gir-scanner.mingw.patch"
  patch -p1 -i "$srcdir/0002-gio-windows.patch"
  patch -p1 -i "$srcdir/0004-fix-fullpath-invocation-and-use-it.mingw.patch"
  patch -p1 -i "$srcdir/0005-hack-unixy-paths.patch"
  
  autoreconf -fi
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  cp -r ${srcdir}/${_realname}-$pkgver "${srcdir}/build-${MINGW_CHOST}"
  #mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  CXXFLAGS+=" -D_POSIX_SOURCE"
  CFLAGS+=" -D_POSIX_SOURCE"
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-introspection \
    --enable-vala \
    --enable-silent-rules
  setconf Makefile AM_CFLAGS '-fno-common'
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install
}
