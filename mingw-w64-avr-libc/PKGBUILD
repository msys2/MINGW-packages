# Maintainer: fauxpark <fauxpark@gmail.com>

_realname=libc
_target=avr

pkgbase=mingw-w64-${_target}-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_target}-${_realname}
pkgver=2.3.1
pkgrel=1
pkgdesc='The C runtime library for the AVR family of microcontrollers (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
license=('spdx:BSD-3-Clause')
url='https://github.com/avrdudes/avr-libc'
groups=("${MINGW_PACKAGE_PREFIX}-${_target}-toolchain")
depends=("${MINGW_PACKAGE_PREFIX}-${_target}-gcc")
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools")
options=(!strip)
source=(https://github.com/avrdudes/avr-libc/releases/download/avr-libc-${pkgver//./_}-release/${_target}-${_realname}-${pkgver}.tar.bz2) #{,.sig})
sha256sums=('2ec0827ec833ca6715a87221f31799d60da1c8638aa98eaafae42e0c2e302f5c')
validpgpkeys=('56628323218C669FF578705C7E9EADC3030D34EB')

build() {
    mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

    CC=avr-gcc
    ../${_target}-${_realname}-${pkgver}/configure \
        --build=${MINGW_CHOST} \
        --host=${_target} \
        --prefix=${MINGW_PREFIX}

    make
}

package() {
    cd "${srcdir}/build-${MSYSTEM}"

    make DESTDIR="$pkgdir" install

    install -Dm644 "${srcdir}"/${_target}-${_realname}-${pkgver}/LICENSE \
      -t "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_target}-${_realname}
}
