# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=incremental
pkgbase=mingw-w64-python-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-${_realname}
pkgver=22.10.0
pkgrel=2
pkgdesc='A small library that versions your Python projects (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64' 'clang32')
url='https://github.com/twisted/incremental'
msys2_references=(
  'pypi: incremental'
)
license=('MIT')
depends=(
    ${MINGW_PACKAGE_PREFIX}-python)
makedepends=(
    ${MINGW_PACKAGE_PREFIX}-python-setuptools)
source=("${_realname}-${pkgver}.tar.gz::https://github.com/twisted/${_realname}/archive/${_realname}-${pkgver}.tar.gz")
sha256sums=('9f66f8730ce65f6503e83f4627d568bdcab2c1136f284ab812de109f0c955ad4')

prepare() {
  cd "${srcdir}"
  rm -rf python-build-${MSYSTEM} | true
  cp -r "incremental-${_realname}-${pkgver}" "python-build-${MSYSTEM}"
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
