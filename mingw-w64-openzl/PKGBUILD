# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=openzl
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.1.0
pkgrel=1
pkgdesc="A novel data compression framework (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://openzl.org'
msys2_repository_url='https://github.com/facebook/openzl'
license=('spdx:BSD-3-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-zstd")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
source=("${msys2_repository_url}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "support-system-zstd.patch")
sha256sums=('3278546dcdbae3aef3887f07b435ebe0aa9f6943a5ac74cf9b7baeefe6526c2e'
            '19e5fb50d62c456c39849f2ff487b54e648b1071036916923d6a5142b2a14690')
noextract=("${_realname}-${pkgver}.tar.gz")

prepare() {
  tar -xzf ${_realname}-${pkgver}.tar.gz || true
  cd ${_realname}-${pkgver}

  # https://github.com/facebook/openzl/pull/288
  patch -p1 -i ../support-system-zstd.patch
}

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DOPENZL_BUILD_TESTS=ON \
      -DOPENZL_ALLOW_INTROSPECTION=OFF \
      -DOPENZL_USE_SYSTEM_ZSTD=ON \
      -S "${_realname}-${pkgver}" \
      -B "build-${MSYSTEM}"

  cmake --build "build-${MSYSTEM}"
}

check() {
  cmake --build "build-${MSYSTEM}" --target test
}

package() {
  DESTDIR="${pkgdir}" cmake --install "build-${MSYSTEM}"

  install -Dm644 "${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
