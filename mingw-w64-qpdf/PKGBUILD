# Maintainer: Oleg Tolmatcev <oleg.tolmatcev@gmail.com>

_realname=qpdf
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=10.5.0
pkgrel=1
pkgdesc="A Content-Preserving PDF Transformation System (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://github.com/qpdf/qpdf"
license=('spdx:Artistic-2.0,Apache')
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gnutls"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-pcre"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=('!staticlibs' 'strip')
source=(https://github.com/qpdf/qpdf/releases/download/release-${_realname}-${pkgver}/${_realname}-${pkgver}.tar.gz{,.asc}
        0001-autoreconf-for-clang.patch
        0002-mingw-configure-fix.patch
        0003-fix-qpdf-dll-class-macro.patch)
sha256sums=('88257d36a44fd5c50b2879488324dd9cafc11686ae49d8c4922a4872203ce006'
            'SKIP'
            'd0e69a4ffbd337597cfb6ccd6d53114ec9acee02b652b88bcaad2dba1c1f088f'
            '1f037633662ba2c3024818012a2ad71c4d79de63b477634260740ff7a4dedb73'
            'e802ba58176d035efb192179bf12e0cb441233332d2a58a521e57478504d0e19')
validpgpkeys=('C2C96B10011FE009E6D1DF828A75D10998012C7E') # Jay Berkenbilt <ejb@ql.org>

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-autoreconf-for-clang.patch
  patch -p1 -i ${srcdir}/0002-mingw-configure-fix.patch
  patch -p1 -i ${srcdir}/0003-fix-qpdf-dll-class-macro.patch

  ./autogen.sh
}

build() {
  [[ -d build-${MSYSTEM} ]] && rm -rf build-${MSYSTEM}
  cp -rf ${_realname}-${pkgver} build-${MSYSTEM} && cd build-${MSYSTEM}

  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-crypto-gnutls \
    --enable-external-libs \
    --disable-implicit-crypto \
    --disable-test-compare-images \
    --with-buildrules=libtool

  make
}

check() {
  cd build-${MSYSTEM}
  PATH=$PWD/libqpdf/build:$PATH make check || true
}

package() {
  cd build-${MSYSTEM}
  make DESTDIR=${pkgdir} install

  install -Dm644 Artistic-2.0 ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/Artistic-2.0
}
