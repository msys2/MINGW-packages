# Maintainer: Oleg Tolmatcev <oleg.tolmatcev@gmail.com>

_realname=qpdf
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=10.0.0
pkgrel=1
pkgdesc="QPDF: A Content-Preserving PDF Transformation System (mingw-w64)"
arch=('any')
url="https://github.com/qpdf/qpdf"
license=('custom:Artistic-2.0' 'Apache')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gnutls"
         "${MINGW_PACKAGE_PREFIX}-libjpeg"
         "${MINGW_PACKAGE_PREFIX}-pcre"
         "${MINGW_PACKAGE_PREFIX}-zlib")
#https://sourceforge.net/projects/${_realname}/files/${_realname}/${pkgver}/${_realname}-${pkgver}.tar.gz
source=(https://github.com/qpdf/qpdf/releases/download/release-${_realname}-${pkgver}/${_realname}-${pkgver}.tar.gz{,.asc}
        "0001-fix-printf-on-mingw-w64.patch"
        "0002-enable-advapi32-mingw.patch"
        "0003-install-rule.patch")
sha512sums=('9d07dddd139dc489f6ffefaee95bdbc430da3948b7ba5ac900e4b8dd027ab65805ba349e1e6d69068d1618d7d0d4f83d2952f6a637f3637613af764d4f26af23'
            'SKIP'
            '0a620e492965b88c7db0116ebee9a7131549f1abce02812876d25806d424e2458305b6423644a2992d701b6908541b75571390e1577498730246c6f819c5cdc0'
            '485f108e96b92e76620fcf8e3ddc350d67c5458eea0adcd6a5e11d29fa980c7d9672f832aaec644b39f57674fa505a8d5748059a6b0da027be38b1a7ed01036e'
            '4b93c1af24d5afc84445b00563da0ab60d46654830104690e53844dc1bdb278ac39d8a059c12501f34afb31411a53e2b46f3f953e690f582e3561a9eeb9131a5')
validpgpkeys=('C2C96B10011FE009E6D1DF828A75D10998012C7E') # Jay Berkenbilt <ejb@ql.org>

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-fix-printf-on-mingw-w64.patch"
  patch -p1 -i "${srcdir}/0002-enable-advapi32-mingw.patch"
  patch -p1 -i "${srcdir}/0003-install-rule.patch"

  ./autogen.sh
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  cp -rf ${_realname}-${pkgver} build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}

  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-crypto-gnutls \
    --disable-crypto-openssl \
    --disable-implicit-crypto \
    --disable-test-compare-images \
    --enable-external-libs \
    --with-buildrules=mingw
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install
}
