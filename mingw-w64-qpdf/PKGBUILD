# Maintainer: Oleg Tolmatcev <oleg.tolmatcev@gmail.com>

_realname=qpdf
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=7.0.0
pkgrel=1
pkgdesc="QPDF is a program that does structural, content-preserving transformations on PDF files (mingw-w64)"
arch=('any')
url="http://qpdf.sourceforge.net/"
license=("custom:Artistic-2.0")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
  "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
  "${MINGW_PACKAGE_PREFIX}-zlib"
  "${MINGW_PACKAGE_PREFIX}-pcre"
  "${MINGW_PACKAGE_PREFIX}-libjpeg")
source=("http://sourceforge.net/projects/${_realname}/files/${_realname}/${pkgver}/${_realname}-${pkgver}.tar.gz"
  "0001-fix-printf-on-mingw-w64.patch")
md5sums=('c3ff408f69b3a6b2b3b4c8b373b2600c'
         'f5d252e8aa12c68acf294306137d2375')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-fix-printf-on-mingw-w64.patch"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  ./configure \
  --prefix=${MINGW_PREFIX} \
  --disable-test-compare-images \
  --enable-external-libs \
  --with-windows-wordsize=${MINGW_PREFIX: -2} \
  --with-buildrules=mingw
  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  mkdir -p "${pkgdir}/${MINGW_PREFIX}/bin"
  cp qpdf/build/qpdf.exe "${pkgdir}/${MINGW_PREFIX}/bin/"
  cp libqpdf/build/qpdf18.dll "${pkgdir}/${MINGW_PREFIX}/bin/"
  cp qpdf/fix-qdf "${pkgdir}/${MINGW_PREFIX}/bin/"
  mkdir -p "${pkgdir}/${MINGW_PREFIX}/share/doc/${_realname}"
  cp doc/qpdf-manual.html "${pkgdir}/${MINGW_PREFIX}/share/doc/${_realname}/"
  cp doc/qpdf-manual.pdf "${pkgdir}/${MINGW_PREFIX}/share/doc/${_realname}/"
  cp doc/stylesheet.css "${pkgdir}/${MINGW_PREFIX}/share/doc/${_realname}/"
  mkdir -p "${pkgdir}/${MINGW_PREFIX}/include/${_realname}"
  cp include/qpdf/*.h "${pkgdir}/${MINGW_PREFIX}/include/${_realname}/"
  cp include/qpdf/*.hh "${pkgdir}/${MINGW_PREFIX}/include/${_realname}/"
  mkdir -p "${pkgdir}/${MINGW_PREFIX}/lib"
  cp libqpdf/build/libqpdf.a "${pkgdir}/${MINGW_PREFIX}/lib/"
  
  mkdir -p ${pkgdir}/${MINGW_PREFIX}/share/licenses/${_realname}
  cp Artistic-2.0 ${pkgdir}/${MINGW_PREFIX}/share/licenses/${_realname}/
}
