# Maintainer: Tetsuma Hoshino <t-hoshino@pony-e.jp>
# Based on mingw-w64-arm-none-eabi-binutils

_realname=binutils
_target=v850-none-eabi

pkgbase=mingw-w64-${_target}-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_target}-${_realname}")
pkgver=2.37
pkgrel=1
pkgdesc='GNU Tools for V850 Microcontrollers - Binutils (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=('GPL')
url='https://www.gnu.org/software/binutils/binutils.html'
groups=("${MINGW_PACKAGE_PREFIX}-${_target}-toolchain")
options=('staticlibs')
source=(
  "https://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.xz"
  "001-binutils-2.37-change-uint-to-unsigned.patch"
  "002-binutils-2.37-fix-relocations.patch"
)
sha256sums=(
  '820d9724f020a3e69cb337893a0b63c2db161dadcb0e06fc11dc29eb1e84a32c'
  '1a3a1850c928bbaaa189e319c66ada6157e108b1502458a04e60f19b138ba94c'
  '9ca225344e8727c06c46c1ed2bc408657b87f39380da16c2cddc85d971f68070'
)

prepare() {
  cd ${srcdir}/binutils-${pkgver}

  patch -p 1 -i ${srcdir}/001-binutils-2.37-change-uint-to-unsigned.patch
  patch -p 1 -i ${srcdir}/002-binutils-2.37-fix-relocations.patch

  mkdir -p binutils-build-${MINGW_CHOST}
}

build() {
  cd ${srcdir}/binutils-${pkgver}/binutils-build-${MINGW_CHOST}

  ../configure \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --target=${_target} \
    --enable-multilib \
    --enable-interwork \
    --with-gnu-as \
    --with-gnu-ld \
    --disable-nls \
    --enable-ld=default \
    --enable-gold \
    --enable-plugins \
    --enable-deterministic-archives
  make
}

package() {
  cd ${srcdir}/binutils-${pkgver}/binutils-build-${MINGW_CHOST}

  make DESTDIR="${pkgdir}" install

  cd ${pkgdir}${MINGW_PREFIX}

  for info in as bfd binutils gprof ld; do
    mv share/info/${info}.info share/info/v850-none-eabi-${info}.info
  done
}
