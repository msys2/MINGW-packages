# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libguess
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.2
pkgrel=2
pkgdesc="High-speed character set detection library (mingw-w64)"
arch=('any')
url="http://atheme.org/projects/libguess.html"
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-libmowgli")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('staticlibs' 'strip')
source=("http://rabbit.dereferenced.org/%7Enenolod/distfiles/${_realname}-${pkgver}.tar.bz2")
md5sums=('7633fbfbeb75b1ded7f33cca3d8d4762')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  sed 's/libmowgli >= 0.9.50/libmowgli-2/g' -i configure
}

build() {
  cp -rf ${_realname}-${pkgver} ${srcdir}/build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}

  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
