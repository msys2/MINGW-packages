# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=klavaro
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=3.14
pkgrel=1
pkgdesc="A flexible touch typing tutor. (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://klavaro.sourceforge.io'
msys2_repository_url="https://sourceforge.net/p/klavaro/code/HEAD/tree/trunk/"
license=('spdx:GPL-3.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-gettext-runtime"
         "${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-gtk3"
         "${MINGW_PACKAGE_PREFIX}-gtkdatabox"
         "${MINGW_PACKAGE_PREFIX}-pango")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cairo"
             "intltool")
source=("https://downloads.sourceforge.net/${_realname}/${_realname}/${_realname}-${pkgver}.tar.bz2"
        "001-fix-build-with-clang.patch")
sha256sums=('87187e49d301c510e6964098cdb612126bf030d2a875fd799eadcad3eae56dab'
            '57709404f7df88d83181c1da7ab435eaf6026d8017f1c3cc7c4019ad42b2121b')

prepare() {
  cd "${_realname}-${pkgver}"

  patch -p1 -i "${srcdir}/001-fix-build-with-clang.patch"
}

build() {
  mkdir -p "build-${MSYSTEM}" && cd "build-${MSYSTEM}"

  CFLAGS+=" -Wno-unused-parameter" \
  ../"${_realname}-${pkgver}"/configure \
    --prefix="${MINGW_PREFIX}" \
    --enable-shared

  make
}

package() {
  cd "build-${MSYSTEM}"

  make install DESTDIR="${pkgdir}"

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/COPYING \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
