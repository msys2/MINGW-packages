# Contributor: Dirk Stolle

_realname=deno
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.5.5
pkgrel=1
_rusty_v8_ver=140.2.0
pkgdesc="A secure runtime for JavaScript and TypeScript (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://deno.land/'
msys2_references=(
  'archlinux: deno'
  'gentoo: dev-lang/deno-bin'
  'purl: pkg:cargo/deno'
)
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-cargo-edit"
             "${MINGW_PACKAGE_PREFIX}-clang"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-gn"
             "${MINGW_PACKAGE_PREFIX}-lld"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-protobuf"
             "${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-v8"
             'git')
source=("git+https://github.com/denoland/deno.git#tag=v$pkgver"
        "git+https://github.com/denoland/rusty_v8.git#tag=v$_rusty_v8_ver")
sha256sums=('d9c0d7091e5d174db5e10098d9baa953fc04f3fcddf3dcd311217ef2955c0c44'
            '417a2374cfd553ce07612e690e57fc3a450e3a59e45da9f316bb26acd5b62643')

prepare() {
  cd rusty_v8
  git config -f .gitmodules submodule.v8.shallow true
  git submodule update --init --recursive

#  patch -Np1 -i "${srcdir}"/0001-A-really-important-fix.patch
#  patch -Np1 -i "${srcdir}"/0002-A-less-important-fix.patch

  cd ../deno
  echo -e "\n[patch.crates-io]\nv8 = { path = '../rusty_v8' }" >> Cargo.toml

  # if cargo wants to make an http request at build stage, use `cargo fetch --locked` instead
  cargo fetch --target "${RUST_CHOST}"
}

build() {
  cd "${_realname}"

  export CC=clang CXX=clang++ AR=ar NM=nm
  export CLANG_BASE_PATH=${MINGW_PREFIX}
  export GN=${MINGW_PREFIX}/bin/gn
  export NINJA=${MINGW_PREFIX}/bin/ninja
  

  cargo build --release --frozen
}

check() {
  cd "${_realname}"

  cargo test --release --frozen
}

package() {
  cd "${_realname}"

#  cargo install \
#    --offline \
#    --no-track \
#    --frozen \
#    --path . \
#    --root "${pkgdir}${MINGW_PREFIX}"


  install -Dm755 target/release/deno "$pkgdir"/usr/bin/deno

  install -dm755 "$pkgdir${MINGW_PREFIX}"/share/bash-completion/completions
  ./target/release/deno completions bash > "$pkgdir${MINGW_PREFIX}"/share/bash-completion/completions/deno
  install -dm755 "$pkgdir${MINGW_PREFIX}"/share/zsh/site-functions
  ./target/release/deno completions zsh > "$pkgdir${MINGW_PREFIX}"/share/zsh/site-functions/_deno
  install -dm755 "$pkgdir${MINGW_PREFIX}"/share/fish/vendor_completions.d
  ./target/release/deno completions fish > "$pkgdir${MINGW_PREFIX}"/share/fish/vendor_completions.d/deno.fish

  install -Dm644 LICENSE.md "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
