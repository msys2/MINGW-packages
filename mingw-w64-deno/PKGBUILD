# Contributor: Dirk Stolle

_realname=deno
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.5.5
pkgrel=1
_rusty_v8_ver=140.2.0
pkgdesc="A secure runtime for JavaScript and TypeScript (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url='https://deno.land/'
msys2_references=(
  'archlinux: deno'
  'gentoo: dev-lang/deno-bin'
  'purl: pkg:cargo/deno'
)
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-icu")
makedepends=("${MINGW_PACKAGE_PREFIX}-cargo-edit"
             "${MINGW_PACKAGE_PREFIX}-clang"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-gn"
             "${MINGW_PACKAGE_PREFIX}-lcms2"
             "${MINGW_PACKAGE_PREFIX}-lld"
             "${MINGW_PACKAGE_PREFIX}-llvm"
             "${MINGW_PACKAGE_PREFIX}-nasm"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-protobuf"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-rust-bindgen"
             'git')
source=("git+https://github.com/denoland/deno.git#tag=v${pkgver}"
        "git+https://github.com/denoland/rusty_v8.git#tag=v${_rusty_v8_ver}"
        "001-add-mingw-toolchain.patch"
        "002-buildflags-fixes.patch"
        "003-fix-macros-and-functions.patch"
        "004-fix-static-assert-implementations.patch"
        "005-fix-conflicting-macros.patch"
        "006-support-clang-in-mingw-mode.patch"
        "007-snapshot-use-system-zlib-header.patch"
        "008-prioritized-native-thread-on-windows.patch"
        "009-unicode-for-wide-char-functions.patch"
        "010-disable-msvc-hack.patch"
        "011-make-sure-that-__rdtsc-is-declared.patch"
        "012-remove-dllimport-attributes.patch"
        "013-builtin-deps-fixes.patch"
        "014-heap-use-proper-sources.patch"
        "015-abseil-build-as-static-lib.patch"
        "016-rusty_v8-mingw-toolchain.patch"
        "017-deno-use-windows-sys-for-all.patch"
        "018-deno-update-aws-lc.patch"
        "019-rusty_v8-support-icu78.patch"
        "020-rusty_v8-link-system-libs.patch"
        "icu.gn"
        "zlib.gn")
sha256sums=('d9c0d7091e5d174db5e10098d9baa953fc04f3fcddf3dcd311217ef2955c0c44'
            '417a2374cfd553ce07612e690e57fc3a450e3a59e45da9f316bb26acd5b62643'
            '978f447390c550d2b972681ef4b417e2ac35ae89f641da84c2b0611374bac97e'
            'ebd7b8e2b91e02879cb6c83154cb276be2a820f939b6d166e98b39c175b0eb51'
            '274d4cfc5a8a4f99699e67fed36565f315dd3bc0d1335bbf5070ba7052d99541'
            'aa7255002773aa3944027e0aab19627eee089c2ec3893713c8c7ff082e214553'
            '75a0d6fbf04cb9bbb724230104cf28595cd9b1169a0e91a4090bd0d3b6f80a8c'
            'c860f43dfaeb77b7567044a45bcbc6d5508e1571fbb31cb49cfdfbe86a031916'
            '1fca982ec6ede3b29d2692231569944ce0ad33519b47fd6fb01b0975c10eb6bd'
            '3c7b480cd4c19e8120c9adb27d7fd45b8d98c5bfc13c153479afa0636c319153'
            '4edcd5f7d2f9b52c8783e81b7f06f6e5b386aa6c0143cc2f9054754db53919cf'
            'e4d8e57914cfcf6c0ea299d0556d7b747271e5a7ec66e1d8d52394fcd0da34b6'
            '1ef6854b6a68101ab177a7a95389e9b03e7b8c4e3223320dfc9ce4405cbf5bfe'
            '1d03e6c517528a31f6ff38d251b011e34a667f0b60968817b573677501dc20c1'
            '37a29668fb02d4fa60062c02f0d24dce4ab9b7fce4d7f5a8e6eca745fa125be2'
            '5b1c2a1c4faefdbb918640b394a100b08c5b9940547d4ce5a04b8d9dc40559a7'
            '7e3555a4128d8b69cd9733b14b114d27063667ae0687b693c51fd1881f847e93'
            'dca224f07c1c316eda44feff1baf741f62068e103f4644994f4290f811fc5f61'
            'ce8692b28ca6977c33a4a7b4e4d7403834da0be032316a7bffdd409b2557e532'
            'ed15a6beeedea5201e91923e327c997814e77fc4f0c4543a9b409e39982e84f6'
            'd1406cbcd27900d64a7e19eb755d010db9c4ec5b67ba7f9e41d02fff5c0df0bf'
            'dcdbab75a062b225f5d91023454acf949044a59f068b8e4f6a259a6a33010cf3'
            'e1bc88af48143f29f5805b8ac5d77addef1b09318d1f5f127a9281872c00303b'
            '7e1e39a3dc69632f7bf9182c92405186ce709aca921cf0abefadb6fa8b825510')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -p1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd rusty_v8
  git config -f .gitmodules submodule.v8.shallow true
  git submodule update --init --recursive
  local _srcdir="${srcdir}"/rusty_v8

  echo ":: Patching rusty_v8 source"
  patch -p1 -i "${srcdir}"/016-rusty_v8-mingw-toolchain.patch
  patch -p1 -i "${srcdir}"/019-rusty_v8-support-icu78.patch
  patch -p1 -i "${srcdir}"/020-rusty_v8-link-system-libs.patch

  # apply patches for v8 from mingw-w64-v8 package
  echo ":: Patching build directory"
  cd "${_srcdir}"/build
  patch -p1 -i "${srcdir}"/001-add-mingw-toolchain.patch
  python "${_srcdir}"/build/util/lastchange.py -o "${_srcdir}"/build/util/LASTCHANGE
  echo "build_with_chromium = false" > "${_srcdir}"/build/config/gclient_args.gni

  echo ":: Patching abseil-cpp directory"
  cd "${_srcdir}"/third_party/abseil-cpp
  patch -p1 -i "${srcdir}"/015-abseil-build-as-static-lib.patch

  echo ":: Patching icu directory"
  ln -sf "${srcdir}"/icu.gn "${_srcdir}"/third_party/icu/BUILD.gn
  echo "icu_use_data_file = false" > "${_srcdir}"/third_party/icu/config.gni

  echo ":: Patching v8 source"
  cd "${_srcdir}/v8"
  apply_patch_with_msg \
    002-buildflags-fixes.patch \
    003-fix-macros-and-functions.patch \
    004-fix-static-assert-implementations.patch \
    005-fix-conflicting-macros.patch \
    006-support-clang-in-mingw-mode.patch \
    007-snapshot-use-system-zlib-header.patch \
    008-prioritized-native-thread-on-windows.patch \
    009-unicode-for-wide-char-functions.patch \
    010-disable-msvc-hack.patch \
    011-make-sure-that-__rdtsc-is-declared.patch \
    012-remove-dllimport-attributes.patch \
    013-builtin-deps-fixes.patch \
    014-heap-use-proper-sources.patch

  echo ":: Patching deno source"
  cd "${srcdir}"/deno
  patch -p1 -i "${srcdir}"/017-deno-use-windows-sys-for-all.patch
  patch -p1 -i "${srcdir}"/018-deno-update-aws-lc.patch
  echo -e "\n[patch.crates-io]\nv8 = { path = '../rusty_v8' }" >> Cargo.toml

  rm rust-toolchain.toml
  cargo update -p v8
  cargo fetch --locked --target "${RUST_CHOST}"
}

build() {
  cd "${_realname}"

  local _arch=x64
  local _cfg=true
  if [[ ${CARCH} == aarch64 ]]; then
    _arch=arm64
    _cfg=false
  fi

  local _clang_version=$(clang --version | grep -m1 version | sed 's/.* \([0-9]\+\).*/\1/')
  local _extra_gn_args="
    target_cpu=\"${_arch}\"
    is_debug=false
    use_lld=true
    use_siso=false
    v8_enable_etw_stack_walking=false
    v8_enable_system_instrumentation=false
    v8_enable_temporal_support=false
    v8_enable_verify_heap=false
    v8_symbol_level=0
    chrome_pgo_phase=0
    enable_iterator_debugging=false
    win_enable_cfg_guards=${_cfg}
    clang_version=\"${_clang_version}\""

  export CC=clang CXX=clang++ AR=llvm-ar NM=llvm-nm
  export CLANG_BASE_PATH="${MINGW_PREFIX}"
  export GN="${MINGW_PREFIX}/bin/gn"
  export NINJA="${MINGW_PREFIX}/bin/ninja"
  export V8_FROM_SOURCE=1
  export LCMS2_LIB_DIR="${MINGW_PREFIX}/lib"
  export LIBCLANG_PATH="${MINGW_PREFIX}/bin"
  export EXTRA_GN_ARGS="${_extra_gn_args[@]}"
  # to properly link cxx std libraries
  export GN_ARGS="use_custom_libcxx=false"
  export BINDGEN_EXTRA_CLANG_ARGS="--target=${CARCH}-pc-windows-gnu"
  # manually link system libraries...
  if [[ ${MINGW_PACKAGE_PREFIX} != *-clang-* ]]; then
    export LDFLAGS+=" -licuin -licuuc -licudt -latomic"
  fi

  cargo build --release --frozen -p deno
}

check() {
  cd "${_realname}"

  cargo test --release --frozen -p deno
}

package() {
  cd "${_realname}"

  install -Dm755 target/release/deno "${pkgdir}${MINGW_PREFIX}"/bin/deno

  install -dm755 "${pkgdir}${MINGW_PREFIX}"/share/bash-completion/completions
  ./target/release/deno completions bash > "${pkgdir}${MINGW_PREFIX}"/share/bash-completion/completions/deno
  install -dm755 "${pkgdir}${MINGW_PREFIX}"/share/zsh/site-functions
  ./target/release/deno completions zsh > "${pkgdir}${MINGW_PREFIX}"/share/zsh/site-functions/_deno
  install -dm755 "${pkgdir}${MINGW_PREFIX}"/share/fish/vendor_completions.d
  ./target/release/deno completions fish > "${pkgdir}${MINGW_PREFIX}"/share/fish/vendor_completions.d/deno.fish

  install -Dm644 LICENSE.md "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
