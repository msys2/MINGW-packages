# $Id$
# Maintainer: Zach Bacon <11doctorwhocanada@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Maintainer: Bartlomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Hilinus <itahilinus[at]hotmail[dot]it>

_realname=gtk-engine-unico
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.0.2
pkgrel=2
pkgdesc='Unico GTK3 theme engine'
arch=('any')
url='https://launchpad.net/unico'
license=('GPL')
depends=("${MINGW_PACKAGE_PREFIX}-gtk3")
makedepends=("${MINGW_PACKAGE_PREFIX}-gnome-common")
source=("gtk-engine-unico-139.tar.gz::http://bazaar.launchpad.net/~unico-team/unico/trunk/tarball/139")
sha256sums=('9ef0ef5dfbbb5f352a864a3d91f13872d05361343e5d4cc3be48d1f061189251')

build() {
  cd \~unico-team/unico/trunk

  autoreconf -fi
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ${srcdir}/\~unico-team/unico/trunk/configure --prefix=${MINGW_PREFIX} --disable-static
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
