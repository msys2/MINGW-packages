# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=coin-or-cbc
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.10.11
pkgrel=1
pkgdesc="COIN-OR Branch-and-Cut solver. (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64')
url='https://github.com/coin-or/Cbc'
msys2_references=(
  'archlinux: coin-or-cbc'
)
license=('spdx:EPL-2.0')
groups=("${MINGW_PACKAGE_PREFIX}-coin-or")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-coin-or-asl"
         "${MINGW_PACKAGE_PREFIX}-coin-or-cgl"
         "${MINGW_PACKAGE_PREFIX}-coin-or-clp"
         "${MINGW_PACKAGE_PREFIX}-coin-or-coinutils"
         "${MINGW_PACKAGE_PREFIX}-coin-or-osi"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread-git")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-nauty")
source=("${url}/archive/releases/${pkgver}/${_realname}-${pkgver}.tar.gz"
        001-fix-build-on-mingw.patch
        002-fix-build-with-clang.patch)
sha256sums=('1fb591dd88336fdaf096b8e42e46111e41671a5eb85d4ee36e45baff1678bd33'
            '9654c64bc275e923a4788bba213cf76805edbc4c3f1736c4eecc9617a96e0832'
            '424e74241f23f259682257e6019d701206d7df48a96e50a72b3262981906f7d8')

prepare() {
  cd Cbc-releases-${pkgver}
  patch -p1 -i "${srcdir}"/001-fix-build-on-mingw.patch

  if [[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]]; then
    patch -p1 -i "${srcdir}"/002-fix-build-with-clang.patch
  fi
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  export lt_cv_deplibs_check_method='pass_all'

  CFLAGS+=" -Wno-implicit-function-declaration" \
  CXXFLAGS+=" -Wno-register" \
  ../"Cbc-releases-${pkgver}"/configure \
    --prefix="${MINGW_PREFIX}" \
    --enable-shared \
    --disable-static \
    --enable-dependency-linking \
    --enable-cbc-parallel \
    --with-pthreadsw32-lib="-lpthread" \
    --with-pthreadsw32-incdir=${MINGW_PREFIX}/include \
    --with-nauty-lib=${MINGW_PREFIX}/lib/libnauty.a \
    --with-nauty-incdir=${MINGW_PREFIX}/include/nauty

  make
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  make check
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  make install DESTDIR="${pkgdir}"

  # Remove nauty from linker flags in pc file, it is statically compiled
  sed -i -e "s|${MINGW_PREFIX}/lib/libnauty.a||g" "${pkgdir}"${MINGW_PREFIX}/lib/pkgconfig/cbc.pc

  install -Dm644 "${srcdir}/Cbc-releases-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
