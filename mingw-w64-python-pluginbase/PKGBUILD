# Maintainer: Joel Holdsworth <jholdsworth@nvidia.com>

_realname=pluginbase
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.0.1
pkgrel=2
pkgdesc="Python library that enables the development of flexible plugin systems (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
license=('spdx:BSD-3-Clause')
url="http://pluginbase.pocoo.org/"
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/mitsuhiko/pluginbase/archive/v${pkgver}.tar.gz")
sha256sums=('bb95fe6a84ec6583e1be399ef6705011262757c69fb99fa24317fcc6e8998ffc')

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}/$_realname-$pkgver"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/$_realname-$pkgver"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
   ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/COPYING"
}
