# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=libpaper
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.1.24
_suffix=+nmu4
pkgrel=1
pkgdesc="library for handling paper characteristics (mingw-w64)"
arch=('any')
url='http://packages.debian.org/unstable/source/libpaper'
license=('GPL-2')
source=("http://http.debian.net/debian/pool/main/libp/libpaper/${_realname}_${pkgver}${_suffix}.tar.gz")
sha256sums=('2491fce3f590d922d2d3070555df4425921b89c76a18e1c62e36336d6657526a')

prepare() {
  cd $srcdir/${_realname}-${pkgver}${_suffix}
}

build() {
  cd "$srcdir"/${_realname}-${pkgver}${_suffix}
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}
  ../${_realname}-${pkgver}${_suffix}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared

  make
}

check() {
  cd "${srcdir}"/build-${CARCH}
  make check
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install DESTDIR="${pkgdir}"
}
