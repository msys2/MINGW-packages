# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=unrar
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=7.2.4
pkgrel=1
pkgdesc='The RAR uncompression program (mingw-w64)'
arch=(any)
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://www.rarlab.com/rar_add.htm'
msys2_references=(
  'anitya: 13306'
  'archlinux: unrar'
  "cpe: cpe:/a:rarlab:unrar"
  'gentoo: app-arch/unrar'
)
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc")
source=("https://www.rarlab.com/rar/unrarsrc-${pkgver}.tar.gz"
        0001-fix-makefile.patch
        0002-fix-redefinition.patch)
sha256sums=('b02e571a33af7711cd803080500370dc1d28eea82b2032480819d27462ad8b31'
            '3746f9c07b6e41879787de69e9e31bbff2de36832e06a624818337b5c461f7cd'
            '1733f6cd2cdcaa0edd20d0a5b3f1eabba7457ecc4c8a83eee82b2d6d965faa60')

_apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -p1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd ${_realname}

  _apply_patch_with_msg \
    0001-fix-makefile.patch \
    0002-fix-redefinition.patch
}

build() {
  cd ${_realname}
  make
}

package() {
  cd ${_realname}
  install -Dm755 unrar.exe "${pkgdir}${MINGW_PREFIX}/bin/unrar.exe"
  install -Dm644 license.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
