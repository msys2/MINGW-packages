# Maintainer: Andrew Sun <adsun701@gmail.com>

_realname=4th
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=3.62.5
pkgrel=2
pkgdesc="A Forth Compiler (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://www.xs4all.nl/~thebeez/4tH/index.html"
license=('LGPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-binutils")
options=(!strip)
source=("${_realname}-${pkgver}.tar.gz"::"https://downloads.sourceforge.net/sourceforge/forth-4th/${_realname}-${pkgver}-x86_64.tar.gz")
sha256sums=('e51ced7e71c942a27104baeae6cff68f44ae37a1c5813b6c12f9a3a1cfde309a')

build() {
  cd "${srcdir}/${_realname}-${pkgver}/sources/"
  [[ -d "${srcdir}"/build-${MSYSTEM} ]] && rm -rf "${srcdir}"/build-${MSYSTEM}
  mkdir -p "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}
  cp -R "${srcdir}"/${_realname}-${pkgver}/sources/. "${srcdir}"/build-${MSYSTEM}
  CROSS="${MINGW_PREFIX}/bin/" \
  make -f Makefile.W32 CC="$CC"
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}/sources/"
  install -D -m644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
  install -d "${pkgdir}${MINGW_PREFIX}/"{share/doc/${_realname},lib,bin}
  CROSS="${MINGW_PREFIX}/bin/" \
  make -f Makefile.W32 CC="$CC" BINARIES="${pkgdir}${MINGW_PREFIX}/bin" \
       LIBRARIES="${pkgdir}${MINGW_PREFIX}/lib" \
       DOCDIR="${pkgdir}${MINGW_PREFIX}/share/doc" \
       MANDIR="${pkgdir}${MINGW_PREFIX}/share/man" install
}
