# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=docutils
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgname=("${_mingw_suff}-python2-${_realname}" "${_mingw_suff}-python3-${_realname}")
pkgver=0.11
pkgrel=3
pkgdesc="Set of tools for processing plaintext docs into formats such as HTML, XML, or LaTeX (mingw-w64)"
arch=('any')
license=('custom')
url="http://docutils.sourceforge.net"
depends=("${_mingw_suff}-python2" "${_mingw_suff}-python3")
source=("http://downloads.sourceforge.net/${_realname}/${_realname}-$pkgver.tar.gz")
md5sums=('20ac380a18b369824276864d98ec0ad6')
 
build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  ${MINGW_PREFIX}/bin/python3 setup.py build --build-lib=build/python3
  find build/python3 -type f -exec \
    sed -i '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/python3,' {} \;
  
  ${MINGW_PREFIX}/bin/python2 setup.py build --build-lib=build/python2
  find build/python2 -type f -exec \
    sed -i '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' {} \;
}

check() {
  cd ${_realname}-$pkgver
  # we need utf locale to valid utf8 tests
  export LANG=en_US.UTF-8
  # Disable python3 check
  #msg2 'python checks'
  #PYTHONPATH="$PWD/build/python3/" ${MINGW_PREFIX}/bin/python3 test3/alltests.py
  msg2 'python2 checks'
  PYTHONPATH="$PWD/build/python2/" ${MINGW_PREFIX}/bin/python2 test/alltests.py
}

package_python3-docutils() {
  depends=("${_mingw_suff}-python3")

  pushd ${MINGW_PREFIX} > /dev/null
  local _mingw_prefix=`pwd -W`
  popd > /dev/null
  
  cd ${_realname}-$pkgver
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py build --build-lib=build/python3 \
      install --prefix=${MINGW_PREFIX} --root="$pkgdir" --optimize=1

  # fix python-docutils conflict
  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*.py; do
    sed -e "s|${_mingw_prefix}|${MINGW_PREFIX}|g" -i ${_f}
    mv -v "$_f" "${_f%.py}3.py"
  done
  
  # symlink without .py
  for f in "${pkgdir}${MINGW_PREFIX}"/bin/*.py; do
    ln -s "$(basename $f)" "${pkgdir}${MINGW_PREFIX}/bin/$(basename $f .py)"
  done
  # setup license
  install -D -m644 COPYING.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/$pkgname/COPYING.txt"
  install -D -m644 licenses/python* "${pkgdir}${MINGW_PREFIX}/share/licenses/$pkgname/"
}
 
package_python2-docutils() {
  depends=("${_mingw_suff}-python2")
  provides=("${_mingw_suff}-docutils=$pkgver")
  replaces=("${_mingw_suff}-docutils")

  pushd ${MINGW_PREFIX} > /dev/null
  local _mingw_prefix=`pwd -W`
  popd > /dev/null
  
  cd ${_realname}-$pkgver
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py build --build-lib=build/python2 \
         install --prefix=${MINGW_PREFIX} --root="$pkgdir" --optimize=1

  # fix python command in files
  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*.py; do
    sed -e "s|${_mingw_prefix}|${MINGW_PREFIX}|g" -i ${_f}
  done
  # symlink without .py
  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*.py; do
  ln -s "$(basename $_f)" "${pkgdir}${MINGW_PREFIX}/bin/$(basename $_f .py)"
  done
  # setup license
  install -D -m644 COPYING.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/$pkgname/COPYING.txt"
  install -D -m644 licenses/python* "${pkgdir}${MINGW_PREFIX}/share/licenses/$pkgname/"
}

package_mingw-w64-i686-python2-docutils() {
  install=${_realname}2-${CARCH}.install
  package_python2-docutils
}

package_mingw-w64-i686-python3-docutils() {
  install=${_realname}3-${CARCH}.install
  package_python3-docutils
}

package_mingw-w64-x86_64-python2-docutils() {
  install=${_realname}2-${CARCH}.install
  package_python2-docutils
}

package_mingw-w64-x86_64-python3-docutils() {
  install=${_realname}3-${CARCH}.install
  package_python3-docutils
}
