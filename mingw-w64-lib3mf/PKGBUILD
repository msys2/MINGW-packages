# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=lib3mf
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.3.0
pkgrel=1
pkgdesc='Lib3MF is the reference implementation of the 3D Manufacturing Format file standard https://3mf.io/ (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64')
msys2_repository_url='https://github.com/3MFConsortium/lib3mf'
url='https://3mf.io/'
msys2_references=(
  "cpe: cpe:/a:3mf:lib3mf"
)
license=('spdx:BSD-2-Clause')
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}-devel")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}-devel")
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}-devel")
depends=(
  "${MINGW_PACKAGE_PREFIX}-gcc-libs"
  "${MINGW_PACKAGE_PREFIX}-libzip"
  "${MINGW_PACKAGE_PREFIX}-zlib"
)
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-pkgconf")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/3MFConsortium/lib3mf/archive/v${pkgver}.tar.gz"
        "0001-lib3mf-cmake-fix-linking-function.patch")
sha256sums=('54d98ed1026294b27229f35c035971023a9b5e6f1b3fccb0ae5e230e5ef1c241'
            '4613080dd0b2004f0c80bb88260d3f9e85ecc86b7228ca4f63f389ab12cc9eff')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-lib3mf-cmake-fix-linking-function.patch"
}

build() {
  [[ -d "${srcdir}/build-${MSYSTEM}" ]] && rm -rf "${srcdir}/build-${MSYSTEM}"
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G "Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DLIB3MF_TESTS=OFF \
    -DUSE_INCLUDED_ZLIB=OFF \
    -DUSE_INCLUDED_LIBZIP=OFF \
    -DUSE_INCLUDED_SSL=OFF \
    ../${_realname}-${pkgver}

  cmake --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  DESTDIR="${pkgdir}" cmake --install .
}
