# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=lib3mf
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}" "${MINGW_PACKAGE_PREFIX}-${_realname}-debug")
pkgver=1.8.1
pkgrel=1
pkgdesc="Lib3MF is the reference implementation of the 3D Manufacturing Format file standard (mingw-w64)"
arch=('any')
license=('custom')
url="https://github.com/3MFConsortium/lib3mf"
makedepends=("make"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cmake")
options=(!strip staticlibs !buildflags)
source=(${_realname}-${pkgver}.tar.gz::https://github.com/3MFConsortium/${_realname}/archive/v${pkgver}.tar.gz)
sha256sums=('207dd142c9ca86a4fb1a4b2baadbdf579f35e03f9b8bf5c02dae027da5ae9d17')

build() {
  [[ -d ${srcdir}/release-${MINGW_CHOST} ]] && rm -rf ${srcdir}/release-${MINGW_CHOST}
  mkdir -p ${srcdir}/release-${MINGW_CHOST} && cd ${srcdir}/release-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB3MF_TESTS=OFF \
    ../${_realname}-${pkgver}
  make

  [[ -d ${srcdir}/debug-${MINGW_CHOST} ]] && rm -rf ${srcdir}/debug-${MINGW_CHOST}
  mkdir -p ${srcdir}/debug-${MINGW_CHOST} && cd ${srcdir}/debug-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Debug \
    -DCMAKE_DEBUG_POSTFIX=d \
    -DLIB3MF_TESTS=OFF \
    ../${_realname}-${pkgver}
}

package_release() {
  depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")

  cd ${srcdir}/release-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install
}

package_debug() {
  depends=("${MINGW_PACKAGE_PREFIX}-${_realname}")

  cd ${srcdir}/debug-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install

  rm -rf ${pkgdir}${MINGW_PREFIX}/include
  rm -rf ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig
}

package_mingw-w64-i686-lib3mf() {
  package_release
}

package_mingw-w64-i686-lib3mf-debug() {
  package_debug
}

package_mingw-w64-x86_64-lib3mf() {
  package_release
}

package_mingw-w64-x86_64-lib3mf-debug() {
  package_debug
}
