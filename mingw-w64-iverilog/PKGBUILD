# Maintainer: umarcor <unai.martinezcorral@ehu.eus>

_realname=iverilog
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=11.0
pkgrel=1
pkgdesc="Icarus Verilog, is a Verilog simulation and synthesis tool (mingw-w64)"
arch=('any')
url="http://iverilog.icarus.com/"
license=('GPLv2+')
depends=("${MINGW_PACKAGE_PREFIX}-readline"
         "${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-ghostscript")

_ver="11_0"
source=("iverilog::https://codeload.github.com/steveicarus/iverilog/tar.gz/v${_ver}")
sha256sums=('6327fb900e66b46803d928b7ca439409a0dc32731d82143b20387be0833f1c95')

build() {
  cd "${srcdir}/${_realname}-${_ver}/"
  sh autoconf.sh
  ./configure \
    --prefix="${MINGW_PREFIX}" \
    --host="$CARCH"-w64-mingw32
  make
}

check() {
  cd "${srcdir}/${_realname}-${_ver}/"
  make check
#  git clone git://github.com/steveicarus/ivtest.git
#  cd ivtest
#  ./regress
}

package() {
  cd "${srcdir}/${_realname}-${_ver}/"
  make DESTDIR="${pkgdir}" install
  mkdir -p "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}"
  mv "${pkgdir}${MINGW_PREFIX}"/*.pdf "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}"

  _licenses="${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
  mkdir -p "${_licenses}"
  install -m 644 COPYING "${_licenses}"
}
