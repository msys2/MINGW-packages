# Maintainer: Bjoern Lemke <lemke@lemke-it.com>

_realname=cego
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.30.26
pkgrel=1
pkgdesc="Cego database system (mingw-w64)"
arch=('any')
url='http://www.lemke-it.com/'
license=('GPL3')
source=("http://www.lemke-it.com/${_realname}-${pkgver}.tar.gz")
depends=("${MINGW_PACKAGE_PREFIX}-lfcbase"
         "${MINGW_PACKAGE_PREFIX}-lfcxml")
sha256sums=('c4d95202fbd1a4657902ca56e0e91489617e9bcfcaac3bb285b2e8233283c22a')

prepare() {
  cd $srcdir/${_realname}-${pkgver}
}

build() {
  cd "$srcdir"/${_realname}-${pkgver}
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared

  make
}

check() {
  cd "${srcdir}"/build-${CARCH}
  make check
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install DESTDIR="${pkgdir}"
}

