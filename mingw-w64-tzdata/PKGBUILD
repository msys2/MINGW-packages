# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=tzdata
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2025c
pkgrel=1
pkgdesc="Sources for time zone and daylight saving time data (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://www.iana.org/time-zones"
license=('custom: public domain')
makedepends=('tzcode')
options=('!emptydirs')
source=(https://www.iana.org/time-zones/repository/releases/${_realname}${pkgver}.tar.gz{,.asc}
        https://www.iana.org/time-zones/repository/releases/tzcode${pkgver}.tar.gz{,.asc})
sha512sums=('1e33f7212fd0ae2ad3c16e68f0c1fc7a6ad26a126b8406c379a5768d79604c6a816054bd0fe3a63228d70cd6a1fc2b1bae2a9f8014e102d3727eb9d21affa1f1'
            'SKIP'
            'd14bc0680b9cb33d6526a2c0e483f2abafc2593cfe5fbfe7dcab940aafdc1baaec065dbaa1bb58b03586a8631ffdf43ebb22ff5a70503298b6ea2cf061d82c16'
            'SKIP')
validpgpkeys=('7E3792A9D8ACF7D633BC1588ED97E90E62AA7E34') # Paul Eggert <eggert@cs.ucla.edu>

_timezones=('africa' 'antarctica' 'asia' 'australasia'
           'europe' 'northamerica' 'southamerica'
           'etcetera' 'backward' 'factory')

build() {
  cd "${srcdir}"

  touch version  # avoid it re-sourcing from git in case anything is patched above
  make tzdata.zi leapseconds
}

package() {
  cd "${srcdir}"

  # install tzdata stuff
  zic -b fat -d "${pkgdir}${MINGW_PREFIX}"/share/zoneinfo ${_timezones[@]}
  zic -b fat -d "${pkgdir}${MINGW_PREFIX}"/share/zoneinfo/posix ${_timezones[@]}
  zic -b fat -d "${pkgdir}${MINGW_PREFIX}"/share/zoneinfo/right -L leapseconds ${_timezones[@]}
  # This creates the posixrules file. We use New York because POSIX requires the daylight savings time rules to be in accordance with US rules.
  zic -b fat -d "${pkgdir}${MINGW_PREFIX}"/share/zoneinfo -p America/New_York
  install -m444 -t "${pkgdir}${MINGW_PREFIX}"/share/zoneinfo iso3166.tab leap-seconds.list leapseconds tzdata.zi zonenow.tab zone1970.tab zone.tab SECURITY # zone.tab is depricated and will go soon

  # install license
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSE
}
