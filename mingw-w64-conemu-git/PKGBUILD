# Maintainer: Martell Malone <martellmalone@gmail.com>
# Contributer: Haroogan <Haroogan@gmail.com>

_name='conemu'
pkgname="${MINGW_PACKAGE_PREFIX}-${_name}-git"
pkgver=r1093.261fe53
pkgrel=2
pkgdesc="Customizable Windows console emulator with tabs."
url="http://code.google.com/p/${_name}-maximus5/"
license=(
  'BSD New'
)
arch=(
  'any'
)
conflicts=(
  "${MINGW_PACKAGE_PREFIX}-${_name}"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-gcc"
  "git"
)
options=(
  '!strip'
  'staticlibs'
)
source=(
  "${_name}"::"git+http://github.com/martell/ConEmu.git#branch=mingw64"
  'ConEmu.xml'
  'ConEmu.cpp.diff'
  'ConfirmDlg.h.diff'
)
sha256sums=(
  'SKIP'
  'SKIP'
  '5352992b27005f6d2ca5c8cc9aaff86ee6069c8d961d805912c0f1e00fede429'
  '18b8c48218a6f2955b09e0c47e5c772875bf5f2ed07a63bcd21a727c26f4fd46'
)

pkgver() {
  cd "${_name}"
  printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${_name}"
  patch --binary --forward -p0 < "${srcdir}/ConEmu.cpp.diff"
  patch --binary --forward -p0 < "${srcdir}/ConfirmDlg.h.diff"
}

build() {
  cd "${_name}/src"

  # NOTE:
  # The ugliest build system I've ever seen, yuck!
  # NOTE:
  # Don't remove `-static*' switches from `USERLIBSFIRST'! Otherwise when
  # running `cmd.exe' (Command Prompt) in ConEmu, it cannot find
  # `libstdc++-6.dll' if it is not in the current `PATH' environment variable.
  make                                                                         \
    -j1                                                                        \
    -f makefile_all_gcc                                                        \
             WIDE="1"                                                          \
          USERCPP="-pipe -O3 -fomit-frame-pointer -fpermissive -std=c++11"     \
    USERLIBSFIRST="-static -static-libgcc -static-libstdc++"
}

package() {
  mkdir -p "${pkgdir}${MINGW_PREFIX}/bin"

  local suffix=''

  if test "${CARCH}" == 'x86_64'; then
    suffix='64'
  fi

  cp -r "ConEmu.xml" "${pkgdir}${MINGW_PREFIX}/bin"

  cd "${_name}/Release"

  cp -r "ConEmu.exe" "${pkgdir}${MINGW_PREFIX}/bin/ConEmu.exe"

  cd "ConEmu"

  cp -r "ConEmuC.exe"  "${pkgdir}${MINGW_PREFIX}/bin/ConEmuC${suffix}.exe"
  cp -r "ConEmuCD.dll" "${pkgdir}${MINGW_PREFIX}/bin/ConEmuCD${suffix}.dll"
  cp -r "ConEmuHk.dll" "${pkgdir}${MINGW_PREFIX}/bin/ConEmuHk${suffix}.dll"
}
