# Maintainer: gym603 <gui_yuan_miao@163.com>

_realname=utf8proc
_pkgname=lib${_realname}
pkgbase=mingw-w64-${_pkgname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_pkgname}"
pkgver=2.4.0
pkgrel=1
pkgdesc="A clean C library for processing UTF-8 Unicode data (mingw-w64)"
arch=('any')
url="https://github.com/JuliaStrings/utf8proc"
license=('custom')
depends=()
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "make"
             "${MINGW_PACKAGE_PREFIX}-gcc")
source=(${_realname}-${pkgver}.tar.gz::https://github.com/JuliaStrings/utf8proc/archive/v${pkgver}.tar.gz)
sha256sums=('b2e5d547c1d94762a6d03a7e05cea46092aab68636460ff8648f1295e2cdfbd7')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G "MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=ON \
    ../${_realname}-${pkgver}

  make -j1

  cat "${srcdir}/${_realname}-${pkgver}/libutf8proc.pc.in" \
      | sed "s#PREFIX#${MINGW_PREFIX}#" \
      | sed "s#LIBDIR#lib#" \
      | sed "s#INCLUDEDIR#include#" \
      | sed "s#VERSION#${pkgver}#" \
      > libutf8proc.pc
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"

  #make test
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"

  make DESTDIR="${pkgdir}" install
  install -Dm644 "libutf8proc.pc" \
    "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/libutf8proc.pc"

  cd "${srcdir}/${_realname}-${pkgver}"
  install -Dm644 "LICENSE.md" \
    "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.md"
}
