# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=meilisearch
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.26.0
pkgrel=1
pkgdesc="A lightning-fast search engine API bringing AI-powered hybrid search to your sites and applications (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://www.meilisearch.com'
msys2_documentation_url='https://www.meilisearch.com/docs/home'
msys2_repository_url='https://github.com/meilisearch/meilisearch'
license=('spdx:MIT AND BUSL-1.1')
depends=("${MINGW_PACKAGE_PREFIX}-oniguruma"
         "${MINGW_PACKAGE_PREFIX}-zstd")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust" "${MINGW_PACKAGE_PREFIX}-pkgconf" 'git')
source=("${msys2_repository_url}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "ntapi-rs.tar.gz::https://crates.io/api/v1/crates/ntapi/0.4.1/download"
        "zstd-sys.tar.gz::https://crates.io/api/v1/crates/zstd-sys/2.0.16+zstd.1.5.7/download"
        "ntapi-link-ntdll-arm64.patch"
        "zstd-sys-remove-statik.patch")
sha256sums=('7c2d148a8f52c5cd12b92b7dc2ba2db1238d09cfcdb4fb72a3f4856f5ed1093a'
            'e8a3895c6391c39d7fe7ebc444a87eb2991b2a0bc718fdabd071eec617fc68e4'
            '91e19ebc2adc8f83e43039e79776e3fda8ca919132d68a1fed6a5faca2683748'
            '9994d5b61f020f85f67df54c56c793bd4faf41e39b62ddd4e67c482b9a11b73e'
            '48f4900ceb02d3aaf9a1020f33d56629156e96759f456c0e7ca18bfcf910767b')

_env() {
  export ZSTD_SYS_USE_PKG_CONFIG=1
  export RUSTONIG_DYNAMIC_LIBONIG=1
  export WINAPI_NO_BUNDLED_LIBRARIES=1
  export RUSTFLAGS="${RUSTFLAGS/+crt-static/-crt-static}"
}

prepare() {
  cd "${_realname}-${pkgver}"
  rm rust-toolchain.toml

  patch -d ../ntapi-0.4.1 -i ../ntapi-link-ntdll-arm64.patch
  patch -d ../zstd-sys-2.0.16+zstd.1.5.7 -i ../zstd-sys-remove-statik.patch
  cat >> Cargo.toml <<END

[patch.crates-io]
ntapi.path = "../ntapi-0.4.1"
zstd-sys.path = "../zstd-sys-2.0.16+zstd.1.5.7"
END

  cargo update -p ntapi -p zstd-sys --config='net.git-fetch-with-cli=true'
  cargo fetch --locked --target "${RUST_CHOST}"
}

build() {
  cd "${_realname}-${pkgver}"

  _env
  cargo build --release --frozen -p meilisearch
}

check() {
  cd "${_realname}-${pkgver}"

  _env
  cargo test --release --frozen -p meilisearch
}

package() {
  cd "${_realname}-${pkgver}"

  _env
  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path crates/meilisearch \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
