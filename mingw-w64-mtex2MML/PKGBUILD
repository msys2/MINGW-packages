# Maintainer: Dominic Sisneros <dsisnero@gmail.com>

_realname=mtex2MML
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3.1
pkgrel=3
pkgdesc="A Bison grammar to convert TeX math into MathML (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://github.com/gjtorikian/mtex2MML/"
license=('LGPL')
license=('GPL' 'MPL' 'LGPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-python3"
             'bison'
             'flex'
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/gjtorikian/${_realname}/archive/v${pkgver}.tar.gz"
        v1.3.1...master.patch)
# we extract this in prepare to prevent patch from failing due to files already
#created by a previous run.  That can happen when doing "makepkg" by default.
noextract=("${_realname}-${pkgver}.tar.gz")
sha256sums=('977be19419572da01c4c86789c28a6a0d8cd74cdab6e30f77d6ab7dbd38e7195'
            '22867cc4295a968979c05dd0301e05c1bc2d7f5bf81a28ad75a40c8523e09fcd')

prepare() {
  rm -rf "${srcdir}/${_realname}-${pkgver}"
  tar zxfv "${_realname}-${pkgver}.tar.gz"
  cd "${srcdir}/${_realname}-${pkgver}"
  #We probably want to sync with the master branch to fix various issues that appeared
  #since the release.  The last release was in 2016 and the Master is at 2018.
  patch -Np1 -i "${srcdir}/v1.3.1...master.patch"
}

build() {
  rm -rf "${srcdir}/build-${MSYSTEM}"
  mkdir "${srcdir}/build-${MSYSTEM}"
  cd "${srcdir}/build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake.exe \
      -GNinja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -DBUILD_TESTING=YES \
    ../${_realname}-${pkgver}
    
     ${MINGW_PREFIX}/bin/cmake.exe --build .
}

check() {
     cd "${srcdir}/build-${MSYSTEM}"
     ${MINGW_PREFIX}/bin/ctest -V
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install .  
# strip relative paths such as ../dep and ../../ that aren't
# are not there in our distribution.  The build system is sloppy
# with that.
  find "${pkgdir}${MINGW_PREFIX}/include/mtex2MML" -name "*.h" -exec \
    sed -e 's|../deps/uthash/||g' \
        -e 's|../../src/||g' -i {} \; 
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/README.md"  "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/README.md"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/CONTRIBUTING.md"  "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/CONTRIBUTING.md"

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/SUPPORTED.md" "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/SUPPORTED.md"
}
