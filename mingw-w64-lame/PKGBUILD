# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=lame
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.99.5
pkgrel=4
pkgdesc="A high quality MPEG Audio Layer III (MP3) encoder (mingw-w64)"
arch=('any')
url="http://lame.sourceforge.net/"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-mpg123"
             "${MINGW_PACKAGE_PREFIX}-nasm"
             "${MINGW_PACKAGE_PREFIX}-ncurses"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-libiconv")
options=('strip' 'staticlibs')
source=("http://downloads.sourceforge.net/${_realname}/${_realname}-${pkgver}.tar.gz"
        0001-fix-cr-for-awk-in-configure.mingw.patch
        0002-07-field-width-fix.all.patch
        0003-parallel-builds-fix.all.patch
        0004-disable-wrong-xmmintrin-detection.all.patch
        0005-no-gtk.all.patch)
md5sums=('84835b313d4a8b68f5349816d33e07ce'
         '629d7bf75acfcd57b4a648b8ad7e264e'
         'c3db698a821ff3705a714bda599935af'
         '57a745d28754964c0db74e316a3e0e4d'
         '4faa6e53e4a6619e5dd9c0758e13c59e'
         '8d21190eee329d0b14333a9c75e6ec6d')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/0001-fix-cr-for-awk-in-configure.mingw.patch
  patch -p1 -i ${srcdir}/0002-07-field-width-fix.all.patch
  patch -p1 -i ${srcdir}/0003-parallel-builds-fix.all.patch
  patch -p1 -i ${srcdir}/0004-disable-wrong-xmmintrin-detection.all.patch
  patch -p1 -i ${srcdir}/0005-no-gtk.all.patch

  autoreconf -fi
}

build() {
  [[ -d "build-${MINGW_CHOST}" ]] && rm -rf "build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-nasm \
    --enable-shared \
    --enable-static
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
