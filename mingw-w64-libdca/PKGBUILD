# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libdca
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.0.6
pkgrel=1
pkgdesc="Free library for decoding DTS Coherent Acoustics streams (mingw-w64)"
provides=("${MINGW_PACKAGE_PREFIX}-libdts")
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}-svn" "${MINGW_PACKAGE_PREFIX}-${_realname}-git" "${MINGW_PACKAGE_PREFIX}-libdts")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}-svn" "${MINGW_PACKAGE_PREFIX}-${_realname}-git" "${MINGW_PACKAGE_PREFIX}-libdts")
arch=('any')
url="https://www.videolan.org/developers/libdca.html"
license=("GPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
options=('strip' 'staticlibs')
source=(https://download.videolan.org/pub/videolan/${_realname}/${pkgver}/${_realname}-${pkgver}.tar.bz2
        0001-Proper-use-of-destdir.all.patch
        0003-fix-symlink.mingw.patch
        0004-fix-regex.all.patch)
sha256sums=('98f98a9aa000a26b927c6facd15d18dcf664238adfc5db24f533c5932cdb1f40'
            '3798f095105d0cbb0463aa9d63a567e4d61207fd777e2eaf48664482fd429421'
            '18f674363ee357452352b315173193d844a5631b698958978e0b74520f75c77e'
            '5ec11ea54f0ce49d780daf31ac5b395290f41a8fbdd4726bbd884e5519b06a80')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/0001-Proper-use-of-destdir.all.patch
  patch -p1 -i ${srcdir}/0003-fix-symlink.mingw.patch
  patch -p1 -i ${srcdir}/0004-fix-regex.all.patch

  autoreconf -fi
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-static \
    --enable-shared \
    --mandir=${MINGW_PREFIX}/share/man

  make
}

package() {
  cd build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}${MINGW_PREFIX}/share/man/man1"/{extract_dts,dtsdec}.1
  cp "${pkgdir}${MINGW_PREFIX}/share/man/man1/dcadec.1" "${pkgdir}${MINGW_PREFIX}/share/man/man1/dtsdec.1"
  cp "${pkgdir}${MINGW_PREFIX}/share/man/man1/extract_dca.1" "${pkgdir}${MINGW_PREFIX}/share/man/man1/extract_dts.1"

  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
