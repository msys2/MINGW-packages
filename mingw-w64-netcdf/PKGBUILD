# Contributor: Robert T. McGibbon <rmcgibbo@gmail.com>

_realname=netcdf
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.6.2
pkgrel=1
pkgdesc="Interface for scientific data access to large binary data (mingw-w64)"
arch=('any')
license=('custom')
url="https://www.unidata.ucar.edu/software/netcdf/"
depends=("${MINGW_PACKAGE_PREFIX}-hdf5")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cmake")
source=(${_realname}-${pkgver}.tar.gz::"https://github.com/Unidata/netcdf-c/archive/v${pkgver}.tar.gz"
        0001-mingw-cmake.patch
        0002-dutil-correcttypo.patch
        0003-ncconfigure-strlcat-export.patch
        0004-ddispatch-set-temp.patch
        0006-cvt-conversion.patch
        0007-mingw-config.patch
        0008-mingw-and-msvc-common.patch)
sha256sums=('673936c76ae0c496f6dde7e077f5be480afc1e300adb2c200bf56fbe22e5a82a'
            'a306c20f6593d314701147323f952d4df80e77174c45ab201949f402d191e221'
            'da666e1f762b3ecd2becc9ad55fe1e32be9a47b5bae4f7990e7d94aae69697cd'
            'a1047a8feb86732e2f6087db5240da955045f5f653f9406103ce733c4850b536'
            'aeb61f0a4d69e26b2235b15948ac6b99ac92be5b0f8155bfa7aa4867af84efa8'
            'f5f5291c2486e9e765a93975d99871cee584bdd3d64e16745982aadeb799f00c'
            'a1aa112779a83ec74acd7de8a0a57915f5d37cae44633d0a4d0d20784a2d288c'
            'bf8e82aaba512f8f21dd8b2a040e6ac1cc53a182a67049ebd5fda3e87de2fa13')

prepare() {
  cd "${srcdir}/${_realname}-c-${pkgver}"
  patch -p1 -i ${srcdir}/0001-mingw-cmake.patch
  patch -p1 -i ${srcdir}/0002-dutil-correcttypo.patch
  patch -p1 -i ${srcdir}/0003-ncconfigure-strlcat-export.patch
  patch -p1 -i ${srcdir}/0004-ddispatch-set-temp.patch
  patch -p1 -i ${srcdir}/0006-cvt-conversion.patch
  patch -p1 -i ${srcdir}/0007-mingw-config.patch
  patch -p1 -i ${srcdir}/0008-mingw-and-msvc-common.patch
}

build() {
  [[ -d ${srcdir}/build-static-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-static-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-static-${MINGW_CHOST} && cd ${srcdir}/build-static-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G "MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=OFF \
    -DENABLE_DLL=OFF \
    -DENABLE_DAP=ON \
    -DENABLE_TESTS=OFF \
    -DENABLE_DYNAMIC_LOADING=OFF \
    -DENABLE_NETCDF_4=ON \
    "${srcdir}/${_realname}-c-${pkgver}"
  make

  [[ -d ${srcdir}/build-shared-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-shared-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-shared-${MINGW_CHOST} && cd ${srcdir}/build-shared-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G "MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=ON \
    -DENABLE_DAP=ON \
    -DENABLE_TESTS=OFF \
    -DENABLE_DYNAMIC_LOADING=OFF \
    -DENABLE_NETCDF_4=ON \
    -DENABLE_LOGGING=ON \
    "${srcdir}/${_realname}-c-${pkgver}"
  make
}

package() {
  cd ${srcdir}/build-static-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install

  cd ${srcdir}/build-shared-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/${_realname}-c-${pkgver}/COPYRIGHT \
    ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYRIGHT

  local PREFIX_DEPS=$(cygpath -am ${MINGW_PREFIX})
  find ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig -name *.pc -exec sed -i -e"s|${PREFIX_DEPS}|${MINGW_PREFIX}|g" {} \;
}
