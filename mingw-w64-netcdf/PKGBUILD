# Contributor: Robert T. McGibbon <rmcgibbo@gmail.com>

_realname=netcdf
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.9.0
pkgrel=1
pkgdesc="Interface for scientific data access to large binary data (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=('custom')
url="https://www.unidata.ucar.edu/software/netcdf/"
depends=("${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-hdf5"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             autoconf
             automake
             libtool)
checkdepends=("unzip")
source=(${_realname}-${pkgver}.tar.gz::"https://github.com/Unidata/netcdf-c/archive/v${pkgver}.tar.gz")
sha256sums=('9f4cb864f3ab54adb75409984c6202323d2fc66c003e5308f3cdf224ed41c0a6')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${srcdir}/${_realname}-c-${pkgver}"

#  apply_patch_with_msg *.patch

  autoreconf -if
}

build() {
  cd "${srcdir}/${_realname}-c-${pkgver}"

  ./configure \
    --prefix=${MINGW_PREFIX} \
    --enable-shared \
    --enable-static \
    --enable-hdf5 \
    --enable-dap \
    --disable-plugins \
    --disable-byterange \
    --disable-logging \
    --disable-dap-remote-tests \
    LDFLAGS=-Wl,--export-all-symbols

  make ${MAKEFLAGS}
}

check() {
  cd "${srcdir}/${_realname}-c-${pkgver}"
  make check ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${_realname}-c-${pkgver}"
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/${_realname}-c-${pkgver}/COPYRIGHT \
    ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYRIGHT

  sed -e "s|${srcdir}|.|" -i "${pkgdir}${MINGW_PREFIX}/lib/libnetcdf.settings"
}
