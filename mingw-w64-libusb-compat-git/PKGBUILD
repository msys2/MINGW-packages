# Maintainer: Andrew Leech <andrew@alelec.net>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=libusb-compat
pkgbase="mingw-w64-${_realname}-git"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=r72.92deb38
pkgrel=1
pkgdesc="Allows applications written for libusb-0.1 to work with libusb-1.0 (mingw-w64)"
arch=('any')
url="http://libusb.info"
depends=(${MINGW_PACKAGE_PREFIX}-libusb)
options=('staticlibs')
makedepends=("git"
             "pkgconfig"
             "m4")
source=(${_realname}::git+https://github.com/libusb/libusb-compat-0.1.git)
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_realname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_realname}"

  ./bootstrap.sh
}

build() {
  [[ -d ${srcdir}/build-${CARCH} ]] && rm -rf ${srcdir}/build-${CARCH}
  mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}

  ../${_realname}/configure \
      --prefix=${MINGW_PREFIX} \
      --{host,build}=${MINGW_CHOST} \
      --includedir=${MINGW_PREFIX}/include/libusb-compat \
      --disable-dependency-tracking \
      --enable-static \
      --enable-shared

  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install
}
