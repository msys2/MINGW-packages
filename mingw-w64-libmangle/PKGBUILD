# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libmangle
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}-svn"
_ver_base=4.0.0
pkgver=4.0.0.6390
pkgrel=1
pkgdesc="MinGW-w64 libmangle"
arch=('any')
url="http://mingw-w64.sourceforge.net"
license=('custom')
groups=("${_mingw_suff}-toolchain" "${_mingw_suff}")
makedepends=("subversion" "${_mingw_suff}-gcc" "${_mingw_suff}-binutils" "${_mingw_suff}-crt-svn" "${_mingw_suff}-headers-svn")
provides=("${_mingw_suff}-${_realname}")
conflicts=("${_mingw_suff}-${_realname}")
depends=()
optdepends=()
backup=()
options=('strip' 'staticlibs' '!emptydirs')
source=("${_realname}"::"svn+svn://svn.code.sf.net/p/mingw-w64/code/trunk/mingw-w64-libraries/libmangle")
md5sums=(SKIP)

pkgver() {
  cd "${srcdir}/${_realname}"
  local ver="$(svnversion)"
  printf "%s.%s" "${_ver_base}" "${ver//[[:alpha:]]}"
}

build() {
	mkdir -p ${srcdir}/${MINGW_CHOST}-${_realname}
	cd ${srcdir}/${MINGW_CHOST}-${_realname}
	${srcdir}/${_realname}/configure \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST}
	make	
}

package() {
	cd ${srcdir}/${MINGW_CHOST}-${_realname}
	make DESTDIR=${pkgdir} install
	install -Dm644 ${srcdir}/${_realname}/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/mingw-w64-libraries/COPYING.${_realname}
}
