# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=pngquant
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=3.0.3
pkgrel=1
pkgdesc="Command-line utility to quantize PNGs down to 8-bit paletted PNGs (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://pngquant.org/'
msys2_references=(
  "cpe: cpe:/a:pngquant:pngquant"
)
license=("spdx:GPL-3.0-or-later")
depends=("${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-lcms2")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-cargo-c")
source=("https://github.com/kornelski/${_realname}/archive/${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('ddd8889a9c269ba454d0c5e4f7167948d55d77c4570b23f671809fd3a68b6822')

prepare() {
  cd $srcdir/${_realname}-${pkgver}

  sed -i 's|, path = "lib/imagequant-sys"||' Cargo.toml

  cargo fetch \
    --config='net.git-fetch-with-cli=true' \
    --target "${RUST_CHOST}"
}

build() {
  cp -r ${_realname}-${pkgver} build-${MSYSTEM}

  cd "build-${MSYSTEM}"

  cargo build \
    --release \
    --frozen
}

package() {
  cd build-${MSYSTEM}

  install -Dm755 target/release/pngquant.exe "${pkgdir}"${MINGW_PREFIX}/bin/pngquant.exe
  install -Dm644 CHANGELOG "${pkgdir}"${MINGW_PREFIX}/share/doc/${_realname}/CHANGELOG
  install -Dm644 README.md "${pkgdir}"${MINGW_PREFIX}/share/doc/${_realname}/README.md
  install -Dm644 COPYRIGHT "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/COPYRIGHT
}

