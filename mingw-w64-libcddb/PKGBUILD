# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: AlexWMF <alexxwmf@gmail.com>

_realname=libcddb
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3.2
pkgrel=3
pkgdesc="Library that implements the different protocols (CDDBP, HTTP, SMTP) to access data on a CDDB server (e.g. http://freedb.org)."
arch=('any')
license=('LGPL')
url="http://sourceforge.net/projects/libcddb/"
options=('staticlibs' 'strip')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-libsystre")
source=(http://downloads.sourceforge.net/sourceforge/${_realname}/${_realname}-${pkgver}.tar.bz2
        0001-include-winsock2-before-windows.mingw.patch
        0002-fix-header-conflict.mingw.patch
        0003-silent-rules.mingw.patch
        0004-hack-around-dummy-alarm.mingw.patch
        0005-fix-m4-dir.all.patch
        0006-update-gettext-req.mingw.patch
        0007-link-to-libiconv-properly.mingw.patch)
md5sums=('8bb4a6f542197e8e9648ae597cd6bc8a'
         '002cfcf7e6546d0f8a61db838ffcf8ee'
         'f10190b965b393b88342bed19cb885c8'
         'fa707c87961396c60208ea5252886d2b'
         '6b2f413c58cddd59d7438d7b9c424abb'
         '981ff1dd9cb3f64751ec565a93fed8e6'
         'c5ad153f6e223bf31e43d764f6d3aa85'
         '880ef92c7e93bfefc8e96c5201c225f8')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-include-winsock2-before-windows.mingw.patch
  patch -p1 -i ${srcdir}/0002-fix-header-conflict.mingw.patch
  patch -p1 -i ${srcdir}/0003-silent-rules.mingw.patch
  patch -p1 -i ${srcdir}/0004-hack-around-dummy-alarm.mingw.patch
  patch -p1 -i ${srcdir}/0005-fix-m4-dir.all.patch
  patch -p1 -i ${srcdir}/0006-update-gettext-req.mingw.patch
  patch -p1 -i ${srcdir}/0007-link-to-libiconv-properly.mingw.patch

  WANT_AUTOMAKE=latest autoreconf -fi
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --enable-static

  make
}

package() {
  cd build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
}
