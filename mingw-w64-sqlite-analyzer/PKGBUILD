# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=sqlite-analyzer
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.10.2
pkgrel=1
pkgdesc="An analysis program SQLite database files (mingw-w64)"
url="http://www.sqlite.org/"
arch=('any')
license=(PublicDomain)
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-tcl")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-fossil")
source=("LICENSE"
        "lemon.patch")
md5sums=('99ad5d4fba7d423c054b7bc74133253a'
         '5894795373d34f149a09fb7c7a07c20c')

prepare() {
  if [ ! -f sqlite.fossil ]; then
    fossil clone http://www.sqlite.org/cgi/src sqlite.fossil
  fi
  if [ ! -f .fslckout ]; then
    fossil open sqlite.fossil
  fi
  fossil update version-${pkgver}
  fossil revert
  patch -p0 -i "${srcdir}"/lemon.patch
}

build() {
  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"
  CFLAGS+=" -fexceptions -DSQLITE_ENABLE_COLUMN_METADATA=1 -DSQLITE_DISABLE_DIRSYNC=1 -DSQLITE_ENABLE_RTREE=1 -fno-strict-aliasing"
  ../configure \
    --prefix=${MINGW_PREFIX} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --enable-threadsafe \
    --with-tcl=${MINGW_PREFIX}/lib
  make sqlite3_analyzer.exe
}

package() {
  cd ${srcdir}/build-${CARCH}
  install -Dm755 .libs/sqlite3_analyzer.exe "${pkgdir}${MINGW_PREFIX}/bin/sqlite3_analyzer.exe"
  install -Dm644 "${srcdir}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
