# Maintainer: AndyRTR <andyrtr@archlinux.org>

_realname=opengl-man-pages
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=20240713
_commit=9813a5569411cad5250d719a9efed2fac732e56f
pkgrel=1
pkgdesc="OpenGL Man Pages - OpenGL 4.x (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/KhronosGroup/OpenGL-Refpages"
license=('custom')
makedepends=("${MINGW_PACKAGE_PREFIX}-docbook5-xml"
    "${MINGW_PACKAGE_PREFIX}-docbook-xsl"
    "${MINGW_PACKAGE_PREFIX}-libxslt"
    "${MINGW_PACKAGE_PREFIX}-docbook-mathml"
    "${MINGW_PACKAGE_PREFIX}-w3c-mathml2"
    "${MINGW_PACKAGE_PREFIX}-python3" 'git')
source=(${_realname}::git+https://github.com/KhronosGroup/OpenGL-Refpages#commit=$_commit
        'LICENSE')
sha256sums=('848033ced8fb614dd10cff688dcff878d4758544c654a541a72f362f1746b49e'
            'a7b2f6669d7ead91dcaf5a03620cdf9d37c54d83fd1899b4ef84683c7e6d4024')

build() {
  for manpages in gl4; do
    cd "${srcdir}/${_realname}/${manpages}"
      for file in *.xml; do
        ${MINGW_PREFIX}/bin/xsltproc --xinclude --noout --nonet ${MINGW_PREFIX}/share/xml/docbook/xsl-stylesheets-*/manpages/docbook.xsl ${file}
      done
  done
}

package() {
  install -d "${pkgdir}${MINGW_PREFIX}/share/man/man3"
  for manpages in gl4; do
    cd "${srcdir}/${_realname}/${manpages}"
    install -m644 *.3G "${pkgdir}${MINGW_PREFIX}/share/man/man3/"
  done
  # license
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
