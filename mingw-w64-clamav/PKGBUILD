# Contributor: kreijstal <kreijstal@hotmail.com>

_realname=clamav
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.5.1
pkgrel=1
pkgdesc="Open source antivirus engine (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64')
url='https://www.clamav.net/'
license=('spdx:GPL-2.0-only')
depends=("${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-pcre2"
         "${MINGW_PACKAGE_PREFIX}-json-c"
         "${MINGW_PACKAGE_PREFIX}-bzip2"
         "${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-libiconv"
         "${MINGW_PACKAGE_PREFIX}-ncurses")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-check")
source=("https://github.com/Cisco-Talos/clamav/archive/refs/tags/clamav-${pkgver}.zip"
        "0001-mingw-build-fixes.patch")
sha256sums=('67e565619c0edf398192d2b028a7b028e62d2f0e901df448e25bd1defa73ce2a'
            'c73059bcba59dc89bf3b557aeea55989e96b1e33829178e3277c4e0374408f1a')
options=('!strip')

prepare() {
  cd "${_realname}-${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-mingw-build-fixes.patch"
}

build() {
  cd "${_realname}-${_realname}-${pkgver}"
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DCMAKE_INSTALL_INCLUDEDIR=include \
      -DCVD_CERTS_DIRECTORY="etc/clamav/certs" \
      -DAPP_CONFIG_DIRECTORY="etc/clamav" \
      -DDATABASE_DIRECTORY="share/clamav" \
      "${extra_config[@]}" \
      -DBUILD_SHARED_LIBS=ON \
      -DENABLE_TESTS=OFF \
      -DOPTIMIZE=ON \
      -S . \
      -B "../build-${MSYSTEM}"


  # Retry build once if it fails (workaround for potential race conditions)
  cmake --build "../build-${MSYSTEM}" || cmake --build "../build-${MSYSTEM}"
}

check() {
  cd "${_realname}-${_realname}-${pkgver}"
}


package() {
  cd "${_realname}-${_realname}-${pkgver}"
  DESTDIR="${pkgdir}" cmake --install "../build-${MSYSTEM}"

  cd "${pkgdir}${MINGW_PREFIX}"
  mv etc/clamav/conf_examples/clamd.conf.sample etc/clamav/clamd.conf
  mv etc/clamav/conf_examples/freshclam.conf.sample etc/clamav/freshclam.conf
  rm -rf etc/clamav/conf_examples

  install -d bin lib
  mv *.exe bin/

  mv *.a lib/

  # ClamAV on Windows bundles ALL dependencies (openssl, curl, etc.) into the root.
  for _dll in *.dll; do
    if [[ "$_dll" == libclam* ]] || [[ "$_dll" == libfreshclam* ]]; then
      mv "$_dll" bin/
    else
      # Delete bundled dependencies (libcurl, libcrypto, zlib1, etc.)
      rm "$_dll"
    fi
  done

  install -Dm644 COPYING.txt "share/licenses/${_realname}/COPYING"
  
  rm COPYING.txt
  rm -rf COPYING
  rm -f NEWS.md README.md
}
