# Contributor: Fabien Chouteau <chouteau@adacore.com>

_realname=gnatcoll-gmp
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.0
pkgrel=2
pkgdesc="GNAT Components Collection - Interface to the GNU Multiple Precision (GMP) arithmetic library (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64')
url="https://github.com/AdaCore/gnatcoll-bindings"
msys2_references=(
  'aur: gnatcoll-gmp'
  'gentoo: dev-ada/gnatcoll-bindings'
)
license=('spdx:GPL-3.0-or-later WITH GCC-exception-3.1')
makedepends=("${MINGW_PACKAGE_PREFIX}-gprbuild"
             "${MINGW_PACKAGE_PREFIX}-python")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gcc-ada"
         "${MINGW_PACKAGE_PREFIX}-gnatcoll-core"
         "${MINGW_PACKAGE_PREFIX}-gmp")
source=("https://github.com/AdaCore/gnatcoll-bindings/archive/v${pkgver}/gnatcoll-bindings.tar.gz"
        'gmp.gpr')
sha256sums=('d123733ca9a9206572e77bfbffbe0cd50e96ea8f8e276055048dc209e5327791'
            'd2456f318fee53f010dcf9e236f1a06dfcec51a541e80b8eaf8c3b58bbce9318')

prepare() {
  echo "${pkgver}" > "${srcdir}/gnatcoll-bindings-${pkgver}/version_information"
  cd "${srcdir}/gnatcoll-bindings-${pkgver}"
}

build() {
  cd "${srcdir}/gnatcoll-bindings-${pkgver}/gmp"
  ${MINGW_PREFIX}/bin/python setup.py build --jobs="$(nproc)" --reconfigure
}

package() {
  cd "${srcdir}/gnatcoll-bindings-${pkgver}/gmp"
  ${MINGW_PREFIX}/bin/python setup.py install --prefix="${pkgdir}${MINGW_PREFIX}"

  # In GNAT Community gnatcoll_gmp is built with libgmp embedded in
  # libgnatcoll_gmp.dll. This is not what we want here, so we install
  # a gmp.gpr with Externally_Built attribute.

  # Copy external gpr files
  cp -pf "${srcdir}/gmp.gpr" "${pkgdir}${MINGW_PREFIX}/share/gpr/"

  # Add with for external gpr files
  sed -i 's/with "gnatcoll";/with "gnatcoll"; with "gmp";/' "${pkgdir}${MINGW_PREFIX}/share/gpr/gnatcoll_gmp.gpr"

  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" \
    "${srcdir}/gnatcoll-bindings-${pkgver}"/COPYING*
}
