# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=swig
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.0.6
pkgrel=1
pkgdesc="Generate scripting interfaces to C/C++ code"
arch=('any')
url="http://www.swig.org/"
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-pcre")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-lua"
             "${MINGW_PACKAGE_PREFIX}-ruby")
source=(https://downloads.sourceforge.net/${_realname}/${_realname}-${pkgver}.tar.gz
        001-relocate.patch)
sha256sums=('c67f63ea11956106e4cda66416d5020330dc4ce2ee45057d39a9494ce33eca05'
            'b620efcd725546ed74ade96c26b56c4709fc34af3394d8419159ef2cdcc39036')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-relocate.patch

  ./autogen.sh
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure\
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --without-perl5 \
    --without-guile
  make
}

package() {
  cd build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
  install -D -m644 ${srcdir}/${_realname}-${pkgver}/LICENSE "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSE
  install -D -m644 ${srcdir}/${_realname}-${pkgver}/LICENSE-UNIVERSITIES "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSE-UNIVERSITIES
}
