# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=mesa-demos
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=8.4.0
pkgrel=1
pkgdesc="Mesa demos and tools incl. glxinfo + glxgears (mingw-w64)"
arch=('any')
url="https://www.mesa3d.org/"
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-glew"
         "${MINGW_PACKAGE_PREFIX}-freeglut")
makedepends=("${MINGW_PACKAGE_PREFIX}-mesa"
             "${MINGW_PACKAGE_PREFIX}-freetype"
             "git")
source=(https://archive.mesa3d.org/demos/${_realname}-${pkgver}.tar.bz2{,.sig}
        "0001-autoconf-use-proper-windows-libraries.patch"
        "0002-autoconf-remove-tests-folder.patch")
sha512sums=('b72d03cad36e0535ff18dcfb222ec4200064b9264f6da51a6e5f03b0dd912abe188bc1d600b6698de3ce6f63b28d2ce01565886ca8e7079edc4967fbf2fb0957'
            'SKIP'
            'f574f1c613d8e448cb49d57a2d03857b9d8bb20cc4fc11e60fae1a0f163b08a06c70b086dcf2225d9664fa64f3eb9739e1d9a42e24c0662ab468759a28e34409'
            '7cf2019893094f8d03c388e5eec68a5f00da773543cc99b285c1735d45c3a625f8a9952cbae2931f797ca12a8a54eaa9ca8f2ebd1a05d9d22696c85cd07bd4ea')
validpgpkeys=('E390B9700582FAEA959ACAD41EEF53D38A3A9C67') # Andreas Boll <andreas.boll.dev@gmail.com>

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Nbp1 -i "${srcdir}/0001-autoconf-use-proper-windows-libraries.patch"
  patch -Nbp1 -i "${srcdir}/0002-autoconf-remove-tests-folder.patch"
  autoreconf -fiv
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --with-system-data-files=${MINGW_PREFIX}/share/mesa-demos

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make install DESTDIR="${pkgdir}"
}
