# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=goocanvas
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.0.2
pkgrel=5
arch=('any')
pkgdesc="Canvas widget for GTK+ that uses the Cairo 2D library (mingw-w64)"
license=("LGPL 2")
url="https://www.gnome.org"
depends=("${MINGW_PACKAGE_PREFIX}-gtk3")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             "make"
             "libtool"
             "automake-wrapper"
             "autoconf"
             "gettext"
             "libintl"
             "patch")
options=('strip' '!debug' 'staticlibs')
source=(https://download.gnome.org/sources/${_realname}/${pkgver:0:3}/${_realname}-${pkgver}.tar.xz
             001-convert-python-path-to-unix.patch
             002-fix-introspection.patch)
sha256sums=('f20e5fbef8d1a2633033edbd886dd13146a1b948d1813a9c353a80a29295d1d0'
            '3e792af4245ebc6a73eb83bf89b2c37734c1c8e8442c84780a8f4ad54e9e8888'
            '9c7f57c503986f95b57a5551fdfda9571b4403f9000caf95d881ceff8a729d3c')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-convert-python-path-to-unix.patch
  patch -p1 -i ${srcdir}/002-fix-introspection.patch
  autoreconf -fi
  cp -r ${srcdir}/${_realname}-${pkgver} ${srcdir}/${MINGW_CHOST}-${_realname}-python2
  cp -r ${srcdir}/${_realname}-${pkgver} ${srcdir}/${MINGW_CHOST}-${_realname}-python3
}

build() {
  # we must build things twice
  for _python in python{2,3}; do
    cd ${srcdir}/${MINGW_CHOST}-${_realname}-${_python}
    export CC=gcc
    ./configure \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST} \
      --build=${MINGW_CHOST} \
      --prefix=${MINGW_PREFIX} \
      --libexecdir=${MINGW_PREFIX}/lib \
      --enable-introspection=yes \
      PYTHON=${MINGW_PREFIX}/bin/${_python}
    make
  done
}

package() {
  for _python in python{2,3}; do
    cd ${srcdir}/${MINGW_CHOST}-${_realname}-${_python}
    make DESTDIR=${pkgdir} install
  done
}
