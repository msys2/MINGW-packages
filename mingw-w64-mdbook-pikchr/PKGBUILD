# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=mdbook-pikchr
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.1.7
pkgrel=1
pkgdesc="A mdbook preprocessor to render pikchr code blocks as images in your book (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/podsvirov/mdbook-pikchr"
msys2_references=(
  'aur: mdbook-pikchr'
  'purl: pkg:cargo/mdbook-pikchr'
)
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-mdbook")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
source=("${_realname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('e3f1bcba19db94fa66ea12152e7082de0e1ad6171b1fd6638f2b65a9cfbb5b60')

prepare() {
  cp -r "${_realname}-${pkgver}" "build-${MSYSTEM}"
  cd "${srcdir}/build-${MSYSTEM}"

  sed -e "s/https:\/\/raw.githubusercontent.com\/podsvirov\/mdbook-pikchr\/master\///" -i README.md
  sed -e "s/debug/release/" -i book.toml

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  msg "Cargo build for ${MSYSTEM}"
  cd "${srcdir}/build-${MSYSTEM}"

  "${MINGW_PREFIX}/bin/cargo.exe" build \
    --release \
    --frozen

  msg "Build documentation"

  "${MINGW_PREFIX}/bin/mdbook.exe" build
  cd book
  rm -rf target src
  rm -f book.toml Cargo.lock Cargo.toml \
    build.rs LICENSE Makefile \
    .gitignore .nojekyll \
    Dockerfile
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  "${MINGW_PREFIX}/bin/cargo.exe" test \
    --release \
    --frozen
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  install -Dm755 "target/release/${_realname}.exe" "${pkgdir}${MINGW_PREFIX}/bin/${_realname}.exe"
  install -d "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}"
  cp -a "book" "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/html"
  install -Dm644 "LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
