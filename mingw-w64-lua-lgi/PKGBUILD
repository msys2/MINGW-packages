# Maintainer: Ricky Wu <rickleaf.wu@gmail.com>

_realname=lgi
pkgbase=mingw-w64-lua-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-lua51-${_realname}"
pkgver=0.9.2
pkgrel=6
pkgdesc="LGI is gobject-introspection based dynamic Lua binding to GObject based libraries (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/pavouk/lgi"
license=("spdx:MIT")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection")
depends=("${MINGW_PACKAGE_PREFIX}-lua51"
         "${MINGW_PACKAGE_PREFIX}-gettext-runtime"
         "${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-gobject-introspection-runtime"
         "${MINGW_PACKAGE_PREFIX}-libffi")
source=("https://github.com/pavouk/${_realname}/archive/${pkgver}/${_realname}-${pkgver}.zip")
sha256sums=('c6cdb7cca6b745790454b45ebb9c3e8be96aca948c7d640d79c969a1c78b35e9')

prepare() {
  cp -r "${_realname}-${pkgver}" "build-${MSYSTEM}-lua51"
  sed -i "s/-llua/-llua5.1/g" "build-${MSYSTEM}-lua51"/lgi/Makefile
}

build() {
  CFLAGS="$CFLAGS -I${MINGW_PREFIX}/include/lua5.1" make -C "build-${MSYSTEM}-lua51" all
}

package() {
  CFLAGS="$CFLAGS -I${MINGW_PREFIX}/include/lua5.1" \
    make -C "build-${MSYSTEM}-lua51" \
    LUA_VERSION=5.1 \
    PREFIX="${MINGW_PREFIX}" DESTDIR="${pkgdir}" install
}
