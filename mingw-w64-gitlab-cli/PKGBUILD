# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=gitlab-cli
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.36.0
pkgrel=1
pkgdesc='A GitLab CLI tool bringing GitLab to your command line (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
msys2_references=(
  'archlinux: gitlab-cli'
)
url='https://gitlab.com/gitlab-org/cli'
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-go" "${MINGW_PACKAGE_PREFIX}-cc" "git")
optdepends=("git: To interact with repositories")
options=('!strip')
source=("${url}/-/archive/v${pkgver}/cli-v${pkgver}.tar.gz")
noextract=("cli-v${pkgver}.tar.gz")
sha256sums=('8d6c759ebfe9c6942fcdb7055a4a5c7209a3b22beb25947f906c9aef3bc067e8')

prepare() {
  cd "${srcdir}"
  tar xf "cli-v${pkgver}.tar.gz" \
    --exclude=pkg/execext/testdata/PATH/git.exe \
    --exclude=pkg/execext/testdata/cwd/git.exe
}

build() {
  [[ -d "${srcdir}/build-${MSYSTEM}" ]] && rm -rf "${srcdir}/build-${MSYSTEM}"
  cp -r "${srcdir}/cli-v${pkgver}" "${srcdir}/build-${MSYSTEM}"
  cd "${srcdir}/build-${MSYSTEM}" && mkdir build

  export GOOS=windows
  export GOROOT=${MINGW_PREFIX}/lib/go
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GO_LDFLAGS="-s -w -linkmode=external -compressdwarf=false"
  case "${CARCH}" in
    i686|x86_64)
      GOFLAGS+=" -buildmode=pie"
      ;;
  esac

  ${MINGW_PREFIX}/bin/go build -v \
    -trimpath \
    -mod=readonly \
    -modcacherw \
    -ldflags "${GO_LDFLAGS} -X main.debugMode=false -X main.buildDate=$(date '+%Y-%m-%d') -X main.version=${pkgver}" \
    -o build \
    gitlab.com/gitlab-org/cli/cmd/glab

  mkdir -p build/completions
  ./build/glab completion -s fish > build/completions/glab.fish
  ./build/glab completion -s bash > build/completions/glab
  ./build/glab completion -s zsh > build/completions/_glab
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  install -Dm755 "build/glab.exe" "${pkgdir}${MINGW_PREFIX}/bin/glab.exe"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
  install -Dm644 README.md "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/README.md"
  install -Dm644 build/completions/glab.fish "${pkgdir}${MINGW_PREFIX}/share/fish/vendor_completions.d/glab.fish"
  install -Dm644 build/completions/glab "${pkgdir}${MINGW_PREFIX}/share/bash-completion/completions/glab"
  install -Dm644 build/completions/_glab "${pkgdir}${MINGW_PREFIX}/share/zsh/site-functions/_glab"
}
