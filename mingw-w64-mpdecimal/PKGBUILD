# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=mpdecimal
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.4.2
pkgrel=1
pkgdesc="Package for correctly-rounded arbitrary precision decimal floating point arithmetic (mingw-w64)"
arch=('any')
url='http://www.bytereef.org/mpdecimal/index.html"'
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
checkdepends=("wget")
source=("http://www.bytereef.org/software/${_realname}/releases/${_realname}-$pkgver.tar.gz"
        "msys2-mingw-fixes.patch")
sha256sums=('83c628b90f009470981cf084c5418329c88b19835d8af3691b930afccb7d79c7'
            '9a49bfe1e67cb51f77c8d211130f7bc7d1150a26385550bcae111f29d5e0d46f')

prepare() {
  cd $srcdir/${_realname}-${pkgver}
  patch -Nbp1 -i ${srcdir}/msys2-mingw-fixes.patch
  autoreconf -fiv
}

build() {
  cd "$srcdir"/${_realname}-${pkgver}
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  cp -R "$srcdir/${_realname}-${pkgver}" "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} 
#    --enable-static \
#    --enable-shared

  make
}

check() {
  cd "${srcdir}"/build-${CARCH}
  make check
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install DESTDIR="${pkgdir}"
}
