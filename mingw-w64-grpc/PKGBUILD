# Maintainer: Mario Emmenlauer <memmenlauer@biodataanalysis.de>

_realname=grpc
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.0.1
pkgrel=1
pkgdesc="gRPC - Google's high performance, open source, general RPC framework (mingw-w64)"
arch=('any')
url='http://www.grpc.io/'
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-protobuf>=3.0.0")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-cmake" "automake" "autoconf")
options=('staticlibs' 'strip')
source=(${_realname}-${pkgver}.tar.gz::https://github.com/google/grpc/archive/v${pkgver}.tar.gz
        googletest-release-1.7.0.tar.gz::https://github.com/google/googletest/archive/release-1.7.0.tar.gz
        0001-grpc-mingw64.patch)
sha256sums=('efad782944da13d362aab9b81f001b7b8b1458794751de818e9848c47acd4b32'
            'f73a6546fdf9fce9ff93a5015e0333a8af3062a152a9ad6bcb772c96687016cc'
            'ddb217448e5184c50d545fca818e14d51251f44ebdbf3d0e405acedfbbc23be6')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  rmdir ./third_party/googletest
  mv ../googletest-release-1.7.0 ./third_party/googletest

  patch -p1 -i ${srcdir}/0001-grpc-mingw64.patch
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  cp -rf "${_realname}-${pkgver}" "build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  # remove __USE_MINGW_ANSI_STDIO=1 define, it breaks grpc string handling for
  # cases like 'gpr_log(GPR_DEBUG, "Failed to free %" PRIuPTR ...'
  CPPFLAGS=${CPPFLAGS//-D__USE_MINGW_ANSI_STDIO=1/}
  # add build platform 0x0600 (minimum required for grpc):
  CPPFLAGS+=" -D_WIN32_WINNT=0x0600"

  make run_dep_checks
  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  #make test
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make prefix="${pkgdir}/${MINGW_PREFIX}" install
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/LICENSE "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSE
}
