# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=libical
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.0.1
pkgrel=1
arch=('any')
pkgdesc="An Open Source implementation of the iCalendar protocols and protocol data units (mingw-w64)"
license=("LGPL 2.1")
url="https://github.com/libical/libical"
depends=()
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake")
options=('strip' '!debug' 'staticlibs')
source=(https://github.com/${_realname}/${_realname}/archive/v$pkgver.tar.gz
        001-win.patch)
sha256sums=('7d5f613454ec6c7d1bcfb441c919215be53292aa15cd1cb14249d1413d6c610c'
            'f080396da2203af16ed770141595fed86934df51c2ce40edfeccea23eaf25e00')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-win.patch
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ${MINGW_PREFIX}/bin/cmake \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS}" \
    -DCMAKE_C_FLAGS="${CFLAGS}" \
    -G "MSYS Makefiles" \
    ../${_realname}-${pkgver}
  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make install

  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  local PREFIX_WIN=`pwd -W`
  popd > /dev/null
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/libical.pc
}
