# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=w32pth
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.0.5
pkgrel=2
pkgdesc="An emulation of PTH for MS Windows (mingw-w64)"
arch=('any')
license=("LGPL")
url="https://gnupg.org"
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('strip' 'staticlibs')
source=("http://ftp.g10code.com/g10code/w32pth/w32pth-${pkgver}.tar.bz2"
        w32pth-2.0.4-deffile.patch
        w32pth-2.0.4-mingw.patch)
sha256sums=('5e462d06ed50e9e961546ab5264258e606b101860a74c1e410d086432ec27a06'
            '963dcc6585173c0d184d876a5782b1b5a40aeec0e7d7001e653b4d5fab83ae85'
            '3bbb51002ad54f451a52573122435e57b7a4d966e4d2788859b937378f6f5443')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/w32pth-2.0.4-deffile.patch
  patch -p1 -i ${srcdir}/w32pth-2.0.4-mingw.patch

  ./autogen.sh
}

build() {
  export lt_cv_deplibs_check_method='pass_all'
  mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --enable-shared \
    --disable-static
  make
}

package() {
  cd ${srcdir}/build-${CARCH}
  make -j1 DESTDIR="${pkgdir}" install
  rm ${pkgdir}${MINGW_PREFIX}/lib/*.def
}
