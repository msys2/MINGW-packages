# $Id$
# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>
# stolen from Archlinux

_realname=docbook-dsssl
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.79
pkgrel=1
pkgdesc="DSSSL Stylesheets for DocBook (mingw-w64)"
arch=('any')
url="https://docbook.sourceforge.io/"
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-sgml-common" "perl")
install="${_realname}-${CARCH}.install"
source=(https://sourceforge.net/projects/docbook/files/${_realname}/${pkgver}/${_realname}-${pkgver}.tar.bz2
        docbook-style-dsssl.Makefile)
sha1sums=('0ee935247c9f850e930f3b5c162dbc03915469cd'
          '104a665b2c3e1520337533862cbd1c1c55d6e01c')

prepare() {
  cd "$srcdir"/ 
  cp -r "${_realname}-${pkgver}" "build-${CARCH}"
  cd "build-${CARCH}"
  cp ${srcdir}/docbook-style-dsssl.Makefile Makefile
}

package(){
  cd "$srcdir/build-${CARCH}"
  make install \
     BINDIR="${pkgdir}${MINGW_PREFIX}/bin" \
     DESTDIR="${pkgdir}${MINGW_PREFIX}/share/sgml/docbook/dsssl-stylesheets-${pkgver}" \
     MANDIR="${pkgdir}${MINGW_PREFIX}/share/man"
  
  ln -s dsssl-stylesheets-${pkgver} ${pkgdir}${MINGW_PREFIX}/share/sgml/docbook/dsssl-stylesheets
  # license
  install -Dm644 README "$pkgdir${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
