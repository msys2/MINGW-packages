# Maintainer: Yasuhiro Matsumoto <mattn.jp@gmail.com>

pkgname=figlet
pkgver=2.2.5
pkgrel=1
pkgdesc="A program for making large letters out of ordinary text"
arch=('i686' 'x86_64')
url="http://www.figlet.org/"
license=('BSD')
depends=('sh')
source=(ftp://ftp.figlet.org/pub/figlet/program/unix/${pkgname}-${pkgver}.tar.gz
		htonl.patch)
sha1sums=('dda696958c161bd71d6590152c94c4f705415727'
          '0649faa69fcbfb886d5197dd5b8dd5fd0b0296ae')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < ${srcdir}/htonl.patch
  make DEFAULTFONTDIR=${MINGW_PREFIX}/share/figlet/fonts XCFLAGS= all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" BINDIR=${MINGW_PREFIX}/bin MANDIR=${MINGW_PREFIX}/share/man \
    DEFAULTFONTDIR=${MINGW_PREFIX}/share/figlet/fonts install 
  mkdir -p "${pkgdir}${MINGW_PREFIX}/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${pkgname}/LICENSE"
}
