# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=luv
_pkgname=lib${_realname}
pkgbase=mingw-w64-${_pkgname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_pkgname}"
_pkgver=1.42.0-1
_compat53ver=0.10
pkgver=${_pkgver/-/_}
pkgrel=1
pkgdesc='Bare libuv bindings for lua (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='https://github.com/luvit/luv'
_compat53url='https://github.com/keplerproject/lua-compat-5.3'
license=('apache')
depends=("${MINGW_PACKAGE_PREFIX}-luajit"
         "${MINGW_PACKAGE_PREFIX}-libuv")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cc")
options=('staticlibs' '!strip' '!buildflags')
source=("${_realname}-${_pkgver}.tar.gz"::"${url}/archive/${_pkgver}.tar.gz"
        "lua-compat-5.3-${_compat53ver}.tar.gz"::"${_compat53url}/archive/v${_compat53ver}.tar.gz"
        '001-luv-1.42.0-win-install.patch')
sha256sums=('a55563e6da9294ea26f77a2111598aa49188c5806b8bd1e1f4bdf402f340713e'
            'd1ed32f091856f6fffab06232da79c48b437afd4cd89e5c1fc85d7905b011430'
            'SKIP')
prepare() {
  cd "${srcdir}/${_realname}-${_pkgver}"

  patch -Np1 -i ${srcdir}/001-luv-1.42.0-win-install.patch
}

build() {
  [[ -d "${srcdir}/build-${MSYSTEM}" ]] && rm -rf "${srcdir}/build-${MSYSTEM}"
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_MODULE=OFF \
    -DWITH_SHARED_LIBUV=ON \
    -DLUA_BUILD_TYPE=System \
    -DLUA_COMPAT53_DIR="${srcdir}/lua-compat-5.3-${_compat53ver}" \
    ../${_realname}-${_pkgver}

  cmake --build ./
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR=${pkgdir} cmake --install ./

  install -Dm644 "${srcdir}/${_realname}-${_pkgver}/LICENSE.txt" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_pkgname}/LICENSE.txt"
}
