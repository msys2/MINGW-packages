# Maintainer: Brian McKenna <brian@brianmckenna.org>

_realname=nix
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.33.0
pkgrel=1
pkgdesc="The purely functional package manager"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://nixos.org/"
license=('spdx:LGPL-2.1-or-later')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-meson"
  "${MINGW_PACKAGE_PREFIX}-ninja"
)
depends=(
  "${MINGW_PACKAGE_PREFIX}-aws-crt-cpp"
  "${MINGW_PACKAGE_PREFIX}-boost"
  "${MINGW_PACKAGE_PREFIX}-brotli"
  "${MINGW_PACKAGE_PREFIX}-curl"
  "${MINGW_PACKAGE_PREFIX}-expat"
  "${MINGW_PACKAGE_PREFIX}-gcc-libs"
  "${MINGW_PACKAGE_PREFIX}-libarchive"
  "${MINGW_PACKAGE_PREFIX}-libblake3"
  "${MINGW_PACKAGE_PREFIX}-libgit2"
  "${MINGW_PACKAGE_PREFIX}-libsodium"
  "${MINGW_PACKAGE_PREFIX}-libunistring"
  "${MINGW_PACKAGE_PREFIX}-nlohmann_json"
  "${MINGW_PACKAGE_PREFIX}-readline"
  "${MINGW_PACKAGE_PREFIX}-sqlite3"
  "${MINGW_PACKAGE_PREFIX}-termcap"
  "${MINGW_PACKAGE_PREFIX}-toml11"
)
source=("https://github.com/NixOS/nix/archive/${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('98ac8f96db53fff7c8847d7199ab579cc1f80d6d548dadf600bf618893471065')
# Nix purposely has broken symlinks (for tests) in the package.
# We have to use this form of symlinks in order to extract.
export MSYS=winsymlinks:native

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # Remove all symlinks in the functional tests.
  # Ignore the broken symlink.
  rm -r src/nix-functional-tests
  cp -rL tests/functional src/nix-functional-tests || true
}

build() {
  export BOOST_ROOT="${MINGW_PREFIX}"
  export BOOST_INCLUDEDIR="${MINGW_PREFIX}/include"
  export BOOST_LIBRARYDIR="${MINGW_PREFIX}/lib"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    "${MINGW_PREFIX}"/bin/meson.exe setup \
      --prefix="${MINGW_PREFIX}" \
      --wrap-mode=nodownload \
      --auto-features=enabled \
      --buildtype=plain \
      -Dlibcmd:markdown=disabled \
      -Dlibcmd:readline-flavor=readline \
      -Dlibexpr:gc=disabled \
      -Dlibstore:seccomp-sandboxing=disabled \
      -Dlibutil:cpuid=disabled \
      -Ddoc-gen=false \
      -Dunit-tests=false \
      -Djson-schema-checks=false \
      -Dkaitai-struct-checks=false \
      -Dbindings=false \
      -Dbenchmarks=false \
      -Dc_args='-Wno-error=sign-compare' \
      "${_realname}-${pkgver}" \
      "build-${MSYSTEM}"

  "${MINGW_PREFIX}"/bin/meson.exe compile -C "build-${MSYSTEM}"
}

package() {
  "${MINGW_PREFIX}"/bin/meson.exe install -C "build-${MSYSTEM}" --destdir "${pkgdir}"
}
