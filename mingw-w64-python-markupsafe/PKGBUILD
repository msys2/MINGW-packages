# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=markupsafe
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}" "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=1.0
pkgrel=2
pkgdesc="Implements a XML/HTML/XHTML Markup safe string for Python (mingw-w64)"
arch=('any')
license=('custom')
url="https://pypi.python.org/pypi/MarkupSafe"
makedepends=("${MINGW_PACKAGE_PREFIX}-python2-setuptools" "${MINGW_PACKAGE_PREFIX}-python3-setuptools")
source=("https://pypi.org/packages/source/M/MarkupSafe/MarkupSafe-${pkgver}.tar.gz")
sha256sums=('a6be69091dac236ea9c6bc7d012beab42010fa914c459791d627dad4910eb665')

build() {
  cd "${srcdir}"

  rm -rf python{2,3}-build
  for builddir in python{2,3}-build; do
    cp -r MarkupSafe-${pkgver} $builddir
    pushd ${builddir}
    ${MINGW_PREFIX}/bin/${builddir%-build} setup.py build
    popd
  done
}

package_python3-markupsafe() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3")

  cd "${srcdir}/python3-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}

package_python2-markupsafe() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2")

  cd "${srcdir}/python2-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE"
}

package_mingw-w64-i686-python2-markupsafe() {
  package_python2-markupsafe
}

package_mingw-w64-i686-python3-markupsafe() {
  package_python3-markupsafe
}

package_mingw-w64-x86_64-python2-markupsafe() {
  package_python2-markupsafe
}

package_mingw-w64-x86_64-python3-markupsafe() {
  package_python3-markupsafe
}
