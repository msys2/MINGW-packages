# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=crt
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}-svn"
_ver_base=4.0.0
_revision=
_fragment=
[[ -n ${_revision} ]] && _fragment="#revision=${_revision}"
pkgver=4.0.0.6594
pkgrel=1
pkgdesc='MinGW-w64 CRT for Windows'
arch=('any')
url='http://mingw-w64.sourceforge.net'
license=('custom')
groups=("${_mingw_suff}-toolchain" "${_mingw_suff}")
depends=("${_mingw_suff}-headers-svn")
makedepends=("subversion" "${_mingw_suff}-gcc" "${_mingw_suff}-binutils")
provides=("${_mingw_suff}-${_realname}")
conflicts=("${_mingw_suff}-${_realname}")
options=('!strip' 'staticlibs' '!buildflags' '!emptydirs')
source=("${_realname}"::"svn+svn://svn.code.sf.net/p/mingw-w64/code/trunk/mingw-w64-crt${_fragment}")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/${_realname}"
  local ver="$(svnversion)"
  printf "%s.%s" "${_ver_base}" "${ver//[[:alpha:]]}"
}

prepare() {
  cd ${srcdir}/${_realname}
}

build() {
  msg "Building ${MINGW_CHOST} CRT"
  case "$CARCH" in
    i686)
      _crt_configure_args="--disable-lib64 --enable-lib32"
    ;;
    x86_64)
      _crt_configure_args="--disable-lib32 --enable-lib64"
    ;;
  esac
  mkdir -p ${srcdir}/crt-${MINGW_CHOST} && cd ${srcdir}/crt-${MINGW_CHOST}
  ${srcdir}/${_realname}/configure \
    --prefix=${MINGW_PREFIX}/${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --with-sysroot=${MINGW_PREFIX}/${MINGW_CHOST} \
    --enable-wildcard \
    ${_crt_configure_args}
  make
}

package() {
  msg "Installing ${MINGW_CHOST} crt"
  cd ${srcdir}/crt-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install-strip
}

