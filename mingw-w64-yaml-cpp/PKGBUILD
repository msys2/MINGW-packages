# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=yaml-cpp
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=0.5.1
pkgrel=2
pkgdesc="A YAML parser and emitter in C++ matching the YAML 1.2 spec (mingw-w64)"
arch=('any')
url="https://code.google.com/p/yaml-cpp/"
license=("MIT")
makedepends=("${_mingw_suff}-gcc"
    "${_mingw_suff}-pkg-config"
    "${_mingw_suff}-cmake"
    "${_mingw_suff}-boost")
options=('staticlibs' 'strip')
source=(http://yaml-cpp.googlecode.com/files/${_realname}-${pkgver}.tar.gz
    mingw-install-pkgconfig.patch)
md5sums=('0fa47a5ed8fedefab766592785c85ee7'
         'acf096cb06ee28ecb730a8e0b0cf044d')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/mingw-install-pkgconfig.patch
}

build() {
  mkdir -p ${pkgdir}${MINGW_PREFIX}
  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null

  pushd ${MINGW_PREFIX} > /dev/null
  export PREFIX_DEPS=`pwd -W`
  popd > /dev/null

  mkdir $srcdir/build-${MINGW_CHOST}

  cd "$srcdir/build-${MINGW_CHOST}"
  ${MINGW_PREFIX}/bin/cmake.exe \
    ../${_realname}-${pkgver} \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX:PATH=${PREFIX_WIN} \
    -DBUILD_SHARED_LIBS=OFF \
    ../${_realname}-${pkgver}
  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make -j1 install
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/license.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE

  mkdir -p ${pkgdir}${MINGW_PREFIX}
  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null

  cd ${pkgdir}${MINGW_PREFIX}
  sed -i "s|$PREFIX_WIN|$MINGW_PREFIX|g" lib/pkgconfig/yaml-cpp.pc 
}
