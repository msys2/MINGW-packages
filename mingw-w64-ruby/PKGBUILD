# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=ruby
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=2.1.2
pkgrel=1
pkgdesc="An object-oriented language for quick and easy programming (mingw-w64)"
arch=('any')
url="http://www.ruby-lang.org/en"
license=("BSD, custom")
makedepends=("${_mingw_suff}-gcc" "${_mingw_suff}-pkg-config")
depends=("${_mingw_suff}-gcc-libs"
        "${_mingw_suff}-gdbm"
        "${_mingw_suff}-libyaml"
        "${_mingw_suff}-openssl"
        "${_mingw_suff}-tk"
        "${_mingw_suff}-ncurses"
        "${_mingw_suff}-libffi")
options=('staticlibs' 'strip')
source=("http://cache.ruby-lang.org/pub/ruby/${pkgver%.*}/ruby-${pkgver}.tar.bz2"
        0002-use-gnu-printf.patch)
md5sums=('ed9b8565bdeccb401d628ec8d54a0774'
         '6d54f98b9e3a0d0077e9348dc7f69482')

prepare() {
  cd $srcdir/ruby-${pkgver}
  patch -p1 -i ${srcdir}/0002-use-gnu-printf.patch
  
  autoreconf -fi
}

build() {
  pushd ${MINGW_PREFIX} > /dev/null
  local PREFIX_WIN=`pwd -W`
  popd > /dev/null

  local _ffi=$(pkg-config --cflags libffi)
  local FFI_INC=${PREFIX_WIN}${_ffi#-I${MINGW_PREFIX}}
  #echo "FFI_INC $FFI_INC"
  #pushd "${FFI_INC}" > /dev/null
  #local FFI_INC=`pwd -W`
  #popd > /dev/null

  CPPFLAGS+=" -DFD_SETSIZE=2048 -I${FFI_INC} -I${MINGW_PREFIX}/include/ncurses"
  CFLAGS+=" -I${MINGW_PREFIX}/include/ncurses -I${FFI_INC}"
  CXXFLAGS+=" -I${MINGW_PREFIX}/include/ncurses -I${FFI_INC}"
  LDFLAGS+=" -L${PREFIX_WIN}/lib"
  rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../ruby-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install
  #rm -r "${pkgdir}${MINGW_PREFIX}/share"
}
