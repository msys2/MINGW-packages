# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=ruby
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.3.4
pkgrel=1
pkgdesc="An object-oriented language for quick and easy programming (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://www.ruby-lang.org/en"
msys2_references=(
  "cpe: cpe:/a:ruby-lang:ruby"
  "cpe: cpe:/a:yukihiro_matsumoto:ruby"
)
msys2_repository_url='https://github.com/ruby/ruby'
msys2_documentation_url='https://docs.ruby-lang.org/en/master/index.html'
license=('spdx:Ruby AND BSD-2-Clause')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-pkgconf")
depends=("${MINGW_PACKAGE_PREFIX}-gdbm"
         "${MINGW_PACKAGE_PREFIX}-libyaml"
         "${MINGW_PACKAGE_PREFIX}-libffi"
         "${MINGW_PACKAGE_PREFIX}-readline"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-gmp"
         "${MINGW_PACKAGE_PREFIX}-tk"
         "${MINGW_PACKAGE_PREFIX}-zlib")
source=("https://cache.ruby-lang.org/pub/ruby/${pkgver%.*}/${_realname}-${pkgver}.tar.xz"
        0008-ucrt-32-win11-insns.patch
        0009-ucrt-platform-arch.patch)
sha256sums=('1caaee9a5a6befef54bab67da68ace8d985e4fb59cd17ce23c28d9ab04f4ddad'
            'fa75de298371bb01e308ced6d1c5a01c829ea8de60ed707b0c76b2c72f0905dc'
            '01bdedf2403fc5746b69c90ab74fe489427f3b25c99d153c8a3d24b017a69e12')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

prepare() {
  cd ${_realname}-${pkgver}
  apply_patch_with_msg \
    0008-ucrt-32-win11-insns.patch \
    0009-ucrt-platform-arch.patch
}

build() {
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  if [[ ${MSYSTEM} == CLANG* ]]; then
    CFLAGS+=" -Wno-dll-attribute-on-redeclaration"
  else
    CFLAGS+=" -Wno-incompatible-pointer-types"
  fi

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --disable-install-doc \
    --with-setjmp-type=setjmp

  make
}

check() {
  cd build-${MSYSTEM}
  make test || true
}

package() {
  cd build-${MSYSTEM}
  make DESTDIR="${pkgdir}" install-nodoc

  for script in {gem,}; do
    install ${srcdir}/ruby-${pkgver}/bin/${script} \
      ${pkgdir}${MINGW_PREFIX}/bin/
  done

  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING \
     "${pkgdir}${MINGW_PREFIX}/share/licenses/ruby/LICENSE"
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/BSDL \
     "${pkgdir}${MINGW_PREFIX}/share/licenses/ruby/BSDL"
}
