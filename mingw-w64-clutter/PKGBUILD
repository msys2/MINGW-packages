# Maintainer: Lubosz Sarnecki <lubosz@gmail.com>

_realname=clutter
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.23.2
pkgrel=1
pkgdesc="A GObject based library for creating fast, visually rich graphical user interfaces (mingw-w64)"
arch=('any')
url="http://clutter-project.org/"
license=("LGPL")
makedepends=('automake' 'libtool' 'intltool' 'itstool' 'gtk-doc' 'gnome-doc-utils' 'yelp-tools'
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection")
depends=("${MINGW_PACKAGE_PREFIX}-gobject-introspection-runtime"
         "${MINGW_PACKAGE_PREFIX}-atk"
         "${MINGW_PACKAGE_PREFIX}-json-glib"
         "${MINGW_PACKAGE_PREFIX}-cogl")
options=(!libtool strip staticlibs)
source=(http://download.gnome.org/sources/${_realname}/${pkgver%.*}/${_realname}-${pkgver}.tar.xz
        #"git://git.gnome.org/$_realname#tag=${pkgver}"
        "0001-msys2-add-header-for-BOOL-defintion.patch"
        "0002-msys2-add-CC-flags-to-gir-scanner.patch")
md5sums=('48001c0b7f499cbf07f2b8af1999a2f9'
         '3a92db8341129b0db058d47f97062f65'
         '993eabbeb6fa31af855a593956727e6f')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-msys2-add-header-for-BOOL-defintion.patch
  patch -p1 -i ${srcdir}/0002-msys2-add-CC-flags-to-gir-scanner.patch
  autoreconf -fvi
  #NOCONFIGURE=1 ./autogen.sh
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-static \
    --enable-shared \
    --enable-silent-rules \
    --disable-gtk-doc

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
