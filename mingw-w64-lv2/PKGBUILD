# Maintainer: Alexandros Theodotou <alex at zrythm dot org>

_realname=lv2
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.18.4
pkgrel=1
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
pkgdesc='LV2 audio plugin specification'
makedepends=("${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "perl"
             "python")
license=('spdx:ISC')
url="http://lv2plug.in/"
source=("http://lv2plug.in/spec/lv2-$pkgver.tar.bz2")
sha256sums=('4ddc0ed20579aabbaeb4df6fb42b69949c01dc7169ab0b945c709339509e6760')

prepare() {
  cd ${_realname}-${pkgver}

  [[ -x ./waf ]] || /usr/bin/python ./bootstrap.py
  sed -i 's:bin/env python$:bin/env python:' waf
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  export LINKFLAGS="$LDFLAGS"

  # do not build example plugins (needs gtk2)
  DEST_OS=win32 \
  TARGET=${MINGW_CHOST} \
  PKG_CONFIG=${MINGW_PREFIX}/bin/pkg-config \
  CC=gcc PERL=/usr/bin/perl \
  AR=${MINGW_PREFIX}/bin/ar \
  WINDRES=${MINGW_PREFIX}/bin/windres \
  RST2MAN=${MINGW_PREFIX}/bin/rst2man3 \
  /usr/bin/python3 ./waf configure \
    --prefix=${MINGW_PREFIX} \
    --no-plugins \
    --lv2dir=${MINGW_PREFIX}/lib/lv2

  /usr/bin/python ./waf build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  export PYTHON=${MINGW_PREFIX}/bin/python
  /usr/bin/python ./waf install --destdir="${pkgdir}"

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
