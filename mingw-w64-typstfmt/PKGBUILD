# Maintainer: Wu, Zhenyu <wuzhenyu@ustc.edu>

_realname=typstfmt
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.2.10
pkgrel=1
pkgdesc="formatter for the Typst language (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url=https://github.com/astrale-sharp/$_realname
license=('spdx:MIT OR Apache-2.0')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust" git)
source=("$url/archive/$pkgver.tar.gz")
sha256sums=('5a3f413a428b2590552c2d0ab0ab04c7a745e1cca128844b7b82ea49326d65c4')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # if cargo wants to make an http request at build stage, use `cargo fetch --locked` instead
  cargo fetch --locked  --config='net.git-fetch-with-cli=true' --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  cargo build --release --frozen
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  cargo test --release --frozen
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE-* -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
}
