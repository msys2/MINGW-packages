# Maintainer: Olivier XILLO <oxdev_at_mailo.com>

_realname=miniaudio
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.11.24
pkgrel=1
pkgdesc="Audio playback and capture library written in C, in a single source file."
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://miniaud.io/"
msys2_repository_url="https://github.com/mackron/miniaudio"
msys2_references=(
  "cpe: cpe:/a:mackron:miniaudio"
)
license=('spdx:Unlicense' 'spdx:MIT-0')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-opusfile"
             "${MINGW_PACKAGE_PREFIX}-libvorbis"
             "${MINGW_PACKAGE_PREFIX}-SDL2"
             'git')
source=("${_realname}"::"git+https://github.com/mackron/miniaudio.git#tag=${pkgver}")
sha256sums=('602a1aaf1ff4d996ec34dd765d6f1c332d029a344cbf6f3038b46507e6c22014')

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DMINIAUDIO_BUILD_TESTS=ON \
      "${extra_config[@]}" \
      -S "${_realname}" \
      -B "build-${MSYSTEM}"

  cmake --build "build-${MSYSTEM}"
}

check() {
  cmake --build "build-${MSYSTEM}" --target test
}

package() {
  DESTDIR="${pkgdir}" cmake --install "build-${MSYSTEM}"

  install -Dm644 "${srcdir}/${_realname}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
