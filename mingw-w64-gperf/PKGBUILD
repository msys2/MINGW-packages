# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gperf
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=3.0.4
pkgrel=2
pkgdesc="Perfect hash function generator (mingw-w64)"
arch=('any')
url="https://www.gnu.org/software/gperf/"
license=('GPL3')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
source=("https://ftp.gnu.org/pub/gnu/gperf/${_realname}-${pkgver}.tar.gz"{,.sig})
sha256sums=('767112a204407e62dbc3106647cf839ed544f3cf5d0f0523aaa2508623aad63e'
            'SKIP')
options=('strip')

build() {
  mkdir ${srcdir}/build-${MINGW_CHOST}
  cd "${srcdir}"/build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX}
  make
}

check() {
  cd "${srcdir}"/build-${MINGW_CHOST}
  make check
}

package() {
  cd "${srcdir}"/build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}${MINGW_PREFIX}/share
}
