# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gperf
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=3.1
pkgrel=2
pkgdesc="Perfect hash function generator (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://www.gnu.org/software/gperf/"
license=('GPL3')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools" "${MINGW_PACKAGE_PREFIX}-cc")
source=("https://ftp.gnu.org/pub/gnu/gperf/${_realname}-${pkgver}.tar.gz"{,.sig}
        "gperf-3.1-clang-16-wregister.patch")
sha256sums=('588546b945bba4b70b6a3a616e80b4ab466e3f33024a352fc2198112cdbb3ae2'
            'SKIP'
            'd25b2be0fec1eced1e4cd39cf4bed3d726b9b20c5fd7b1046d96ab6809d04f32')
validpgpkeys=('93C08C88471097CD'
'EDEB87A500CC0A211677FBFD93C08C88471097CD')

prepare() {
  cd "${_realname}-${pkgver}"

  # https://gitweb.gentoo.org/repo/gentoo.git/tree/dev-util/gperf/files/gperf-3.1-clang-16-wregister.patch
  patch -Np1 -i "${srcdir}/gperf-3.1-clang-16-wregister.patch"
}

build() {
  mkdir "${srcdir}"/build-${MSYSTEM}
  cd "${srcdir}"/build-${MSYSTEM}
  ../${_realname}-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX}
  make
}

check() {
  cd "${srcdir}"/build-${MSYSTEM}
  make check
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"${MINGW_PREFIX}/share
}
