# Maintainer:  Matheus Catarino <matheus-catarino@hotmail.com>

_realname=zig
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
_pkgver=0.14.0-dev.2599+ff4f2753e
pkgver=${_pkgver/-/}
pkgrel=1
pkgdesc='A general-purpose programming language and toolchain for maintaining robust, optimal, and reusable software (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url='https://ziglang.org'
msys2_repository_url='https://github.com/ziglang/zig/'
msys2_documentation_url='https://github.com/ziglang/zig?tab=readme-ov-file#readme'
msys2_references=(
  'archlinux: zig'
)
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-clang-bootstrap"
             "${MINGW_PACKAGE_PREFIX}-llvm-bootstrap"
             "${MINGW_PACKAGE_PREFIX}-lld-bootstrap"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-zlib"
             "${MINGW_PACKAGE_PREFIX}-zstd")
depends=("${MINGW_PACKAGE_PREFIX}-crt"
         "${MINGW_PACKAGE_PREFIX}-headers"
         "${MINGW_PACKAGE_PREFIX}-winpthreads")
source=("https://ziglang.org/builds/${_realname}-${_pkgver}.tar.xz")
sha256sums=('274c8aa4c341836b27655e64f1c5c8f0df5743ef95192d8911dd650520fa640f')

build() {
  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  declare _arch=x86_64
  if [[ $CARCH == aarch64 ]]; then
    _arch=aarch64
  fi

  if [[ $MSYSTEM == CLANG* ]]; then
    _extra_config+=("-DZIG_SYSTEM_LIBCXX=c++")
  else
    _extra_config+=("-DZIG_SYSTEM_LIBCXX=c++")
  fi

  export PATH="${MINGW_PREFIX}/bootstrap/bin:${PATH}"

  CFLAGS+=" -Wno-incompatible-pointer-types" \
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  cmake -Wno-dev \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_C_COMPILER=clang \
    -DCMAKE_CXX_COMPILER=clang++ \
    -DZIG_HOST_TARGET_TRIPLE=${_arch}-windows-gnu \
    -DZIG_TARGET_TRIPLE=${_arch}-windows-gnu \
    -DZIG_STATIC=ON \
    -DZIG_STATIC_LLVM=ON \
    -DZIG_STATIC_ZLIB=ON \
    -DZIG_STATIC_ZSTD=ON \
    -DZIG_TARGET_MCPU=baseline \
    -DZIG_USE_LLVM_CONFIG=ON \
    "${_extra_config[@]}" \
    -S ${_realname}-${_pkgver} \
    -B build-${MSYSTEM}

  cmake --build build-${MSYSTEM} -v
}

package() {
  DESTDIR="${pkgdir}" cmake --install build-${MSYSTEM}

  install -Dm644 "${srcdir}"/${_realname}-${_pkgver}/LICENSE \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
