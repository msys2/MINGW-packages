# Maintainer:  Matheus Catarino <matheus-catarino@hotmail.com>

_realname=zig
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
_zigver=0.10.0
pkgver=${_zigver}dev.4519+09236d29b
pkgrel=1
_commit=09236d29b7722d71533478aa7080706acde28d0d
pkgdesc='Zig is a general-purpose programming language and toolchain for maintaining robust, optimal, and reusable software. (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url='https://ziglang.org'
license=(MIT)
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-lld"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "git")
depends=("${MINGW_PACKAGE_PREFIX}-llvm"
         "${MINGW_PACKAGE_PREFIX}-clang")
source=("${_realname}::git+https://github.com/ziglang/zig.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_realname}"
  _latest_tag=$(git describe --tags --abbrev=0)
  echo "${_zigver}dev."`git rev-list --count $_latest_tag..$_commit`+`git rev-parse --short=9 $_commit`
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  CXXFLAGS+=" -fms-extensions" \
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -G"Ninja" \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      ${_extra_config[@]} \
      ../${_realname}

  ${MINGW_PREFIX}/bin/cmake --build . --target zig1
  ${MINGW_PREFIX}/bin/cmake --build . -j1
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}

  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .

  install -Dm644 "${srcdir}"/${_realname}/LICENSE "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
