# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>
# Contributor: Mateusz Mikula <mati865@gmail.com>

_realname=binutils
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.35.1
pkgrel=2
pkgdesc="A set of programs to assemble and manipulate binary and object files (mingw-w64)"
arch=('any')
url="https://www.gnu.org/software/binutils/"
license=('GPL')
groups=("${MINGW_PACKAGE_PREFIX}-toolchain")
depends=("${MINGW_PACKAGE_PREFIX}-libiconv" "${MINGW_PACKAGE_PREFIX}-zlib")
#checkdepends=('dejagnu' 'bc')
makedepends=("${MINGW_PACKAGE_PREFIX}-libiconv" "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' '!distcc' '!ccache') # 'debug' '!strip')
#install=binutils.install
source=(https://ftp.gnu.org/gnu/binutils/${_realname}-${pkgver}.tar.xz{,.sig}
        0001-enable-gold-on.mingw32.patch
        0002-check-for-unusual-file-harder.patch
        0010-bfd-Increase-_bfd_coff_max_nscns-to-65279.patch
        0020-binutils_2.31_mkdtemp_impl.patch
        0110-binutils-mingw-gnu-print.patch
        0120-windres-handle-spaces.patch
        1000-Change-the-default-characteristics-of-DLLs-built-by-.patch
        1001-mingw-plugin-test-regressions-due-to-commit-514b4e19.patch
        1002-Fixes-for-testsuite-failures-introduced-by-the-chang.patch
        1003-Pass-disable-reloc-section-on-PE-targets-for-PR-2566.patch
        1004-Fix-a-segfault-when-creating-an-import-library-with-.patch
        1005-After-preventing-creating-an-import-library-for-an-e.patch
        1006-Import-patch-from-mainline-to-fix-decoding-DWARF-inf.patch
        2000-Temporarily-revert-default-dll-characteristics.patch
        2001-ld-option-to-move-default-bases-under-4GB.patch
        2002-default-to-4GB-image-base-for-now.patch)
sha256sums=('3ced91db9bf01182b7e420eab68039f2083aed0a214c0424e257eae3ddee8607'
            'SKIP'
            '93296b909e1a4f9d8a4bbe2437aafa17ca565ef6642a9812b0360c05be228c9d'
            '2c99345fc575c3a060d6677537f636c6c4154fac0fde508070f3b6296c1060d4'
            '604e76e0f702ced493ee22aa3c1768b4776b2008a7d70ae0dd35fe5be3522141'
            '34ba6c001ff7f95ae1da0619c73130112b76d0d2a31bb8a00602eb22f1f84cb8'
            '76658ef1bb8c5fc3fe6c26e2b5dd9ee0f1d12661988c0c65562b0a3e2d32ae1f'
            '86ae90d997e986a54aaebb5251f3a71800b0c5c3f5b57b9094a42995e9f5c478'
            '57350c7740a3558ba08fe606e5defd9e5f5901ef3fb746719c27b1a019305038'
            '4f320cfc6f1a3f86ace33248ff0f95341362a1f7093049f603e06ecab395a452'
            'd45be8a496d78de59d7a5973edb2f5ca5317ffc6f70f984ecb488eec98bc2131'
            'ab1cdeeb3c51a7df319c3e39ef2db5a4e09629e76991dc4f3c457ba25154ded4'
            'b433c306262c6a708537bf92672a3ab8c3895c6a3478b20afb5c3f1a913a7624'
            'c7b85f0436072c2ada294a9e8284a4bd340a6f9e5570b94b55b1d33bf2f45e38'
            '1d80810b8a799c81df6db1e373f37bd202f8e6953f4ecb51f9bf8eea945fd7a8'
            '29c1aab1eb96bc188c1ad29422f611e7c946cca901aed29ee2d0b9e861c75a23'
            '35518b99189914b4700fad7a42b3268f67b5b6f4e23ffcc6cd9ef31b7b2b7c9f'
            'f4b3f350a86f1311a4c9a4d3ea1845292a6b4534b05eab085206280dc0543e31')
validpgpkeys=('EAF1C276A747E9ED86210CBAC3126D3B4AE55E93'
              '3A24BC1E8FB409FA9F14371813FCEF89DD9E3C4F')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/0001-enable-gold-on.mingw32.patch
  patch -p1 -i "${srcdir}"/0002-check-for-unusual-file-harder.patch
  patch -p1 -i "${srcdir}"/0010-bfd-Increase-_bfd_coff_max_nscns-to-65279.patch
  patch -p1 -i "${srcdir}"/0020-binutils_2.31_mkdtemp_impl.patch
  patch -p1 -i "${srcdir}"/0110-binutils-mingw-gnu-print.patch
  patch -p1 -i "${srcdir}"/0120-windres-handle-spaces.patch
  # Upstream patches
  patch -p1 -i "${srcdir}"/1000-Change-the-default-characteristics-of-DLLs-built-by-.patch
  patch -p1 -i "${srcdir}"/1001-mingw-plugin-test-regressions-due-to-commit-514b4e19.patch
  patch -p1 -i "${srcdir}"/1002-Fixes-for-testsuite-failures-introduced-by-the-chang.patch
  patch -p1 -i "${srcdir}"/1003-Pass-disable-reloc-section-on-PE-targets-for-PR-2566.patch
  patch -p1 -i "${srcdir}"/1004-Fix-a-segfault-when-creating-an-import-library-with-.patch
  patch -p1 -i "${srcdir}"/1005-After-preventing-creating-an-import-library-for-an-e.patch
  patch -p1 -i "${srcdir}"/1006-Import-patch-from-mainline-to-fix-decoding-DWARF-inf.patch

  # Temporarily revert defaults change while testing.
  patch -p1 -i "${srcdir}"/2000-Temporarily-revert-default-dll-characteristics.patch
  # Add an option to change default bases back below 4GB to ease transition
  patch -p1 -i "${srcdir}"/2001-ld-option-to-move-default-bases-under-4GB.patch
  # https://github.com/msys2/MINGW-packages/issues/7027
  # https://github.com/msys2/MINGW-packages/issues/7023
  patch -p1 -i "${srcdir}"/2002-default-to-4GB-image-base-for-now.patch
}

build() {
  if [ "${CARCH}" = "x86_64" ]; then
    local _conf='--enable-64-bit-bfd'
  else
    local _conf=''
    LDFLAGS+=" -Wl,--large-address-aware"
  fi

  if check_option "debug" "y"; then
    CFLAGS+=" -O0"
  fi

  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --disable-werror \
    --enable-lto \
    --with-sysroot=${MINGW_PREFIX} \
    --with-libiconv-prefix=${MINGW_PREFIX} \
    ${_conf} \
    --enable-nls \
    --disable-rpath \
    --disable-multilib \
    --enable-install-libiberty \
    --enable-plugins \
    --enable-gold \
    --disable-shared

  make
}

check() {
  cd ${srcdir}/build-${MINGW_CHOST}

  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # do not abort on errors - manually check log files
  make LDFLAGS="" -k check || true
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install

  mkdir -p ${pkgdir}${MINGW_PREFIX}/{lib,include}/${_realname}
  mkdir -p ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}/include
  cp ${pkgdir}${MINGW_PREFIX}/include/ansidecl.h ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}/include/
  mv ${pkgdir}${MINGW_PREFIX}/include/*.h ${pkgdir}${MINGW_PREFIX}/include/${_realname}
  mv ${pkgdir}${MINGW_PREFIX}/lib/*.a ${pkgdir}${MINGW_PREFIX}/lib/${_realname}

  # Add some useful headers
  #install -m644 ${srcdir}/${_realname}-${pkgver}/include/libiberty.h ${pkgdir}${MINGW_PREFIX}/include
  #install -m644 ${srcdir}/${_realname}-${pkgver}/include/demangle.h ${pkgdir}${MINGW_PREFIX}/include

  find ${pkgdir}${MINGW_PREFIX}/share -type f -iname "opcodes.mo" -o -iname "bfd.mo" | xargs -rtl1 rm

  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING      ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING.LIB  ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING.LIB
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING3     ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING3
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING3.LIB ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING3.LIB
}
