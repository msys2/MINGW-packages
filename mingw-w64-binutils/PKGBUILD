# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=binutils
_mingw_suff=mingw-w64-${CARCH}
pkgname=("${_mingw_suff}-${_realname}")
pkgver=2.24
pkgrel=3
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('any')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
groups=("${_mingw_suff}-toolchain")
depends=("${_mingw_suff}-libiconv" "${_mingw_suff}-zlib")
checkdepends=('dejagnu' 'bc')
makedepends=("${_mingw_suff}-libiconv" "${_mingw_suff}-zlib")
options=('staticlibs' '!distcc' '!ccache')
#install=binutils.install
source=("ftp://mirrors.kernel.org/sources.redhat.com/binutils/releases/binutils-${pkgver}.tar.bz2")
md5sums=('e0f71a7b2ddab0f8612336ac81d9636b')

prepare() {
  cd ${srcdir}/binutils-${pkgver}
  
  #do not install libiberty
  #sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in
  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  mkdir ${srcdir}/binutils-build
}

build() {
  cd ${srcdir}/binutils-build

  if [ "${CARCH}" = "x86_64" ]; then
    local _conf='--enable-64-bit-bfd'
  else
    local _conf=''
	LDFLAGS+=" -Wl,--large-address-aware"
  fi
  ${srcdir}/binutils-${pkgver}/configure \
    --build=${MINGW_CHOST} \
	--host=${MINGW_CHOST} \
	--target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --disable-werror \
	--enable-lto \
	--with-sysroot=${MINGW_PREFIX} \
	--with-libiconv-prefix=${MINGW_PREFIX} \
	$_conf \
	--disable-nls \
	--disable-rpath \
	--disable-multilib

  make
}

check() {
  cd ${srcdir}/binutils-build
  
  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # do not abort on errors - manually check log files
  make LDFLAGS="" -k check || true
}

package() {
  cd ${srcdir}/binutils-build
  make DESTDIR=${pkgdir} install
  
  mkdir -p ${pkgdir}${MINGW_PREFIX}/{lib,include}/binutils
  mkdir -p ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}/include
  cp ${pkgdir}${MINGW_PREFIX}/include/ansidecl.h ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}/include/
  mv ${pkgdir}${MINGW_PREFIX}/include/*.h ${pkgdir}${MINGW_PREFIX}/include/binutils
  mv ${pkgdir}${MINGW_PREFIX}/lib/*.a ${pkgdir}${MINGW_PREFIX}/lib/binutils
  
  # Remove unwanted files
  rm -rf ${pkgdir}${MINGW_PREFIX}/share
  
  # Add some useful headers
  #install -m644 ${srcdir}/binutils-${pkgver}/include/libiberty.h ${pkgdir}${MINGW_PREFIX}/include
  #install -m644 ${srcdir}/binutils-${pkgver}/include/demangle.h ${pkgdir}${MINGW_PREFIX}/include
}
