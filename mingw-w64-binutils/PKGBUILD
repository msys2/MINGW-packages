# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=binutils
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.26
pkgrel=22
pkgdesc="A set of programs to assemble and manipulate binary and object files (mingw-w64)"
arch=('any')
url="https://www.gnu.org/software/binutils/"
license=('GPL')
groups=("${MINGW_PACKAGE_PREFIX}-toolchain")
depends=("${MINGW_PACKAGE_PREFIX}-libiconv" "${MINGW_PACKAGE_PREFIX}-zlib")
#checkdepends=('dejagnu' 'bc')
makedepends=("${MINGW_PACKAGE_PREFIX}-libiconv" "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' '!distcc' '!ccache') # 'debug' '!strip')
#install=binutils.install
source=(https://ftp.gnu.org/gnu/binutils/${_realname}-${pkgver}.tar.bz2{,.sig}
        0001-enable-gold-on.mingw32.patch
        0002-check-for-unusual-file-harder.patch
        0003-enable-shared-bfd.all.patch
        0003-link-to-libibtl-and-libiberty.mingw.patch
        0004-libiberty-la.mingw.patch
        0005-shared-opcodes.mingw.patch
        0008-fix-libiberty-makefile.mingw.patch
        0009-fix-libiberty-configure.mingw.patch
        0010-bfd-Increase-_bfd_coff_max_nscns-to-65279.patch
        0013-dont-link-gas-to-libiberty.mingw.patch
        0014-dont-link-binutils-to-libiberty.mingw.patch
        0015-dont-link-ld-to-libiberty.mingw.patch
        0017-fix-iconv-linking.all.patch
        0110-binutils-mingw-gnu-print.patch
        0050-coff-linker-weak-nt-externals.patch
        0051-gas-aux-nt-weak-externals.patch
        0052-objcopy-weak-nt-externals2local.patch
        0053-fix-erroneous-relocations-applied.patch)
sha256sums=('c2ace41809542f5237afc7e3b8f32bb92bc7bc53c6232a84463c423b0714ecd9'
            'SKIP'
            '93296b909e1a4f9d8a4bbe2437aafa17ca565ef6642a9812b0360c05be228c9d'
            '09a56647c86af0ae3ef7c6de877a5b1b7774bf84df726e43b32023097f6ace23'
            'f084cf708da231584d1b1dd0fbdb6294ce176849c4f77d1666fa778df8d07781'
            '617adc1fd9fa66cc6e1da840b49edc1f88ae4bfd5b2807eeb34984eff8827e39'
            'b3fc8b894b9cfe9d0d27c49e36a808a0843020fccb3019737794fcde69af2352'
            'acae8564367601b4f601dca724596dbffbf056c0690e2aa090b185f3dcf2bc4f'
            '78bd3d792f616626e88c04b136f92ba2493b21007aaf4e5955e80c645d59efdf'
            '4849fe969fc7abb6b6dfcef4d4c6506f23831d86ba22c6dbf8d3039212654837'
            'bd38317b28b894d5c0e0ba043fc2445af415f6e96bb9ac93c348748d21a43625'
            'fa009b8a2baf122700b9583fce47c0e304a37211af09c7dcd0a1169a511be362'
            '177426db0ac7f9e9794b1052cbd15c59fa0167ece4cb3ac4f72673403aabc97a'
            '376931c0c2f8f5d655458016ac496f93e75c00bf5538e445c29984920a2f5415'
            '6b336f218443a95e141d231e4fab726c85d27de7c60ad1ce9fb1a1687943a1ce'
            'bb20764800b394af3ffa12567158ae1cfcd4fb44077d4c44d4e261251e60cf9f'
            '76663a2493f81e2306140259a2e4306b7cc4592fc4381951df87b5aa52c0fb62'
            'a5764911df3bde0e51fe2c244cac38a2f4169081017796b8cc9f214d34c17930'
            '05933fb77e5577257fe1d7a6ff2e701e93fd43ce417a29165bc6a71da5326459'
            '3ac2dccb420a2f86bb7341de9b2b1a43e2bb6065ce54b600b1bd63c7de3e01b9')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/0001-enable-gold-on.mingw32.patch
  patch -p1 -i "${srcdir}"/0002-check-for-unusual-file-harder.patch
  #patch -p1 -i "${srcdir}"/0003-enable-shared-bfd.all.patch
  #patch -p1 -i "${srcdir}"/0003-link-to-libibtl-and-libiberty.mingw.patch
  #patch -p1 -i "${srcdir}"/0004-libiberty-la.mingw.patch
  #patch -p1 -i "${srcdir}"/0005-shared-opcodes.mingw.patch
  patch -p1 -i "${srcdir}"/0008-fix-libiberty-makefile.mingw.patch
  patch -p1 -i "${srcdir}"/0009-fix-libiberty-configure.mingw.patch
  patch -p1 -i "${srcdir}"/0010-bfd-Increase-_bfd_coff_max_nscns-to-65279.patch
  #patch -p1 -i "${srcdir}"/0013-dont-link-gas-to-libiberty.mingw.patch
  #patch -p1 -i "${srcdir}"/0014-dont-link-binutils-to-libiberty.mingw.patch
  #patch -p1 -i "${srcdir}"/0015-dont-link-ld-to-libiberty.mingw.patch
  #patch -p1 -i "${srcdir}"/0017-fix-iconv-linking.all.patch
  patch -p1 -i "${srcdir}"/0110-binutils-mingw-gnu-print.patch
  
  patch -p1 -i "${srcdir}"/0050-coff-linker-weak-nt-externals.patch
  patch -p1 -i "${srcdir}"/0051-gas-aux-nt-weak-externals.patch
  patch -p1 -i "${srcdir}"/0052-objcopy-weak-nt-externals2local.patch
  patch -p1 -i "${srcdir}"/0053-fix-erroneous-relocations-applied.patch
}

build() {
  if [ "${CARCH}" = "x86_64" ]; then
    local _conf='--enable-64-bit-bfd'
  else
    local _conf=''
    LDFLAGS+=" -Wl,--large-address-aware"
  fi

  if check_option "debug" "y"; then
    CFLAGS+=" -O0"
  fi

  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --disable-werror \
    --enable-lto \
    --with-sysroot=${MINGW_PREFIX} \
    --with-libiconv-prefix=${MINGW_PREFIX} \
    $_conf \
    --enable-nls \
    --disable-rpath \
    --disable-multilib \
    --enable-install-libiberty \
    --enable-plugins \
    --enable-gold \
    --disable-shared

  make
}

check() {
  cd ${srcdir}/build-${MINGW_CHOST}

  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # do not abort on errors - manually check log files
  make LDFLAGS="" -k check || true
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install

  mkdir -p ${pkgdir}${MINGW_PREFIX}/{lib,include}/${_realname}
  mkdir -p ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}/include
  cp ${pkgdir}${MINGW_PREFIX}/include/ansidecl.h ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}/include/
  mv ${pkgdir}${MINGW_PREFIX}/include/*.h ${pkgdir}${MINGW_PREFIX}/include/${_realname}
  mv ${pkgdir}${MINGW_PREFIX}/lib/*.a ${pkgdir}${MINGW_PREFIX}/lib/${_realname}

  # Add some useful headers
  #install -m644 ${srcdir}/${_realname}-${pkgver}/include/libiberty.h ${pkgdir}${MINGW_PREFIX}/include
  #install -m644 ${srcdir}/${_realname}-${pkgver}/include/demangle.h ${pkgdir}${MINGW_PREFIX}/include

  find  ${pkgdir}${MINGW_PREFIX}/share -type f -iname "opcodes.mo" -o -iname "bfd.mo" | xargs -rtl1 rm
}
