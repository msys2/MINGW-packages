# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=binutils
_mingw_suff=mingw-w64-${CARCH}
pkgname=("${_mingw_suff}-${_realname}")
pkgver=2.24
pkgrel=6
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('any')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
groups=("${_mingw_suff}-toolchain")
depends=("${_mingw_suff}-libiconv" "${_mingw_suff}-zlib")
#checkdepends=('dejagnu' 'bc')
makedepends=("${_mingw_suff}-libiconv" "${_mingw_suff}-zlib")
options=('staticlibs' '!distcc' '!ccache')
#install=binutils.install
source=("ftp://mirrors.kernel.org/sources.redhat.com/${_realname}/releases/${_realname}-${pkgver}.tar.bz2"
		'010-2.23.52-install-libiberty.patch'
		'100-dont-escape-arguments-that-dont-need-it-in-pex-win32.c.patch'
		'020-add-bigobj-format.patch'
		'030-binutils-mingw-gnu-print.patch')
md5sums=('e0f71a7b2ddab0f8612336ac81d9636b'
         '01590d2835f6eda9932f78f84718a1d9'
         'cc15baf6f7939980c7f406416736baa8'
         '62c646e3f8c4cb08d0916ab34faa8254'
         '6e8b708d437bccbfa46ba56421dabb40')

prepare() {
	cd ${srcdir}/${_realname}-${pkgver}
	patch -p1 -i "${srcdir}"/010-2.23.52-install-libiberty.patch
	patch -p1 -i "${srcdir}"/100-dont-escape-arguments-that-dont-need-it-in-pex-win32.c.patch
	patch -p1 -i "${srcdir}"/020-add-bigobj-format.patch
	patch -p1 -i "${srcdir}"/030-binutils-mingw-gnu-print.patch

	#do not install libiberty
	#sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in
	# hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
	#sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
	if [ "${CARCH}" = "x86_64" ]; then
		local _conf='--enable-64-bit-bfd'
	else
		local _conf=''
		LDFLAGS+=" -Wl,--large-address-aware"
	fi

	cd $srcdir
	rm -rf build-${MINGW_CHOST}
	mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}
	../${_realname}-${pkgver}/configure \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST} \
		--target=${MINGW_CHOST} \
		--prefix=${MINGW_PREFIX} \
		--disable-werror \
		--enable-lto \
		--with-sysroot=${MINGW_PREFIX} \
		--with-libiconv-prefix=${MINGW_PREFIX} \
		$_conf \
		--disable-nls \
		--disable-rpath \
		--disable-multilib \
		--enable-install-libiberty

	make
}

check() {
	cd ${srcdir}/build-${MINGW_CHOST}

	# unset LDFLAGS as testsuite makes assumptions about which ones are active
	# do not abort on errors - manually check log files
	make LDFLAGS="" -k check || true
}

package() {
	cd ${srcdir}/build-${MINGW_CHOST}
	make DESTDIR=${pkgdir} install

	mkdir -p ${pkgdir}${MINGW_PREFIX}/{lib,include}/${_realname}
	mkdir -p ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}/include
	cp ${pkgdir}${MINGW_PREFIX}/include/ansidecl.h ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}/include/
	mv ${pkgdir}${MINGW_PREFIX}/include/*.h ${pkgdir}${MINGW_PREFIX}/include/${_realname}
	mv ${pkgdir}${MINGW_PREFIX}/lib/*.a ${pkgdir}${MINGW_PREFIX}/lib/${_realname}

	# Remove unwanted files
	# rm -rf ${pkgdir}${MINGW_PREFIX}/share

	# Add some useful headers
	#install -m644 ${srcdir}/${_realname}-${pkgver}/include/libiberty.h ${pkgdir}${MINGW_PREFIX}/include
	#install -m644 ${srcdir}/${_realname}-${pkgver}/include/demangle.h ${pkgdir}${MINGW_PREFIX}/include
}
