# Maintainer: David Grayson <davidegrayson@gmail.com>

# Note: We currently build an older version of usbview from a ZIP file
# that was on a page that Microsoft has since unpublished.  We are not
# yet able to build the version of usbview from github because
# mingw-w64 is missing some new structs/typedefs.
#
# https://github.com/Microsoft/Windows-driver-samples/tree/master/usb/usbview

_realname=usbview
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.1.20140402
pkgrel=1
pkgdesc='GUI for browsing all USB controllers and connected USB devices on your computer (mingw-w64)'
arch=('any')
url='https://web.archive.org/web/20140824072648/http://code.msdn.microsoft.com/windowshardware/USBView-sample-application-e3241039'
license=('custom')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-gcc"
  'patch'
)
depends=()
options=('strip')
source=(
  "https://code.msdn.microsoft.com/windowshardware/USBView-sample-application-e3241039/file/51333/38/USBView%20sample%20application.zip"
  'my_xmlhelper.c'
  '01-strsafe-after-tchar.patch'
  '02-sal.patch'
  '03-resource-fix.patch'
)

sha256sums=('fd07e8b20f9cff7efd8c01bf2b9cc9ac39ba85c76e56287b80d8abf38ec92287'
            'b3d551683be0398bca58e67bf7c91b22d88283e10292c9cda5ab367921511ebf'
            'dd0dade894b612cc0d30b0cbe1c47647de3697c9feeff65b866c32b01430154e'
            'b6959253bde6fd85af82cdd74c0993d0e9c82871f95cb507811b1fd8528cd301'
            '2be3167e95f8d5d8abf91e43a49fac4593b8264df5c20478f476bdbe1bae89a3')

prepare() {
  cd "${srcdir}/C++"

  # dos2unix -q *.c *.h *.rc

  # strsafe.h must be after tchar.h in mingw-w64
  patch -p1 -i "../01-strsafe-after-tchar.patch"

  # Include sal.h in some places it is needed.
  patch -p1 -i "../02-sal.patch"

  # We exclude the XML stuff because it requires Visual C++ (.NET).
  rm usbschema.hpp xmlhelper.cpp
  cp "../my_xmlhelper.c" .

  # Fix some syntax errors that windres complains about
  patch -p1 -i "../03-resource-fix.patch"
}

build() {
  cd "${srcdir}"
  mkdir -p "build-${MINGW_CHOST}"
  cd "build-${MINGW_CHOST}"

  windres ../C++/uvcview.rc rc.so

  gcc -mwindows --std=c99 ${CFLAGS} ${LDFLAGS} \
    -DNTDDI_VERSION=0x06020000 -D_WIN32_WINNT=0x0602 \
    -DSTRSAFE_NO_DEPRECATE \
    ../C++/*.c rc.so \
    -lcomctl32 -lcomdlg32 -lsetupapi -lshell32 -lshlwapi -lole32 -lgdi32 \
    -o usbview.exe
}

package() {
  cd "${srcdir}"
  install -Dm644 license.rtf "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/license.rtf"
  cd "build-${MINGW_CHOST}"
  mkdir "${pkgdir}${MINGW_PREFIX}/bin"
  install -Dm755 usbview.exe "${pkgdir}${MINGW_PREFIX}/bin"
}
