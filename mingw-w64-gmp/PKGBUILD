# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=gmp
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=6.2.1
pkgrel=5
pkgdesc="A free library for arbitrary precision arithmetic (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://gmplib.org/"
license=('spdx:LGPL-3.0-or-later OR GPL-2.0-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools" "${MINGW_PACKAGE_PREFIX}-cc")
source=(https://gmplib.org/download/gmp-${pkgver}/${_realname}-${pkgver}.tar.xz{,.sig}
        001-gmp-enable-both-static-shared.patch
        002-fix-gmp-export-defines.patch)
sha256sums=('fd4829912cddd12f84181c3451cc752be224643e87fac497b69edddadc49b4f2'
            'SKIP'
            '4c9be5532c5dc68697013180ab92ee2251d011c3a083d53cd0fabbdc851eedf9'
            '2c62a5abf0dd422683d847191a012c65dbc8886f4d712e84f76aef2ac97e2def')
validpgpkeys=('343C2FF0FBEE5EC2EDBEF399F3599FF828C67298') # Niels MÃ¶ller <nisse@lysator.liu.se>"

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  [[ -d ../stash ]] && rm -rf ../stash
  mkdir ../stash
  cp config.{guess,sub} ../stash

  patch -p1 -i ../001-gmp-enable-both-static-shared.patch
  patch -p1 -i ../002-fix-gmp-export-defines.patch

  autoreconf -fiv
  cp -f ../stash/config.{guess,sub} .
}

build() {
  [[ -d "${srcdir}"/build-${MSYSTEM} ]] && rm -rf "${srcdir}"/build-${MSYSTEM}
  mkdir -pv "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

  local -a extra_config
  case "$CARCH" in
    i?86|x86_64)
      extra_config+=(--enable-fat)
      ;;
    *)
      extra_config+=(--disable-assembly)
      ;;
  esac

  export CFLAGS+=" -Wno-attributes -Wno-ignored-attributes -Wno-strict-prototypes"
  export CXXFLAGS+=" -Wno-attributes -Wno-ignored-attributes -Wno-strict-prototypes"

  ../${_realname}-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --enable-cxx \
    "${extra_config[@]}" \
    --enable-static \
    --enable-shared
  make
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"
  make check
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
}
