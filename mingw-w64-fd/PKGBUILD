# Maintainer: Wu Zhenyu <wuzy01@qq.com>
_realname=fd

pkgbase=mingw-w64-$_realname
pkgname=$MINGW_PACKAGE_PREFIX-$_realname
pkgver=9.0.0
pkgrel=1
pkgdesc='Simple, fast and user-friendly alternative to find (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url=https://github.com/sharkdp/fd
makedepends=("$MINGW_PACKAGE_PREFIX-rust")
license=('spdx:Apache-2.0 OR MIT')
msys2_references=(
  'archlinux: fd'
)
source=("$url/archive/v$pkgver/$_realname-$pkgver.tar.gz")
sha512sums=('e992db9170884c5c426d51ba06a0684b000c65df3fae392fe9ffb3555b94f1d0cfde9b5ae01da37508a1b11bc75b023534db380c992cb4ccf17084d27ec87c84')

prepare() {
  cd "${_realname}-${pkgver}"

  local _target="${CARCH}-pc-windows-gnu"
  if [[ $MINGW_PACKAGE_PREFIX == *-clang-aarch64 ]]; then
    _target="${CARCH}-pc-windows-gnullvm"
  fi
  cargo fetch --locked --target "${_target}"
}

build() {
  cd "${_realname}-${pkgver}"

  export WINAPI_NO_BUNDLED_LIBRARIES=1
  make prefix="${MINGW_PREFIX}"
}

check() {
  cd "${_realname}-${pkgver}"

  WINAPI_NO_BUNDLED_LIBRARIES=1 \
    cargo test --release --frozen
}

package() {
  cd "${_realname}-${pkgver}"

  make install DESTDIR="${pkgdir}" prefix="${MINGW_PREFIX}"

  install -Dm644 LICENSE-{APACHE,MIT} -t "$pkgdir"$MINGW_PREFIX/share/licenses/fd/
}
