# Maintainer: Wu Zhenyu <wuzy01@qq.com>

_realname=fd
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=10.1.0
pkgrel=1
pkgdesc='Simple, fast and user-friendly alternative to find (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url=https://github.com/sharkdp/fd
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
license=('spdx:Apache-2.0 OR MIT')
msys2_references=(
  'archlinux: fd'
)
source=("${url}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha512sums=('6ab75965cb1852dd677ffb07d51db852c993b59551ab2e3a87bf81560a41c5fff35e63e9e09aa495c6c42cd9d038bf8ad85c491095435c9881101cf6284385df')

prepare() {
  cd "${_realname}-${pkgver}"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${_realname}-${pkgver}"

  export WINAPI_NO_BUNDLED_LIBRARIES=1
  make prefix="${MINGW_PREFIX}"
}

check() {
  cd "${_realname}-${pkgver}"

  cargo test --release --frozen
}

package() {
  cd "${_realname}-${pkgver}"

  make install DESTDIR="${pkgdir}" prefix="${MINGW_PREFIX}"

  install -Dm644 LICENSE-{APACHE,MIT} -t "${pkgdir}${MINGW_PREFIX}/share/licenses/fd/"
}
