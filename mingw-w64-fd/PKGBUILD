# Maintainer: Wu Zhenyu <wuzy01@qq.com>
_realname=fd

pkgbase=mingw-w64-$_realname
pkgname=$MINGW_PACKAGE_PREFIX-$_realname
pkgver=8.5.2
pkgrel=1
pkgdesc='Simple, fast and user-friendly alternative to find (mingw-w64)'
arch=(any)
mingw_arch=(mingw32 mingw64 ucrt64 clang64)
url=https://github.com/sharkdp/fd
makedepends=("$MINGW_PACKAGE_PREFIX-rust")
license=(MIT APACHE)
source=("$url/archive/v$pkgver.tar.gz")
sha512sums=('24496f5553eb2e7dfdac295e1844d2ec542e87eef3ffbd888e68aea4f9a235144d4bb5b955dda31510fccbb8f6f1de09f4bfa852ab4990b035e8d8dbbe7a7f70')

prepare() {
  cd "${_realname}-${pkgver}"

  cargo fetch --locked
}

build() {
  cd "${_realname}-${pkgver}"

  WINAPI_NO_BUNDLED_LIBRARIES=1 \
  cargo build --release --frozen
}

check() {
  cd "${_realname}-${pkgver}"

  cargo test --release --frozen
}

package() {
  cd "${_realname}-${pkgver}"

  install -Dm755 target/release/fd "$pkgdir"$MINGW_PREFIX/bin/fd

  make completions
  install -Dm644 autocomplete/fd.bash "$pkgdir"${MINGW_PREFIX}/share/bash-completion/completions/fd
  install -Dm644 autocomplete/fd.fish "$pkgdir"${MINGW_PREFIX}/share/fish/vendor_completions.d/fd.fish
  install -Dm644 autocomplete/_fd "$pkgdir"${MINGW_PREFIX}/share/zsh/site-functions/_fd

  install -Dm644 doc/fd.1 "$pkgdir"$MINGW_PREFIX/share/man/man1/fd.1
  install -Dm644 LICENSE-APACHE "$pkgdir"$MINGW_PREFIX/share/licenses/fd/LICENSE-APACHE
  install -Dm644 LICENSE-MIT "$pkgdir"$MINGW_PREFIX/share/licenses/fd/LICENSE-MIT
}
