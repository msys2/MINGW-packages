# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=libspatialaudio
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.4.0
pkgrel=1
pkgdesc="Ambisonic encoding / decoding and binauralization library (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/videolabs/libspatialaudio"
msys2_repository_url="https://github.com/videolabs/libspatialaudio"
license=("spdx:LGPL-2.1-or-later")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-meson"
  "${MINGW_PACKAGE_PREFIX}-pkgconf"
)
depends=(
  "${MINGW_PACKAGE_PREFIX}-libmysofa"
  "${MINGW_PACKAGE_PREFIX}-cc-libs"
)
source=("https://github.com/videolabs/libspatialaudio/releases/download/${pkgver}/libspatialaudio-${pkgver}.tar.xz")
sha256sums=('79f00d6f2695844764604897f704f4520440e1c9e63ccb2aff482717f66b6187')

build() {
  mkdir -p "build-${MSYSTEM}" && cd "build-${MSYSTEM}"

  CXXFLAGS+=" -D_USE_MATH_DEFINES"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
  meson setup \
    --prefix="${MINGW_PREFIX}" \
    --buildtype=plain \
    --wrap-mode=nodownload \
    --default-library=shared \
    --auto-features=enabled \
    ../"${_realname}-${pkgver}"

  meson compile
}

package() {
  cd "build-${MSYSTEM}"

  DESTDIR="${pkgdir}" meson install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
