# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=flameshot
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=13.3.0
pkgrel=2
pkgdesc="Powerful yet simple to use screenshot software (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://flameshot.org/'
msys2_repository_url="https://github.com/flameshot-org/flameshot"
license=('spdx:GPL-2.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-hicolor-icon-theme"
         "${MINGW_PACKAGE_PREFIX}-kdsingleapplication"
         "${MINGW_PACKAGE_PREFIX}-qt6-base"
         "${MINGW_PACKAGE_PREFIX}-qt6-svg")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-qt6-tools"
             "${MINGW_PACKAGE_PREFIX}-openssl"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-git")
source=(
  "${msys2_repository_url}/archive/refs/tags/v${pkgver}/${_realname}-${pkgver}.tar.gz"
  0001-fix-translation-path.patch
  0002-stringify-version-in-rc.patch
  0003-fix-static-linking-with-widgets.patch
  0004-install-all-files.patch
  0005-remove-MSVC-flag.patch
)
sha256sums=('bd1666313c875400e9588b47eb3fd2f4d0828460b3705a215b97746ea654c1b4'
            '2264badcaa81f3980ecf7318adc54f8b2951b9fd10bc2eeaf084732f85ab2f7c'
            '959dff8fc2aa0b6a605928b88a960ad0191476de447f937cdbe2f32266767fce'
            '56e999649a6ff256e70b604df4026b2c0f28468797df3b09a625fb172099d644'
            'ed5be030f12f0cb2d3ed96c48276cbe007ecca7474c13792794daa0146e5c191'
            '81deb4bc0a7b84145052c2ce2d9202367c5accc2a7c46b87296d0810449c4d02')

_apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Np1 -i "${srcdir}"/${_patch}
  done
}

prepare() {
  cd "${_realname}-${pkgver}"

  _apply_patch_with_msg \
    0001-fix-translation-path.patch \
    0002-stringify-version-in-rc.patch \
    0003-fix-static-linking-with-widgets.patch \
    0004-install-all-files.patch \
    0005-remove-MSVC-flag.patch
}

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DENABLE_OPENSSL=ON \
      -DUSE_BUNDLED_KDSINGLEAPPLICATION=OFF \
      -Wno-dev \
      -S "${_realname}-${pkgver}" \
      -B "build-${MSYSTEM}"

  cmake --build "build-${MSYSTEM}"
}

package() {
  DESTDIR="${pkgdir}" cmake --install "build-${MSYSTEM}"

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/LICENSE \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
