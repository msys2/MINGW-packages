_realname=flameshot
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=12.1.0
pkgrel=1
pkgdesc="Powerful yet simple to use screenshot software (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/flameshot-org/flameshot"
license=('GPL')
depends=("${MINGW_PACKAGE_PREFIX}-qt5-svg"
         "${MINGW_PACKAGE_PREFIX}-hicolor-icon-theme"
         "${MINGW_PACKAGE_PREFIX}-kguiaddons-qt5")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-qt5-tools"
             'git')
source=("git+$url#tag=v$pkgver")
sha256sums=('108f70340fa3facd440ac9428d590befe225305d00943a920ee5594c0007cb38')

prepare() {
  cd "${srcdir}/${_realname}"

  # Hardcode FLAMESHOT_VERSION to avoid syntax error
  sed -i "/VALUE \"FileVersion\"/s/VER_FILEVERSION_STR/\"v$pkgver\"/" data/flameshot.rc
  sed -i "/VALUE \"ProductVersion\"/s/VER_PRODUCTVERSION_STR/\"v$pkgver\"/" data/flameshot.rc

  # Remove all QCP_EXPORT used by MSVC
  find "external/Qt-Color-Widgets/include/QtColorWidgets" -type f -name "*.hpp" \
    -exec sed -i 's/class QCP_EXPORT/class/g' {} +

  # Patch translation path
  sed -i "/QString trPath/s/binaryPath + \"\/translations\"/\"$(cygpath -m ${MINGW_PREFIX}/share/translations/${_realname})\"/" src/utils/pathinfo.cpp
}

build() {
  declare -a extra_config
  if check_option "debug" "y"; then
    extra_config+=("-DFLAMESHOT_DEBUG_CAPTURE=ON")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DCMAKE_BUILD_TYPE=None \
      -DDISABLE_UPDATE_CHECKER=ON \
      -Wno-dev \
      -S "${_realname}" \
      -B "build-${MSYSTEM}"

  cmake --build "build-${MSYSTEM}"
}

check() {
  cmake --build "build-${MSYSTEM}" --target test
}

package() {
  DESTDIR="${pkgdir}" cmake --install "build-${MSYSTEM}"

  install -Dm644 "${srcdir}/${_realname}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
  install -Dm644 "${srcdir}/${_realname}/data/translations" "${pkgdir}${MINGW_PREFIX}/share/translations/${_realname}"
}
