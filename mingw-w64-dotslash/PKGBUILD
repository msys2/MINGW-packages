# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=dotslash
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.5.8
pkgrel=1
pkgdesc="Simplified executable deployment (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://github.com/facebook/dotslash'
msys2_repository_url='https://github.com/facebook/dotslash'
license=('spdx:Apache-2.0 OR MIT')
depends=("${MINGW_PACKAGE_PREFIX}-bzip2" "${MINGW_PACKAGE_PREFIX}-zstd")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             'git')
options=('!strip')
source=("${url}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "bzip2-sys.tar.gz::https://crates.io/api/v1/crates/bzip2-sys/0.1.11+1.0.8/download"
        "zstd-sys.tar.gz::https://crates.io/api/v1/crates/zstd-sys/2.0.12+zstd.1.5.6/download"
        "bzip2-sys-use-pkgconfig.patch"
        "zstd-sys-remove-statik.patch")
sha256sums=('c80f7ce8b659c8f510659298ef5427bfaf7ee3258760bcffe2c45f7628de858e'
            '736a955f3fa7875102d57c82b8cac37ec45224a07fd32d58f9f7a186b6cd4cdc'
            '0a4e40c320c3cb459d9a9ff6de98cff88f4751ee9275d140e2be94a2b74e4c13'
            'f73309fb98b89b2085bccee6ffb66820082628d096bc989922b25d33a1f5f378'
            '48f4900ceb02d3aaf9a1020f33d56629156e96759f456c0e7ca18bfcf910767b')

prepare() {
  cd "${_realname}-${pkgver}"

  patch -d "../bzip2-sys-0.1.11+1.0.8" -i "../bzip2-sys-use-pkgconfig.patch"
  patch -d "../zstd-sys-2.0.12+zstd.1.5.6" -i "../zstd-sys-remove-statik.patch"
  cat >> Cargo.toml <<END

[patch.crates-io]
bzip2-sys.path = "../bzip2-sys-0.1.11+1.0.8"
zstd-sys.path = "../zstd-sys-2.0.12+zstd.1.5.6"
END

  cargo update -p bzip2-sys -p zstd-sys --config='net.git-fetch-with-cli=true'
  cargo fetch --locked --target "${RUST_CHOST}"
}

build() {
  cd "${_realname}-${pkgver}"

  export ZSTD_SYS_USE_PKG_CONFIG=1
  cargo build --release --frozen
}

check() {
  cd "${_realname}-${pkgver}"

  export ZSTD_SYS_USE_PKG_CONFIG=1
  cargo test --release --frozen
}

package() {
  cd "${_realname}-${pkgver}"

  export ZSTD_SYS_USE_PKG_CONFIG=1
  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE-{APACHE,MIT} -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/"
}
