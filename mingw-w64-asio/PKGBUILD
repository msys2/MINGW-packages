# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=asio
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.28.1
pkgrel=2
pkgdesc='Cross-platform C++ library for ASynchronous network I/O (mingw-w64)'
url='https://think-async.com/Asio/'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64' 'clang32')
license=('spdx:BSL-1.0')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             #"${MINGW_PACKAGE_PREFIX}-boost"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-openssl")
_realver=${pkgver}
_realpath=${_realname}-${_realver}
source=("https://sourceforge.net/projects/asio/files/asio/${pkgver}%20%28Stable%29/asio-${pkgver}.tar.bz2")
sha256sums=('3bcf0357e2e19a2ee5adc805e6cc209338a7461c406d3047d282ea1c4b566101')

prepare() {
  cd ${srcdir}/${_realpath}

  autoreconf -fiv
}

build() {
  cd ${srcdir}/${_realpath}

  CPPFLAGS+=" -D_WIN32_WINNT=0x601" \
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --with-boost=no \
    --with-openssl=yes

  # Allow it to fail
  make || true
}

check() {
  cd ${srcdir}/${_realpath}
  make check || true
}

package() {
  cd ${srcdir}/${_realpath}
  # make DESTDIR=${pkgdir} install
  make DESTDIR=${pkgdir} install-data-am
  make DESTDIR=${pkgdir} -C include install-data-am

  install -Dm 644 COPYING LICENSE_1_0.txt -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
}
