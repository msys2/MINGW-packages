# Maintainer: Cyril Arnould <cyril.arnould@outlook.com>

_realname=findutils
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=4.9.0
pkgrel=1
pkgdesc="GNU utilities to locate files"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
license=('GPL3')
depends=("${MINGW_PACKAGE_PREFIX}-gettext"
         "${MINGW_PACKAGE_PREFIX}-libiconv"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread-git")
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-gcc")
checkdepends=("${MINGW_PACKAGE_PREFIX}-diffutils")
url="https://www.gnu.org/software/findutils"
source=("https://ftp.gnu.org/gnu/${_realname}/${_realname}-${pkgver}.tar.xz"{,.sig}
        "001-add-gnulib-modules.patch"               
        "002-configure-allow-mountlist.patch"        
        "003-replace-grp-pwd.patch"                  
        "004-define-limits.patch"                    
        "005-skip-undefined-symbols.patch"                 
        "006-disable-globbing.patch"                 
        "007-replace-fork.patch"                
        "008-lib-regextype-rename-context-enum.patch"
        "009-lib-fdleak-disable-check.patch"         
        "010-find-util-fix-get-statinfo.patch"       
        "011-find-parser-fix-defines.patch"          
        "012-find-parser-fix-path-safety.patch"          
        "013-find-print-fix-time-format.patch"          
        "014-locate-skip-privs.patch"          
        "015-xargs-fix-tty-streams.patch"          
        "016-fix-clang-linking.patch")
sha256sums=('a2bfb8c09d436770edc59f50fa483e785b161a3b7b9d547573cb08065fd462fe'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')
validpgpkeys=('A5189DB69C1164D33002936646502EF796917195')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Np1 -i "${srcdir}/001-add-gnulib-modules.patch"               
  patch -Np1 -i "${srcdir}/002-configure-allow-mountlist.patch"        
  patch -Np1 -i "${srcdir}/003-replace-grp-pwd.patch"                  
  patch -Np1 -i "${srcdir}/004-define-limits.patch"                    
  patch -Np1 -i "${srcdir}/005-skip-undefined-symbols.patch"                 
  patch -Np1 -i "${srcdir}/006-disable-globbing.patch"                 
  patch -Np1 -i "${srcdir}/007-replace-fork.patch"                
  patch -Np1 -i "${srcdir}/008-lib-regextype-rename-context-enum.patch"
  patch -Np1 -i "${srcdir}/009-lib-fdleak-disable-check.patch"         
  patch -Np1 -i "${srcdir}/010-find-util-fix-get-statinfo.patch"       
  patch -Np1 -i "${srcdir}/011-find-parser-fix-defines.patch"          
  patch -Np1 -i "${srcdir}/012-find-parser-fix-path-safety.patch"          
  patch -Np1 -i "${srcdir}/013-find-print-fix-time-format.patch"          
  patch -Np1 -i "${srcdir}/014-locate-skip-privs.patch"          
  patch -Np1 -i "${srcdir}/015-xargs-fix-tty-streams.patch"          
  patch -Np1 -i "${srcdir}/016-fix-clang-linking.patch"
  autoreconf -fi  
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  export gl_cv_have_weak=no
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}

  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR=${pkgdir} install
}
