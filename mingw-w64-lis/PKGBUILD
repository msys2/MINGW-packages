# Contributor: Oleg A. Khlybov <fougas@mail.ru>

_realname=lis

pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.0.8
pkgrel=1
arch=('any')
pkgdesc="Sparse iterative linear solver library (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" "${MINGW_PACKAGE_PREFIX}-gcc-libgfortran")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-gcc-fortran" "${MINGW_PACKAGE_PREFIX}-msmpi")
options=('strip' 'staticlibs')
license=('3-clause BSD')
url="http://www.ssisc.org/lis/"
source=("http://www.ssisc.org/lis/dl/${_realname}-${pkgver}.zip")
sha256sums=('0d41c2b46f7b0877429cee6f84e968fbaa787492ed00a9a6872313d48b5b4e64')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  for p in ${srcdir}/../*.patch; do
	 patch -p1 < $p
  done
}

builds="dso dmo dto zso zmo zto"

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  for build in ${builds}; do
    opts="--enable-fortran --enable-f90 --enable-static"
    ld=gfortran
    ldflags=
    pc=
    case ${build} in
      ?m?)
        opts="${opts} --enable-mpi"
        ld=mpif90
      ;;
      ?t?)
        opts="${opts} --enable-omp"
        ldflags=-fopenmp
      ;;
    esac
    case ${build} in
      c*|z*)
        opts="${opts} --enable-complex"
      ;;
    esac
    [[ -d ${srcdir}/build-${MINGW_CHOST}-${build} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}-${build}
    mkdir -p ${srcdir}/build-${MINGW_CHOST}-${build} && cd ${srcdir}/build-${MINGW_CHOST}-${build}
    ../${_realname}-${pkgver}/configure ${opts}
    make
    cd src/.libs
    lib=liblis-${build}
    mv liblis.a ${lib}.a
    ${ld} -shared ${ldflags} -Wl,--enable-auto-import -Wl,--export-all-symbols -o ${lib}.dll -Wl,--out-implib,${lib}.dll.a -Wl,--whole-archive ${lib}.a -Wl,--no-whole-archive
  done
}

package() {
  mkdir -p ${pkgdir}${MINGW_PREFIX}/{bin,lib/pkgconfig,lib/lis,include/lis}
  cd ${srcdir}/${_realname}-${pkgver}/include
  cp lis.h lis_*.h ${pkgdir}${MINGW_PREFIX}/include/lis
  cd ${pkgdir}${MINGW_PREFIX}/include/lis
  rm -f *_config*.h 
  for build in ${builds}; do
    cd ${srcdir}/build-${MINGW_CHOST}-${build}
    (
      cd src/.libs
      cp *.a ${pkgdir}${MINGW_PREFIX}/lib/lis
      cp *.dll ${pkgdir}${MINGW_PREFIX}/bin
    )
    (
      cd include
      mkdir -p ${pkgdir}${MINGW_PREFIX}/include/lis/${build}
      cp *.h ${pkgdir}${MINGW_PREFIX}/include/lis/${build}
    )
    lib=lis-${build}
    rmpi=
    cflags="-include lis_config.h"
    ldflags=
    case ${build} in
      ?m?)
        rmpi=msmpi
        cflags="${cflags} -DUSE_MPI"
      ;;
      ?t?)
        ldflags="${ldflags} -fopenmp"
      ;;
    esac
    echo "
      prefix=${MINGW_PREFIX}
      libdir=\${prefix}/lib/lis
      includedir=\${prefix}/include/lis
      Name: ${_realname}
      URL: ${url}
      Version: ${pkgver}
      Description: ${pkgdesc}
      Requires.private: ${rmpi}
      Cflags: -I\${includedir}/${build} -I\${includedir} ${cflags}
      Libs.private: ${ldflags} -L\${libdir} -l${lib} -lgfortran -lquadmath
      Libs: -L\${libdir} -l${lib}
    " | sed '/^\s*$/d;s/^\s*//' > ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/${lib}.pc
  done
}