# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libksba
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.3.0
pkgrel=1
pkgdesc="A CMS and X.509 access library"
arch=('any')
license=('GPL')
url="ftp://ftp.gnupg.org/gcrypt/alpha/libksba"
depends=("${_mingw_suff}-libgpg-error")
groups=("${_mingw_suff}")
options=('strip' 'staticlibs')
source=(ftp://ftp.gnupg.org/gcrypt/$_realname/$_realname-$pkgver.tar.bz2
    libksba-1.3.0-deffile.patch)
md5sums=('cd86fad9c9d360b2cf80449f8a4a4075'
         '60ee30b3f79fe15897cc01d58be72276')

prepare() {
  cd $srcdir/$_realname-$pkgver
  patch -p1 -i ${srcdir}/libksba-1.3.0-deffile.patch
}

build() {
  mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}
  ../$_realname-$pkgver/configure --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
  --host=${MINGW_CHOST} \
  --target=${MINGW_CHOST} \
  --enable-shared --disable-static
  make
}

check() {
   cd ${srcdir}/build-${CARCH}
   make check
}  

package() {
  cd ${srcdir}/build-${CARCH}
  make -j1 DESTDIR=$pkgdir install
  rm -r ${pkgdir}${MINGW_PREFIX}/share/info
}
