# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libksba
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3.3
pkgrel=2
pkgdesc="A CMS and X.509 access library (mingw-w64)"
arch=('any')
license=('GPL')
url="https://www.gnupg.org/related_software/libksba/"
depends=("${MINGW_PACKAGE_PREFIX}-libgpg-error")
options=('strip' 'staticlibs')
source=(https://gnupg.org/ftp/gcrypt/${_realname}/${_realname}-${pkgver}.tar.bz2
        0001-fix-def.mingw.patch)
sha256sums=('0c7f5ffe34d0414f6951d9880a46fcc2985c487f7c36369b9f11ad41131c7786'
            '0252e688224bac2f008100a7c9139cd79b3c1b8321529ef672fea2d08206598a')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-fix-def.mingw.patch
}

build() {
  [ -d ${srcdir}/build-${MINGW_CHOST} ] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --enable-static

  make
}

check() {
   cd ${srcdir}/build-${MINGW_CHOST}
   make check
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make -j1 DESTDIR=${pkgdir} install
}
