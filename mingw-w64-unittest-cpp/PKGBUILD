# Maintainer: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=unittest-cpp
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.0.0
pkgrel=2
pkgdesc="A lightweight unit testing framework for C++"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
license=(MIT)
url="https://unittest-cpp.github.io/"
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake")
options=('staticlibs' 'strip')
source=("https://github.com/unittest-cpp/unittest-cpp/archive/v${pkgver}.tar.gz"
        0001-cmake-fix-version.patch)
sha256sums=('74852198877dc2fdebdc4e5e9bd074018bf8ee03a13de139bfe41f4585b2f5b9'
            'bfb53f904729f5d552977e7c740b5614367d55cac5121f7e51fb03e9c87f6c02')

prepare() {
  cd "${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-cmake-fix-version.patch"
}

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  mkdir -p build && cd build
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake .. \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DUTPP_AMPLIFY_WARNINGS=OFF
  make -j$(nproc)
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}/build
  make DESTDIR="${pkgdir}" install
  install -Dm644 ../LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
