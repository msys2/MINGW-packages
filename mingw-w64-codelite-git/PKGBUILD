# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=codelite
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=9.0.313.gc621cd8
pkgrel=1
pkgdesc="Open-source, cross platform IDE for the C/C++ programming languages (mingw-w64)"
arch=('any')
license=('GPL')
url="http://www.codelite.org"
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-hunspell"
         "${MINGW_PACKAGE_PREFIX}-libssh"
         "${MINGW_PACKAGE_PREFIX}-drmingw"
         "${MINGW_PACKAGE_PREFIX}-wxWidgets"
         )
optdepends=("${MINGW_PACKAGE_PREFIX}-clang")
makedepends=("${MINGW_PACKAGE_PREFIX}-boost"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-clang"
             "git")
options=(strip staticlibs !debug)
source=("${_realname}::git+https://github.com/eranif/codelite.git"
        include-winsock-header.patch
        mingw-use-wx-manifests.patch
        use-gnu-extensions.patch
        windows-headers-conflict.patch
        fix-sqlite3-find-and-build.patch
        clStandardPaths-missing-member.patch
        mingw-posix-layout-fix.patch
        proper-libraries-names-mingw.patch)
md5sums=('SKIP'
         '926c4848932586577192bd950ffc604b'
         'b18e62316a10dac7c274e2faccd86180'
         'f7ce106c3565835c019fbcda0f9bea56'
         '1ae47bc9b913d425f98533c237b45ceb'
         'e6ad6fec1b50070f68ea65bab4ec93a2'
         '84bc65336879f9ca6d3dd9eed21b8215'
         '47ab3dc498add139293f136c96925efa'
         'a53dd1fdbd27970d6ce1a51ffad2eb35')

pkgver() {
  cd "${_realname}"
  git describe --tags | sed 's|-|.|g'
}

prepare() {
  cd "${srcdir}/${_realname}"
  patch -p1 -i ${srcdir}/include-winsock-header.patch
  patch -p1 -i ${srcdir}/mingw-use-wx-manifests.patch
  patch -p1 -i ${srcdir}/use-gnu-extensions.patch
  patch -p1 -i ${srcdir}/windows-headers-conflict.patch
  patch -p1 -i ${srcdir}/fix-sqlite3-find-and-build.patch
  patch -p1 -i ${srcdir}/clStandardPaths-missing-member.patch
  patch -p1 -i ${srcdir}/mingw-posix-layout-fix.patch
  patch -p1 -i ${srcdir}/proper-libraries-names-mingw.patch
}

package() {
  local -a extra_config

  if check_option "debug" "y"; then
    extra_config+=( -DCMAKE_BUILD_TYPE=Debug )
  else
    extra_config+=( -DCMAKE_BUILD_TYPE=Release )
  fi

  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}
  #cd ${srcdir}/${_realname}
  ${MINGW_PREFIX}/bin/cmake \
    -Wno-dev \
    -G"MSYS Makefiles" \
    -DPREFIX=${pkgdir}${MINGW_PREFIX} \
    -DENABLE_CLANG=1 \
    -DWITH_WEBVIEW=1 \
    -DwxWidgets_CONFIG_EXECUTABLE=${MINGW_PREFIX}/bin/wx-config \
    -DwxWidgets_wxrc_EXECUTABLE=${MINGW_PREFIX}/bin/wxrc-3.0 \
    "${extra_config[@]}" \
    ../${_realname}

  make # -j1 VERBOSE=1
  make install

  rm -f ${pkgdir}${MINGW_PREFIX}/lib/codelite/*.a
  rm -f ${pkgdir}${MINGW_PREFIX}/lib/*.a
}
