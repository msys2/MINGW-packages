# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=rapidfuzz-cpp
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=3.3.3
pkgrel=1
pkgdesc="Rapid fuzzy string matching in C++ using the Levenshtein Distance (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://rapidfuzz.github.io/rapidfuzz-cpp/'
msys2_repository_url='https://github.com/rapidfuzz/rapidfuzz-cpp/'
msys2_changelog_url='https://github.com/rapidfuzz/rapidfuzz-cpp/blob/main/CHANGELOG.md'
msys2_references=(
  'anitya: 369163'
  'archlinux: rapidfuzz-cpp'
  'gentoo: dev-cpp/rapidfuzz-cpp'
)
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
source=("https://github.com/rapidfuzz/rapidfuzz-cpp/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('fa0fbd40110df8134cf05bddbaa4e237dbc4fd915ab9a3029ff481a8d3e8b757')

build() {
  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${_extra_config[@]}" \
      -S "${_realname}-${pkgver}" \
      -B "build-${MSYSTEM}"

  "${MINGW_PREFIX}"/bin/cmake.exe --build "build-${MSYSTEM}"
}

package() {
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install "build-${MSYSTEM}"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
