# Contributor: Dirk Stolle

_realname=passagemath-environment
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=10.6.38
pkgrel=2
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgdesc="Subset of the Sage library providing the connection to the system and software environment (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/passagemath/passagemath'
msys2_references=(
  'purl: pkg:pypi/passagemath-environment'
)
license=('spdx:GPL-2.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-packaging"
         "${MINGW_PACKAGE_PREFIX}-python-platformdirs")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools")
options=('!strip')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname/-/_}-${pkgver}.tar.gz")
sha256sums=('edb8126ed54867f44d729565e89601ac88895fef813183d510c9cb78624ad9fc')

build() {
  cp -r "${_realname/-/_}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  # fix wrong shebang created by python-installer
  sed -i 's|#!<launcher_dir>\\python.exe|#!/usr/bin/env python3|g' "${pkgdir}${MINGW_PREFIX}/bin/sage-num-threads.py"
  sed -i 's|#!<launcher_dir>\\python.exe|#!'"${MINGW_PREFIX}"'/bin/python3|g' "${pkgdir}${MINGW_PREFIX}/bin/sage-venv-config"
}
