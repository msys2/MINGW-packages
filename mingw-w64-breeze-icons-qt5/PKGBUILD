# Maintainer (MSYS2): Ray Donnelly <mingw.android@gmail.com>
# Contributor (MSYS2): Mateusz Miku≈Ça <mati865@gmail.com>

_variant=-${KF5_VARIANT:-shared}
source "$(dirname ${BASH_SOURCE[0]})"/../mingw-w64-PKGBUILD-common/kde-frameworks5
_kde_f5_init_package "${_variant}" "breeze-icons"
pkgver=5.26.0
pkgrel=1
arch=('any')
pkgdesc="Breeze icon theme (mingw-w64-qt5${_namesuff})"
license=('LGPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-extra-cmake-modules")
depends=()
_kde_f5_add_depends "${_variant}" "${MINGW_PACKAGE_PREFIX}-qt5${_namesuff}"
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${_basename}-${pkgver}.tar.xz")
sha256sums=('a9c0d742a37ef3b3b2dae5a5bb97aee2927dd613c17c606f3bcbc6845a8c6bcd')
noextract=(${_basename}-${pkgver}.tar.xz)

prepare() {
  #[[ -d "${srcdir}/${_basename}-${pkgver}" ]] && rm -rf "${srcdir}/${_basename}-${pkgver}"
  plain "Upacking the archive..."
  MSYS='winsymlinks:lnk' tar -xf ${_basename}-${pkgver}.tar.xz -C ${srcdir}
  
  # Replace links with files
  cd ${srcdir}
  [[ -d "${srcdir}/unlinked-${_basename}-${pkgver}" ]] && rm -rf "${srcdir}/unlinked-${_basename}-${pkgver}"
  plain "Creating unlinked-${_basename}-${pkgver} with dereferenced symlinks..."
  cp -arL ${_basename}-${pkgver} unlinked-${_basename}-${pkgver}

  mkdir -p build-${CARCH}${_variant}
}

build() {
  local -a extra_config
  cd build-${CARCH}${_variant}
  if [ "${_variant}" = "-static" ]; then
    extra_config+=( -DBUILD_SHARED_LIBS=NO )
    QT5_PREFIX=${MINGW_PREFIX}/qt5-static
    export PATH=${QT5_PREFIX}/bin:"$PATH"
  else
    QT5_PREFIX=${MINGW_PREFIX}
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake ../unlinked-${_basename}-${pkgver} \
      -DCMAKE_BUILD_TYPE=$(_kde_f5_CMAKE_BUILD_TYPE) \
      -DCMAKE_INSTALL_PREFIX=${QT5_PREFIX} \
      "${extra_config[@]}" \
      -G'MSYS Makefiles'
  make
}

package() {
  cd build-${CARCH}${_variant}
  make DESTDIR="${pkgdir}" install
}
