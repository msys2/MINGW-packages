# Maintainer (MSYS2): Michael Hansen <zrax0111@gmail.com>

_variant=-${KF5_VARIANT:-shared}
source "$(dirname ${BASH_SOURCE[0]})"/../mingw-w64-PKGBUILD-common/kde-frameworks5
_kde_f5_init_package "${_variant}" "breeze-icons"
pkgver=5.75.0
pkgrel=1
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64')
pkgdesc="Breeze icon themes (mingw-w64-qt5${_namesuff})"
license=('LGPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-extra-cmake-modules>=${pkgver}"
             "${MINGW_PACKAGE_PREFIX}-python-lxml"
             "unzip")
depends=()
_kde_f5_add_depends "${_variant}" "${MINGW_PACKAGE_PREFIX}-qt5${_namesuff}"
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${_basename}-${pkgver}.zip"
        #${_basename}-${pkgver}::git+https://github.com/KDE/breeze-icons.git#tag=v${pkgver}
        01-mingw-python-script.patch)
sha256sums=('28ae8a401cac10ef789f9199b8934b77c19d4fc00989662d8bb72cc188894516'
            '2589efa413e5540668eaa6156d415b9e423cba566643aaef449392992bcb46c2')
#noextract=("${_basename}-${pkgver}.tar.xz")

prepare() {
  # bsdtar fails to extract this on Windows because it includes symlinks
  # before the targets.  Worse, it has symlinks to symlinks, so extracting
  # it twice isn't sufficient.  So we must keep trying until everything is
  # resolved :(.  However, we also enforce hard limit at 32 tries -- if it
  # doesn't work after that, it's time to just give up.
  #_tries=0
  #while ! /usr/bin/bsdtar xJf "${_basename}-${pkgver}.tar.xz" ; do
  #  _tries=$(( $_tries + 1 ))
  #  if (( $_tries == 7 )); then
  #    echo "Could not extract ${_basename}-${pkgver}.tar.xz after 32 tries.  Giving up!"
  #    exit 1
  #  fi
  #done

  mkdir -p build-${CARCH}${_variant}
  cd ${_basename}-${pkgver}
  patch -p1 -i ${srcdir}/01-mingw-python-script.patch
}

build() {
  local -a extra_config
  cd build-${CARCH}${_variant}
  if [ "${_variant}" = "-static" ]; then
    extra_config+=( -DBUILD_SHARED_LIBS=NO )
  fi

  _kde_f5_build_env
  cmake ../${_basename}-${pkgver} \
    -DCMAKE_BUILD_TYPE=$(_kde_f5_CMAKE_BUILD_TYPE) \
    "${_kde_f5_KDE_INSTALL_DIRS[@]}" \
    -DBUILD_QCH=OFF \
    -DBUILD_TESTING=OFF \
    -DECM_DIR=${MINGW_PREFIX}/share/ECM \
    "${extra_config[@]}" \
    -G'MSYS Makefiles'
  make
}

package() {
  cd build-${CARCH}${_variant}
  make DESTDIR="${pkgdir}" install

  # KDE apps won't get an icon theme by default on Windows unless it's
  # provided via <QStandardPaths::GenericDataLocation>/icontheme.rcc
  # TODO: Make this better...
  _kde_f5_build_env
  install -Dm644 icons/breeze-icons.rcc "${pkgdir}/${QT5_PREFIX}"/bin/data/icontheme.rcc
}
