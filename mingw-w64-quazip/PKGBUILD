# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=quazip
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}-qt5"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-qt6")
pkgver=1.3
pkgrel=1
pkgdesc="C++ wrapper for the Gilles Vollant's ZIP/UNZIP C package (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://github.com/stachenov/quazip/"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-qt6-5compat"
             "${MINGW_PACKAGE_PREFIX}-qt5-base"
             "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' 'strip')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/stachenov/${_realname}/archive/v${pkgver}.tar.gz")
sha256sums=('c1239559cd6860cab80a0fd81f4204e606f9324f702dab6166b0960676ee1754')

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  for qt_ver in 5 6; do 
    [[ -d "${srcdir}"/build-${qt_ver}-${MSYSTEM} ]] && rm -rf "${srcdir}"/build-${qt_ver}-${MSYSTEM}
    mkdir -p "${srcdir}"/build-${qt_ver}-${MSYSTEM} && cd "${srcdir}"/build-${qt_ver}-${MSYSTEM}

    MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -G"Ninja" \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DQUAZIP_QT_MAJOR_VERSION=${qt_ver} \
      ${extra_config[@]} \
      ../${_realname}-${pkgver}

    ${MINGW_PREFIX}/bin/cmake --build .
  done
}


package_quazip-qt5() {
  pkgdesc="C++ wrapper for the Gilles Vollant's ZIP/UNZIP C package - Qt5 (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-qt5-base"
           "${MINGW_PACKAGE_PREFIX}-zlib")
  conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
  replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}")

  cd "${srcdir}"/build-5-${MSYSTEM}
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .
}

package_quazip-qt6() {
  pkgdesc="C++ wrapper for the Gilles Vollant's ZIP/UNZIP C package - Qt6 (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-qt6-5compat"
           "${MINGW_PACKAGE_PREFIX}-zlib")

  cd "${srcdir}"/build-6-${MSYSTEM}
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
