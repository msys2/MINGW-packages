# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=fish
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.0b1
pkgrel=1
pkgdesc='Smart and user friendly shell intended mostly for interactive use (mingw-w64)'
arch=('x86_64')
url="https://fishshell.com/"
msys2_repository_url="https://github.com/fish-shell/fish-shell"
msys2_references=(
  "cpe: cpe:/a:fishshell:fish"
)
license=('spdx:GPL-2.0-only')
depends=("${MINGW_PACKAGE_PREFIX}-pcre2"
         "${MINGW_PACKAGE_PREFIX}-gettext-runtime"
         'man-db')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-gettext-tools")
optdepends=("${MINGW_PACKAGE_PREFIX}-python: for manual page completion parser and web configuration tool")
install=fish-${MSYSTEM}.install
backup=("${MINGW_PREFIX:1}/etc/fish/config.fish"
        "${MINGW_PREFIX:1}/etc/fish/msys2.fish"
        "${MINGW_PREFIX:1}etc/fish/perlbin.fish")
source=("https://github.com/fish-shell/fish-shell/releases/download/${pkgver}/${_realname}-${pkgver}.tar.xz"{,.asc}
        config-CLANG64.fish
        config-UCRT64.fish
        config-MINGW64.fish
        msys2-CLANG64.fish
        msys2-UCRT64.fish
        msys2-MINGW64.fish
        msystem.fish
        perlbin.fish)
sha256sums=('534334e10f85722214e9daff82a57cc3501235523f16f8f131c2344e4ec98da7'
            'SKIP'
            '63b7634cda9048834cdbbd57bc842b7e6a49cb8541a578cafc2797a4a42ae8f8'
            '925f9a45b9887f4bb74a5589cf1ab099dec376dd30b07e830042554befac9bdb'
            'a6097b634e236d2272744a6d5e0b31685a2198807d88eb091f4829ee4913ce36'
            '921c8725fcc53157752d3c100da8b6585a68d5fee5d476d6867310984937fb36'
            '1fdab0aba85dede9b29644957923d40314799b5d6541a25f7ba68ae9834ee267'
            '30ec15d5fe54078e275d9b7f1e62ea4240f3bc069881fdcfe95eb9442f2121f0'
            'b1a7b7b4238170373dd8acdc36bcbd1fc3978b3525403b877576139d6090e30d'
            'b136a9fa94abf53e302f7a1cc28def03b58dd2326990c5f02ceb4988341a5ac6')
validpgpkeys=('003837986104878835FA516D7A67D962D88A709A') # David Adam <zanchey@gmail.com>

build() {
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=;-DCMAKE_INSTALL_SYSCONFDIR="
  cmake \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_INSTALL_SYSCONFDIR=${MINGW_PREFIX}/etc \
    -DCMAKE_BUILD_TYPE=None \
    -S ${_realname}-${pkgver} \
    -B build-${MSYSTEM}

  cmake --build build-${MSYSTEM}
}

package() {
  rm -rf "${_realname}-${pkgver}/build-${MSYSTEM}/user_doc"
  cmake --install build-${MSYSTEM}

  install -Dm644 config-${MSYSTEM}.fish "${pkgdir}${MINGW_PREFIX}/etc/fish/config.fish"
  install -Dm644 msys2-${MSYSTEM}.fish "${pkgdir}${MINGW_PREFIX}/etc/fish/msys2.fish"
  install -Dm644 msystem.fish "${pkgdir}${MINGW_PREFIX}/etc/fish/msystem.fish"
  install -Dm644 perlbin.fish "${pkgdir}${MINGW_PREFIX}/etc/fish/perlbin.fish"
}
