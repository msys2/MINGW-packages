# Maintainer: Ramin Mojab <rmojab63@gmail.com>

_realname=ldt
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.1
pkgrel=1
pkgdesc="An automatic regression selection and regression averaging toolkit (mingw-w64)"
arch=(any)
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://github.com/rmojab63/LDT"
license=('LGPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cmake")
depends=("${MINGW_PACKAGE_PREFIX}-boost"
         "${MINGW_PACKAGE_PREFIX}-openblas")
checkdepends=("${MINGW_PACKAGE_PREFIX}-gtest")
options=('!strip' 'staticlibs' '!buildflags')
source=(${_realname}-${pkgver}.tar.gz::https://github.com/rmojab63/LDT/archive/refs/tags/v${pkgver}.tar.gz)
sha256sums=('f9dd44ff530cdb87fde0f28ac9bd7f171b558f3503ba859d5f2c2c12a1e67fb7')
# updpkgsums 

build() {

  [[ -d build-${MSYSTEM} ]] && rm -rf build-${MSYSTEM}
  mkdir build-${MSYSTEM} && cd build-${MSYSTEM}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -S "../${_realname}-${pkgver}" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DMINGW_ARCH=${MINGW_PREFIX}\
    -DCMAKEPRESETS=OFF\
    -DBUILD_TEST=OFF \
    -DBUILD_COMPLETE=OFF

  cmake --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  DESTDIR="${pkgdir}" cmake --install .
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}

