# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: @naveen521kk on Github <naveen@syrusdark.website>

_realname=pip
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
provides=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
replaces=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=21.0.1
pkgrel=3
pkgdesc="The PyPA recommended tool for installing Python packages. (mingw-w64)"
arch=('any')
license=('MIT')
url="https://pip.pypa.io/"
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-setuptools"
         "winpty")
# checkdepends=("${MINGW_PACKAGE_PREFIX}-python-pytest"
#               "${MINGW_PACKAGE_PREFIX}-python-scripttest"
#               "${MINGW_PACKAGE_PREFIX}-python-csv23"
#               "${MINGW_PACKAGE_PREFIX}-python-cryptography"
#               "${MINGW_PACKAGE_PREFIX}-python-werkzeug"
#               "${MINGW_PACKAGE_PREFIX}-python-virtualenv")
source=(
  ${_realname}-${pkgver}.tar.gz::https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz
  "pip"
  "pip3"
  "pip3.8"
)
sha256sums=('99bbde183ec5ec037318e774b0d8ae0a64352fe53b2c7fd630be1d07e94f41e5'
            '58da571aaca0ff9af4e15ef4e4fc71c0875ef852fa0e11aac3ec11fc612e85c5'
            'dedff8ac199e773e7b135274267f4a888c896b7712df9e490b4ce49c6ee8b62f'
            '10f1052d161f91c3e1afec3aed8738c1947d378ae6618ab529a3df88996bebfb')

prepare() {
  rm -rf python-build-${CARCH}| true
  cp -r "${_realname}-${pkgver}" "python-build-${CARCH}"
  cd "python-build-${CARCH}"
}

build() {
  msg "Python build for ${CARCH}" 
  cd "${srcdir}/python-build-${CARCH}"
  ${MINGW_PREFIX}/bin/python setup.py build
}
# check() {
#   msg "Python test for ${CARCH}"
#   cd "${srcdir}/python-build-${CARCH}"
#   ${MINGW_PREFIX}/bin/pytest tests
# }
package() {
  cd "${srcdir}/python-build-${CARCH}"
  install -Dm755 "${srcdir}/pip" "${pkgdir}${MINGW_PREFIX}/bin/pip"
  install -Dm755 "${srcdir}/pip3" "${pkgdir}${MINGW_PREFIX}/bin/pip3"
  install -Dm755 "${srcdir}/pip3.8" "${pkgdir}${MINGW_PREFIX}/bin/pip3.8"
  
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX#\/} --root="${pkgdir}" --optimize=1 --skip-build -v

  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  # fix python command in files
  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*.py; do
    sed -i 's/\#\!.*//' $_f
  done

  install -D -m644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
