# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=aspell-en
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=7.1
pkgrel=1
pkgdesc="English dictionary for aspell (mingw-w64)"
arch=('any')
url="http://aspell.net/"
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-aspell")
source=(https://ftp.gnu.org/gnu/aspell/dict/en/aspell6-en-${pkgver}-0.tar.bz2
        001-unixy-dirs.patch)
md5sums=('beba5e8f3afd3ed1644653bb685b2dfb'
         '59759a5f0047312a49f5817a83a8a2f9')
sha1sums=('d45ccda0c03e2a679c2936487ec851a1896b8150'
          'a624663ef98117b62f45cab4ade44cce3105ef4d')

prepare() {
  cd "${srcdir}/aspell6-en-${pkgver}-0"
  patch -p1 -i ${srcdir}/001-unixy-dirs.patch
}

build() {
  cd "${srcdir}/aspell6-en-${pkgver}-0"
  ./configure
  sed -i 's/C\:\\msys64\\/\//' Makefile
  make
}

package() {
  cd "${srcdir}/aspell6-en-${pkgver}-0"
  make DESTDIR="${pkgdir}" install

  install -D -m644 Copyright "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
