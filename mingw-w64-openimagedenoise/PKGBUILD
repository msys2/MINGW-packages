# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=openimagedenoise
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.4.3
pkgrel=1
pkgdesc="Intel Open Image Denoise library of high-performance, high-quality denoising filters for images rendered with ray tracing (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url="https://openimagedenoise.github.io/"
license=("Apache 2.0")
depends=("${MINGW_PACKAGE_PREFIX}-tbb")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-ispc"
             "git")
options=('strip' 'buildflags' '!debug')
source=(https://github.com/OpenImageDenoise/oidn/releases/download/v${pkgver}/oidn-${pkgver}.src.tar.gz
        001-Fix-finding-TBB-2021.patch
        002-use-the-same-_cpuid-as-msvc.patch)
sha256sums=('3276e252297ebad67a999298d8f0c30cfb221e166b166ae5c955d88b94ad062a'
            '18637ffced00319ca3fd894527ef2e528cdff3a31afb386b9994f3a9f834ab2d'
            '96d2a7346c6d50a281ab89a800ee45b29045ae73a28134c28cd6324df4ef0179')

prepare() {
  cd ${srcdir}/oidn-${pkgver}
  patch -p1 -i ${srcdir}/001-Fix-finding-TBB-2021.patch
  patch -p1 -i ${srcdir}/002-use-the-same-_cpuid-as-msvc.patch
}

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  [[ -d "build-${MSYSTEM}" ]] && rm -rf "build-${MSYSTEM}"
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    ${extra_config} \
    ../oidn-${pkgver}

  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}

  DESTDIR=${pkgdir} ${MINGW_PREFIX}/bin/cmake --build .

  install -Dm644 ${srcdir}/oidn-${pkgver}/LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.txt"
}
