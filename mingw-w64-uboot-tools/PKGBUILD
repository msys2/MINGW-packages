# Maintainer: Joel Holdsworth <jholdsworth@nvidia.com>

_realname=uboot-tools
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2025.04
pkgrel=1
pkgdesc='U-Boot bootloader utility tools'

_archive_name="u-boot-$pkgver"
_archive_file_name="${_archive_name}.tar.bz2"

arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://www.denx.de/wiki/U-Boot/WebHome'
msys2_references=(
  'archlinux: uboot-tools'
)
license=('spdx:GPL-2.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-dtc"
	"${MINGW_PACKAGE_PREFIX}-openssl")
makedepends=("bison"
             "flex"
             "gcc"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
	     "${MINGW_PACKAGE_PREFIX}-swig")
source=("https://ftp.denx.de/pub/u-boot/${_archive_file_name}")
sha256sums=('439d3bef296effd54130be6a731c5b118be7fddd7fcc663ccbc5fb18294d8718')

noextract=("${_archive_file_name}")
prepare() {
  # Ignore errors from symbolic links
  bsdtar -xf "${_archive_file_name}" 2>/dev/null || true
}

build() {
  cd "${_archive_name}"
  export DTC=${MINGW_PREFIX}/bin/dtc
  export CROSS_COMPILE=${MINGW_CHOST}-
  make defconfig
  make tools-all
}

package() {
  install -m 755 -d "$pkgdir"/usr/bin
  install -m 755 -t "$pkgdir"/usr/bin u-boot-$pkgver/tools/{mk{,env}image,env/fw_printenv,img2srec,dumpimage,netconsole,jtagconsole,ncb,kwboot}
  install -m 644 -Dt "$pkgdir"/usr/share/man/man1/ u-boot-$pkgver/doc/{mkimage,dumpimage,kwboot}.1
}
