# Maintainer: RÃ©mi BERTHO <remi.bertho@openmailbox.org>

_realname=muparser
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.2.4
pkgrel=1
pkgdesc="A fast math parser library"
arch=('i686' 'x86_64')
url="http://muparser.sourceforge.net/"
license=('custom')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake")
options=('!makeflags')
source=("${_realname}-${pkgver}.zip::https://drive.google.com/uc?export=download&id=0BzuB-ydOOoduejdwdTQwcF9JLTA" "CMakeLists.txt")
sha1sums=('78db09680cafd5db4fd08a5d472afd2fa65f9274' '76f802239c1a5e0bff713db99ce60a9956a20257')

prepare() {
  cd $srcdir/${_realname}_v${pkgver//./_}
  cp "$srcdir/CMakeLists.txt" .
}

build() {
  cd $srcdir/${_realname}_v${pkgver//./_}
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe -G"MSYS Makefiles" \
    -DCMAKE_CXX_COMPILER=${MINGW_PREFIX}/bin/g++.exe \
    -DCMAKE_MAKE_PROGRAM=${MINGW_PREFIX}/bin/mingw32-make.exe \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} .
  mingw32-make
}

package() {
  cd $srcdir/${_realname}_v${pkgver//./_}
  mingw32-make DESTDIR=${pkgdir} install

  # license
  install -D -m644 License.txt \
  $pkgdir${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}

