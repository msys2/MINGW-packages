# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=python2-cairo
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.10.0
pkgrel=3
pkgdesc="Python2 bindings for the cairo graphics library (mingw-w64)"
url="http://www.cairographics.org/pycairo"
arch=('any')
license=('LGPL' 'MPL')
depends=("${MINGW_PACKAGE_PREFIX}-python2" "${MINGW_PACKAGE_PREFIX}-python3" "${MINGW_PACKAGE_PREFIX}-cairo")
source=(http://cairographics.org/releases/py2cairo-${pkgver}.tar.bz2
        waflib.patch)
sha256sums=('d30439f06c2ec1a39e27464c6c828b6eface3b22ee17b2de05dc409e429a7431'
            '9845997835da558d465b129e917157b1a9cb1cf17db298134bcc51ffc719ed2e')

build() {
  cd "${srcdir}/py2cairo-${pkgver}"
  sed -i 's:bin/env python:bin/env python2:' waf
  /usr/bin/python2 ./waf --version
  pushd .waf-*
  patch -p0 -i ${srcdir}/waflib.patch
  rm -f waflib/*.pyc
  popd

  export PYTHON=${MINGW_PREFIX}/bin/python2
  CFLAGS+=" -I${MINGW_PREFIX}/include/python2.7 -I${MINGW_PREFIX}/include/cairo"
  /usr/bin/python2 ./waf --help
  /usr/bin/python2 ./waf configure --prefix=${MINGW_PREFIX}
  /usr/bin/python2 ./waf build
}

package() {
  cd "${srcdir}/py2cairo-${pkgver}"
  export PYTHON=${MINGW_PREFIX}/bin/python2
  /usr/bin/python2 ./waf install --destdir="${pkgdir}"

  rm -rf ${pkgdir}${MINGW_PREFIX}/lib
  mv ${pkgdir}${MINGW_PREFIX}/bin ${pkgdir}${MINGW_PREFIX}/lib
}
