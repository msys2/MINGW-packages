# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=libbdplus
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.2.0
pkgrel=1
pkgdesc="Library for handling the BD+ protection scheme on bluray discs (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
license=('LGPL')
url="https://www.videolan.org/developers/libbdplus.html"
depends=("${MINGW_PACKAGE_PREFIX}-libaacs>=0.7.0" "${MINGW_PACKAGE_PREFIX}-libgpg-error")
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools" "${MINGW_PACKAGE_PREFIX}-cc")
source=("https://download.videolan.org/pub/videolan/${_realname}/${pkgver}/${_realname}-${pkgver}.tar.bz2")
sha256sums=('b93eea3eaef33d6e9155d2c34b068c505493aa5a4936e63274f4342ab0f40a58')

build() {
  cd "$srcdir"/${_realname}-${pkgver}
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared

  make
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install DESTDIR="${pkgdir}"
  install -Dm644 "$srcdir"/${_realname}-${pkgver}/COPYING "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}"/COPYING
  install -Dm644 "$srcdir"/${_realname}-${pkgver}/README.md "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}"/README.md
}
