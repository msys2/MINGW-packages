# Maintainer: Philipp Smirnov <psmirnov@airmail.cc>

_realname=markdown
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.r39.522ac06
pkgrel=1
_branch=24.0

pkgdesc="A Markdown parser for Ada (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
msys2_references=(
  'aur: markdown'
)

url="https://github.com/AdaCore/markdown"
license=('GPL-3.0-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-gprbuild")
depends=("${MINGW_PACKAGE_PREFIX}-libvss")
source=("${_realname}::git+https://github.com/AdaCore/markdown.git#branch=${_branch}")
sha256sums=('SKIP')

pkgver() {
  cd ${_realname}
  printf "%s.r%s.%s" "$_branch" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
  cd "${srcdir}/${_realname}/"
  make BUILD_MODE=prod
}

package() {
  cd "${srcdir}/${_realname}/"
  make BUILD_MODE=prod DESTDIR="${pkgdir}" PREFIX="${MINGW_PREFIX}" install

  # Copy License Files
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" \
    "${srcdir}/${_realname}"/LICENSE*
}
