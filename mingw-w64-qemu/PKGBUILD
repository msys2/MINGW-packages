# Maintainer: Martell Malone <martellmalone@gmail.com>

_realname=qemu

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"

pkgver=2.1.2
pkgrel=1
pkgdesc="A generic and open source processor emulator (mingw-w64)"
arch=('any')
license=('GPL2' 'LGPL2')
url="http://qemu.org"
makedepends=('perl' 'python2' 'bison')
depends=("${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-libjpeg"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libssh2" 
         #"${MINGW_PACKAGE_PREFIX}-libusb"
         "${MINGW_PACKAGE_PREFIX}-lzo2"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-snappy"
         "${MINGW_PACKAGE_PREFIX}-SDL2")
source=(http://wiki.qemu.org/download/${_realname}-${pkgver}.tar.bz2
        001-time-functions.patch)
md5sums=('0ff197c4ed4b695620bc4734e77c888f'
         '455e088562ac76945feb9252baa1a9fd')
options=('!strip' 'debug')

_pkgfqn="${_realname}-${pkgver}"
noextract=(${_pkgfqn}.tar.bz2)
prepare() {
  cd $startdir/
  [ -d ${srcdir}/${_pkgfqn} ] && rm -rf ${srcdir}/${_pkgfqn}
  tar -xjf ${startdir}/${_pkgfqn}.tar.bz2 -C $srcdir || true
  
  cd ${srcdir}/${_pkgfqn}
  patch -p1 -i ${srcdir}/001-time-functions.patch
}

build() {

  export CC=${MINGW_PREFIX}/bin/gcc
  export CXX=${MINGW_PREFIX}/bin/g++

  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  declare -a extra_config
  if check_option "strip" "n"; then
    extra_config+=("--disable-strip")
  fi

  ${srcdir}/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --host-cc=$CC \
    --python=${MINGW_PREFIX}/bin/python2 \
    --datadir=${MINGW_PREFIX}/etc/qemu \
    --mandir=${MINGW_PREFIX}/share/qemu \
    --bindir=${MINGW_PREFIX}/bin \
    --libdir=${MINGW_PREFIX}/lib \
    --sysconfdir=${MINGW_PREFIX}/etc/qemu \
    --enable-libssh2 \
    --enable-lzo \
    --enable-snappy \
    --disable-docs \
    --disable-gtk \
    --disable-xen \
    --disable-smartcard-nss \
    --disable-vnc-sasl \
    --disable-kvm \
    --disable-libusb \
    --disable-bluez \
    --disable-spice \
    --target-list=x86_64-softmmu,arm-softmmu,i386-softmmu \
    "${extra_config[@]}"

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="${pkgdir}" install
}
