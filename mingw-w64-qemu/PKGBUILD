# Maintainer: Martell Malone <martellmalone@gmail.com>

_realname=qemu
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
_base_ver=4.0.0
_rc=
pkgver=${_base_ver}${_rc//-/.}
pkgrel=1
pkgdesc="A generic and open source processor emulator (mingw-w64)"
arch=('any')
license=('GPL2' 'LGPL2')
url="https://qemu.org/"
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-python2"
             'perl' 'bison' 'bsdtar')
depends=("${MINGW_PACKAGE_PREFIX}-capstone"
         "${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-cyrus-sasl"
         "${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-gnutls"
         "${MINGW_PACKAGE_PREFIX}-gtk3"
         "${MINGW_PACKAGE_PREFIX}-libjpeg"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libssh2"
         "${MINGW_PACKAGE_PREFIX}-libusb"
         "${MINGW_PACKAGE_PREFIX}-lzo2"
         "${MINGW_PACKAGE_PREFIX}-pixman"
         "${MINGW_PACKAGE_PREFIX}-snappy"
         "${MINGW_PACKAGE_PREFIX}-SDL2"
         "${MINGW_PACKAGE_PREFIX}-usbredir")
source=(https://download.qemu.org/${_realname}-${pkgver}.tar.xz
        001-iovec-sasl-conflict.patch)
sha256sums=('13a93dfe75b86734326f8d5b475fde82ec692d5b5a338b4262aeeb6b0fa4e469'
            '87525c95012de861fc7d5ff1554741ed23fae732bb50258a9d8083c43c5a2420')
noextract=(${_realname}-${pkgver}.tar.xz)

prepare() {
    [[ -d "${srcdir}"/${_realname}-${pkgver} ]] && rm -rf "${srcdir}"/${_realname}-${pkgver}
    tar -xJf ${srcdir}/${_realname}-${pkgver}.tar.xz -C ${srcdir} || true

    cd "${srcdir}/${_realname}-${pkgver}"
    patch -p1 -i ${srcdir}/001-iovec-sasl-conflict.patch
}

build() {
    cd "${srcdir}/${_realname}-${pkgver}"
    ./configure \
        --disable-stack-protector \
        --disable-werror \
        --disable-kvm \
        --disable-bluez \
        --disable-spice \
        --disable-libnfs \
        --enable-libssh2 \
        --enable-lzo \
        --enable-snappy \
        --enable-hax \
        --enable-gtk \
        --enable-sdl \
        --prefix=${MINGW_PREFIX} \
        --bindir=${MINGW_PREFIX}/bin \
        --datadir=${MINGW_PREFIX}/bin \
        --firmwarepath=${MINGW_PREFIX}/etc/qemu \
        --python=${MINGW_PREFIX}/bin/python2 \
        --mandir=${MINGW_PREFIX}/share/qemu \
        --docdir=${MINGW_PREFIX}/share/qemu
    make -j1
}

package() {
    cd "${srcdir}/${_realname}-${pkgver}"
    make DESTDIR="${pkgdir}/" install
}
