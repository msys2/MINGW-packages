_realname=qemu
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
_base_ver="6.1.0"
# QEMU Versioning of RC-SourcePackage and RC-Version differs
# e.g. qemu-6.1.0-rc0.tar.xz contains 6.0.90, qemu-6.1.0-rc1.tar.xz contains 6.0.91
# Unset _rc_no to create Release-Build
#_rc_no="4"
_rc_base_ver="6.0.9${_rc_no}"
_rc_file_ver="rc${_rc_no}"
_tarname=$( [ -z "${_rc_no}" ] && echo ${_realname}-${_base_ver} || echo ${_realname}-${_base_ver}-${_rc_file_ver} )
pkgver=$( [ -z "${_rc_no}" ] && echo ${_base_ver} || echo ${_rc_base_ver} )
pkgrel=1
pkgdesc="QEMU - a generic and open source machine emulator and virtualizer (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64')
license=('GPL2' 'LGPL2')
url="https://qemu.org/"
options=('!emptydirs')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-meson"
  "${MINGW_PACKAGE_PREFIX}-ninja"
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-python-sphinx"
  "${MINGW_PACKAGE_PREFIX}-python-sphinx_rtd_theme"
  'perl' 'bison' 'bsdtar')
depends=(
  "${MINGW_PACKAGE_PREFIX}-capstone"
  "${MINGW_PACKAGE_PREFIX}-curl"
  "${MINGW_PACKAGE_PREFIX}-cyrus-sasl"
  "${MINGW_PACKAGE_PREFIX}-glib2"
  "${MINGW_PACKAGE_PREFIX}-gnutls"
  "${MINGW_PACKAGE_PREFIX}-gtk3"
  "${MINGW_PACKAGE_PREFIX}-libjpeg"
  "${MINGW_PACKAGE_PREFIX}-libnfs"
  "${MINGW_PACKAGE_PREFIX}-libpng"
  "${MINGW_PACKAGE_PREFIX}-libslirp"
  "${MINGW_PACKAGE_PREFIX}-libssh"
  "${MINGW_PACKAGE_PREFIX}-libssp"
  "${MINGW_PACKAGE_PREFIX}-libtasn1"
  "${MINGW_PACKAGE_PREFIX}-libusb"
  "${MINGW_PACKAGE_PREFIX}-libxml2"
  "${MINGW_PACKAGE_PREFIX}-lzo2"
  "${MINGW_PACKAGE_PREFIX}-nettle"
  "${MINGW_PACKAGE_PREFIX}-pixman"
  "${MINGW_PACKAGE_PREFIX}-SDL2"
  "${MINGW_PACKAGE_PREFIX}-SDL2_image"
  "${MINGW_PACKAGE_PREFIX}-spice"
  "${MINGW_PACKAGE_PREFIX}-snappy"
  "${MINGW_PACKAGE_PREFIX}-usbredir"
  "${MINGW_PACKAGE_PREFIX}-zstd"
)
optdepends=(
  "${MINGW_PACKAGE_PREFIX}-gtk-vnc: Contains gvncviewer. For connections to Qemu VNC server"
  "${MINGW_PACKAGE_PREFIX}-spice-gtk: Contains spicy. For connections to Qemu SPICE server"
  "${MINGW_PACKAGE_PREFIX}-virt-viewer: Contains remote-viewer. For connections to Qemu SPICE or VNC server"
)
source=(
  "https://download.qemu.org/${_tarname}.tar.xz"{,.sig}
  msys2.readme.txt
  msys2.examples.tests.sh
)
sha256sums=(
  'eebc089db3414bbeedf1e464beda0a7515aad30f73261abc246c9b27503a3c96' 'b2ed5560a97b89347b1c4ece5462b9ba11763e5634555b63aaf8192ddc619102'
  'cefb3ff87299c104241561aac275bdff6031dbc75ea74d56e1018a9e3ca0e220'
  '2af9452c5cf16871d99154dece8117925a0c1dab365c1c8766bfcbdd3c5e5ccf'
)
validpgpkeys=('CEACC9E15534EBABB82D3FA03353C9CEF108B584') # Michael Roth <flukshun@gmail.com>
# tar cannot create links, to keep build running, manual extraction is required
noextract=(${_tarname}.tar.xz)

prepare() {
  [[ -d "${srcdir}"/${_tarname} ]] && rm -rf "${srcdir}"/${_tarname}
  # tar cannot create links here, therefore hide the failure
  tar -xf ${srcdir}/${_tarname}.tar.xz -C ${srcdir} || true

  cd "${srcdir}/${_tarname}"
}

build() {
  # Requirements as documented in https://wiki.qemu.org/Hosts/W32#Native_builds_with_MSYS2
  #   Execution of
  #     configure --cross-prefix=${CARCH}-w64-mingw32-
  #   and
  #   Providing cross-prefixed binaries by copying
  #     ${CARCH}-w64-mingw32-objcopy.exe using objcopy.exe
  #     ${CARCH}-w64-mingw32-windres.exe using windres.exe
  #     ${CARCH}-w64-mingw32-ranlib.exe  using ${CARCH}-w64-mingw32-gcc-ranlib.exe
  #     ${CARCH}-w64-mingw32-ar.exe      using ${CARCH}-w64-mingw32-gcc-ar.exe
  #     ${CARCH}-w64-mingw32-nm.exe      using nm.exe
  LDFLAGS+=" -fstack-protector"
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}

  # We need to provide all expected cross-prefixed tools without links or copying!
  # We will also provide undocumented ${CARCH}-w64-mingw32-strip.exe using strip.exe
  # Msys2 already provides some duplicates in ${MINGW_PREFIX}/bin:
  #   gcc-ar.exe is ${CARCH}-w64-mingw32-gcc-ar.exe
  #   gcc-ranlib.exe is ${CARCH}-w64-mingw32-gcc-ranlib.exe
  # These common, not prefixed exports read by configure fulfill all qemu requirements
  export AR=gcc-ar
  export NM=nm
  export OBJCOPY=objcopy
  export RANLIB=gcc-ranlib
  export STRIP=strip
  export WINDRES=windres

  # configure failes to create links, which can be ignored
  # qemu enables all features if possible, so keep it simple
  #  -cross-prefix is necessary for build to succeed
  # For faster testing:
  #TARGETLIST="--target-list=x86_64-softmmu"
  ../${_tarname}/configure $TARGETLIST \
    --cross-prefix=${CARCH}-w64-mingw32- \
    --prefix=${MINGW_PREFIX} \
    --bindir=bin \
    --datadir=share/qemu \
    --localedir=share/locale \
    --mandir=share/man \
    --docdir=share/doc/qemu

  # Finally build
  make
}

check() {
  cd "${srcdir}"/build-${CARCH}
  # Tests are not performed in CI
  # For local testing: perform, but do not fail
  make test || true
}

package() {
  cd "${srcdir}"/build-${CARCH}

  # Install to ${pkgdir}
  make DESTDIR="${pkgdir}" install

  # ${MINGW_PREFIX} was installed somewhere below ${pkgdir}, determine ...
  local P_INSTALL=$(find "${pkgdir}" -name "$(basename ${MINGW_PREFIX})" -type d)
  # Move directly to ${pkgdir}, if not already there
  [ -d "${pkgdir}"/${MINGW_PREFIX} ] || mv -v "$P_INSTALL" "${pkgdir}"/

  # To enable usage relative paths for next operations
  cd "${pkgdir}"/${MINGW_PREFIX}

  # Move content to standard dirs in share/
  mkdir -pv share/qemu/icons
  mv -v share/qemu/icons share/
  mkdir -pv share/qemu/applications
  mv -v share/qemu/applications share/

  # Fix firmware descriptors
  # see https://bugzilla.redhat.com/show_bug.cgi?id=1728652#c7
  mkdir -pv share/qemu/firmware
  find share/qemu/firmware -type f -exec \
    sed -i "s%\(\"filename\"\s*:\s*\"\).*edk2%\1${MINGW_PREFIX}/share/qemu/edk2%" {} \;

  # Add all licenses found in qemu sources to share/licences/qemu/
  mkdir -pv share/licenses/qemu
  tar -C "${srcdir}"/${_tarname} -c $(
    cd "${srcdir}"/${_tarname} &&
      find -iname "*COPYING*" -or -iname "*LICENSE*" |
      grep -v "\s" |
      egrep -v "meson.(test|msi|doc)" |
      egrep -v "(license.c|relicense.pl|license.doctree|LicenseCheck)"
    ) | tar -C share/licenses/qemu -xv

  # Add msys2 docs
  mkdir -pv share/doc/qemu
  cp -pv "${srcdir}"/msys2.readme.txt share/doc/qemu/
  cp -pv "${srcdir}"/msys2.examples.tests.sh share/doc/qemu/
}
