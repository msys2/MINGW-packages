# Maintainer: Donald Nguyen <donaldnguyen99@gmail.com>

_realname=plutovg
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.3.2
pkgrel=1
pkgdesc="A standalone 2D vector graphics library in C for (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/sammycage/plutovg"
msys2_references=(
  'aur: plutovg'
)
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cc")
options+=('!lto') # Otherwise build of 'plutosvg' throws linking  errors.
source=("${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('7bd4e79ce18b1d47517e7e91fbb7cf19d4f01942804a519bc7c0bf32b6325dd5')

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DBUILD_SHARED_LIBS=ON \
      -S "${_realname}-${pkgver}" \
      -B "build-${MSYSTEM}" \
      -Wno-dev

  cmake --build "build-${MSYSTEM}"
}

package() {
  DESTDIR="${pkgdir}" cmake --install "build-${MSYSTEM}"

  _docfiles=(
    "${srcdir}/${_realname}-${pkgver}/README.md"
    "${srcdir}/${_realname}-${pkgver}/smiley.png"
  )
  _docdirs=(
    "${srcdir}/build-${MSYSTEM}/examples"
  )
  _licensefiles=(
    "${srcdir}/${_realname}-${pkgver}/LICENSE"
  )
  for _docfile in "${_docfiles[@]}"; do
    install -D -v -m644 "${_docfile}" "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/$(basename "${_docfile}")"
  done
  for _docdir in "${_docdirs[@]}"; do
    cp -rv "${_docdir}" "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/$(basename "${_docdir}")"
  done
  for _licensefile in "${_licensefiles[@]}"; do
    install -D -v -m644 "${_licensefile}" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/$(basename "${_licensefile}")"
    ln -svr "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/$(basename "${_licensefile}")" "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/$(basename "${_licensefile}")"
  done
}
