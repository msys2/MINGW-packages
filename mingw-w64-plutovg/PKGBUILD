# Maintainer: Donald Nguyen <donaldnguyen99@gmail.com>

_realname=plutovg
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.3.2
pkgrel=1
pkgdesc="A standalone 2D vector graphics library in C for (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
_owner="https://github.com/sammycage"
url="${_owner}/${_realname}"
msys2_repository_url="${url}"
msys2_references=("aur : https://aur.archlinux.org/packages/plutovg"
                  "aur : https://aur.archlinux.org/packages/plutovg-git")
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cc")
provides=(
  "${_realname}=${pkgver}"
  "libplutovg.dll"
  "libplutovg.dll.a"
)
conflicts=(
  "${_realname}" "${MINGW_PACKAGE_PREFIX}-${_realname}"
)
options+=('!lto') # Otherwise build of 'plutosvg' throws linking  errors.
source=("${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('SKIP')


build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DBUILD_SHARED_LIBS=ON \
      -S "${_realname}-${pkgver}" \
      -B "build-${MSYSTEM}" \
      -Wno-dev

  cmake --build "build-${MSYSTEM}"
}


package() {
  DESTDIR="${pkgdir}" cmake --install "build-${MSYSTEM}"

  _docfiles=(
    "${srcdir}/${_realname}-${pkgver}/README.md"
    "${srcdir}/${_realname}-${pkgver}/smiley.png"
  )
  _docdirs=(
    "${srcdir}/build-${MSYSTEM}/examples"
  )
  _manfiles=()
  _infofiles=()
  _licensefiles=(
    "${srcdir}/${_realname}-${pkgver}/LICENSE"
  )
  printf '%s\n' " --> installing documentation ..."
  for _docfile in "${_docfiles[@]}"; do
    install -D -v -m644 "${_docfile}" "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/$(basename "${_docfile}")"
  done
  for _docdir in "${_docdirs[@]}"; do
    cp -rv "${_docdir}" "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/$(basename "${_docdir}")"
  done
  for _manfile in "${_manfiles[@]}"; do
    _section="$(basename "${_manfile}" .gz | sed -E -e 's|^.*\.([^.]*)$|\1|')"
    install -D -v -m644 "${srcdir}/docs/build/man/${_manfile}" "${pkgdir}${MINGW_PREFIX}/share/man/man${_section}/$(basename "${_manfile}")"
  done
  for _infofile in "${_infofiles[@]}"; do
    install -D -v -m644 "${_infofile}" "${pkgdir}${MINGW_PREFIX}/share/info/$(basename "${_infofile}")"
  done
  printf '%s\n' " --> installing license ..."
  for _licensefile in "${_licensefiles[@]}"; do
    install -D -v -m644 "${_licensefile}" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/$(basename "${_licensefile}")"
    ln -svr "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/$(basename "${_licensefile}")" "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/$(basename "${_licensefile}")"
  done
}