# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_dataname=colobot-data
_realname=colobot-gold
pkgbase=mingw-w64-${_realname}-data
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}-data")
pkgver=0.2.2_alpha
_pkgver=${pkgver/_/-}
pkgrel=1
pkgdesc='Data pack for Colobot GOLD real-time strategy game (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://colobot.info/'
msys2_repository_url='https://github.com/colobot/colobot-data'
license=('spdx:GPL3')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-python")
source=("${msys2_repository_url}/archive/${_realname}-${_pkgver}/${_dataname}-${_realname}-${_pkgver}.tar.gz")
sha256sums=('5c959a65ffc6047b656339bad425e2c050d3eb826e857dd80e2a03da0418c000')

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=('-DCMAKE_BUILD_TYPE=Release')
  else
    extra_config+=('-DCMAKE_BUILD_TYPE=Debug')
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -S "${_dataname}-${_realname}-${_pkgver}" \
      -B "build-${MSYSTEM}"

  cmake --build "build-${MSYSTEM}"
}

package() {
  DESTDIR="${pkgdir}" cmake --install "build-${MSYSTEM}"

  install -Dm644 "${srcdir}/${_dataname}-${_realname}-${_pkgver}/LICENSE.txt" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}-data/LICENSE.txt"
}
