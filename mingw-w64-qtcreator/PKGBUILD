# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=qt-creator
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
#_pre=rc1
#__pre=-rc1
_base_ver=3.1.0
pkgver=${_base_ver}${_pre}
pkgrel=1
pkgdesc='Cross-patform IDE'
url='http://qt-project.org'
arch=('any')
depends=("${_mingw_suff}-qt5" "${_mingw_suff}-gcc" "${_mingw_suff}-make")
makedepends=("${_mingw_suff}-clang")
options=('docs' 'staticlibs') # 'debug' '!strip'
optdepends=("${_mingw_suff}-gdb: for the debugger"
      "${_mingw_suff}-cmake: for cmake project support"
      "${_mingw_suff}-clang: for clang code model"
      #'openssh-askpass: for ssh support'
      #'git: for git support'
      #'mercurial: for mercurial support'
      #'bzr: for bazaar support'
      )
_pkgfqn="${_realname}-opensource-src-${_base_ver}${__pre}"
source=(#http://download.qt-project.org/development_releases/qtcreator/${_base_ver%.*}/${_base_ver}-${_pre}/${_pkgfqn}.tar.gz
        http://download.qt-project.org/official_releases/qtcreator/${pkgver%.*}/${pkgver}/${_pkgfqn}.tar.gz
        qt-creator-3.0.0-path-quote.patch
        qt-creator-3.1.0-Allow-static-clang-via-LLVM_STATIC-env-var.patch
        qt-creator-3.1.0-Allow-iOS-plugin-on-any-platform.patch)
noextract=(${_pkgfqn}.tar.gz)
sha256sums=('c8c648f4988b707393e0f1958a8868718f27e59263f05f3b6599fa62290c2bbf'
            '26f9f487cd609091900a6d900ab5067023b4e4349e06f4477c80debbbd32ec6f'
            '606e66eae324312f168c543fd0a3bbfe78b35f4b1f592ad7a11c5b1b7a6c83ae'
            'a86fb90fa133be45a4c73e6cdf3f4c2fe726d46c6d00d258e67860e714bdc5ad')
prepare() {
  cd $startdir/
  [ -d ${srcdir}/${_pkgfqn} ] && rm -rf ${srcdir}/${_pkgfqn}
  tar -xzf ${_pkgfqn}.tar.gz -C $srcdir || true

  cd ${srcdir}/${_pkgfqn}
  patch -p0 -i ${srcdir}/qt-creator-3.0.0-path-quote.patch
  patch -p1 -i ${srcdir}/qt-creator-3.1.0-Allow-static-clang-via-LLVM_STATIC-env-var.patch
  patch -p1 -i ${srcdir}/qt-creator-3.1.0-Allow-iOS-plugin-on-any-platform.patch
}

build() {
  [ -d ${srcdir}/build-${MINGW_CHOST} ] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
  export LLVM_INSTALL_DIR=${MINGW_PREFIX}
  export LLVM_STATIC=1
  if check_option "debug" "y"; then
    _config_variant=debug
  else
    _config_variant=release
  fi
  ${MINGW_PREFIX}/bin/qmake.exe ../${_pkgfqn}/qtcreator.pro CONFIG+=${_config_variant}
  make
  make docs
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make INSTALL_ROOT="${pkgdir}${MINGW_PREFIX}" install
  make INSTALL_ROOT="${pkgdir}${MINGW_PREFIX}" install_docs
  
  install -Dm644 ${srcdir}/${_pkgfqn}/LGPL_EXCEPTION.TXT ${pkgdir}${MINGW_PREFIX}/share/licenses/qtcreator/LGPL_EXCEPTION.TXT
}

