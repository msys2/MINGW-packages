# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>

_realname=oauthlib
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=3.3.1
pkgrel=3
pkgdesc='A generic, spec-compliant, thorough implementation of the OAuth request-signing logic (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/idan/oauthlib"
msys2_repository_url='https://github.com/idan/oauthlib'
msys2_references=(
  'purl: pkg:pypi/oauthlib'
  "cpe: cpe:2.3:a:oauthlib_project:oauthlib"
)
license=('spdx:BSD-3-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools")
optdepends=("${MINGW_PACKAGE_PREFIX}-python-cryptography: rsa and signedtoken"
            "${MINGW_PACKAGE_PREFIX}-python-pyjwt: signedtoken"
            "${MINGW_PACKAGE_PREFIX}-python-blinker: signals")
options=('!emptydirs')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('0f0f8aa759826a193cf66c12ea1af1637f87b9b4622d46e866952bb022e538c9')

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}"/python-build-${MSYSTEM}

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -D -m644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
