# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ingo Maindorfer <ingo@liquidcooling.de>
# Based on work by AlexWMF <alexxwmf@gmail.com>

_realname=qwt
pkgbase=mingw-w64-${_realname}-qt5
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-qt5"
pkgver=6.1.2
pkgrel=2
pkgdesc="Qt Widgets for Technical Applications (mingw-w64)"
arch=('any')
license=("custom:$pkgname")
url="https://qwt.sourceforge.io/"
depends=("${MINGW_PACKAGE_PREFIX}-qt5")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=(!strip staticlibs !buildflags)
source=("https://downloads.sourceforge.net/${_realname}/${_realname}-${pkgver}.tar.bz2"
        "qwtconfig-msys2.pri")
sha256sums=('2b08f18d1d3970e7c3c6096d850f17aea6b54459389731d3ce715d193e243d0c'
            'a2ed9222424c41930dc8c4c1329bea46d2b23e0f05f894a891e07fd753f99f3b')

prepare() {
  local PREFIX_WIN=$(cygpath -am ${pkgdir}${MINGW_PREFIX})

  cd ${srcdir}/${_realname}-${pkgver}
  # copy our config file to the right place
  cp ${srcdir}/qwtconfig-msys2.pri qwtconfig.pri
  sed -s "s|%VALUE_QWT_INSTALL_PREFIX%|${PREFIX_WIN}|g" \
    -i qwtconfig.pri
}

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  ${MINGW_PREFIX}/bin/qmake qwt.pro

  make ${MAKEFLAGS} # VERBOSE=1
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  make -j1 install

  mkdir -p ${pkgdir}${MINGW_PREFIX}/bin/
  mv "${pkgdir}${MINGW_PREFIX}"/lib/*.dll "${pkgdir}${MINGW_PREFIX}"/bin/

  local PREFIX_WIN=$(cygpath -am ${pkgdir}${MINGW_PREFIX})

  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i ${pkgdir}${MINGW_PREFIX}/share/qt5/mkspecs/features/qwtconfig.pri
}
