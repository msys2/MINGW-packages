# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: AlexWMF <alexxwmf@gmail.com>

_realname=qjson
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-qt4"
pkgver=0.8.1
pkgrel=1
pkgdesc="QJson is a qt-based library that maps JSON data to QVariant objects (mingw-w64)"
arch=('any')
license=('GPL')
url="http://qjson.sourceforge.net"
depends=("${MINGW_PACKAGE_PREFIX}-qt4")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake")
install=${_realname}-qt4-${CARCH}.install
options=(!strip staticlibs !buildflags)

source=(${_realname}-${pkgver}.tar.gz::"https://github.com/flavio/${_realname}/archive/${pkgver}.tar.gz")
md5sums=('4eef13da988edf8f91c260a3e1baeea9')

build() {
  mkdir $srcdir/build-${MINGW_CHOST}
  cd $srcdir/build-${MINGW_CHOST}

  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX:PATH=${pkgdir}${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    ${srcdir}/${_realname}-${pkgver}

  make # VERBOSE=1
}

package() {
  cd $srcdir/build-${MINGW_CHOST}
  make install

  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null

  for f in ${pkgdir}${MINGW_PREFIX}/lib/cmake/${_realname}/*.cmake; do
    sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" -i ${f}
  done
}
