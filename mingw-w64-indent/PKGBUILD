# $Id$
# Maintainer (MSYS2): Ray Donnelly <mingw.android@gmail.com>

_realname=indent
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.2.11
pkgrel=4
pkgdesc="C language source code formatting program (mingw-w64)"
arch=('any')
url="https://www.gnu.org/software/indent/"
license=('GPL3')
makedepends=('texinfo-tex')
source=(http://ftp.de.debian.org/debian/pool/main/i/indent/${_realname}_${pkgver}.orig.tar.gz
        indent-2.2.11-segfault.patch
        indent-2.2.11-LC_ALL.patch
        indent-2.2.11-decimal_format.patch
        indent-2.2.11-win32-wildexp-automake-support.patch
        indent-2.2.11-disable-documentation.patch)
sha1sums=('9b242528bbc4914464117c69ffae9df24d3fa4a7'
          '31a6dcd5edcac4decac81dbb52bc9415b8b148e4'
          '91b210b666de59ca44524288a4c1ebd213570daa'
          '2b7d5a725fe307ce81565f920a0a66b319b50c91'
          '2593b852bcacd5897d7aab5470d36c761c73838e'
          '37962fa20909f698eeafec7609020b30b2c26ea6')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  rm -rf regression/input/float-constant-suffix.c \
         regression/standard/float-constant-suffix.c > /dev/null 2>&1 || true

  patch -Np1 -i ../indent-2.2.11-segfault.patch

  # http://bugs.debian.org/cgi-bin/bugreport.cgi?bug=205692
  patch -Np1 -i ../indent-2.2.11-LC_ALL.patch

  # https://lists.gnu.org/archive/html/bug-indent/2011-08/msg00000.html
  patch -Np1 -i ../indent-2.2.11-decimal_format.patch

  patch -Np1 -i ../indent-2.2.11-win32-wildexp-automake-support.patch

  patch -Np1 -i ../indent-2.2.11-disable-documentation.patch

  # sed -i 's/-number/-number-sections/' doc/Makefile.in

  autoreconf -vfi
}

build() {
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --{host,build}=${MINGW_CHOST}

  make -j1
}

check() {
  cd "${srcdir}"/${_realname}-${pkgver}/regression
  make test
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make DESTDIR="${pkgdir}" install
  rm "${pkgdir}"${MINGW_PREFIX}/bin/texinfo2man.exe
}
