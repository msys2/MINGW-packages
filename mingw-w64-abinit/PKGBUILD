# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=abinit
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=10.4.7
pkgrel=3
pkgdesc="Atomic-scale simulation software. (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64')
url='https://www.abinit.org'
msys2_repository_url="https://github.com/abinit/abinit"
msys2_references=(
  'aur: abinit'
)
license=('spdx:GPL-3.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-fftw"
         "${MINGW_PACKAGE_PREFIX}-cc-libs"
         $([[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]] || echo "${MINGW_PACKAGE_PREFIX}-gcc-libgfortran")
         "${MINGW_PACKAGE_PREFIX}-libpsml"
         "${MINGW_PACKAGE_PREFIX}-libxc"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-netcdf-fortran"
         "${MINGW_PACKAGE_PREFIX}-openblas"
         "${MINGW_PACKAGE_PREFIX}-omp")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-fc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             $([[ ${CARCH} == aarch64 ]] || echo "${MINGW_PACKAGE_PREFIX}-msmpi")
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-wannier90")
source=("https://forge.abinit.org/${_realname}-${pkgver}.tar.gz"
        "001-fix-build-on-mingw.patch"
        "002-fix-pkgconfig-file.patch"
        "003-fix-version.patch"
        "004-include-missing-cmake-module.patch")
sha256sums=('161c60ef90686c19e0718363eb5a17bfa29494e5fbdc59be45a7342e20349768'
            '992bc51d0965fd96131cfd5f2630f88fdb8874bf05625543802434da570ec02e'
            '466f7f86384f02c99e9f7a948f2b38229a10962ff849cd1e8bf06b0c6ddd7481'
            'cfe9c539d96d62fe2b296513da431602aaf98d697cd61d1e9cbca2c329383e87'
            'bd9f482732e3ba7c8c5e11a2bc545808cd5e0891fb9a03f235dae9a4c5822976')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  apply_patch_with_msg \
    001-fix-build-on-mingw.patch \
    002-fix-pkgconfig-file.patch \
    003-fix-version.patch \
    004-include-missing-cmake-module.patch

  # Remove native flags
  sed -i "s|;-march=native||g" cmake/compiler_setup.cmake
  sed -i "s|;-mcpu=native||g" cmake/compiler_setup.cmake
  sed -i "s|\:-mtune=native|:|g" cmake/compiler_setup.cmake
}

build() {
  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  if [[ ${CARCH} == x86_64 ]]; then
    FFLAGS="-march=x86-64-v3"
    CFLAGS+=" -march=x86-64-v3"
  fi

  if [[ ${CC} == clang ]]; then
    LDFLAGS+=" -lomp"
  fi

  FFLAGS+=" -O2 $(pkgconf --cflags xmlf90)" \
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=;-DPYMOD_INSTALL_LIBDIR=" \
    "${MINGW_PREFIX}"/bin/cmake.exe -Wno-dev \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DCMAKE_C_STANDARD=17 \
      "${_extra_config[@]}" \
      -DBLA_VENDOR=OpenBLAS \
      -DPython3_EXECUTABLE=${MINGW_PREFIX}/bin/python \
      -S "${_realname}-${pkgver}" \
      -B "build-${MSYSTEM}"

  "${MINGW_PREFIX}"/bin/cmake.exe --build "build-${MSYSTEM}"
}

package() {
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install "build-${MSYSTEM}"

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/COPYING \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
