# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=abinit
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=10.0.9
pkgrel=2
pkgdesc="Atomic-scale simulation software. (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64')
url='https://www.abinit.org'
msys2_repository_url="https://github.com/abinit/abinit"
msys2_references=(
  'aur: abinit'
)
license=('spdx:GPL-3.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-fftw"
         "${MINGW_PACKAGE_PREFIX}-cc-libs"
         $([[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]] || echo "${MINGW_PACKAGE_PREFIX}-gcc-libgfortran")
         "${MINGW_PACKAGE_PREFIX}-libpsml"
         "${MINGW_PACKAGE_PREFIX}-libxc"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-netcdf-fortran"
         "${MINGW_PACKAGE_PREFIX}-openblas"
         "${MINGW_PACKAGE_PREFIX}-omp")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-fc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-msmpi"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-wannier90")
source=("https://forge.abinit.org/${_realname}-${pkgver}.tar.gz"
        "001-fix-build-on-mingw.patch"
        "002-fix-pkgconfig-file.patch"
        "003-fix-version.patch")
sha256sums=('17650580295e07895f6c3c4b1f3f0fe0e0f3fea9bab5fd8ce7035b16a62f8e5e'
            '41ee6253fffc56503ebf2a11ddc5016f67fb014717588b28eedd9381af96399d'
            '466f7f86384f02c99e9f7a948f2b38229a10962ff849cd1e8bf06b0c6ddd7481'
            '72f10de92d58e54ffc814ff03956ca1db89e92c893b9896b774a29f0abff516f')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  patch -Np1 -i "${srcdir}"/001-fix-build-on-mingw.patch
  patch -Np1 -i "${srcdir}"/002-fix-pkgconfig-file.patch
  patch -Np1 -i "${srcdir}"/003-fix-version.patch
}

build() {
  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  FFLAGS="-O2" \
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=;-DPYMOD_INSTALL_LIBDIR=" \
    "${MINGW_PREFIX}"/bin/cmake.exe -Wno-dev \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DCMAKE_C_STANDARD=17 \
      "${_extra_config[@]}" \
      -DBLA_VENDOR=OpenBLAS \
      -DWANNIER_ROOT=${MINGW_PREFIX} \
      -S "${_realname}-${pkgver}" \
      -B "build-${MSYSTEM}"

  "${MINGW_PREFIX}"/bin/cmake.exe --build "build-${MSYSTEM}"
}

package() {
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install "build-${MSYSTEM}"

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/COPYING \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
