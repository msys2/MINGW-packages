# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Martell Malone <martellmalone@gmail.com>

_realname=libuv
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.10.0
pkgrel=1
pkgdesc="Cross-platform asychronous I/O (mingw-w64)"
arch=('any')
url="https://github.com/libuv/libuv"
license=("custom")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
options=('staticlibs' 'strip')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/libuv/libuv/archive/v${pkgver}.tar.gz"
         d9d207774ea1f734310982300e55dd64b3f5682b.patch
         060877d483cfcbc0ede85059617be5ad7dae127a.patch)
sha256sums=('50f4ed57d65af4ab634e2cbdd90c49213020e15b4d77d3631feb633cbba9239f'
            'b953848b470bf4939c89767ed924e6eaee0a7be685ab267e05f5ac9cc2c76a8e'
            '28d76b8e2fa624fdc055cec355ba3271dc33e7cf0045aa2c9021737db652073b')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/060877d483cfcbc0ede85059617be5ad7dae127a.patch
  patch -p1 -i ${srcdir}/d9d207774ea1f734310982300e55dd64b3f5682b.patch
  ./autogen.sh
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST}

  make
}

check(){
  cd "${srcdir}/build-${MINGW_CHOST}"
# One these tests fails
  make check || true
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}"/${_realname}-${pkgver}/LICENSE "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSE
}
