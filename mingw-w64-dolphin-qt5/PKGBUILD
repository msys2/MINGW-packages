# Maintainer: Mateusz Miku≈Ça <mati865@gmail.com>

_variant=-${KF5_VARIANT:-shared}
source "$(dirname ${BASH_SOURCE[0]})"/../mingw-w64-PKGBUILD-common/kde-frameworks5
_kde_f5_init_package "${_variant}" "dolphin"
pkgver=16.04.3
pkgrel=1
pkgdesc="Dolphin is a file manager focusing on usability. (mingw-w64)"
arch=('any')
license=('GPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-extra-cmake-modules" "${MINGW_PACKAGE_PREFIX}-python3")
source=("http://download.kde.org/stable/applications/${pkgver}/src/${_basename}-${pkgver}.tar.xz"
        "fix-win-detection.patch")
sha256sums=('201f42d2f709c359a2f7b0974b1e35b6f22034a165540fd2af12b146b1a58599'
            '60b83416debeb8bc45cfd351f801097a7e6e32058234c8cb6f6a591b19ad12bc')

prepare() {
  mkdir -p build-${CARCH}${_variant}
  
  cd ${srcdir}/${_basename}-${pkgver}
  patch -p1 -i ${srcdir}/fix-win-detection.patch
}

build() {
  local -a extra_config
  cd build-${CARCH}${_variant}
  if [ "${_variant}" = "-static" ]; then
    extra_config+=( -DBUILD_SHARED_LIBS=NO )
    QT5_PREFIX=${MINGW_PREFIX}/qt5-static
    export PATH=${QT5_PREFIX}/bin:"$PATH"
  else
    QT5_PREFIX=${MINGW_PREFIX}
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=;-DECM_MKSPECS_INSTALL_DIR=" \
    cmake ../${_basename}-${pkgver} \
      -DCMAKE_BUILD_TYPE=$(_kde_f5_CMAKE_BUILD_TYPE) \
      -DCMAKE_INSTALL_PREFIX=${QT5_PREFIX} \
      -DKDE_INSTALL_LIBDIR=lib \
      -DECM_MKSPECS_INSTALL_DIR=${QT5_PREFIX}/share/qt5/mkspecs \
      -DBUILD_TESTING=OFF \
      "${extra_config[@]}" \
      -G'MSYS Makefiles'
  make
}

package() {
  cd build-${CARCH}${_variant}
  make DESTDIR="${pkgdir}" install
}