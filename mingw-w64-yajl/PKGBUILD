# Contributor: Tobias Smolka <tsmolka@gmail.com>

_realname=yajl
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.1.0
pkgrel=2
pkgdesc='Yet Another JSON Library (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://lloyd.github.io/yajl/'
msys2_repository_url='https://github.com/lloyd/yajl'
msys2_references=(
  "cpe: cpe:2.3:a:yajl_project:yajl"
)
license=('spdx:ISC')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake")
source=("https://github.com/lloyd/$_realname/archive/${pkgver}/${_realname}-${pkgver}.tar.gz"
         '01-fix-compiler.patch'
         '02-fix-tests.patch')
sha256sums=('3fb73364a5a30efe615046d07e6db9d09fd2b41c763c5f7d3bfb121cd5c5ac5a'
            'fa629191df14f32d54cc03999e9913ed2afa83c032249f9399ee93d87380b49a'
            'fdd8995e383ae1cae50b66b464427e46045dc8889e55ec2f60fa0b91c9017a10')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -p1 -i "${srcdir}/01-fix-compiler.patch"
  patch -p1 -i "${srcdir}/02-fix-tests.patch"
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    ../${_realname}-${pkgver}

  cmake --build .
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  make test
  PATH="$PATH:${srcdir}/build-${MSYSTEM}/${_realname}-${pkgver}/lib" make test-api
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" cmake --install .
  mv "${pkgdir}"${MINGW_PREFIX}/lib/*.dll "${pkgdir}"${MINGW_PREFIX}/bin/

  install -Dm644 "../${_realname}-${pkgver}/COPYING" \
    "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
