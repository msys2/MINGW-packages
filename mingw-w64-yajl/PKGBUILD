_realname=yajl
if [ ! -z "${MINGW_PACKAGE_PREFIX}" ]; then
    pkgprefix="${MINGW_PACKAGE_PREFIX}-"
    pkgdirprefix="${MINGW_PREFIX}/"
    cmakegen="MSYS Makefiles"
else
    pkgprefix=""
    pkgdirprefix=""
    cmakegen="Unix Makefiles"
fi
pkgname="${pkgprefix}${_realname}"
pkgver=2.1.0
pkgrel=1
pkgdesc='Yet Another JSON Library.'
arch=('any')
url='http://lloyd.github.com/yajl/'
license=('ISC')
source=(
    "$_realname-$pkgver.tar.gz::https://github.com/lloyd/$_realname/archive/$pkgver.tar.gz"
    '01-fix-compiler.patch'
)
md5sums=(
    '6887e0ed7479d2549761a4d284d3ecb0'
    '250b39d3592eb7042a48c421d1d0eefb'
)
makedepends=(
  "${pkgprefix}cmake"
)
build() {
  cd "$_realname-$pkgver"
  
  patch -p1 -i ../01-fix-compiler.patch
  cmake -G "${cmakegen}" -DCMAKE_INSTALL_PREFIX= .
  make
}
package() {
  cd "$_realname-$pkgver"
  
  make DESTDIR="${pkgdir}${pkgdirprefix}" install
  install -Dm644 "COPYING" \
    "${pkgdir}${pkgdirprefix}/share/licenses/${_realname}/LICENSE"
}
