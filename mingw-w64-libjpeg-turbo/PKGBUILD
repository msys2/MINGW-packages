# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libjpeg-turbo
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=1.3.1
pkgrel=2
pkgdesc="JPEG image codec with accelerated baseline compression and decompression (mingw-w64)"
arch=('any')
url="http://libjpeg-turbo.virtualgl.org"
license=("GPL, custom")
depends=("${_mingw_suff}-gcc-libs")
groups=("${_mingw_suff}")
makedepends=("nasm" "${_mingw_suff}-gcc")
provides=("${_mingw_suff}-libjpeg")
conflicts=("${_mingw_suff}-libjpeg")
replaces=("${_mingw_suff}-libjpeg")
options=('staticlibs' 'strip')
source=("http://downloads.sourceforge.net/libjpeg-turbo/libjpeg-turbo-$pkgver.tar.gz"
    "libjpeg-turbo-match-autoconf-behavior.patch"
    "jpeg-7-tiff-ojpeg.patch"
    "jpeg-typedefs.patch"
    "libjpeg-buf-oflo.patch")
md5sums=('2c3a68129dac443a72815ff5bb374b05'
         '6b00863ecdd729cb757e7f3672324f6f'
         '4f927780387c77244c2f89b2c6a66eb2'
         'b758ab3ac6a0604c6ed5fb9d850c9a73'
         '8835de1a830ae9979460f14f79e90630')

prepare() {
  cd "$srcdir/libjpeg-turbo-$pkgver"
  patch -p1 -i ${srcdir}/libjpeg-turbo-match-autoconf-behavior.patch
  patch -p1 -i ${srcdir}/jpeg-7-tiff-ojpeg.patch
  patch -p1 -i ${srcdir}/jpeg-typedefs.patch
  patch -p1 -i ${srcdir}/libjpeg-buf-oflo.patch
}

build() {
  export CFLAGS+=" -O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"
  export CXXFLAGS+=" -O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"
  mkdir -p "${srcdir}/build-${CARCH}"
  cd "${srcdir}/build-${CARCH}"
  "${srcdir}"/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --with-jpeg8 \
    --mandir=${MINGW_PREFIX}/share/man
  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="$pkgdir" install
  find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
  #rm -rf "${pkgdir}${MINGW_PREFIX}/"{doc,share}
  cp "${srcdir}"/${_realname}-${pkgver}/{jinclude,transupp}.h ${pkgdir}${MINGW_PREFIX}/include/
}
