# Maintainer: @naveen521kk on Github, Naveen M K <naveen@syrusdark.website>

_realname=clisp
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.49
pkgrel=1
pkgdesc="ANSI Common Lisp interpreter, compiler and debugger (mingw-w64)"
arch=('any')
url="https://clisp.sourceforge.io/"
license=('GPL')
depends=("${MINGW_PACKAGE_PREFIX}-readline"
         "${MINGW_PACKAGE_PREFIX}-libsigsegv"
         "${MINGW_PACKAGE_PREFIX}-ffcall")
source=(https://ftp.gnu.org/pub/gnu/clisp/release/$pkgver/clisp-$pkgver.tar.gz)
sha256sums=('bd2f6252c681337c0b6aa949fae3f92d3202dee3998c98a88ec28c72b115e866')
options=('!makeflags' '!emptydirs')
build() {
  cd ${_realname}-${pkgver}
  ./configure --prefix=${MINGW_PREFIX} --with-readline --with-ffcall src
  cd src
  ./makemake --prefix=${MINGW_PREFIX} --win32gcc --with-readline --with-ffcall --with-dynamic-ffi > Makefile
  make
}
check() {
  cd ${_realname}-${pkgver}
  make check || :
}

package() {
  cd ${_realname}-${pkgver}/src
  make DESTDIR="${pkgdir}" install
}
