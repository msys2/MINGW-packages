# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname='firebird'
pkgbase=mingw-w64-${_realname}2-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}2-git"
pkgver=2.5.6.26956.d6e92a44ca
pkgrel=1
pkgdesc="Cross-platform relational database offering many ANSI SQL standard features - version 2.x (mingw-w64)"
url="http://www.firebirdsql.org/"
license=('custom')
arch=('any')
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}" "${MINGW_PACKAGE_PREFIX}-${_realname}2" "${MINGW_PACKAGE_PREFIX}-libfbclient")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}2" "${MINGW_PACKAGE_PREFIX}-libfbclient")
replaces=("${MINGW_PACKAGE_PREFIX}-libfbclient")
depends=(
         "${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-icu"
         "${MINGW_PACKAGE_PREFIX}-zlib"
        )
makedepends=(#"${MINGW_PACKAGE_PREFIX}-btyacc" # Maybe add check in firebird presence of system btyacc
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-libatomic_ops"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "git"
            )
options=('!debug' 'strip')
source=("${_realname}"::"git+https://github.com/Alexpux/firebird-git-svn.git#branch=B2_5"
        mingw-w64-2_5.patch)
sha256sums=('SKIP'
            'dc930c9a5eb1dbf0dc797a0812a5292e081d7a6f7163c6fe88143ad5eba09958')


pkgver() {
  cd "${_realname}"
  source ${srcdir}/${_realname}/src/misc/writeBuildNum.sh
  printf '%s.%s.%s.%s.%s' ${MajorVer} ${MinorVer} ${RevNo} ${BuildNum} "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${_realname}
  git am ${srcdir}/mingw-w64-2_5.patch
  
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  
  cp -rf ${_realname} build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}
  local _debug_conf=
  if check_option "debug" "y"; then
    _debug_conf="--enable-debug"
  fi
  
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared \
    ${_debug_conf} \
    --disable-rpath \
    --with-system-icu \
    --with-fbbin=${MINGW_PREFIX}/bin \
    --with-fbsbin=${MINGW_PREFIX}/bin \
    --with-fbconf=${MINGW_PREFIX}/etc/${_realname} \
    --with-fblib=${MINGW_PREFIX}/lib \
    --with-fbinclude=${MINGW_PREFIX}/include \
    --with-fbdoc=${MINGW_PREFIX}/share/${_realname}/doc \
    --with-fbudf=${MINGW_PREFIX}/share/${_realname}/udf \
    --with-fbsample=${MINGW_PREFIX}/share/${_realname}/samples \
    --with-fbsample-db=${MINGW_PREFIX}/share/${_realname}/samples/empbuild \
    --with-fbhelp=${MINGW_PREFIX}/share/${_realname}/help \
    --with-fbintl=${MINGW_PREFIX}/share/${_realname}/intl \
    --with-fbmisc=${MINGW_PREFIX}/share/${_realname}/misc \
    --with-fbsecure-db=${MINGW_PREFIX}/etc/${_realname} \
    --with-fbmsg=${MINGW_PREFIX}/etc/${_realname} \
    --with-fblog=${MINGW_PREFIX}/var \
    --with-fbglock=${MINGW_PREFIX}/var \
    --with-fbplugins=${MINGW_PREFIX}/share/${_realname}

  CC=${MINGW_PREFIX}/bin/gcc \
  make -j1
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}

  mkdir -p ${pkgdir}${MINGW_PREFIX}/{bin,etc/${_realname},include,lib,share/${_realname}/{doc,help,intl,misc,plugins,samples,udf},var}
  cp -f gen/firebird/bin/*       ${pkgdir}${MINGW_PREFIX}/bin/
  cp -f gen/firebird/lib/*       ${pkgdir}${MINGW_PREFIX}/lib/
  cp -f gen/firebird/include/*   ${pkgdir}${MINGW_PREFIX}/include/
  cp -f gen/firebird/UDF/*       ${pkgdir}${MINGW_PREFIX}/share/${_realname}/udf/
  cp -rf gen/firebird/examples/* ${pkgdir}${MINGW_PREFIX}/share/${_realname}/samples/
  cp -f gen/firebird/help/*      ${pkgdir}${MINGW_PREFIX}/share/${_realname}/help/
  cp -f gen/firebird/intl/*      ${pkgdir}${MINGW_PREFIX}/share/${_realname}/intl/
  cp -f gen/firebird/plugins/*   ${pkgdir}${MINGW_PREFIX}/share/${_realname}/plugins/
  #cp -f gen/firebird/misc/*      ${pkgdir}${MINGW_PREFIX}/share/${_realname}/misc/
  
  cp -f gen/firebird/*.bat       ${pkgdir}${MINGW_PREFIX}/bin/
  cp -f gen/firebird/*.conf      ${pkgdir}${MINGW_PREFIX}/etc/${_realname}/
  cp -f gen/firebird/*.msg       ${pkgdir}${MINGW_PREFIX}/etc/${_realname}/
  cp -f gen/firebird/*.fdb       ${pkgdir}${MINGW_PREFIX}/etc/${_realname}/
  
  cp -rf doc/* ${pkgdir}${MINGW_PREFIX}/share/${_realname}/doc/
  
  touch ${pkgdir}${MINGW_PREFIX}/var/${_realname}.log
  
  # Remove build tools
  rm -f ${pkgdir}${MINGW_PREFIX}/bin/{brltable,codes,create_db,*build*,*boot*,*static*,*current*}.exe
}
