# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=yt-dlp-ejs
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.3.2
pkgrel=3
pkgdesc="External JavaScript for yt-dlp supporting many runtimes (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'gentoo: dev-python/yt-dlp-ejs'
  'purl: pkg:pypi/yt-dlp-ejs'
)
url='https://github.com/yt-dlp/ejs'
license=('spdx:Unlicense AND MIT AND ISC')
depends=(
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-nodejs"
)
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-hatchling"
             "${MINGW_PACKAGE_PREFIX}-python-hatch-vcs"
             "${MINGW_PACKAGE_PREFIX}-python-installer")
options=('!strip')
source=("https://github.com/yt-dlp/ejs/releases/download/${pkgver}/yt_dlp_ejs-${pkgver}.tar.gz")

sha256sums=('31a41292799992bdc913e03c9fac2a8c90c82a5cbbc792b2e3373b01da841e3e')

build() {
  cd "yt_dlp_ejs-${pkgver}"

  # rollup doesn't support aarch64-gnu, so just use the slower wasm version
  _pnpm=./pnpm.py
  npm_config_yes=true "${_pnpm}" --version # let pnpm install itself
  _rollup_ver=$("${_pnpm}" pkg get devDependencies.rollup | xargs)
  "${_pnpm}" pkg set "pnpm.overrides.rollup=npm:@rollup/wasm-node@${_rollup_ver}"
  "${_pnpm}" install --no-frozen-lockfile

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "yt_dlp_ejs-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
