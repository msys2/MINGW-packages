# Maintainer: Donald Nguyen <donaldnguyen99@gmail.com>

_realname=plutosvg
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.0.7
pkgrel=1
pkgdesc="Tiny SVG rendering library in C. (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/sammycage/plutosvg"
msys2_repository_url="https://github.com/sammycage/plutosvg"
msys2_references=(
  "aur : plutosvg"
)
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-plutovg"
         "${MINGW_PACKAGE_PREFIX}-freetype")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cc")
source=("${_realname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('78561b571ac224030cdc450ca2986b4de915c2ba7616004a6d71a379bffd15f3')

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DBUILD_SHARED_LIBS=ON \
      -DCMAKE_DLL_NAME_WITH_SOVERSION=ON \
      -S "${_realname}-${pkgver}" \
      -B "build-${MSYSTEM}" \
      -DCMAKE_SKIP_RPATH=ON \
      -DPLUTOSVG_BUILD_EXAMPLES=OFF \
      -DPLUTOSVG_ENABLE_FREETYPE=ON

  cmake --build "build-${MSYSTEM}"
}

package() {
  DESTDIR="${pkgdir}" cmake --install "build-${MSYSTEM}"

  install -D -v -m644 "${_realname}-${pkgver}/README.md" "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/README.md"
  install -d -v "${_realname}-${pkgver}/examples" "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/examples"
  install -D -v -m644 "${_realname}-${pkgver}/examples/camera.svg" "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/examples/camera.svg"
  install -D -v -m644 "${_realname}-${pkgver}/examples/camera2png.c" "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/examples/camera2png.c"
  install -D -v -m644 "${_realname}-${pkgver}/examples/emoji2png.c" "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/examples/emoji2png.c"
  install -D -v -m644 "${_realname}-${pkgver}/examples/svg2png.c" "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/examples/svg2png.c"
  install -D -v -m644 "${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}