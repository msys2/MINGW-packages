# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=avisynthplus
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.6.1
pkgrel=1
pkgdesc='An improved version of the AviSynth frameserver (mingw-w64)'
arch=('any')
url='https://avs-plus.net/'
license=('GPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-gcc")
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/AviSynth/AviSynthPlus/archive/v${pkgver}.tar.gz"
        "mingw.patch"
        "staticlib.patch")
sha256sums=('a157648c0535013ec4895b8f03640d74ada43b93ac3d07b91087177f93278a30'
            'a354b983354c02e42bc7b95fe17dbad99104c74a3ee7e31255069f3919abbb62'
            'f2e83d6da6e6114455688d33287588889890c08a00fc85559c90c52213ba0535')

prepare() {
  cd "${srcdir}/AviSynthPlus-${pkgver}"

  patch -p1 -i "${srcdir}/mingw.patch"
  patch -p1 -i "${srcdir}/staticlib.patch"
}

build() {
  if check_option "debug" "y"; then
    _extra_config+=(-DCMAKE_BUILD_TYPE=Debug)
  else
    _extra_config+=(-DCMAKE_BUILD_TYPE=Release)
  fi

  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    ${_extra_config[@]} \
    -DWITH_STATIC_LIB=ON \
    -DBUILD_SHIBATCH=OFF \
    ../AviSynthPlus-${pkgver}

  ${MINGW_PREFIX}/bin/cmake.exe --build ./
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --build ./ --target install
}
