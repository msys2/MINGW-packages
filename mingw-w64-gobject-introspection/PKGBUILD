# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=gobject-introspection
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-runtime")
pkgver=1.62.0
pkgrel=2
arch=('any')
url="https://live.gnome.org/GObjectIntrospection"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cairo"
             "${MINGW_PACKAGE_PREFIX}-glib2"
             "${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-pygobject-devel"
             "${MINGW_PACKAGE_PREFIX}-python3-mako"
             "${MINGW_PACKAGE_PREFIX}-python3-markdown"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools")
source=(https://download.gnome.org/sources/gobject-introspection/${pkgver%.*}/${_realname}-${pkgver}.tar.xz
        0002-relocate-tools.patch
        0022-change-pkg-config-invocations.mingw.patch
        0024-Support-passing-arguments-to-g-ir-scanner-through-a-.all.patch
        0026-giscanner-assertions-and-waits.patch
        0027-wait-for-xml-parse-too.patch
        pyscript2exe.py)

sha256sums=('b1ee7ed257fdbc008702bdff0ff3e78a660e7e602efa8f211dc89b9d1e7d90a2'
            '0f75bd0a270a44599a5df51ec2218d7353678a406e0757aa392dc94f816e6d0b'
            'bb5f48047613216a6c3c652e0ef65c099475ffdddbea4a835b5a51279d7755ac'
            '7a256fe30eb5db3e2216ae7862b4f20c6691e7a01de4c480091f03d1e4e9f4ba'
            '19ca830262339c4ac9f21bfb8d669ee8e126a949a4237d55656e00c5ae81c451'
            '3f38bdd0dd43d9cf626cbca7c2abd22a7ed0213e6756252c6d467d470d9c5948'
            '00e03a8b9d45e620c6fabbf96f5ccb41d1e49cc9b35ccb846d423f235ad5bec6')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  patch -p1 -i "${srcdir}"/0002-relocate-tools.patch
  patch -p1 -i "${srcdir}"/0022-change-pkg-config-invocations.mingw.patch
  patch -p1 -i "${srcdir}"/0024-Support-passing-arguments-to-g-ir-scanner-through-a-.all.patch
  patch -p1 -i "${srcdir}"/0026-giscanner-assertions-and-waits.patch
  patch -p1 -i "${srcdir}"/0027-wait-for-xml-parse-too.patch
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  meson \
    --buildtype=plain \
    -Ddoctool=true \
    -Dcairo=true \
    "../${_realname}-${pkgver}"

  GI_SCANNER_DISABLE_CACHE=1 ninja
}

package_gobject-introspection() {
  pkgdesc="Introspection system for GObject-based libraries (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-gobject-introspection-runtime=${pkgver}"
           "${MINGW_PACKAGE_PREFIX}-pkg-config"
           "${MINGW_PACKAGE_PREFIX}-python3"
           "${MINGW_PACKAGE_PREFIX}-python3-mako")
  options=('!emptydirs')
  install=${_realname}-${CARCH}.install

  cd "${srcdir}/build-${MINGW_CHOST}"

  DESTDIR="${pkgdir}${MINGW_PREFIX}" ninja install

  local _PRE_WIN="$(cygpath -m ${MINGW_PREFIX})"
  for name in g-ir-scanner g-ir-doc-tool g-ir-annotation-tool; do
    ${MINGW_PREFIX}/bin/python3 \
      "${srcdir}/pyscript2exe.py" "${pkgdir}${MINGW_PREFIX}/bin/${name}"
    sed -e "s|${_PRE_WIN}|${MINGW_PREFIX}|g" \
      -i ${pkgdir}${MINGW_PREFIX}/bin/${name}-script.py
  done

  for pcfile in  "${pkgdir}${MINGW_PREFIX}"/lib/pkgconfig/*.pc; do
    sed -s "s|${_PRE_WIN}/lib/libffi|\${prefix}/lib/libffi|g" -i "${pcfile}"
    sed -s "s|${_PRE_WIN}|${MINGW_PREFIX}|g" -i "${pcfile}"
  done

  rm "${pkgdir}${MINGW_PREFIX}/bin/libgirepository"*.dll
  rm "${pkgdir}${MINGW_PREFIX}/lib/libgirepository"*.a
  rm -R "${pkgdir}${MINGW_PREFIX}/lib/girepository-1.0"
}

package_gobject-introspection-runtime() {
  pkgdesc="Introspection system for GObject-based libraries - runtime files (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-glib2")

  cd "${srcdir}/build-${MINGW_CHOST}"
  local tmp_install="$(pwd)/_temp"

  DESTDIR="${tmp_install}" ninja install

  install -Dt "${pkgdir}${MINGW_PREFIX}/bin" "${tmp_install}/bin/libgirepository"*.dll
  install -Dt "${pkgdir}${MINGW_PREFIX}/lib" "${tmp_install}/lib/libgirepository"*.a
  install -Dt "${pkgdir}${MINGW_PREFIX}/lib/girepository-1.0" "${tmp_install}/lib/girepository-1.0/"*.typelib
}

package_mingw-w64-i686-gobject-introspection()
{
  package_gobject-introspection
}

package_mingw-w64-i686-gobject-introspection-runtime()
{
  package_gobject-introspection-runtime
}

package_mingw-w64-x86_64-gobject-introspection()
{
  package_gobject-introspection
}

package_mingw-w64-x86_64-gobject-introspection-runtime()
{
  package_gobject-introspection-runtime
}
