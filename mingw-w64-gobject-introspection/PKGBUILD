# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=gobject-introspection
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-runtime")
pkgver=1.46.0
pkgrel=3
arch=('any')
url="https://live.gnome.org/GObjectIntrospection"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cairo"
             "${MINGW_PACKAGE_PREFIX}-glib2"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-python2-mako")
# Building this in debug invokes pdb when things go wrong.
# options=('!strip' 'debug')
source=(https://download.gnome.org/sources/gobject-introspection/${pkgver%.*}/${_realname}-${pkgver}.tar.xz
        0001-Revert-Windows-port-Work-arount-MSYS-weirdness-where.patch
        0018-debug-rmtree-errors.mingw.patch
        0021-tests-no-undefined.patch
        0022-change-pkg-config-invocations.mingw.patch
        0024-Support-passing-arguments-to-g-ir-scanner-through-a-.all.patch
        0050-dont-load-msvcrt.patch
        0055-fix-python-detection.patch)

md5sums=('adb40a31c7c80b65b0f4c8fd71b493dc'
         '37d1683439e0b31da62220418c8965fb'
         'f0ecde986ebf1f0e28b752f9c36fb6c1'
         '69ef34e2f57abc8afe1b9f6fa3786e97'
         '8d3589fb959a0a68ce7ce9051fa441fe'
         'd22ceb61d87fc049373d4589453d4fc3'
         'e3598d539258678eef8dde2216419faf'
         '6d809bf266e42a20b31a7acbaf0384d1')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/0001-Revert-Windows-port-Work-arount-MSYS-weirdness-where.patch
  patch -p1 -i "${srcdir}"/0018-debug-rmtree-errors.mingw.patch
  patch -p1 -i "${srcdir}"/0021-tests-no-undefined.patch
  patch -p1 -i "${srcdir}"/0022-change-pkg-config-invocations.mingw.patch
  # Source for this patch is:
  # https://bazaar.launchpad.net/~lrn1986/stupidbuild/trunk/view/head:/src/mingw/gobject-introspection-1.0-1.44.0-2/patches/0024-Support-passing-arguments-to-g-ir-scanner-through-a-.all.patch
  patch -p1 -i "${srcdir}"/0024-Support-passing-arguments-to-g-ir-scanner-through-a-.all.patch
  patch -p1 -i "${srcdir}"/0050-dont-load-msvcrt.patch
  patch -p1 -i "${srcdir}"/0055-fix-python-detection.patch

  autoreconf -fi
}

build() {
  export PYTHON=${MINGW_PREFIX}/bin/python2
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-silent-rules

  make
}

package_gobject-introspection() {
  pkgdesc="Introspection system for GObject-based libraries (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-gobject-introspection-runtime=${pkgver}"
           "${MINGW_PACKAGE_PREFIX}-python2"
           "${MINGW_PACKAGE_PREFIX}-python2-mako")
  options=('!emptydirs')
  
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="${pkgdir}" install
  make -j1 DESTDIR="${pkgdir}" uninstall-libLTLIBRARIES uninstall-typelibsDATA

  # I've spent too long debugging gobject-introspection and it's too painful each
  # time. Therefore, I've taken the decision to copy the .pyd also as a _d.pyd
  # in-case I happen to be investigating it with a debug version of Python which
  # only loads _d.pyd extensions.
  # You will still have to rebuild this package after having built and installed
  # the debug mingw-w64-python2 of course .. still one less thing to forget to
  # remember.
  # There will be a way to communicate the nature of the Python through to Autoconf
  # and that would be better, but this will have to do for now.
  # (see:
  #  @OS_WIN32_TRUE@	-shrext ".pyd"
  #  in gobject-introspection/Makefile.in)
  PYDS=$(find "${pkgdir}" -name "*.pyd")
  for PYD in "${PYDS}"; do
    pushd $(dirname "${PYD}")
      cp $(basename "${PYD}") $(basename "${PYD}" .pyd)_d.pyd
    popd
  done
}

package_gobject-introspection-runtime() {
  pkgdesc="Introspection system for GObject-based libraries - runtime files (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-glib2")
  
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="${pkgdir}" install-libLTLIBRARIES install-typelibsDATA
}

# Wrappers
package_mingw-w64-i686-gobject-introspection()
{
  package_gobject-introspection
}

package_mingw-w64-i686-gobject-introspection-runtime()
{
  package_gobject-introspection-runtime
}

package_mingw-w64-x86_64-gobject-introspection()
{
  package_gobject-introspection
}

package_mingw-w64-x86_64-gobject-introspection-runtime()
{
  package_gobject-introspection-runtime
}
