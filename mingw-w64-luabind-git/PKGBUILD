# Maintainer: Alexey Kasatkin <alexeikasatkin@gmail.com>

_realname=luabind
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=0.9.1.142.g2b904b3
pkgrel=1
pkgdesc="A library that helps you create bindings between C++ and Lua (mingw-w64)"
arch=('any')
url="http://www.rasterbar.com/products/luabind.html"
license=("MIT")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-cmake" "git")
depends=("${MINGW_PACKAGE_PREFIX}-boost" "${MINGW_PACKAGE_PREFIX}-lua51")
options=('strip' 'staticlibs')
source=("${_realname}"::"git+https://github.com/DennisOSRM/luabind.git")
md5sums=(SKIP)

pkgver() {
  cd "$_realname"
  git describe --tags | sed -e 's|-|.|g' -e 's|v||g'
}

build() {
  echo "Build static version..."
  [[ -d ${srcdir}/static-${MINGW_CHOST} ]] && rm -rf ${srcdir}/static-${MINGW_CHOST}
  mkdir ${srcdir}/static-${MINGW_CHOST}
  cd "${srcdir}/static-${MINGW_CHOST}"
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    ../${_realname}
  make
}

package() {
  cd "${srcdir}/static-${MINGW_CHOST}"
  make install
}
