# Maintainer: Ilya Rakhlin <i.rakhlin@gmail.com>

_realname=pkgconf
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3.7.33.g9505213
pkgrel=1
pkgdesc='pkg-config compatible utility which does not depend on glib'
url='https://github.com/pkgconf/pkgconf'
arch=('any')
license=('ISC')
options=('!debug' 'strip')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
source=("$_realname::git+https://github.com/pkgconf/pkgconf.git"
        001_fix_mingw64_and_static.patch)
sha256sums=('SKIP'
            '2298b6b7fbbbb38a3a149c963ba6c3142015ed9da293dcf03e866198cef5eaf3')

prepare() {
  cd "$srcdir/$_realname"
  git describe --tags | sed 's|-|.|g'
  
  patch -p1 -i ${srcdir}/001_fix_mingw64_and_static.patch
  ./autogen.sh
}

build() {

  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  
  $srcdir/$_realname/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --disable-shared \
    --sysconfdir=${MINGW_PREFIX}/etc \
    --mandir=${MINGW_PREFIX}/share/man \
    --infodir=${MINGW_PREFIX}/share/info \
    --localstatedir=${MINGW_PREFIX}/var
  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR="$pkgdir" install
  cp "${pkgdir}${MINGW_PREFIX}"/bin/pkgconf "${pkgdir}${MINGW_PREFIX}"/bin/${MINGW_CHOST}-pkgconf
}
