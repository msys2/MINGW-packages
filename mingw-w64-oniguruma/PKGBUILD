# Maintainer: Martell Malone <martellmalone@gmail.com>

_realname=onig
_fullname=oniguruma
pkgname=("${MINGW_PACKAGE_PREFIX}-${_fullname}")
pkgver=5.9.5
pkgrel=2
pkgdesc="A regular expressions library (mingw-w64)"
arch=('any')
groups=("${MINGW_PACKAGE_PREFIX}")
license=('BSD')
url="http://www.geocities.jp/kosako3/oniguruma"
options=('staticlibs')
source=("http://www.geocities.jp/kosako3/${_fullname}/archive/${_realname}-${pkgver}.tar.gz"
        001-no-undefined.patch
        002-autoconf.patch)
md5sums=('970f98a4cd10021b545d84e34c34aae4'
         'f3e53db45e17dacc3c380751be2f2030'
         'de6f42a1b58d18850dde64747925bf00')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-no-undefined.patch
  patch -p1 -i ${srcdir}/002-autoconf.patch

  touch NEWS
  touch ChangeLog
  autoreconf -fi
}

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  ./configure --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST} \
      --enable-shared \
      --enable-static

  make
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  make install DESTDIR="${pkgdir}"
}
