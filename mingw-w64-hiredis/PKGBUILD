# Maintainer: taozuhong <taozuhong@gmail.com>

_realname=hiredis
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3.0
pkgrel=1
pkgdesc="Minimalistic C client for Redis (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/redis/hiredis"
msys2_references=(
  "cpe: cpe:/a:redis:hiredis"
  "cpe: cpe:/a:redislabs:hiredis"
)
license=('spdx:BSD-3-Clause')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-pkgconf")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-openssl")
source=("https://github.com/redis/hiredis/archive/refs/tags/v${pkgver}.tar.gz"
			   "0001-fixed-ssl-clang-issue.patch")
sha256sums=('25cee4500f359cf5cad3b51ed62059aadfc0939b05150c1f19c7e2829123631c'
						'883ef8e093c27f53ecdfeca0e33860902d079d0babf0feefabdcfdd45fa7b92a')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/0001-fixed-ssl-clang-issue.patch
}

build() {
  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  mkdir -p "${srcdir}/static-${MSYSTEM}" && cd "${srcdir}/static-${MSYSTEM}"

  CFLAGS+=" -Wno-int-conversion" \
  MSYS2_ARG_CONV_EXCL=- \
    ${MINGW_PREFIX}/bin/cmake \
    -G"Ninja" \
    ${_extra_config[@]} \
    -DBUILD_SHARED_LIBS=OFF \
    -DCMAKE_INSTALL_PREFIX=$MINGW_PREFIX \
    -DENABLE_SSL=ON \
    -DDISABLE_TESTS=ON \
    ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake --build .

  mkdir -p "${srcdir}/shared-${MSYSTEM}" && cd "${srcdir}/shared-${MSYSTEM}"

  CFLAGS+=" -Wno-int-conversion" \
  MSYS2_ARG_CONV_EXCL=- \
    ${MINGW_PREFIX}/bin/cmake \
    -G"Ninja" \
    ${_extra_config[@]} \
    -DCMAKE_INSTALL_PREFIX=$MINGW_PREFIX \
    -DENABLE_SSL=ON \
    -DDISABLE_TESTS=ON \
    ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  cd "${srcdir}/static-${MSYSTEM}"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .
  cd "${srcdir}/shared-${MSYSTEM}"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .

  # Remove NuGet package
  rm -rf "${pkgdir}"${MINGW_PREFIX}/build
}