# Maintainer: taozuhong <taozuhong@gmail.com>

_realname=hiredis
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.0.2
pkgrel=1
pkgdesc="Minimalistic C client for Redis"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://github.com/redis/hiredis"
license=("BSD-3-Clause License")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
            "${MINGW_PACKAGE_PREFIX}-pkg-config"
            "${MINGW_PACKAGE_PREFIX}-cmake"
            "${MINGW_PACKAGE_PREFIX}-ninja"
            "${MINGW_PACKAGE_PREFIX}-openssl")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-crt"
         "${MINGW_PACKAGE_PREFIX}-openssl")
source=("https://github.com/redis/hiredis/archive/v${pkgver}.tar.gz"
        '0001-Add-static-library-target-and-cpack-support.patch')
sha256sums=('e0ab696e2f07deb4252dda45b703d09854e53b9703c7d52182ce5a22616c3819'
            '22461977c6d23bab3d14f9a35f35b65ecaeefa4cf3e520fdff4dd22b0ea0bc24')
            
prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-Add-static-library-target-and-cpack-support.patch
}

build() {
  rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  MSYS2_ARG_CONV_EXCL=- \
    ${MINGW_PREFIX}/bin/cmake ../${_realname}-${pkgver} \
    -G"Ninja" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=$MINGW_PREFIX \
    -DENABLE_SSL=ON
  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --build . --target install
}
