# Maintainer: Junjie Mao <eternal.n08@gmail.com>

_realname=flexdll
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.34
pkgrel=1
pkgdesc="An implementation of a dlopen-like API for Windows (mingw-w64)"
arch=('any')
url='http://alain.frisch.fr/flexdll.html'
license=('zlib/libpng License')
makedepends=("${MINGW_PACKAGE_PREFIX}-ocaml")
source=("http://alain.frisch.fr/flexdll/${_realname}-${pkgver}.tar.gz"
        "0001-Fix-the-List.hd-exception.patch"
        "0002-Add-path-to-libcrt2.o-into-the-default-library-searc.patch"
        "0003-Remove-the-usages-of-stdout-to-workaround-the-missin.patch"
        "0004-use-lib-flexdll-as-the-default-library-path.patch")
sha1sums=('86acfd2d965dcf7a650f3d8e8f8db7bfe99de2a0'
          '642c99ad0d2035ef028254aff41dbff49014bed3'
          '31ebdca246a4df6069d733350c9a2b8c94d33dec'
          '43f232f04934a0c7797f05e8db03f04ae156d8c4'
          '87754de44fd9a51299e52908b5948b31383ca6eb')

if [ "${CARCH}" == "i686" ]; then
  chain="mingw"
else
  chain="mingw64"
fi

prepare() {
  cd ${srcdir}/${_realname}

  patch -p1 -i ${srcdir}/0001-Fix-the-List.hd-exception.patch
  patch -p1 -i ${srcdir}/0002-Add-path-to-libcrt2.o-into-the-default-library-searc.patch
  patch -p1 -i ${srcdir}/0003-Remove-the-usages-of-stdout-to-workaround-the-missin.patch
  patch -p1 -i ${srcdir}/0004-use-lib-flexdll-as-the-default-library-path.patch
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  cp -rf ${_realname} build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}

  unset FLEXDIR
  unset OCAMLLIB
  make flexlink.exe -j1
  make build_${chain} -j1
}

check() {
  # The test demo works on x86_64 but fails on i686. Root causes of the failure
  # is still unknown, but the tool is good enough to be used in other
  # applications such as ocaml and coq.
  plain "skip"
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}

  install -Dm755 flexlink.exe ${pkgdir}${MINGW_PREFIX}/bin/flexlink.exe
  for f in flexdll_${chain}.o flexdll_initer_${chain}.o flexdll.h flexdll.c flexdll_initer.c default.manifest default_amd64.manifest; do
    install -D ${f} ${pkgdir}${MINGW_PREFIX}/lib/flexdll/${f}
  done
  [ -f LICENSE ] && install -Dm644 LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
  [ -f CHANGES ] && install -Dm644 CHANGES ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/CHANGES
}
