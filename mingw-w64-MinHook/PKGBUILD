# Contributor: Pavel Minaev <int19h@gmail.com>

_realname=MinHook
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3.4
pkgrel=2
pkgdesc="The Minimalistic x86/x64 API Hooking Library for Windows (mingw-w64)"
url="https://github.com/TsudaKageyu/minhook"
license=('BSD')
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc" "${MINGW_PACKAGE_PREFIX}-cmake")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/TsudaKageyu/${_realname}/archive/v${pkgver}.tar.gz")
sha256sums=('1aebeae4ca898330c507860acc2fca2eb335fe446a3a2b8444c3bf8b2660a14e')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  
  ${MINGW_PREFIX}/bin/cmake -DCMAKE_BUILD_TYPE=Release -DBUILD_SHARED_LIBS=ON -DWIN32=0 .
  ${MINGW_PREFIX}/bin/cmake --build . --clean-first

  ${MINGW_PREFIX}/bin/dlltool --dllname libminhook.dll --input-def dll_resources/MinHook.def --output-lib libminhook.dll.a

  ${MINGW_PREFIX}/bin/cmake -DCMAKE_BUILD_TYPE=Release -DBUILD_SHARED_LIBS=OFF -DWIN32=0 .
  ${MINGW_PREFIX}/bin/cmake --build . --clean-first
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  install -Dm644 include/MinHook.h "${pkgdir}${MINGW_PREFIX}/include/MinHook.h"
  install -Dm644 libminhook.a "${pkgdir}${MINGW_PREFIX}/lib/libMinHook.a"
  install -Dm644 libminhook.dll.a "${pkgdir}${MINGW_PREFIX}/lib/libMinHook.dll.a"
  install -Dm755 libminhook.dll "${pkgdir}${MINGW_PREFIX}/bin/MinHook.dll"
}