# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gtk3
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.14.8
pkgrel=1
pkgdesc="GObject-based multi-platform GUI toolkit (v3) (mingw-w64)"
arch=('any')
url="http://www.gtk.org"
license=("LGPL")
install=gtk3-${CARCH}.install
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection")
# python2 is required to run gdbus-codegen
makedepends+=("autoconf" "automake" "libtool")
# autotools are required because several Makefile.am are modified
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-atk"
         "${MINGW_PACKAGE_PREFIX}-pango"
         "${MINGW_PACKAGE_PREFIX}-glib2>=2.41.0"
         "${MINGW_PACKAGE_PREFIX}-cairo"
         "${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2"
         "${MINGW_PACKAGE_PREFIX}-json-glib"
         "${MINGW_PACKAGE_PREFIX}-shared-mime-info")
options=('strip' '!debug' 'staticlibs')
source=("http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver%.*}/gtk+-${pkgver}.tar.xz"
        0010-put-gtk-dll-into-path.mingw.patch
        0013-fix-mingw-w64-fstat-call.mingw.patch
        0035-enable-rgba-w32-windows.all.patch
        0037-W32-better-dwm-loading-and-support-dwm-detection.all.patch
        0054-no-transparency-for-children.all.patch
        0055-skip-testsuite.all.patch
        Make-reftest-plugins-W32-compatible.patch)
md5sums=('e46a12448b348d384b8bb29f0c22718f'
         '9e0296da2986be7697cf343563b85d1f'
         '21789d52c1debcab59f8b6a99232de68'
         '397bf8001d3da75e06b5794e378ae25f'
         'ad383a497a9134355e0a549f844c3a33'
         '31d098e6856ddc8dde7259ae572f8bf1'
         '751ee83452f3e06a460a8867f8e479b8'
         '864d4b2cd6f72115cc78a393ccd96b47')

prepare() {
  cd "${srcdir}/gtk+-${pkgver}"
  patch -Np1 -i "${srcdir}"/0010-put-gtk-dll-into-path.mingw.patch
  patch -Np1 -i "${srcdir}"/0013-fix-mingw-w64-fstat-call.mingw.patch
  patch -Np1 -i "${srcdir}"/0035-enable-rgba-w32-windows.all.patch
  patch -Np1 -i "${srcdir}"/0037-W32-better-dwm-loading-and-support-dwm-detection.all.patch
  patch -Np1 -i "${srcdir}"/0054-no-transparency-for-children.all.patch
  patch -Np1 -i "${srcdir}"/0055-skip-testsuite.all.patch
  #patch -Np1 -i "${srcdir}"/Make-reftest-plugins-W32-compatible.patch

  autoreconf -i
}

build() {
  export MSYS2_ARG_CONV_EXCL="-//OASIS//DTD"
  [[ -d "${srcdir}/${CHOST}" ]] && rm -rf "${srcdir}/${CHOST}"
  cp -rf ${srcdir}/gtk+-${pkgver} "${srcdir}/${CHOST}"
  cd "${srcdir}/${CHOST}"
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-win32-backend \
    --disable-gtk2-dependency \
    --enable-shared \
    --enable-introspection \
    --disable-cups \
    --with-included-immodules
  make
}

package() {
  #export MSYS2_ARG_CONV_EXCL="-//OASIS"
  cd "${srcdir}/${CHOST}"
  make -j1 DESTDIR="$pkgdir" install
  find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' -o -name '*.manifest' | xargs -rtl1 rm

  mv ${pkgdir}${MINGW_PREFIX}/bin/gtk-update-icon-cache{,-3.0}.exe
  mv ${pkgdir}${MINGW_PREFIX}/share/man/man1/gtk-update-icon-cache{,-3.0}.1
}
