# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gtk3
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.18.2
pkgrel=3
pkgdesc="GObject-based multi-platform GUI toolkit (v3) (mingw-w64)"
arch=('any')
url="http://www.gtk.org"
license=("LGPL")
install=gtk3-${CARCH}.install
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection")
# python2 is required to run gdbus-codegen
makedepends+=("autoconf" "automake" "libtool")
# autotools are required because several Makefile.am are modified
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-adwaita-icon-theme"
         "${MINGW_PACKAGE_PREFIX}-atk>=2.15.1"
         "${MINGW_PACKAGE_PREFIX}-cairo>=1.14.0"
         "${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2>=2.30.0"
         "${MINGW_PACKAGE_PREFIX}-glib2>=2.43.4"
         "${MINGW_PACKAGE_PREFIX}-json-glib"
         "${MINGW_PACKAGE_PREFIX}-libepoxy"
         "${MINGW_PACKAGE_PREFIX}-pango>=1.36.7"
         "${MINGW_PACKAGE_PREFIX}-shared-mime-info")
options=('strip' '!debug' 'staticlibs')
source=("http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver%.*}/gtk+-${pkgver}.tar.xz"
        0013-fix-mingw-w64-fstat-call.mingw.patch
        0056-missing-file-toarray-pl.patch
        0080-backport-notebook-tab-drag-fixes.patch
        0090-backport-fix-introspection-syntax.patch
)
md5sums=('230fd78e42419d35ff790784b457e5f2'
         '21789d52c1debcab59f8b6a99232de68'
         'f25cb2486d761b60e04036947552bf82'
         '8d86218ed960cd86c8b04c987f7038e9'
         '664ce09ba116bda6207ff2ef898392fa'
)

prepare() {
  cd "${srcdir}/gtk+-${pkgver}"
  patch -Np1 -i "${srcdir}"/0013-fix-mingw-w64-fstat-call.mingw.patch

  rm -f gdk/broadway/toarray.pl
  patch -p1 -i ${srcdir}/0056-missing-file-toarray-pl.patch

  patch -Np1 -i ${srcdir}/0080-backport-notebook-tab-drag-fixes.patch

  patch -Np1 -i ${srcdir}/0090-backport-fix-introspection-syntax.patch

  autoreconf -i
}

build() {
  export MSYS2_ARG_CONV_EXCL="-//OASIS//DTD"
  [[ -d "${srcdir}/${CHOST}" ]] && rm -rf "${srcdir}/${CHOST}"
  cp -rf ${srcdir}/gtk+-${pkgver} "${srcdir}/${CHOST}"
  cd "${srcdir}/${CHOST}"
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-win32-backend \
    --enable-shared \
    --enable-introspection \
    --enable-broadway-backend \
    --disable-cups \
    --with-included-immodules
  make
}

package() {
  #export MSYS2_ARG_CONV_EXCL="-//OASIS"
  cd "${srcdir}/${CHOST}"
  make -j1 DESTDIR="${pkgdir}" install
  find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' -o -name '*.manifest' | xargs -rtl1 rm

  mv ${pkgdir}${MINGW_PREFIX}/bin/gtk-update-icon-cache{,-3.0}.exe
  mv ${pkgdir}${MINGW_PREFIX}/share/man/man1/gtk-update-icon-cache{,-3.0}.1

  install -Dm644 "${srcdir}/gtk+-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
