# Contributor: Kazuki Shigemichi <shigemichik@gmail.com>

_realname=libcello
pkgbase="mingw-w64-${_realname}-git"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=2.1.0.301.da28eef
pkgrel=1
pkgdesc="A library brings higher level programming to C (mingw-w64)"
arch=('any')
url="http://libcello.org/"
license=("BSD")
source=(${_realname}::"git+https://github.com/orangeduck/Cello.git")
sha256sums=('SKIP')

pkgver() {
  cd $_realname
  version=$(grep "^VERSION =" Makefile | cut -d " " -f 3)
  revision=$(git rev-list --count HEAD)
  hash=$(git log --pretty=format:'%h' -n 1)
  echo ${version}.${revision}.${hash}
}

build() {
  cd ${_realname}
  make
}

check() {
  cd ${_realname}

  make check
}

package() {
  cd ${_realname}
  mkdir -p ${pkgdir}${MINGW_PREFIX}/{bin,include,lib}
  make PREFIX="${pkgdir}${MINGW_PREFIX}" install
  install -Dm644 LICENSE.md "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
