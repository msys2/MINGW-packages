# $Id: PKGBUILD 174870 2013-01-08 10:06:34Z jgc $
# Maintainer: Zach Bacon <11doctorwhocanada@gmail.com>
# Contributor: damir <damir@archlinux.org>

_realname=libcdio-paranoia
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=10.2+2.0.0
pkgrel=2
pkgdesc="CD paranoia libraries from libcdio (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=('GPL' 'LGPL')
url="https://www.gnu.org/software/libcdio/"
depends=("${MINGW_PACKAGE_PREFIX}-libcdio")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc" "${MINGW_PACKAGE_PREFIX}-autotools")
source=(https://ftp.gnu.org/gnu/libcdio/${_realname}-${pkgver}.tar.bz2{,.sig})
sha256sums=('4565c18caf401083c53733e6d2847b6671ba824cff1c7792b9039693d34713c1'
            'SKIP')
validpgpkeys=('DAA63BC2582034A02B923D521A8DE5008275EC21')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  # autoreconf to get updated libtool files with clang support
  autoreconf -fiv
}

build() {
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-cpp-progs \
    --enable-shared \
    --enable-static \
    --disable-example-progs \
    $( [[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]] && \
      echo "--disable-ld-version-script" || true)

  make
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make -j1 DESTDIR="${pkgdir}" install
}
