# $Id: PKGBUILD 174870 2013-01-08 10:06:34Z jgc $
# Maintainer: Zach Bacon <11doctorwhocanada@gmail.com>
# Contributor: damir <damir@archlinux.org>

_realname=libcdio-paranoia
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=10.2+0.93+1
pkgrel=2
pkgdesc="CD paranoia libraries from libcdio (mingw-w64)"
arch=('any')
license=('GPL' 'LGPL')
url="https://www.gnu.org/software/libcdio/"
depends=("${MINGW_PACKAGE_PREFIX}-libcdio")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
source=(https://ftp.gnu.org/gnu/libcdio/${_realname}-${pkgver}.tar.bz2)
sha256sums=('ec1d9b1d5a28cc042f2cb33a7cc0a2b5ce5525f102bc4c15db1fac322559a493')

build() {
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-cpp-progs \
    --enable-shared \
    --enable-static \
    --disable-example-progs

  make
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make -j1 DESTDIR="${pkgdir}" install
}
