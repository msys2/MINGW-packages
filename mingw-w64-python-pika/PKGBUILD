# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.su>

_realname=pika
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.3.2
pkgrel=3
pkgdesc="Pure Python RabbitMQ/AMQP 0-9-1 client library (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://pika.readthedocs.io/'
msys2_repository_url='https://github.com/pika/pika'
msys2_references=(
  'anitya: 149037'
  'archlinux: python-pika'
  'gentoo: dev-python/pika'
  'purl: pkg:pypi/pika'
)
license=('spdx:BSD-3-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python-wheel")
optdepends=("${MINGW_PACKAGE_PREFIX}-python-tornado: use with Tornado I/O loop"
            "${MINGW_PACKAGE_PREFIX}-python-twisted: use with Twisted I/O loop")
options=('!strip')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('b2a327ddddf8570b4965b3576ac77091b850262d34ce8c1d8cb4e4146aa4145f')

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
