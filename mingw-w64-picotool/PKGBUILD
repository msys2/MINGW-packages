# Contributor: thewh1teagle

_realname=picotool
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver="1.1.2"
pkgrel=1
pkgdesc="Tool for inspecting RP2040 binaries and interacting with RP2040 devices."
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://github.com/raspberrypi/picotool"
pico_sdk_url="https://github.com/raspberrypi/pico-sdk"
pico_sdk_version="1.5.1"

license=('BSD-3-Clause')

makedepends=("git" "${MINGW_PACKAGE_PREFIX}-cmake" "${MINGW_PACKAGE_PREFIX}-libusb" "${MINGW_PACKAGE_PREFIX}-make" "${MINGW_PACKAGE_PREFIX}-gcc")

source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz"
	"pico-sdk-${pico_sdk_version}.tar.gz::${pico_sdk_url}/archive/refs/tags/${pico_sdk_version}.tar.gz"
)
sha256sums=(
	"f1746ead7815c13be1152f0645db8ea3b277628eb0110d42a0a186db37d40a91"
	"95f5e522be3919e36a47975ffd3b208c38880c14468bd489ac672cfe3cec803c"
)

build() {
	export PICO_SDK_PATH="${srcdir}/pico-sdk-${pico_sdk_version}"

	cd "${srcdir}"

	cmake -B build \
		-S "${_realname}-${pkgver}" \
		-G "MinGW Makefiles" \
		-DLIBUSB_LIBRARIES=/ucrt64/lib/libusb-1.0.a \
		-DCMAKE_EXE_LINKER_FLAGS="-static -static-libgcc -static-libstdc++" # Build staticly so we can also use it outside of MinGW environment
	cmake --build build
}

package() {
	# Install application
	install -Dm755 "${srcdir}/build/${_realname}.exe" -t "${pkgdir}/${MINGW_PREFIX}/bin/"

	# Install docs
	install -Dm644 "${srcdir}/${_realname}-${pkgver}/README.md" -t "${pkgdir}/${MINGW_PREFIX}/usr/share/doc/${pkgname}"

	# Install license
	install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE.TXT" -t "${pkgdir}/${MINGW_PREFIX}/usr/share/licenses/${pkgname}"
}
