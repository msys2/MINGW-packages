# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=paraview
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=5.13.1
pkgrel=3
pkgdesc="Parallel Data Analysis and Visualization Application based on VTK. (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://www.paraview.org/'
msys2_repository_url="https://gitlab.kitware.com/paraview/paraview"
msys2_references=(
  'archlinux: paraview'
)
license=('spdx:BSD-3-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-double-conversion"
         "${MINGW_PACKAGE_PREFIX}-cgns"
         "${MINGW_PACKAGE_PREFIX}-expat"
         "${MINGW_PACKAGE_PREFIX}-fmt"
         "${MINGW_PACKAGE_PREFIX}-gdal"
         "${MINGW_PACKAGE_PREFIX}-gl2ps"
         "${MINGW_PACKAGE_PREFIX}-glew"
         "${MINGW_PACKAGE_PREFIX}-hdf5"
         "${MINGW_PACKAGE_PREFIX}-jsoncpp"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-libogg"
         "${MINGW_PACKAGE_PREFIX}-liblas"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libtheora"
         "${MINGW_PACKAGE_PREFIX}-libtiff"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-lz4"
         "${MINGW_PACKAGE_PREFIX}-netcdf"
         "${MINGW_PACKAGE_PREFIX}-openturns"
         "${MINGW_PACKAGE_PREFIX}-pdal"
         #"${MINGW_PACKAGE_PREFIX}-pegtl" https://gitlab.kitware.com/vtk/vtk/-/issues/18151
         "${MINGW_PACKAGE_PREFIX}-protobuf"
         "${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-pugixml"
         "${MINGW_PACKAGE_PREFIX}-qt5-base"
         "${MINGW_PACKAGE_PREFIX}-qt5-svg"
         "${MINGW_PACKAGE_PREFIX}-qt5-tools"
         "${MINGW_PACKAGE_PREFIX}-tbb"
         $([[ ${MSYSTEM} == MINGW64 ]] || echo "${MINGW_PACKAGE_PREFIX}-verdict")
         "${MINGW_PACKAGE_PREFIX}-xz"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread-git"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-qt5-xmlpatterns"
             "${MINGW_PACKAGE_PREFIX}-python-numpy"
             "${MINGW_PACKAGE_PREFIX}-cli11"
             "${MINGW_PACKAGE_PREFIX}-eigen3"
             "${MINGW_PACKAGE_PREFIX}-fast_float"
             "${MINGW_PACKAGE_PREFIX}-libharu"
             "${MINGW_PACKAGE_PREFIX}-nlohmann-json"
             "${MINGW_PACKAGE_PREFIX}-utf8cpp")
optdepends=("${MINGW_PACKAGE_PREFIX}-python-numpy"
            "${MINGW_PACKAGE_PREFIX}-python-matplotlib")
source=("https://www.paraview.org/files/v${pkgver%.*}/ParaView-v${pkgver}.tar.xz"
        "001-dll-export-some-vtk-functions.patch"
        "002-no-exact-version-for-fast_float.patch"
        "003-CellAttribute-Fix-mingw-build.patch::https://gitlab.kitware.com/vtk/vtk/-/commit/5a7de249.patch"
        "004-fix-build-with-fmt-11.patch"
        "005-avoid-file-conflict-with-vtk.patch")
sha256sums=('a16503ce37b999c2967d84234596e7bf67ac98221851a288bb1399c7e1dc2004'
            'a9b9292be90c259f0aa058e3f4a7a08b6218f20e6cb2a71e214acda2d2302a50'
            '411b0521fcd2864acae0af20ba6334f69f7c605c9e31996529f64aa749d61065'
            '784d9345ebb1a83841970df34d743344189604457270df0f594669639a718ec8'
            'c92343b01c50a93805c7068058416cf010cceba6700e4cc16b3be98598d2295f'
            '374c7fbca1d225fdb7eafd646937c202cb0427acd1558f2d8411ab0c394d6287')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${srcdir}"/ParaView-v${pkgver}

  cd VTK
  if [[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]]; then
    # Copied from mingw-w64-vtk/007-dll-export-some-functions.patch
    apply_patch_with_msg \
      001-dll-export-some-vtk-functions.patch
  fi

  apply_patch_with_msg \
    002-no-exact-version-for-fast_float.patch \
    003-CellAttribute-Fix-mingw-build.patch \
    004-fix-build-with-fmt-11.patch \
    005-avoid-file-conflict-with-vtk.patch
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  if [[ ${MSYSTEM} == MINGW64 ]]; then
    _extra_config+=("-DVTK_MODULE_USE_EXTERNAL_VTK_verdict=OFF")
  fi

  _pyver=$(${MINGW_PREFIX}/bin/python -c "import sys; print(f'{sys.version_info.major}.{sys.version_info.minor}')")

  CXXFLAGS+=" -w -Wno-register" \
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe -Wno-dev \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${_extra_config[@]}" \
      -DPARAVIEW_BUILD_SHARED_LIBS=ON \
      -DPARAVIEW_USE_PYTHON=ON \
      -DPARAVIEW_USE_QT=ON \
      -DPARAVIEW_USE_MPI=OFF \
      -DPARAVIEW_ENABLE_GDAL=ON \
      -DPARAVIEW_ENABLE_LAS=ON \
      -DPARAVIEW_ENABLE_OPENTURNS=ON \
      -DPARAVIEW_ENABLE_PDAL=ON \
      -DPARAVIEW_VERSIONED_INSTALL=OFF \
      -DPARAVIEW_CUSTOM_LIBRARY_SUFFIX="pv${pkgver%.*}" \
      -DPARAVIEW_BUILD_WITH_EXTERNAL=ON \
      -DPARAVIEW_PYTHON_SITE_PACKAGES_SUFFIX="lib/python${_pyver}/site-packages/pv/" \
      -DPARAVIEW_IGNORE_CMAKE_CXX11_CHECKS=ON \
      -DVTK_SMP_IMPLEMENTATION_TYPE=TBB \
      -DVTK_MODULE_USE_EXTERNAL_VTK_exprtk=OFF \
      -DVTK_MODULE_USE_EXTERNAL_VTK_ioss=OFF \
      -DVTK_MODULE_USE_EXTERNAL_VTK_pegtl=OFF \
      -DVTK_MODULE_USE_EXTERNAL_VTK_token=OFF \
      -DPython3_EXECUTABLE=${MINGW_PREFIX}/bin/python.exe \
      ../ParaView-v${pkgver}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .

  MSYS2_ARG_CONV_EXCL="-p" python -m compileall \
    -o 0 -o 1 -q -s"${pkgdir}" -p"/" "${pkgdir}${MINGW_PREFIX}/lib/python"*

  install -Dm644 "${srcdir}/ParaView-v${pkgver}/Copyright.txt" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
