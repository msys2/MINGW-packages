# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=BLAKE3
pkgbase=mingw-w64-blake3
pkgname=("${MINGW_PACKAGE_PREFIX}-b3sum"
         "${MINGW_PACKAGE_PREFIX}-libblake3")
pkgver=1.6.0
pkgrel=1
pkgdesc="BLAKE3 hash function (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/BLAKE3-team/BLAKE3'
license=('spdx:CC0-1.0 OR Apache-2.0 OR Apache-2.0 WITH LLVM-exception')
msys2_reference=(
  'anitya: 369414'
  'archlinux: b3sum'
  'aur: libblake3'
  'gentoo: dev-libs/blake3'
  'purl: pkg:cargo/b3sum'
)
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
source=("${url}/archive/${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('cc6839962144126bc6cc1cde89a50c3bb000b42a93d7e5295b1414d9bdf70c12')

prepare() {
  cargo fetch \
    --locked \
    --manifest-path "${_realname}-${pkgver}"/b3sum/Cargo.toml \
    --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cargo build \
    --release \
    --frozen \
    --manifest-path="${_realname}-${pkgver}/b3sum/Cargo.toml"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DBUILD_SHARED_LIBS=ON \
      -DCMAKE_DLL_NAME_WITH_SOVERSION=ON \
      -S "${_realname}-${pkgver}/c" \
      -B "build-${MSYSTEM}"

  cmake --build "build-${MSYSTEM}"
}

check() {
  cargo test \
    --release \
    --frozen \
    --manifest-path="${_realname}-${pkgver}/b3sum/Cargo.toml"
}

package_b3sum() {
  pkgdesc="Command line implementation of the BLAKE3 hash function (mingw-w64)"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path "${_realname}-${pkgver}/b3sum" \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 "${_realname}-${pkgver}"/LICENSE_{A2,A2LLVM,CC0} -t "${pkgdir}${MINGW_PREFIX}/share/licenses/b3sum/"
}

package_libblake3() {
  pkgdesc="C implementation of the BLAKE3 hash function (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")

  DESTDIR="${pkgdir}" cmake --install "build-${MSYSTEM}"
  install -Dm644 "${_realname}-${pkgver}"/LICENSE_{A2,A2LLVM,CC0} -t "${pkgdir}${MINGW_PREFIX}/share/licenses/libblake3/"
}

# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
