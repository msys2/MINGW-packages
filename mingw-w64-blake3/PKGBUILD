# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=BLAKE3
pkgbase=mingw-w64-blake3
pkgname=("${MINGW_PACKAGE_PREFIX}-b3sum"
         "${MINGW_PACKAGE_PREFIX}-libblake3")
pkgver=1.5.3
pkgrel=1
pkgdesc="BLAKE3 hash function (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/BLAKE3-team/BLAKE3'
license=('spdx:CC0-1.0 OR Apache-2.0 OR Apache-2.0 WITH LLVM-exception')
msys2_reference=(
  'anitya: 369414'
  'archlinux: b3sum'
  'aur: libblake3'
  'gentoo: dev-libs/blake3'
)
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
source=("${url}/archive/${pkgver}/${_realname}-${pkgver}.tar.gz"
        "support-gnullvm.patch::https://github.com/BLAKE3-team/BLAKE3/pull/399.patch")
sha256sums=('ec9114480857334858e73b727199c573bfdbed6138a83be573f076d37e671fc1'
            'e4141660dcb85800f715f0d1c48bff605c36c577867588d3e8e2955c7de6c910')

prepare() {
  cd "${_realname}-${pkgver}"

  # https://github.com/BLAKE3-team/BLAKE3/pull/39
  patch -Np1 -i "${srcdir}/support-gnullvm.patch"

  cd b3sum
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  WINAPI_NO_BUNDLED_LIBRARIES=1 \
    cargo build \
      --release \
      --frozen \
      --manifest-path="${_realname}-${pkgver}/b3sum/Cargo.toml"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DBUILD_SHARED_LIBS=ON \
      -DCMAKE_DLL_NAME_WITH_SOVERSION=ON \
      -S "${_realname}-${pkgver}/c" \
      -B "build-${MSYSTEM}"

  "${MINGW_PREFIX}"/bin/cmake.exe --build "build-${MSYSTEM}"
}

check() {
  WINAPI_NO_BUNDLED_LIBRARIES=1 \
    cargo test \
      --release \
      --frozen \
      --manifest-path="${_realname}-${pkgver}/b3sum/Cargo.toml"
}

package_b3sum() {
  pkgdesc="Command line implementation of the BLAKE3 hash function (mingw-w64)"

  WINAPI_NO_BUNDLED_LIBRARIES=1 \
    cargo install \
      --offline \
      --no-track \
      --frozen \
      --path "${_realname}-${pkgver}/b3sum" \
      --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 "${_realname}-${pkgver}"/LICENSE_{A2,A2LLVM,CC0} -t "${pkgdir}${MINGW_PREFIX}/share/licenses/b3sum/"
}

package_libblake3() {
  pkgdesc="C implementation of the BLAKE3 hash function (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install "build-${MSYSTEM}"
  install -Dm644 "${_realname}-${pkgver}"/LICENSE_{A2,A2LLVM,CC0} -t "${pkgdir}${MINGW_PREFIX}/share/licenses/b3sum/"
}

# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
