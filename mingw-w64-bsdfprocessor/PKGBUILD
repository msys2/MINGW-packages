# Maintainer: Marko KÃ¤ning <mk-lists@mailbox.org>

_realname=bsdfprocessor
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.1.2
pkgrel=4
epoch=
pkgdesc="Application for displaying and editing of BSDF files (mingw-w64)"
arch=('any')
url='https://github.com/KimuraRyo/BSDFProcessor'
license=('MPL2')
makedepends=(
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-eigen3"
             "${MINGW_PACKAGE_PREFIX}-libbsdf"
             "${MINGW_PACKAGE_PREFIX}-qt5"
             "${MINGW_PACKAGE_PREFIX}-OpenSceneGraph"
            )
depends=(
             "${MINGW_PACKAGE_PREFIX}-gcc-libs"
             "${MINGW_PACKAGE_PREFIX}-qt5"
             "${MINGW_PACKAGE_PREFIX}-OpenSceneGraph"
            )
source=(${_realname}-${pkgver}.tar.gz::"https://github.com/KimuraRyo/${_realname}/archive/v${pkgver}.tar.gz"
        'CMakeLists.txt.patch'
        'fix-OpenMP-bug+docu.patch'
        'optimisations-CMakeLists.txt.patch')
sha256sums=('66e6ffe33f5eca7540a6a51bbc66e905a623a3bb183dd7ad9738990168ffb1fa'
            '113205a29f3150cdcddecf534f042697518b5e0732e80d499052a2c2a5a04190'
            '350fab92066c4e2dbef724f23557e87650ab15e1e17cb30e3ce179b9de3b0ebc'
            'f98f76ee935d6350d37626f19027f2422d080503c0ea5552a4ab29a6e96784fa')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -Np0 -i "${srcdir}/CMakeLists.txt.patch"
  patch -Np1 -i "${srcdir}/fix-OpenMP-bug+docu.patch"
  patch -Np0 -i "${srcdir}/optimisations-CMakeLists.txt.patch"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir build-${MINGW_CHOST} && cd build-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DEIGEN3_INCLUDE_DIR=${MINGW_PREFIX}/include/eigen3 \
    -DLIBBSDF_DIR=${MINGW_PREFIX}/include/libbsdf \
    -DCMAKE_CXX_FLAGS=-std=c++11 \
    ..

  make
}

package() {
  mkdir -p ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}
  cp -f "${srcdir}"/${_realname}-${pkgver}/LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}

  mkdir -p ${pkgdir}${MINGW_PREFIX}/bin
  cp -f "${srcdir}"/${_realname}-${pkgver}/build-${MINGW_CHOST}/BSDFProcessor.exe ${pkgdir}${MINGW_PREFIX}/bin/bsdfprocessor.exe
}

