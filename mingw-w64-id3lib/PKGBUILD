# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=id3lib
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.8.3
pkgrel=2
pkgdesc='Library for reading, writing, and manipulating ID3v1 and ID3v2 tags (mingw-w64)'
arch=('any')
license=('LGPL')
url="http://id3lib.sourceforge.net/"
options=('staticlibs')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-zlib" )
source=(http://downloads.sourceforge.net/sourceforge/${_realname}/${_realname}-${pkgver}.tar.gz
        00-add-libz-to-compilation.patch
        05-create-m4-directory.patch
        10-fix-compilation-with-cpp-headers.patch
        15-fix-headers-of-main-functions.patch
        20-create-manpages.patch
        30-fix-utf16.patch
        40-deal-with-mkstemp.patch
        50-remove-outdated-check.patch
        60-add-c-wrapper-functions.patch
        60-fix_make_check.patch
        60-id3lib-missing-nullpointer-check.patch
        60-spelling.patch
        61-fix_vbr_stack_smash.patch
        70-id3lib-3.8.3-autoreconf.patch
        100-id3lib-3.8.3-mingw.patch)

md5sums=('19f27ddd2dda4b2d26a559a4f0f402a7'
         '9428ef38283d13adb6e349089537a63f'
         '5a78e0fe586fba334eae2a29e4bf5a28'
         '88f21f1bf4f697be8030706bbec11705'
         'b3de841cd2eda0d37c8c8330119e12d4'
         '0c7c939943bdfcc3a0f6be95fe9b2367'
         '74ba59c03f91fedbe4dd8045b29c7e15'
         'd2d06d46df3867d73746f584d1e6fcc5'
         'd3a61d07a2bfa83f4ac6290794789022'
         '93ef4b26ad08027e28d3f80ad6e492b4'
         '5c4b53a3d5543c5c50b23d6d5b9f230b'
         '964fa0db0da7b1495b34b81bd36539fd'
         '96e0aec669434fa5a9838cfb4bd950ef'
         '0973e285cdc420130b984a259cdd8ae0'
         'd445d4fa60b98b947969cd24f10eac66'
         '5751591c71cfeb7d434666761840b2e7')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -p1 -i ../00-add-libz-to-compilation.patch
  patch -p1 -i ../05-create-m4-directory.patch
  patch -p1 -i ../10-fix-compilation-with-cpp-headers.patch
  patch -p1 -i ../15-fix-headers-of-main-functions.patch
  patch -p1 -i ../20-create-manpages.patch
  patch -p1 -i ../30-fix-utf16.patch
  patch -p1 -i ../40-deal-with-mkstemp.patch
  patch -p1 -i ../60-add-c-wrapper-functions.patch
  patch -p1 -i ../60-fix_make_check.patch
  patch -p1 -i ../60-id3lib-missing-nullpointer-check.patch
  patch -p1 -i ../60-spelling.patch
  patch -p1 -i ../61-fix_vbr_stack_smash.patch
  patch -p1 -i ../50-remove-outdated-check.patch
  patch -p1 -i ../70-id3lib-3.8.3-autoreconf.patch
  patch -p1 -i ../100-id3lib-3.8.3-mingw.patch

  libtoolize -fc
  WANT_AUTOMAKE=latest aclocal
  WANT_AUTOMAKE=latest autoconf
  WANT_AUTOMAKE=latest automake --add-missing --copy
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --enable-static

  LC_ALL=C make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}${MINGW_PREFIX}"/share/man/man1
  cp ${srcdir}/${_realname}-${pkgver}/doc/man/* "${pkgdir}${MINGW_PREFIX}"/share/man/man1
}
