# Maintainer: fsagbuya <fa@m-labs.ph>

_realname=nidaqmx
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.0.2
pkgrel=1
pkgdesc="A Python API for interacting with NI-DAQmx (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://nidaqmx-python.readthedocs.io/"
msys2_repository_url="https://github.com/ni/nidaqmx-python"
msys2_references=(
    'purl: pkg:pypi/nidaqmx'
)
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-click"
         "${MINGW_PACKAGE_PREFIX}-python-decouple"
         "${MINGW_PACKAGE_PREFIX}-python-deprecation"
         "${MINGW_PACKAGE_PREFIX}-python-hightime"
         "${MINGW_PACKAGE_PREFIX}-python-numpy"
         "${MINGW_PACKAGE_PREFIX}-python-requests"
         "${MINGW_PACKAGE_PREFIX}-python-tzlocal")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-poetry-core")
options=('!strip')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz"
        "0001-use-poetry-core-backend.patch")
sha256sums=('d71639cd30125c091489de15ad87cc08d5ff267f27791d6f91ce95453cdc1ea6'
            '243c74402bdb75bd9dd668f4e879f300a5bca2156c5969f225f48ab00a4fe899')

prepare() {
  cd "${_realname}-${pkgver}"

  patch -Np1 -i "${srcdir}/0001-use-poetry-core-backend.patch"
}

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
