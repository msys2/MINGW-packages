# Maintainer: gym603 <gui_yuan_miao@163.com>

_realname=protobuf
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}" "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=3.10.0
pkgrel=1
pkgdesc="Protocol Buffers. (mingw-w64)"
arch=('any')
url="https://github.com/protocolbuffers/protobuf"
license=('3-Clause BSD License')
makedepends=("${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools"
             "${MINGW_PACKAGE_PREFIX}-protobuf=${pkgver}")
_archive=${_realname/_/-}-${pkgver}
source=(${_archive}.tar.gz::https://github.com/protocolbuffers/protobuf/archive/v${pkgver}.tar.gz)
sha256sums=('758249b537abba2f21ebc2d02555bf080917f0f2f88f4cbe2903e0e28c4187ed')
noextract=(${_archive}.tar.gz)

_deps=('six' 'setuptools')
_deps2=('ordereddict' 'unittest2')
_deps3=()

prepare() {
  bsdtar -xf ${_archive}.tar.gz || true
}

build() {
  cd "${srcdir}"
  rm -rf python{2,3}-build
  for builddir in python{2,3}-build; do
    msg2 "Building for ${CARCH} ${builddir%-build} ..."
    cp -r ${_archive} ${builddir}
    pushd $builddir/python
    ${MINGW_PREFIX}/bin/${builddir%-build} setup.py --quiet build
    popd
  done
}

package_python3-protobuf() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3" "${_deps[@]/#/${MINGW_PACKAGE_PREFIX}-python3-}" "${_deps3[@]/#/${MINGW_PACKAGE_PREFIX}-python3-}")

  cd "${srcdir}/python3-build/python"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py --quiet install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1

  install -Dm644 ../LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}

package_python2-protobuf() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2" "${_deps[@]/#/${MINGW_PACKAGE_PREFIX}-python2-}" "${_deps2[@]/#/${MINGW_PACKAGE_PREFIX}-python2-}")

  cd "${srcdir}/python2-build/python"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python2 setup.py --quiet install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1

  install -Dm644 ../LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE"
}

package_mingw-w64-i686-python2-protobuf() {
  package_python2-${_realname}
}

package_mingw-w64-i686-python3-protobuf() {
  package_python3-${_realname}
}

package_mingw-w64-x86_64-python2-protobuf() {
  package_python2-${_realname}
}

package_mingw-w64-x86_64-python3-protobuf() {
  package_python3-${_realname}
}
