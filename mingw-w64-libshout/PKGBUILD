# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libshout
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.4.1
pkgrel=1
pkgdesc="Library for accessing a shoutcast/icecast server (mingw-w64)"
arch=("any")
url="http://www.icecast.org/"
license=('LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-libvorbis"
         "${MINGW_PACKAGE_PREFIX}-libtheora"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-speex")
source=("https://downloads.xiph.org/releases/${_realname}/${_realname}-${pkgver}.tar.gz"
        0001-dont-hate-yourself_m4.all.patch
        0001-dont-hate-yourself.all.patch
        0002-msvc-is-impotent-unlike.all.patch
        0004-no-undefined-on.mingw.patch
        0005-update-ac.all.patch
        0006-winsock.patch
        0007-includes.patch)
sha256sums=('f3acb8dec26f2dbf6df778888e0e429a4ce9378a9d461b02a7ccbf2991bbf24d'
            '6f5a4c56567c4be1f4d6e7bb4c8fe2c3129cfee4ca5a6225e4bdc4d5e63e8f99'
            '5e1fcc545cd76b0318fd8f7ae7e4a9e9d11ed5174693a37c72212b7c2cfaa0c9'
            'ba91f165de0207c58a30744666be0c5843ccc501252b799abef6636b04a4e3ef'
            '6966bd722b6d61411427851e44870b4a3600923e165e514dbf5d214dc9c9786d'
            '3ba2abe53fada942e3218df17acc9bc3453330c110f70e79dd20109797f0037f'
            'd75265c43232cd98af06add40fe37dc130f2d6cd68fd8ecc0cf8f79c5d1299bc'
            '9eb0afaa57fc80448283067e64a013d252e491451184d293717c1ecea04848db')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-dont-hate-yourself_m4.all.patch
  patch -p1 -i ${srcdir}/0001-dont-hate-yourself.all.patch
  patch -p1 -i ${srcdir}/0002-msvc-is-impotent-unlike.all.patch
  patch -p1 -i ${srcdir}/0004-no-undefined-on.mingw.patch
  patch -p1 -i ${srcdir}/0005-update-ac.all.patch
  patch -p1 -i ${srcdir}/0006-winsock.patch
  patch -p1 -i ${srcdir}/0007-includes.patch
  autoreconf -fvi
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --enable-static

  make
}

check() {
  cd build-${MINGW_CHOST}
  make -k check
}

package() {
  cd build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
}
