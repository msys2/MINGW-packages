# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libshout
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.3.1
pkgrel=2
pkgdesc="Library for accessing a shoutcast/icecast server (mingw-w64)"
arch=("any")
url="http://www.icecast.org/"
license=('LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-libvorbis"
         "${MINGW_PACKAGE_PREFIX}-libtheora"
         "${MINGW_PACKAGE_PREFIX}-speex")
source=("http://downloads.xiph.org/releases/${_realname}/${_realname}-${pkgver}.tar.gz"
        0001-dont-hate-yourself_m4.all.patch
        0001-dont-hate-yourself.all.patch
        0002-msvc-is-impotent-unlike.all.patch
        0004-no-undefined-on.mingw.patch
        0005-update-ac.all.patch)
md5sums=('11765b2592e7ea623ccd93d3f8df172c'
         'ea9ce2e3b99a5e06b4ad3f6b477c6c0c'
         '3ff7e52df5894d66d8abc9d13e7fda1c'
         '2560cf5cc14fd91d764a7a070ea80aa3'
         'cf9948c4044c7cde0c2be85ee246e157'
         'e94917fc4662c1712f7e0ed727f10851')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-dont-hate-yourself_m4.all.patch
  patch -p1 -i ${srcdir}/0001-dont-hate-yourself.all.patch
  patch -p1 -i ${srcdir}/0002-msvc-is-impotent-unlike.all.patch
  patch -p1 -i ${srcdir}/0004-no-undefined-on.mingw.patch
  patch -p1 -i ${srcdir}/0005-update-ac.all.patch
  autoreconf -fvi
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --enable-static

  make
}

check() {
  cd build-${MINGW_CHOST}
  make -k check
}

package() {
  cd build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
}
