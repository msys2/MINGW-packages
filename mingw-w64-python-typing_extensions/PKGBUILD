# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>
# Contributor: @naveen521kk on Github, Naveen M K <naveen521kk@gmail.com>

_pyname=typing-extensions
_realname=typing_extensions
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=4.0.1
pkgrel=1
pkgdesc='Backported and Experimental Type Hints for Python 3.5+ (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://github.com/python/typing/tree/master/typing_extensions"
license=('PSF')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=(
    "${MINGW_PACKAGE_PREFIX}-python-build"
    "${MINGW_PACKAGE_PREFIX}-python-flit-core"
    "${MINGW_PACKAGE_PREFIX}-python-pip"
)
source=("${_realname}-${pkgver}.tar.gz::https://pypi.python.org/packages/source/${_pyname:0:1}/${_pyname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('4ca091dea149f945ec56afb48dae714f21e8692ef22a395223bcd328961b6a0e')

prepare() {
  cd "${srcdir}"
  rm -rf python-build-${CARCH} | true
  cp -r "${_realname}-${pkgver}" "python-build-${CARCH}"
}

build() {
  msg "Python build for ${CARCH}"
  cd "${srcdir}/python-build-${CARCH}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python -m pip install dist/*.whl \
    --compile \
    --no-deps \
    --no-warn-script-location \
    --ignore-installed \
    --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}"

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
