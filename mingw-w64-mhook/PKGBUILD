# Maintainer: JÃ¼rgen Pfeifer <juergen@familiepfeifer.de>

_realname=mhook
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=r7.a159eed
pkgrel=3
pkgdesc="An API hooking library (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url="https://github.com/SirAnthony/mhook"
msys2_repository_url='https://github.com/SirAnthony/mhook'
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "git")
_commit=a159eed3cd9e71c67ce13fa37fb4ce4ee16dbb38
source=("${_realname}"::"git+https://github.com/SirAnthony/mhook.git#commit=${_commit}"
        010-char.patch)
sha256sums=('8f0c698d215afd0858964f9c8b2ef4ad31eb659ce325a052fb05e5fe0ed0e3f5'
            '2874a5fdbb7ee717c001a7f2cee0f4fa9f60a38bc7ce1589d978b237b1cec884')

pkgver() {
  cd "${srcdir}/${_realname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_realname}"
  patch -p1 -i "${srcdir}"/010-char.patch
}

build() {
  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G "Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
    "${_extra_config[@]}" \
    -DMHOOK_TESTS=OFF \
    -S ${_realname} \
    -B build-${MSYSTEM}

  cmake --build build-${MSYSTEM}
}

check()
{
  cd "${srcdir}/build-${MSYSTEM}"
  ./mhook-tests
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/lib/
  cp libmhook.a "${pkgdir}"${MINGW_PREFIX}/lib/

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/include/
  cp "${srcdir}"/${_realname}/${_realname}-lib/${_realname}.h \
    "${pkgdir}"${MINGW_PREFIX}/include/

  mkdir -p "${pkgdir}"${MINGW_PREFIX}/share/licenses/$_realname
  cp -pf "${srcdir}"/${_realname}/COPYING* \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/$_realname
}
