# Maintainer: JÃ¼rgen Pfeifer <juergen@familiepfeifer.de>

_realname=mhook
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=r7.a159eed
pkgrel=1
arch=('any')
pkgdesc="An API hooking library (mingw-w64)"
depends=("${_mingw_suff}-gcc-libs")
makedepends=("git"
    "${_mingw_suff}-cmake")
options=('strip' 'staticlibs')
license=('custom')
url="https://github.com/SirAnthony/mhook"
source=("$_realname"::"git+https://github.com/SirAnthony/mhook.git")
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/$_realname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir"
  mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}

  ${MINGW_PREFIX}/bin/cmake \
    -G "MSYS Makefiles" \
    -DCMAKE_BUILD_TYPE=Release \
    -DMHOOK_TESTS=ON \
    ../${_realname}
  make V=1
}

check()
{
  cd "$srcdir/build-${MINGW_CHOST}"
  ./mhook-tests
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"

  mkdir -p ${pkgdir}${MINGW_PREFIX}/lib/
  mkdir -p ${pkgdir}${MINGW_PREFIX}/include/
  mkdir -p ${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname

  cp libmhook.a ${pkgdir}${MINGW_PREFIX}/lib/
  cp ${srcdir}/${_realname}/${_realname}-lib/${_realname}.h \
    ${pkgdir}${MINGW_PREFIX}/include/
  cp -pf ${srcdir}/${_realname}/COPYING* \
    ${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname
}
