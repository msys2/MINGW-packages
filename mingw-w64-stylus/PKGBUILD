# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=stylus
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgdesc='Robust, expressive, and feature-rich CSS superset (mingw-w64)'
pkgver=0.54.6
pkgrel=1
arch=('any')
url='https://github.com/stylus/stylus'
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-nodejs")
#makedepends=("${MINGW_PACKAGE_PREFIX}-npm" 'git')
source=(https://registry.npmjs.org/${_realname}/-/${_realname}-$pkgver.tgz)
noextract=(${_realname}-$pkgver.tgz)
sha256sums=('bbe826092c3a88b85642b332b755c99d61954961463032f6613967ecc8ba679a')

package() {
  ${MINGW_PREFIX}/bin/npm install -g --user root --prefix "$pkgdir${MINGW_PREFIX}" "$srcdir"/${_realname}-$pkgver.tgz
  mkdir -p "$pkgdir${MINGW_PREFIX}/share/licenses/${_realname}"
  ln -s "../../../lib/node_modules/${_realname}/LICENSE" "$pkgdir${MINGW_PREFIX}/share/licenses/${_realname}/"
  find $pkgdir -name "*.json" -exec sed -i "s|$(cygpath -m $srcdir)||g" {} \;
}
