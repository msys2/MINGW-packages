# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=geoipupdate
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=4.8.0
pkgrel=1
pkgdesc='Update GeoIP2 and GeoIP Legacy binary databases from MaxMind (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64')
url="https://dev.maxmind.com/geoip/geoipupdate"
license=('spdx:Apache-2.0 OR MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-go")
backup=("${MINGW_PREFIX:1}/etc/GeoIP.conf")
options=('!strip')
source=("https://github.com/maxmind/geoipupdate/archive/v$pkgver/$_realname-$pkgver.tar.gz"
        "001-relocate.patch")
sha256sums=('ca718c3ffcc595ef441363699888d20150f1d3a6583ac2d60bcbd34f052db09f'
            '13a011e60eb044281c41420723f3854b8870c174a7876c3ecd7ccc22393c909f')

prepare() {
  cd "$srcdir/$_realname-$pkgver"

  sed -i 's/EditionIDs .*/EditionIDs GeoLite2-ASN GeoLite2-City GeoLite2-Country/' conf/GeoIP.conf.default

  patch -Nbp1 -i "${srcdir}/001-relocate.patch"
}

build() {
    cd "$srcdir/$_realname-$pkgver"

    export GOROOT=${MINGW_PREFIX}/lib/go

    cd cmd/geoipupdate
    mkdir -p "$srcdir/build"
    go build  -o "$srcdir/build/geoipupdate.exe" -ldflags "-X main.version=$pkgver"
}

package() {
    cd "$srcdir/$_realname-$pkgver"

    install -dm755 "${pkgdir}${MINGW_PREFIX}/var/lib/GeoIP"
    install -Dm600 conf/GeoIP.conf.default "${pkgdir}${MINGW_PREFIX}/etc/GeoIP.conf"

    cd "$srcdir/build"
    install -Dm755 geoipupdate.exe "${pkgdir}${MINGW_PREFIX}/bin/geoipupdate.exe"

    cd "$srcdir/$_realname-$pkgver"
    install -Dm644 LICENSE-APACHE "${pkgdir}${MINGW_PREFIX}/usr/share/licenses/$_realname/LICENSE-APACHE"
    install -Dm644 LICENSE-MIT "${pkgdir}${MINGW_PREFIX}/usr/share/licenses/$_realname/LICENSE-MIT"
}
