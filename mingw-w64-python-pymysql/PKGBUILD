# Contributor: kreijstal <kreijstal@hotmail.com>

_name=PyMySQL
_realname=python-pymysql
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.1.0
pkgrel=1
pkgdesc="Pure Python MySQL Client (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/PyMySQL/PyMySQL/"
license=(MIT)
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python-build"
  "${MINGW_PACKAGE_PREFIX}-python-installer"
  "${MINGW_PACKAGE_PREFIX}-python-setuptools"
  "${MINGW_PACKAGE_PREFIX}-python-wheel"
)
checkdepends=(
  "${MINGW_PACKAGE_PREFIX}-mariadb"
  "${MINGW_PACKAGE_PREFIX}-python-pytest"
)
optdepends=(
  "${MINGW_PACKAGE_PREFIX}-mariadb: for using a local MariaDB instance"
  "${MINGW_PACKAGE_PREFIX}-python-cryptography: for RSA cryptography"
  "${MINGW_PACKAGE_PREFIX}-python-pynacl: for ed25519 cryptography"
)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('51dddc92e766183ab5d93717121d853415561dc81abc7ec8299a73610cae5bc22b6070006d6ea24405ab31bc1ea68be61466a3477c0f84d47435f78ffc60d31e')
b2sums=('f793d8d409c9bf4316ac13cd249e63d2d11aaa929c30e90b603265308a4704d6ec58c6f5b412bf3f6328b0bc80326e0dce41b8aee7baa55111df177d4634a0cc')

build() {
  cd $_name-$pkgver
  ${MINGW_PREFIX}/bin/python -m build --wheel --no-isolation --skip-dependency-check 
}

# NOTE: tests require a running instance of mariadb
# check() {
#   local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

#   cd $_name-$pkgver
#   python -m installer --destdir=test_dir dist/*.whl
#   export PYTHONPATH="test_dir/$site_packages:$PYTHONPATH"
#   pytest -vv
# }

package() {
  cd $_name-$pkgver
  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl
  install -vDm 644 {CHANGELOG,README}.md -t "${pkgdir}${MINGW_PREFIX}/share/doc/$pkgname/"
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${pkgname}/LICENSE"
}
