# Maintainer: Wu, Zhenyu <wuzhenyu@ustc.edu>

_realname=neocmakelsp
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.8.1
pkgrel=1
pkgdesc='CMake LSP implementation based on Tower and Tree-sitter'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/Decodetalkers/neocmakelsp'
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-ninja")
source=("https://github.com/Decodetalkers/neocmakelsp/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('23d2fd6f6bd0152dad9b6bdf9b5d6932d97ccd106bfb47d6d6fee563ea5a7eec')

prepare() {
  cd "${_realname}-${pkgver}"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  MSYS2_ARG_CONV_EXCL="--prefix=" \
    meson setup --prefix="${MINGW_PREFIX}" "build-${MSYSTEM}" "${_realname}-${pkgver}"

  meson compile -C "build-${MSYSTEM}"
}

check() {
  cd "${_realname}-${pkgver}"

  cargo test --release --frozen
}

package() {
  meson install -C "build-${MSYSTEM}" --destdir "${pkgdir}"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
