# Maintainer: Wu, Zhenyu <wuzhenyu@ustc.edu>

_realname=neocmakelsp
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.10.1
pkgrel=1
pkgdesc='CMake LSP implementation based on Tower and Tree-sitter (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_repository_url='https://github.com/neocmakelsp/neocmakelsp/'
url='https://neocmakelsp.github.io/'
msys2_references=(
  'purl: pkg:cargo/neocmakelsp'
)
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-cmake")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-ninja")
source=("https://github.com/neocmakelsp/neocmakelsp/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        0001-neocmakelsp-meson-disable-cargo-build.patch)
sha256sums=('58f4338fef0012858aeec961873948f37e9bc0167d68ae4cc1b5f458e4206610'
            'a86fcc66a2811849eedf22d82a5dd470ad1482a8126f070cfaebecb29f837403')

prepare() {
  cd "${_realname}-${pkgver}"

  patch -p1 -i ../0001-neocmakelsp-meson-disable-cargo-build.patch

  cargo fetch --locked --target "${RUST_CHOST}"
}

build() {
  cargo build \
    --release \
    --frozen \
    --manifest-path="${_realname}-${pkgver}"/Cargo.toml

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    meson setup \
      --prefix="${MINGW_PREFIX}" \
      -Ddebug=false \
      "build-${MSYSTEM}" \
      "${_realname}-${pkgver}"

  meson compile -C "build-${MSYSTEM}"
}

check() {
  cargo test \
    --release \
    --frozen \
    --manifest-path="${_realname}-${pkgver}"
}

package() {
  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path "${_realname}-${pkgver}" \
    --root "${pkgdir}${MINGW_PREFIX}"

  meson install -C "build-${MSYSTEM}" --destdir "${pkgdir}"

  install -Dm644 "${_realname}-${pkgver}"/LICENSE \
    "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
