# Maintainer: Wu, Zhenyu <wuzhenyu@ustc.edu>

_realname=neocmakelsp
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.9.1
pkgrel=1
pkgdesc='CMake LSP implementation based on Tower and Tree-sitter (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_repository_url='https://github.com/neocmakelsp/neocmakelsp/'
url='https://neocmakelsp.github.io/'
msys2_references=(
  'purl: pkg:cargo/neocmakelsp'
)
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-cmake")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-ninja")
source=("https://github.com/neocmakelsp/neocmakelsp/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        0001-neocmakelsp-meson-disable-cargo-build.patch)
sha256sums=('e3369930e42967d661eca3ac97990f01bac04bd72fb198d954911f995f0220a5'
            'a86fcc66a2811849eedf22d82a5dd470ad1482a8126f070cfaebecb29f837403')

prepare() {
  cd "${_realname}-${pkgver}"

  patch -p1 -i "${srcdir}/0001-neocmakelsp-meson-disable-cargo-build.patch"

  cargo fetch --locked --target "${RUST_CHOST}"
}

build() {
  cd "${_realname}-${pkgver}"

  cargo build --release --frozen

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    meson setup \
      --prefix="${MINGW_PREFIX}" \
      -Ddebug=false \
      "${srcdir}/build-${MSYSTEM}"

  meson compile -C "${srcdir}/build-${MSYSTEM}"
}

check() {
  cd "${_realname}-${pkgver}"

  cargo test --release --frozen
}

package() {
  cd "${_realname}-${pkgver}"

  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  meson install -C "${srcdir}/build-${MSYSTEM}" --destdir "${pkgdir}"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
