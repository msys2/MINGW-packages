# Maintainer: Tao Zuhong <taozuhong@hotmail.com>

_realname=sqlite3mc
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.8.1
pkgrel=1
url="https://github.com/utelle/SQLite3MultipleCiphers"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
pkgdesc="SQLite3 encryption extension with support for multiple ciphers (mingw-w64)"
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-pkgconf")
source=("SQLite3MultipleCiphers-$pkgver.tar.gz::https://github.com/utelle/SQLite3MultipleCiphers/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('44215d5812ec2cfa1a842d9aae2908be0b14dce4231bc3243394b24b3affb78a')

prepare() {
  cd "${srcdir}/SQLite3MultipleCiphers-${pkgver}"
  
  autoreconf -fiv
}

build() {
  cd "${srcdir}/SQLite3MultipleCiphers-${pkgver}"
  
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  
  ../"SQLite3MultipleCiphers-${pkgver}"/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target="${MINGW_CHOST}" \
    --enable-static \
    --enable-shared
  
  make
}


package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  
  make -j1 DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/SQLite3MultipleCiphers-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
