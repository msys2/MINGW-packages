# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=httplib2
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}" "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=0.9.2
pkgrel=2
pkgdesc="Comprehensive HTTP client library, supporting many features (mingw-w64)"
arch=('any')
license=('MIT')
url="https://github.com/jcgregorio/httplib2"
depends=("${MINGW_PACKAGE_PREFIX}-python2"
         "${MINGW_PACKAGE_PREFIX}-python3"
         "${MINGW_PACKAGE_PREFIX}-ca-certificates")
makedepends=("${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-python2-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
             #"git"
            )
source=(${_realname}-${pkgver}.tar.gz::https://github.com/jcgregorio/httplib2/archive/${pkgver}.tar.gz
        ssl_hostname.patch
        cert.patch)
sha256sums=('ce1041b5780737ac13521479fefcdc3c1b9ec2f2d5970307fb56ec49364a6290'
            'd29fa108291a24f0708f04867f0479247ecbdd1fcf1617588b6650038d95554a'
            '40ea256bbea834b7b350151e909316f8bc802a164ca50cf6dbf4a2a4565350c3')

prepare() {
  pushd "${_realname}-${pkgver}"

  # Patch, ref FS#36839
  patch -p1 -i "${srcdir}/ssl_hostname.patch"

  # Patch, ref FS#40179
  patch -p1 -i "${srcdir}/cert.patch"

  popd

  # Prepare one build directory for each package
  cp -a "${_realname}-${pkgver}" build-python2
  cp -a "${_realname}-${pkgver}" build-python3
}

build() {
  echo "Empty"
}

package_python2-httplib2() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2" "${MINGW_PACKAGE_PREFIX}-ca-certificates")

  cd "${srcdir}/build-python2"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX#\/} --root="${pkgdir}" --optimize=1

  #install -Dm644 LICENSE \
  #  "${pkgdir}${MINGW_PREFIX}"/share/licenses/python2-${_realname}/LICENSE
}

package_python3-httplib2() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3" "${MINGW_PACKAGE_PREFIX}-ca-certificates")

  cd "${srcdir}/build-python3"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX#\/} --root="${pkgdir}" --optimize=1

  #install -Dm644 LICENSE \
  #  "${pkgdir}${MINGW_PREFIX}"/share/licenses/python3-${_realname}/LICENSE
}

package_mingw-w64-i686-python2-httplib2() {
  package_python2-httplib2
}

package_mingw-w64-i686-python3-httplib2() {
  package_python3-httplib2
}

package_mingw-w64-x86_64-python2-httplib2() {
  package_python2-httplib2
}

package_mingw-w64-x86_64-python3-httplib2() {
  package_python3-httplib2
}
