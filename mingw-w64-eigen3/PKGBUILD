# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=eigen
pkgbase=mingw-w64-${_realname}3
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}3")
pkgver=3.3.8
pkgrel=1
pkgdesc="Lightweight C++ template library for vector and matrix math (mingw-w64)"
arch=('any')
url='https://eigen.tuxfamily.org/'
license=('MPL2')
options=('strip' 'staticlibs')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-doxygen"
             "${MINGW_PACKAGE_PREFIX}-fftw"
             "${MINGW_PACKAGE_PREFIX}-boost"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-mpfr"
             )
source=("https://gitlab.com/libeigen/eigen/-/archive/${pkgver}/eigen-${pkgver}.tar.bz2"
        'eigen-3.1.2_osversion.patch'
        'TryRunResults.cmake'
        'eigen-pkgconfig.patch'
        'install_FindEigen3.patch'
        'eigen-bug2011.patch'
        'eigen-paradiso.patch')
sha256sums=('0215c6593c4ee9f1f7f28238c4e8995584ebf3b556e9dbf933d84feb98d5b9ef'
            '604b5c33e98a5873a81bb92a734dbf30547b482fde519aed5abc5e9ff2dffc6b'
            '9ebec4761a7de150ade4b421407a4b5148065416fa8010ecfbdd404bab68f899'
            '38d41a475a65de5ad92d1939c9f4fa7460b8152d5beb7256bcbaf630460b5c89'
            'c219f577e51571482fdfb38aec104c806cc6ee4c455627d70e99e59d2df7c32d'
            'c04d624d550b119be0f810786baba7e0d7809edefd4854a2db6dbd98a7da5a7d'
            '14c239a5b6c1efe1c2e6f33fc9c449a500b74ded2e865ca730b213c142f1f750')

prepare() {
  cd "${srcdir}"/eigen-${pkgver}

  patch -Np2 -i "${srcdir}/eigen-3.1.2_osversion.patch"
  patch -Np1 -i "${srcdir}/eigen-pkgconfig.patch"
  patch -Np1 -i "${srcdir}/install_FindEigen3.patch"

  # Upstream patches
  patch -Np1 -i "${srcdir}/eigen-bug2011.patch"
  patch -Np1 -i "${srcdir}/eigen-paradiso.patch"
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DEIGEN_BUILD_PKGCONFIG=ON \
    -DFFTW_USE_STATIC_LIBS=ON \
    -DFFTW_ROOT=${MINGW_PREFIX} \
    ../eigen-${pkgver}

  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
}
