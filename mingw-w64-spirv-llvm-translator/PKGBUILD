# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_srcname=SPIRV-LLVM-Translator
_realname=spirv-llvm-translator
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=16.0.0
pkgrel=2
pkgdesc='Tool and a library for bi-directional translation between SPIR-V and LLVM IR (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://github.com/KhronosGroup/SPIRV-LLVM-Translator'
license=(custom)
depends=("${MINGW_PACKAGE_PREFIX}-llvm-libs"
         "${MINGW_PACKAGE_PREFIX}-spirv-tools")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-llvm"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-spirv-headers")
source=("https://github.com/KhronosGroup/SPIRV-LLVM-Translator/archive/v${pkgver}/${_srcname}-${pkgver}.tar.gz"
        "0001-Fix-linking-with-LLVM-components.patch")
sha256sums=('305fac5bb8efdad9054f0d27b5b765aca8b3349a500e2ba0c927763e42badc2b'
            '28322c4284266f013c83eee733f64e4d6d08164fdeb72af84579b5788ed96d24')

prepare() {
  cd "${srcdir}"/${_srcname}-${pkgver}
  patch -p1 -i "${srcdir}"/0001-Fix-linking-with-LLVM-components.patch
}

build() {
  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  mkdir -p "${srcdir}/build-${MSYSTEM}-static" && cd "${srcdir}/build-${MSYSTEM}-static"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DLLVM_EXTERNAL_SPIRV_HEADERS_SOURCE_DIR=${MINGW_PREFIX}/include/spirv \
      "${_extra_config[@]}" \
      -DBUILD_SHARED_LIBS=OFF \
      -DLLVM_SPIRV_INCLUDE_TESTS=OFF \
      ../${_srcname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake.exe --build ./
  ${MINGW_PREFIX}/bin/cmake.exe --build ./ --target llvm-spirv

  mkdir -p "${srcdir}/build-${MSYSTEM}-shared" && cd "${srcdir}/build-${MSYSTEM}-shared"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DLLVM_EXTERNAL_SPIRV_HEADERS_SOURCE_DIR=${MINGW_PREFIX}/include/spirv \
      "${_extra_config[@]}" \
      -DBUILD_SHARED_LIBS=ON \
      -DLLVM_SPIRV_INCLUDE_TESTS=OFF \
      ../${_srcname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake.exe --build ./
  ${MINGW_PREFIX}/bin/cmake.exe --build ./ --target llvm-spirv
}

package() {
  cd "${srcdir}/build-${MSYSTEM}-static"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install ./

  cd "${srcdir}/build-${MSYSTEM}-shared"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install ./
  install -Dm755 tools/llvm-spirv/llvm-spirv.exe "${pkgdir}${MINGW_PREFIX}/bin"

  install -Dm644 "${srcdir}/${_srcname}-${pkgver}/LICENSE.TXT" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.TXT"
}
