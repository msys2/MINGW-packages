# Maintainer: Lucio Perusini <lucioperusini@gmail.com>

_realname=i2pd
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=2.40.0
pkgrel=2
pkgdesc='A full-featured C++ implementation of the I2P router (mingw-w64)'
url='https://i2pd.website/'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-boost"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-miniupnpc")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc")
options=('strip')
source=("${_realname}-${pkgver}.tar.gz::https://github.com/PurpleI2P/i2pd/archive/${pkgver}.tar.gz"
        "0001-i2pd-endian.patch"
        "0001-i2pd-makefile.patch")
sha256sums=('4443f484ad40753e892170a26c8ee8126e8338bf416d04eab0c55c1c94a4e193'
            'ea7b99f8f814cab85e6905174f237241bbbf72ae71c1c8d8149162e195e10597'
            '2cb0199a381471397ebfb97bad8d8a3bd006745dd2d07fcdb7e3a1ba616eae5d')
noextract=("${_realname}-${pkgver}.tar.gz") # symlinks
prepare() {
  cd "${srcdir}"
  bsdtar -xf "${_realname}-${pkgver}.tar.gz" || true
  cd "${_realname}-${pkgver}"
  patch -Np1 -i "${srcdir}"/0001-i2pd-endian.patch
  patch -Np1 -i "${srcdir}"/0001-i2pd-makefile.patch
}

build() {
  cp -rf "${_realname}-${pkgver}" "build-${MSYSTEM}"
  cd "${srcdir}"/build-${MSYSTEM}

  make USE_UPNP=yes
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}

  #bin
  install -d "${pkgdir}${MINGW_PREFIX}/bin/"
  cp i2pd.exe "${pkgdir}${MINGW_PREFIX}/bin/"

  #config
  install -d "${pkgdir}${MINGW_PREFIX}/etc/i2pd"
  cp contrib/{i2pd,tunnels}.conf "${pkgdir}${MINGW_PREFIX}/etc/i2pd"
  install -d "${pkgdir}${MINGW_PREFIX}/etc/i2pd/tunnels.d"
  cp contrib/tunnels.d/{*.conf,README} "${pkgdir}${MINGW_PREFIX}/etc/i2pd/tunnels.d"

  # certificates
  install -d "${pkgdir}${MINGW_PREFIX}/share/i2pd"
  cp -r contrib/certificates "${pkgdir}${MINGW_PREFIX}/share/i2pd"

  # license
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
}
