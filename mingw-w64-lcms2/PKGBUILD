# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=lcms2
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.6
pkgrel=4
pkgdesc="Small-footprint color management engine, version 2 (mingw-w64)"
arch=(any)
url="http://www.littlecms.com"
license=("MIT")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" "${MINGW_PACKAGE_PREFIX}-libtiff")
options=('staticlibs' 'strip')
source=("http://downloads.sourceforge.net/sourceforge/lcms/lcms2-${pkgver}.tar.gz"
        0001-fix-testcmd2.mingw.patch
        0002-need-jconfig-before-jmoreconfig.mingw.patch
        0003-install-mans.all.patch)
md5sums=('f4c08d38ceade4a664ebff7228910a33'
         '495c11d0c18cd754acaf19740028a919'
         '4017f8307298d6f65e1cb5bce9684fa5'
         '847c756913571406528a30807374b5a7')

prepare() {
  cd ${_realname}-$pkgver
  patch -p1 -i ${srcdir}/0001-fix-testcmd2.mingw.patch
  patch -p1 -i ${srcdir}/0002-need-jconfig-before-jmoreconfig.mingw.patch
  patch -p1 -i ${srcdir}/0003-install-mans.all.patch

  autoreconf -fiv
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  cp -rf ${srcdir}/${_realname}-${pkgver} "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  ./configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --enable-shared \
    --enable-static

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
