# Maintainer: Mazhar Hussain <realmazharhussain@gmail.com>

_reponame=gst-plugins-rs
_realname=gst-plugin-gtk4
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.10.6
pkgrel=1
arch=(any)
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
pkgdesc="Multimedia graph framework - GTK4 plugin (mingw-w64)"
url="https://gitlab.freedesktop.org/gstreamer/gst-plugins-rs"
depends=("${MINGW_PACKAGE_PREFIX}-gstreamer"
         "${MINGW_PACKAGE_PREFIX}-gtk4")
makedepends=(${MINGW_PACKAGE_PREFIX}-pkgconf
             "${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-cargo-c")
license=("spdx:MPL-2.0")
source=("${url}/-/archive/$pkgver/gst-plugins-rs-$pkgver.tar.bz2")
sha256sums=(90af993b28f050e2d682582ca1bde440665e75a5fbaec6577cafc609e24f2d92)

prepare() {
  cd $_reponame-$pkgver
  cargo fetch --manifest-path video/gtk4/Cargo.toml
}

package() {
  cd $_reponame-$pkgver

  MSYS_ARG_CONV_EXCL="--prefix=" \
  cargo cinstall -p gst-plugin-gtk4 \
    --prefix=${MINGW_PREFIX} \
    --destdir="$pkgdir"
}
