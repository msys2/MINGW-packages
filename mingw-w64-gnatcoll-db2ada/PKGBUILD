# Maintainer: Philipp Smirnov <psmirnov@airmail.cc>

_realname=gnatcoll-db2ada
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.r2874.68c48d5
pkgrel=1
_branch=24.0
pkgdesc="GNAT Components Collection - Tool to generate Ada code from database schemas (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
msys2_references=(
  'aur: gnatcoll-db2ada'
)

url="https://github.com/AdaCore/gnatcoll-db"
license=('GPL-3.0-or-later WITH GCC-exception-3.1')
makedepends=("${MINGW_PACKAGE_PREFIX}-gprbuild")
depends=("${MINGW_PACKAGE_PREFIX}-gnatcoll"
         "${MINGW_PACKAGE_PREFIX}-gnatcoll-sql"
         "${MINGW_PACKAGE_PREFIX}-python3")
source=("${_realname}::git+https://github.com/AdaCore/gnatcoll-db.git#branch=${_branch}")
sha256sums=('SKIP')

pkgver() {
  cd ${_realname}
  printf "%s.r%s.%s" "$_branch" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
  cd "${srcdir}/${_realname}/gnatcoll_db2ada"

  make BUILD=PROD PROCESSORS="$(nproc)"
}

package() {
  cd "${srcdir}/${_realname}/gnatcoll_db2ada"

  make DESTDIR="${pkgdir}${MINGW_PREFIX}" install

  # Copy License Files
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" \
    "${srcdir}/${_realname}"/COPYING*
}
