_realname=gnatcoll-db2ada
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.0
pkgrel=1
pkgdesc="GNAT Components Collection - Tool to generate Ada code from database schemas (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
msys2_references=(
  'aur: gnatcoll-db2ada'
)

url="https://github.com/AdaCore/gnatcoll-db"
license=('spdx:GPL-3.0-or-later WITH GCC-exception-3.1')
makedepends=("${MINGW_PACKAGE_PREFIX}-gprbuild")
depends=("${MINGW_PACKAGE_PREFIX}-gnatcoll"
         "${MINGW_PACKAGE_PREFIX}-gnatcoll-sql"
         "${MINGW_PACKAGE_PREFIX}-python3")
source=("https://github.com/AdaCore/gnatcoll-db/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('d284ea84051a2338cc467e7b58ea10334c423dadf3402b473a82a7a5b7f768c6')

build() {
  cd "${srcdir}/${_realname}-${pkgver}/gnatcoll_db2ada"

  make BUILD=PROD PROCESSORS="$(nproc)"
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}/gnatcoll_db2ada"

  make DESTDIR="${pkgdir}${MINGW_PREFIX}" install

  # Copy License Files
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" \
    "${srcdir}/${_realname}-${pkgver}"/COPYING*
}
