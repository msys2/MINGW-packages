# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=h2
pkgbase=mingw-w64-python-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-${_realname}
pkgver=4.1.0
pkgrel=1
pkgdesc='HTTP/2 State-Machine based protocol implementation (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=('MIT')
url='https://github.com/python-hyper/h2'
depends=(
    ${MINGW_PACKAGE_PREFIX}-python
    ${MINGW_PACKAGE_PREFIX}-python-hyperframe
    ${MINGW_PACKAGE_PREFIX}-python-hpack
)
makedepends=(${MINGW_PACKAGE_PREFIX}-python-setuptools)
source=(https://github.com/python-hyper/${_realname}/archive/v${pkgver}.tar.gz)
sha256sums=('4a17b1ec23152bd94ab6260ea61f6cc02de92bd7c36762f13e34fdecea454b41')

prepare() {
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}"
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
