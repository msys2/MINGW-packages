# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=h2
pkgbase=mingw-w64-python-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-${_realname}
pkgver=4.1.0
pkgrel=5
pkgdesc='HTTP/2 State-Machine based protocol implementation (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/python-hyper/h2'
msys2_references=(
  'pypi: h2'
)
license=('spdx:MIT')
depends=(
    ${MINGW_PACKAGE_PREFIX}-python
    ${MINGW_PACKAGE_PREFIX}-python-hyperframe
    ${MINGW_PACKAGE_PREFIX}-python-hpack
)
makedepends=(${MINGW_PACKAGE_PREFIX}-python-build
             ${MINGW_PACKAGE_PREFIX}-python-installer
             ${MINGW_PACKAGE_PREFIX}-python-setuptools)
source=(https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz)
sha256sums=('a83aca08fbe7aacb79fec788c9c0bac936343560ed9ec18b82a13a12c28d2abb')

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}"/python-build-${MSYSTEM}

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
