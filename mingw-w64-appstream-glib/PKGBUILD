# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=appstream-glib
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.7.16
pkgrel=1
arch=('any')
pkgdesc="Objects and methods for reading and writing AppStream metadata (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2"
         "${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-gtk2"
         "${MINGW_PACKAGE_PREFIX}-gtk3"
         "${MINGW_PACKAGE_PREFIX}-json-glib"
         "${MINGW_PACKAGE_PREFIX}-libyaml"
         "${MINGW_PACKAGE_PREFIX}-libsoup"
         "${MINGW_PACKAGE_PREFIX}-gtksourceview3"
         "${MINGW_PACKAGE_PREFIX}-iso-codes"
         "${MINGW_PACKAGE_PREFIX}-libpeas"
         "${MINGW_PACKAGE_PREFIX}-libsoup")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             "${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-vala"
             "${MINGW_PACKAGE_PREFIX}-gtk-doc")
options=('strip' 'staticlibs')
license=("LGPL")
url="https://people.freedesktop.org/~hughsient/appstream-glib/"
source=(${_realname}-${pkgver}.tar.gz::"https://github.com/hughsie/appstream-glib/archive/appstream_glib_${pkgver//./_}.tar.gz"
        windows-support.patch)
sha256sums=('71e19fff681eb74795dbcb428f1dc9cf28bf9207378b6c1c9ce55375f0f22b14'
            'bc909cd7c5aa1874cc2a125b31919125ce8daf62a6c5c8b430eb9aaa1af91db8')

prepare() {
  cd "${srcdir}"/${_realname}-${_realname//-/_}_${pkgver//./_}
  patch -p1 -i ${srcdir}/windows-support.patch
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}

  ${MINGW_PREFIX}/bin/meson \
    --buildtype=plain \
    -Dintrospection=true \
    -Dgtk-doc=true \
    -Dman=true \
    -Dfonts=true \
    -Dstemmer=false \
    -Dalpm=false \
    -Drpm=false \
    -Ddep11=false \
    -Dbuilder=false \
    "../${_realname}-${_realname//-/_}_${pkgver//./_}"
  ninja
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  DESTDIR="${pkgdir}${MINGW_PREFIX}" ninja install
  local _PRE_WIN="$(cygpath -m ${MINGW_PREFIX})"
  sed -e "s|${_PRE_WIN}|${MINGW_PREFIX}|g" \
    -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/appstream-glib.pc \
    -i ${pkgdir}${MINGW_PREFIX}/share/installed-tests/appstream-glib/appdata-validate.test \
    -i ${pkgdir}${MINGW_PREFIX}/share/installed-tests/appstream-glib/destdir-check.test

  install -Dm644 "${srcdir}/${_realname}-${_realname//-/_}_${pkgver//./_}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
