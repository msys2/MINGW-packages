_realname=ada_language_server
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.3
pkgrel=1

pkgdesc="Server implementing the Microsoft Language Protocol for Ada and SPARK (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
msys2_references=(
  'aur: ada_language_server'
)

url="https://github.com/AdaCore/ada_language_server"
license=('GPL3')
makedepends=("${MINGW_PACKAGE_PREFIX}-gprbuild")
depends=("${MINGW_PACKAGE_PREFIX}-ada_spawn"
         "${MINGW_PACKAGE_PREFIX}-libvss"
         "${MINGW_PACKAGE_PREFIX}-libadalang"
         "${MINGW_PACKAGE_PREFIX}-ada-libfswatch"
         "${MINGW_PACKAGE_PREFIX}-lal-refactor"
         "${MINGW_PACKAGE_PREFIX}-libadalang-tools")
source=("https://github.com/AdaCore/ada_language_server/archive/${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('3118cfdb4478e8c79d681b3dea3a7771d4ded8c7fd3f0ccf7d935ba647919c9f')

build() {
  cd "${srcdir}/${_realname}-${pkgver}/"

  make BUILD_MODE=prod LIBRARY_TYPE=relocatable

  LIBRARY_TYPE=relocatable \
  gprbuild gnat/lsp_client_glib.gpr
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}/"

  make install BUILD_MODE=prod LIBRARY_TYPE=relocatable prefix="${pkgdir}${MINGW_PREFIX}"

  LIBRARY_TYPE=relocatable \
  gprinstall --prefix="${pkgdir}${MINGW_PREFIX}" -p gnat/lsp_client_glib.gpr

  # Copy License Files
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" \
    "${srcdir}/${_realname}-${pkgver}"/LICENSE
}
