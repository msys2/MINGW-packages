# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=opencl-headers
_mingw_suff=mingw-w64-${CARCH}
pkgbase=mingw-w64-${_realname}
pkgname="${_mingw_suff}-${_realname}"
_clbasever=2.0
pkgver=${_clbasever}.0
pkgrel=2
pkgdesc="OpenCL (Open Computing Language) header files (mingw-w64)"
arch=('any')
url="http://www.khronos.org/registry/cl/"
license=("custom")
options=('staticlibs' 'strip')
source=("${_realname}-${pkgver}.src.tar.xz"
        'LICENSE.txt')
_headers=("http://www.khronos.org/registry/cl/api/${_clbasever}/opencl.h"
          "http://www.khronos.org/registry/cl/api/${_clbasever}/cl_platform.h"
          "http://www.khronos.org/registry/cl/api/${_clbasever}/cl.h"
          "http://www.khronos.org/registry/cl/api/${_clbasever}/cl_ext.h"
          "http://www.khronos.org/registry/cl/api/${_clbasever}/cl_egl.h"
          "http://www.khronos.org/registry/cl/api/${_clbasever}/cl_dx9_media_sharing.h"
          "http://www.khronos.org/registry/cl/api/${_clbasever}/cl_d3d10.h"
          "http://www.khronos.org/registry/cl/api/${_clbasever}/cl_d3d11.h"
          "http://www.khronos.org/registry/cl/api/${_clbasever}/cl_gl.h"
          "http://www.khronos.org/registry/cl/api/${_clbasever}/cl_gl_ext.h")
sha256sums=('8428893de1889cb8d55edc3ee93c7a5b635a993e6d7d33edb65207145a8507e3'
            'd1059405dce021e0c0a7f7e01b48447e641477e0c14a9bc29983f4202b67c410')

# source PKGBUILD && mksource
mksource() {
   cd "${srcdir}"
   _dirname=${_realname}-${pkgver}
   mkdir ${_dirname}

   pushd ${_dirname}
   for _h in ${_headers[@]}; do
      wget ${_h}
   done
   popd

   tar -cJv --exclude=CVS -f ${_dirname}.src.tar.xz ${_dirname}
   rm -rf ${_dirname}
}

package() {
   cd "${srcdir}/${_realname}-${pkgver}"

   install -dm755 "${pkgdir}${MINGW_PREFIX}"/include/CL

   for h in $(ls -1 *.h *.hpp); do
      install -m 644 ${h} "${pkgdir}${MINGW_PREFIX}"/include/CL/
   done

   install -D -m644 ../LICENSE.txt "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSE
}
