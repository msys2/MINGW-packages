# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>

_realname=ruamel-yaml
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.17.21
pkgrel=1
pkgdesc='YAML parser/emitter that supports roundtrip preservation of comments, seq/map flow style, and map key order (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://sourceforge.net/projects/ruamel-yaml/"
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python-ruamel.yaml.clib")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools")
options=('!emptydirs')
source=("https://files.pythonhosted.org/packages/source/${_realname:0:1}/${_realname//-/.}/${_realname//-/.}-${pkgver}.tar.gz")
sha256sums=('8b7ce697a2f212752a35c1ac414471dc16c424c9573be4926b56ff3f5d23b7af')

prepare() {
  rm -rf "python-build-${MSYSTEM}" | true
  cp -r "${_realname//-/.}-${pkgver}" "python-build-${MSYSTEM}"
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  RUAMEL_NO_PIP_INSTALL_CHECK=1 ${MINGW_PREFIX}/bin/python setup.py install \
    --prefix=${MINGW_PREFIX} --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
