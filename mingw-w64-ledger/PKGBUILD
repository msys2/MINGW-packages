
_realname=ledger
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.3.2
pkgrel=3
pkgdesc="Double-entry accounting system with a command-line reporting interface. (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
makedepends=("${MINGW_PACKAGE_PREFIX}-"{cmake,cc,ninja,utf8cpp,python})
depends=("${MINGW_PACKAGE_PREFIX}-"{gcc-libs,boost,mpfr,gmp})
url="https://ledger-cli.org/"
msys2_repository_url="https://github.com/ledger/ledger"
msys2_references=(
  'archlinux: ledger'
  "cpe: cpe:2.3:a:ledger-cli:ledger"
)
license=("spdx:BSD-3-Clause")
source=("https://github.com/ledger/ledger/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "001-fix-build-with-boost-1.85.patch::https://github.com/ledger/ledger/commit/1da89f6f.patch")
sha256sums=('555296ee1e870ff04e2356676977dcf55ebab5ad79126667bc56464cb1142035'
            '4f39401338535a57078e7dc183d5a2eb7ccf9907d43219065cab7d00085c75a0')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/001-fix-build-with-boost-1.85.patch
}

build() {
  mkdir -p "${srcdir}"/build-${MSYSTEM} &&  cd "${srcdir}"/build-${MSYSTEM}

  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release" "-DBUILD_DEBUG=OFF")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug"  "-DBUILD_DEBUG=ON")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe -Wno-dev \
    -GNinja\
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    "${_extra_config[@]}" \
    -DBUILD_DOCS=OFF \
    -DBUILD_LIBRARY=OFF \
    -DBUILD_WEB_DOCS=OFF \
    -DCMAKE_GNUtoMS=OFF \
    -DUSE_DOXYGEN=OFF \
    -DUSE_GPGME=OFF \
    -DUSE_PYTHON=OFF \
    -DPython_EXECUTABLE=${MINGW_PREFIX}/bin/python \
    ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake.exe --build .
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}

  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install .

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/LICENSE.md -t "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}
}
