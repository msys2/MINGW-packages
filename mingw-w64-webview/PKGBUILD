# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=webview
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
_commit=5c5db3f31bf644f673d9dc817e935815b590c30e
pkgver=0.12.5c5db3f
pkgrel=2
pkgdesc="A tiny cross-platform webview library for C/C++ to build modern cross-platform GUIs. (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://github.com/webview/webview'
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
        "${MINGW_PACKAGE_PREFIX}-webview2-loader")
makedepends=("base-devel"
    "git"
    "${MINGW_PACKAGE_PREFIX}-cc"
    "${MINGW_PACKAGE_PREFIX}-cmake"
    "${MINGW_PACKAGE_PREFIX}-ninja")
source=("git+https://github.com/webview/webview.git#commit=$_commit"
        "webview.pc.in")
sha256sums=('SKIP'
            '7ec83c95556bf5a37e34668a2b83665996986240c2f6a2720d6714afcbe561b7')

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DWEBVIEW_BUILD_SHARED_LIBRARY=ON \
      -DWEBVIEW_BUILD_STATIC_LIBRARY=ON \
      -DWEBVIEW_BUILD_DOCS=OFF \
      -DWEBVIEW_BUILD_EXAMPLES=OFF \
      -DWEBVIEW_BUILD_TESTS=OFF \
      -DWEBVIEW_ENABLE_CHECKS=OFF \
      -DWEBVIEW_ENABLE_PACKAGING=OFF \
      "${extra_config[@]}" \
      -S "${_realname}" \
      -B "build-${MSYSTEM}"

  cmake --build "build-${MSYSTEM}"
}

package() {
  DESTDIR="${pkgdir}" cmake --install "build-${MSYSTEM}"

  cd "${srcdir}/build-${MSYSTEM}"
  cp "${srcdir}"/webview.pc.in webview.pc
  sed -i "s#MINGW_PREFIX#${MINGW_PREFIX}#g" webview.pc
  sed -i "s#VERSION#${pkgver}#g" webview.pc

  install -Dm644 webview.pc "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/webview.pc"
  install -Dm644 ${srcdir}/${_realname}/LICENSE \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
