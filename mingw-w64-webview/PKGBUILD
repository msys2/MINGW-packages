# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=webview
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}"
         "${MINGW_PACKAGE_PREFIX}-${_realname}-docs")
pkgver=0.12.0
pkgrel=2
pkgdesc="A tiny cross-platform webview library for C/C++ to build modern cross-platform GUIs. (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://github.com/webview/webview'
msys2_repository_url='https://github.com/webview/webview'
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-webview2-loader")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-doxygen"
             "${MINGW_PACKAGE_PREFIX}-graphviz"
             "${MINGW_PACKAGE_PREFIX}-ninja")
source=("https://github.com/webview/webview/archive/${pkgver}/${_realname}-${pkgver}.tar.gz"
        '0001-webview-0.12.0-use-mdfile-as-mainpage.patch')
sha256sums=('e2c8d0bed3fcd13e624074448b7d3b1d0b0d03546a15839f17ee0013f2cba1db'
            '1f2bf8b3855cbfa59b17fc34efce8a40ccb3945bfe014c70466c235387296bd4')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -p1 -i "${srcdir}"/0001-webview-0.12.0-use-mdfile-as-mainpage.patch
}

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  _mswebview2_version=1.$(sed -n '/CORE_WEBVIEW_TARGET_PRODUCT_VERSION/ s/.*L"[0-9]\{1,3\}\.\([^"]*\)".*/\1/p' ${MINGW_PREFIX}/include/WebView2EnvironmentOptions.h)

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DCMAKE_DLL_NAME_WITH_SOVERSION=ON \
      -DWEBVIEW_MSWEBVIEW2_VERSION="${_mswebview2_version}" \
      -DWEBVIEW_BUILD_SHARED_LIBRARY=ON \
      -DWEBVIEW_BUILD_STATIC_LIBRARY=OFF \
      -DWEBVIEW_BUILD_DOCS=ON \
      -DWEBVIEW_INSTALL_DOCS=OFF \
      -DWEBVIEW_BUILD_EXAMPLES=OFF \
      -DWEBVIEW_BUILD_TESTS=OFF \
      -DWEBVIEW_ENABLE_CHECKS=OFF \
      -DWEBVIEW_ENABLE_PACKAGING=OFF \
      "${extra_config[@]}" \
      -S "${_realname}-${pkgver}" \
      -B "build-${MSYSTEM}"

  cmake --build "build-${MSYSTEM}"
}

package_webview() {
  DESTDIR="${pkgdir}" cmake --install "build-${MSYSTEM}"

  install -Dm644 ${_realname}-${pkgver}/LICENSE \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}

package_webview-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mkdir -p "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}"
  cp -a "${srcdir}/build-${MSYSTEM}/docs/api/output/html" \
    "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/html"
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;

# vim:set ts=2 sw=2 et:
