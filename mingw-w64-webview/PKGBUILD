# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=webview
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.12.0
pkgrel=1
pkgdesc="A tiny cross-platform webview library for C/C++ to build modern cross-platform GUIs. (mingw-w64)"
arch=('any')
mingw_arch=('ucrt64' 'clang64' 'clangarm64')
url='https://github.com/webview/webview'
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs"
         "${MINGW_PACKAGE_PREFIX}-webview2-loader")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
source=("https://github.com/webview/webview/archive/${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('e2c8d0bed3fcd13e624074448b7d3b1d0b0d03546a15839f17ee0013f2cba1db')

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  _mswebview2_version=1.$(sed -n '/CORE_WEBVIEW_TARGET_PRODUCT_VERSION/ s/.*L"[0-9]\{1,3\}\.\([^"]*\)".*/\1/p' ${MINGW_PREFIX}/include/WebView2EnvironmentOptions.h)

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DCMAKE_DLL_NAME_WITH_SOVERSION=ON \
      -DWEBVIEW_MSWEBVIEW2_VERSION="${_mswebview2_version}" \
      -DWEBVIEW_BUILD_SHARED_LIBRARY=ON \
      -DWEBVIEW_BUILD_STATIC_LIBRARY=OFF \
      -DWEBVIEW_BUILD_DOCS=OFF \
      -DWEBVIEW_BUILD_EXAMPLES=OFF \
      -DWEBVIEW_BUILD_TESTS=OFF \
      -DWEBVIEW_ENABLE_CHECKS=OFF \
      -DWEBVIEW_ENABLE_PACKAGING=OFF \
      "${extra_config[@]}" \
      -S "${_realname}-${pkgver}" \
      -B "build-${MSYSTEM}"

  cmake --build "build-${MSYSTEM}"
}

package() {
  DESTDIR="${pkgdir}" cmake --install "build-${MSYSTEM}"

  install -Dm644 ${_realname}-${pkgver}/LICENSE \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
