# Maintainer: fauxpark <fauxpark@gmail.com>

_realname=mdloader
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=1.0.6
pkgrel=2
pkgdesc='Massdrop keyboard firmware loader (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
license=('GPL3')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc")
url='https://github.com/Massdrop/mdloader'
source=("https://github.com/Massdrop/mdloader/archive/${pkgver}.tar.gz"
        "0001-incbin-mangle.patch")
sha256sums=('1663e1e1f67b48e5d16802587c603092b43aab37020112931a39d6fc65afe94e'
            '4d6daa3880347c280a5e57b1899831cd807f4fbe1e235c2b7ff37d185f2c2b9b')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -Nbp1 -i "${srcdir}/0001-incbin-mangle.patch"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}/build"

  install -Dm755 mdloader.exe "${pkgdir}${MINGW_PREFIX}/bin/mdloader.exe"
}
