# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=daala
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=r2053.24f889a
pkgrel=1
pkgdesc="Daala is next-next-gen video compression technology from Xiph.org, Mozilla and others (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://www.xiph.org/daala/"
msys2_repository_url='https://github.com/xiph/daala'
license=('BSD')
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}-git=${pkgver}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}-git")
depends=("${MINGW_PACKAGE_PREFIX}-libogg"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-SDL2")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-check"
             "${MINGW_PACKAGE_PREFIX}-doxygen"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "git")
_commit=24f889aaabe33bc390b993d737d830b8dd367e61
source=("${_realname}"::"git+https://github.com/xiph/daala.git#commit=${_commit}"
        0003-missing-include.patch
        0004-xp-sp2.all.patch
        0005-no-pkg-config-prefix.mingw.patch)
sha256sums=('f7a5cfbfa73517aaee65dd6602fa958738b2a232dede818e3247cedeec214d91'
            'e0fa61c5a40ece233fab171ede7fc8936d8fe706a820b371e1376f58b09166a5'
            'b4fd487b6475ed108b31c363dd4cf078222dcfd01b04efec3fc7bc656847249a'
            '3272bae9d62b11ba71818f020f6f9968079cd43f07bd9425ba636b69c88ca743')

pkgver() {
  cd "${srcdir}/${_realname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"/${_realname}
  patch -p1 -i "${srcdir}"/0003-missing-include.patch
  patch -p1 -i "${srcdir}"/0004-xp-sp2.all.patch
  patch -p1 -i "${srcdir}"/0005-no-pkg-config-prefix.mingw.patch

  autoreconf -fi
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  ../${_realname}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --enable-shared \
    --enable-static \
    --enable-tools \
    --disable-unit-tests

   make
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/${_realname}/COPYING \
    "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
