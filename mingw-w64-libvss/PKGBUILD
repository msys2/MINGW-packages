_realname=libvss
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.0
pkgrel=1

pkgdesc="A high level string and text processing library for Ada (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
msys2_references=(
  'aur: libvss'
)

url="https://github.com/AdaCore/VSS"
license=('spdx:Apache-2.0 WITH LLVM-exception')
makedepends=("${MINGW_PACKAGE_PREFIX}-gprbuild")
depends=("${MINGW_PACKAGE_PREFIX}-gnatcoll")
source=("https://github.com/adacore/VSS/archive/v24.0.0/VSS-${pkgver}.tar.gz")
sha256sums=('40d467a7e953b8619cc8d42d0630f088c870c2ec699f0affbd8cf800b52826a8')

build() {
  [[ -d "${srcdir}"/build-${MSYSTEM} ]] && rm -rf "${srcdir}"/build-${MSYSTEM}
  cp -r "${srcdir}/VSS-${pkgver}" "${srcdir}"/build-${MSYSTEM}
  cd "${srcdir}"/build-${MSYSTEM}

  make build-all-libs BUILD_PROFILE=release
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}

  make install-all-libs BUILD_PROFILE=release PREFIX="${pkgdir}${MINGW_PREFIX}"

  # Copy License Files
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" \
    ${srcdir}/VSS-${pkgver}/LICENSE*
}
