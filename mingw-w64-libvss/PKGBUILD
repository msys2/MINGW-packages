# Maintainer: Philipp Smirnov <psmirnov@airmail.cc>

_realname=libvss
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=24.0.r1284.1c49a43
pkgrel=1
_branch=24.0

pkgdesc="A high level string and text processing library for Ada (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
msys2_references=(
  'aur: libvss'
)

url="https://github.com/AdaCore/VSS"
license=('Apache-2.0 WITH LLVM-exception')
makedepends=("${MINGW_PACKAGE_PREFIX}-gprbuild")
depends=("${MINGW_PACKAGE_PREFIX}-gnatcoll")
source=("${_realname}-git::git+https://github.com/AdaCore/VSS.git#branch=${_branch}")
sha256sums=('SKIP')

pkgver() {
  cd ${_realname}-git
  printf "%s.r%s.%s" "$_branch" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
  cd "${srcdir}/${_realname}-git/"

  make build-all-libs BUILD_PROFILE=release
}

package() {
  cd "${srcdir}/${_realname}-git/"

  make install-all-libs BUILD_PROFILE=release PREFIX="${pkgdir}${MINGW_PREFIX}"

  # Copy License Files
  install -Dm644 -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}" \
    "${srcdir}/${_realname}-git/"/LICENSE*
}
